defpackage stz/vm :
  import core
  import collections
  import core/stack-trace
  import stz/vm-ir
  import stz/typeset
  import stz/vm-table
  import stz/vm-ids
  import stz/dl-ir
  import stz/basic-ops
  import stz/vm-encoder
  import stz/stable-arrays
  import stz/branch-table
  import stz/backend
  import stz/utils

;<doc>=======================================================
;================= Virtual Machine Interface ================
;============================================================

# Create a virtual machine #

  val vm = VirtualMachine()

# Load new packages into the virtual machine #

  load (vm:VirtualMachine, pkgs:Collection<VMPackage>, keep-globals?:True|False) -> False

If keep-globals? is false, then new loaded globals are bound to
uninitialized variables. If keep-globals? is true, then the globals
are not changed, and retain their current value. In this case, the
globals *must* have the same type as it did previously. 

# Unload the given packages from the virtual machine #

  unload (vm:VirtualMachine, pkgs:Collection<Symbol>) -> False

Unloads the given packages from the virtual machine.

# Run top-level initializers for a package #

  init-package (vm:VirtualMachine, package:Symbol)

Executes the top-level expressions in the given package.
  
Examples:

  init-package(vm, `core)
  init-package(vm, `repl27)

# Compute the current live records #

  compute-live (vm:VirtualMachine, exclude:Seqable<Symbol>) -> Tuple<Rec>

Example: Suppose the user wishes to reload packages stz/algorithms and
stz/parser. This means that all the global variables in these two
packages will be reset to uninitialized, and we will rerun the
top-level expressions in stz/algorithms and stz/parser.

However, there may currently exist live objects that depend upon
certain definitions in stz/algorithms and stz/parser, and we have to
be careful not to change the signatures of these definitions.

The following call will temporarily set the global variables in
stz/algorithms and stz/parser to uninitialized and then retrieve the
records depended upon by all the currently live objects in the heap.
  
  compute-live(vm, [`stz/algorithms, `stz/parser])

# Clear all the global variables #

  clear-globals (vm:VirtualMachine) -> False

Reset all global variables in the virtual machine to uninitialized.
This is typically used before re-executing the top-level expressions
in all packages.

;============================================================
;=======================================================<doc>

;============================================================
;======================= Linker =============================
;============================================================

deftype Linker
defmulti live-map-table (l:Linker) -> LiveMapTable
defn Linker (branch-table:BranchTable) :
  val live-map-table = LiveMapTable()
  new Linker :
    defmethod live-map-table (this) : live-map-table

;============================================================
;================= Live Map Analysis ========================
;============================================================
deftype LiveMapTable
defmulti map-index (t:LiveMapTable, map:LivenessMap) -> Int
defmulti get (t:LiveMapTable, i:Int) -> LivenessMap
defmulti key? (t:LiveMapTable, i:Int) -> True|False

public defstruct LivenessMap <: Hashable&Equalable :
  live-slots: Tuple<Int>
  num-slots: Int
with:
  constructor => #LivenessMap
  printer => true
defn LivenessMap (slots:Tuple<Int>, num-slots:Int) :
  #LivenessMap(qsort(slots), num-slots)
defmethod hash (m:LivenessMap) :
  num-slots(m) + 7 * hash(live-slots(m))
defmethod equal? (a:LivenessMap, b:LivenessMap) :
  live-slots(a) == live-slots(b) and
  num-slots(a) == num-slots(b)

public defn LiveMapTable () :
  val maps = Vector<LivenessMap>()
  val table = HashTable<LivenessMap,Int>()
  defn add-map (m:LivenessMap) :
    table[m] = length(maps)
    add(maps, m)
  add-map(LivenessMap([], 0))
  new LiveMapTable :
    defmethod map-index (this, map:LivenessMap) :
      add-map(map) when not key?(table, map)
      table[map]
    defmethod get (this, i:Int) :
      maps[i]
    defmethod key? (this, i:Int) :
      i >= 0 and i < length(maps)

;============================================================
;==================== Extern Table ==========================
;============================================================

public deftype ExternTable
public defmulti get (t:ExternTable, name:Symbol) -> Long
public defmulti set (t:ExternTable, name:Symbol, addr:Long) -> False

defn ExternTable () :
  val table = HashTable<Symbol,Long>()
  new ExternTable :
    defmethod get (this, name:Symbol) :
      table[name]
    defmethod set (this, name:Symbol, addr:Long) :
      table[name] = addr

public val EXTERN-TABLE = ExternTable()

;============================================================
;===================== Format Table =========================
;============================================================

public defstruct CallFormat <: Hashable&Equalable :
  xs: Tuple<VMType>
  ys: Tuple<VMType>
with :
  printer => true

defmethod hash (f:CallFormat) :
  hash(xs(f)) + 7 * hash(ys(f))
defmethod equal? (a:CallFormat, b:CallFormat) :
  xs(a) == xs(b) and ys(a) == ys(b)

public deftype FormatTable
public defmulti index (t:FormatTable, f:CallFormat) -> Int
public defmulti address (t:FormatTable, index:Int) -> Long
public defmulti get (t:FormatTable, f:CallFormat) -> Long
public defmulti set (t:FormatTable, f:CallFormat, l:Long) -> False

defn FormatTable () :
  val table = HashTable<CallFormat,Int>()
  val addresses = Vector<Long>()
  new FormatTable :
    defmethod index (this, f:CallFormat) : table[f]
    defmethod address (this, index:Int) : addresses[index]
    defmethod get (this, f:CallFormat) : addresses[table[f]]
    defmethod set (this, f:CallFormat, l:Long) :
      add(addresses, l)
      table[f] = length(addresses) - 1

public val FORMAT-TABLE = FormatTable()

;============================================================
;=================== Format Coalescing ======================
;============================================================

defn exemplar-type (t:VMType) :
  match(t:VMRef) : VMLong()
  else : t

public defn coalesce (f:CallFormat) :
  ;New return result
  val x* =
    if empty?(xs(f)) :
      VMLong()
    else :
      match(exemplar-type(xs(f)[0])) :
        (t:VMByte|VMInt) : VMLong()
        (t) : t

  ;New arguments
  val ys* = for y in ys(f) map :
    match(exemplar-type(y)) :
      (y:VMByte|VMInt) : VMLong()
      (y) : y

  ;Return coalesced call format
  CallFormat([x*], ys*)

;============================================================
;==================== Tag Bits ==============================
;============================================================

public lostanza val INT-TAG-BITS:long = 0L
public lostanza val REF-TAG-BITS:long = 1L
public lostanza val MARKER-TAG-BITS:long = 2L
public lostanza val BYTE-TAG-BITS:long = 3L
public lostanza val CHAR-TAG-BITS:long = 4L
public lostanza val FLOAT-TAG-BITS:long = 5L

public lostanza val INT-TAG-INT:ref<Int> = new Int{INT-TAG-BITS as int}
public lostanza val REF-TAG-INT:ref<Int> = new Int{REF-TAG-BITS as int}
public lostanza val MARKER-TAG-INT:ref<Int> = new Int{MARKER-TAG-BITS as int}
public lostanza val BYTE-TAG-INT:ref<Int> = new Int{BYTE-TAG-BITS as int}
public lostanza val CHAR-TAG-INT:ref<Int> = new Int{CHAR-TAG-BITS as int}
public lostanza val FLOAT-TAG-INT:ref<Int> = new Int{FLOAT-TAG-BITS as int}

;============================================================
;==================== VM Constants ==========================
;============================================================

lostanza defn untag (x:long) -> ptr<?> :
  val tagbits = x & 7L
  if tagbits != REF-TAG-BITS : fatal("Not a heap-allocated object!")
  return (x - REF-TAG-BITS + 8) as ptr<?>

lostanza defn tag (x:ptr<?>) -> long :
  return (x + REF-TAG-BITS) as long

public lostanza defn void-marker () -> long :
  return (-1L << 3L) + MARKER-TAG-BITS

public lostanza defn false-marker () -> long :
  return (FALSE-TYPE.value << 3L) + MARKER-TAG-BITS

public lostanza defn true-marker () -> long :
  return (TRUE-TYPE.value << 3L) + MARKER-TAG-BITS

public defn void-marker-int () :
  marker-int(-1)

public defn marker-int (t:Int) :
  (t << 3) + MARKER-TAG-INT

;============================================================
;=================== VM Structures ==========================
;============================================================

public lostanza deftype VirtualMachine :
  backend: ref<Backend>
  vmtable: ref<VMTable>
  vm-ids: ref<VMIds>
  linker: ref<Linker>
  vmstate: ptr<VMState>
  var core-loaded?: ref<True|False>

public lostanza deftype VMState :
  ;Permanent State
  ;Changes in-between each code load
  var instructions: ptr<byte>
  var registers: ptr<long>
  var global-offsets: ptr<long>
  var global-mem: ptr<byte>
  var const-table: ptr<long>
  var const-mem: ptr<byte>
  var data-offsets: ptr<int>
  var data-mem: ptr<byte>
  var code-offsets: ptr<int>
  ;Variable State
  ;Changes in-between each boundary change
  var heap: ptr<long>      
  var heap-top: ptr<long>  
  var heap-limit: ptr<long>
  var free: ptr<long>      
  var free-limit: ptr<long>
  var current-stack: long
  ;Need for system stubs
  var system-stack: long
  var system-registers: ptr<long>
  ;Trie table
  var trie-table: ptr<ptr<int>>

lostanza deftype StackFrameHeader :
  var pool-index:int
  var mark:int
  var frames:StackFrame ...

lostanza deftype StackFrame :
  var return: long
  var liveness-map: long
  var slots: long ...

lostanza deftype Stack :
  var size: long
  var frames: ptr<StackFrame>
  var stack-pointer: ptr<StackFrame>
  var pc: long

lostanza deftype Function :
  var num-slots: long
  var code: long
  slots: long ...

lostanza deftype ObjectLayout :
  var tag: long
  var slots: long ...

lostanza deftype BrokenHeartLayout :
  var tag: long
  var forward: long

lostanza val SIZEOF-FRAME:ref<Int> = new Int{sizeof(StackFrame) as int}
lostanza val SIZEOF-STACK:ref<Int> = new Int{sizeof(Stack) as int}

lostanza defn stack-end (stackref:long) -> ptr<StackFrame> :
  val s:ptr<Stack> = untag(stackref)
  return s.frames + s.size

lostanza defn linker (vm:ref<VirtualMachine>) -> ref<Linker> :
  return vm.linker

lostanza defn vm-ids (vm:ref<VirtualMachine>) -> ref<VMIds> :
  return vm.vm-ids

lostanza defn vmtable (vm:ref<VirtualMachine>) -> ref<VMTable> :
  return vm.vmtable

lostanza defn backend (vm:ref<VirtualMachine>) -> ref<Backend> :
  return vm.backend

lostanza defn update-vmstate (vm:ref<VirtualMachine>) -> ref<False> :
  val vms = vm.vmstate
  val vmt = vm.vmtable
  vms.instructions = vmt.bytecode.mem
  vms.global-offsets = vmt.global-offsets.data
  vms.global-mem = vmt.globals.mem
  vms.const-table = vmt.consts.mem
  vms.const-mem = vmt.consts-data.mem
  vms.data-offsets = vmt.data-positions.data
  vms.data-mem = vmt.data.mem
  vms.code-offsets = vmt.function-addresses.data
  vms.trie-table = trie-table-data(branch-table(vm))
  return false

;============================================================
;==================== VM Implementation =====================
;============================================================

public defn VirtualMachine () :
  #if-defined(PLATFORM-WINDOWS) :
    VirtualMachine(W64Backend())
  #else :
    #if-defined(PLATFORM-LINUX) :
      VirtualMachine(L64Backend())
    #else :
      VirtualMachine(X64Backend())

public lostanza defn VirtualMachine (backend:ref<Backend>) -> ref<VirtualMachine> :
  val initial-heap-size = 1024 * 1024
  val vmstate:ptr<VMState> = call-c clib/malloc(sizeof(VMState))
  vmstate.registers = call-c clib/malloc(8 * 256)
  vmstate.heap = call-c clib/stz_memory_map(initial-heap-size, MAXIMUM-HEAP-SIZE)
  vmstate.heap-limit = vmstate.heap + initial-heap-size
  vmstate.free = call-c clib/stz_memory_map(initial-heap-size, MAXIMUM-HEAP-SIZE)
  vmstate.free-limit = vmstate.free + initial-heap-size
  vmstate.heap-top = vmstate.heap
  vmstate.current-stack = alloc-stack(vmstate)
  vmstate.system-stack = alloc-stack(vmstate)
  vmstate.system-registers = call-c clib/malloc(8 * 256)
  vmstate.trie-table = null
  val class-table = ClassTable()
  val branch-table = BranchTable(class-table)
  val vmtable = VMTable(class-table, branch-table)
  val linker = Linker(branch-table)
  val vm = new VirtualMachine{backend, vmtable, VMIds(), linker, vmstate, false}
  update-vmstate(vm)
  return vm

lostanza defn alloc-heap (vm:ptr<VMState>, tag:long, size:long) -> ptr<?> :
  ;Make space on heap
  val obj = vm.heap-top + 8
  [vm.heap-top] = tag
  vm.heap-top = vm.heap-top + 8 + size
  return obj

lostanza defn alloc-stack (vm:ptr<VMState>) -> long :
  ;Allocate stack
  val sptr:ptr<Stack> = alloc-heap(vm, STACK-TYPE.value, sizeof(Stack))

  ;Allocate stack frames
  val stack-size = 4 * 1024
  val frames-size = sizeof(StackFrameHeader) + stack-size
  val frames:ptr<StackFrameHeader> = call-c clib/malloc(frames-size)
  frames.pool-index = -1
  frames.mark = 0
  
  ;Fill in stack fields
  sptr.size = stack-size
  sptr.frames = addr(frames.frames)
  sptr.stack-pointer = null
  sptr.pc = 0
  sptr.frames.return = -1L
  sptr.frames.liveness-map = 0L

  ;Return stack
  return tag(sptr - 8)

;============================================================
;================= Bytecode Loop ============================
;============================================================

extern vmloop: (ptr<VMState>, long) -> int   ;void return

extern defn retrieve_class_name (vms:ptr<VMState>, id:long) -> ptr<byte> :
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  val name = get(vmtable(vm).class-name-table, new Int{id as int})
  return name.chars

extern defn call_garbage_collector (vms:ptr<VMState>, size:long) -> long :
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  return extend-heap(vm, size)

extern defn call_print_stack_trace (vms:ptr<VMState>, stack:long) -> int :
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  val stk:ptr<Stack> = untag(stack)
  print-stack-trace(stk, vmtable(vm), live-map-table(vm.linker))
  return 0

extern defn call_collect_stack_trace (vms:ptr<VMState>, stack:long) -> ptr<PackedStackTrace> :
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  val stk:ptr<Stack> = untag(stack)
  return collect-stack-trace(stk, vmtable(vm), live-map-table(vm.linker))

;Run the given virtual machine starting from the given starting function.
var VIRTUAL-MACHINE : VirtualMachine|False = false
protected lostanza var register-array:ptr<long>

public lostanza defn run-bytecode (vm:ref<VirtualMachine>, start-func:ref<Int>) -> ref<False> :
  VIRTUAL-MACHINE = vm
  register-array = vm.vmstate.registers
  initialize-stack-pointer(vm)
  set-starting-func(vm, start-func)
  call-c vmloop(vm.vmstate, call-prim crsp() as long)
  null-stack-pointer(vm)
  VIRTUAL-MACHINE = false
  return false

;Called by the extern defn callbacks defined in the generated bindings
public lostanza defn call-extern (extern-index:int) -> int :
  ;Retrieve the currently active virtual machine
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  ;Debug
  ;Retrieve the function id for this extern index
  val fid = get(vm.vmtable.extern-defn-funcs, new Int{extern-index})
  ;Set the returnpc to -1, so that execution will return here.
  var saved-ret:long
  let :
    val stk:ptr<Stack> = untag(vm.vmstate.current-stack)
    val sp = stk.stack-pointer
    saved-ret = sp.return
    sp.return = -1
  ;Set starting function and execute
  set-starting-func(vm, fid)
  call-c vmloop(vm.vmstate, call-prim crsp() as long)
  ;Restore the returnpc and return
  let :
    val stk:ptr<Stack> = untag(vm.vmstate.current-stack)
    val sp = stk.stack-pointer
    sp.return = saved-ret
    return 0

;Called by the extern defn callbacks. Retrieve the registers array
;from the currently active virtual machine.
public lostanza defn vm-registers () -> ptr<long> :
  ;Retrieve vm and vmtable
  val vm = VIRTUAL-MACHINE as ref<VirtualMachine>
  return vm.vmstate.registers

;Set the stack pointer of the stack to point to the beginning
;of its frames. (I.e. It is no longer null.)
lostanza defn initialize-stack-pointer (vm:ref<VirtualMachine>) -> ref<False> :
  val stk:ptr<Stack> = untag(vm.vmstate.current-stack)
  stk.stack-pointer = stk.frames
  return false

;Set the stack pointer of the stack to null. (I.e. It is no longer active.)
lostanza defn null-stack-pointer (vm:ref<VirtualMachine>) -> ref<False> :
  val stk:ptr<Stack> = untag(vm.vmstate.current-stack)
  stk.stack-pointer = null
  return false

;Set the starting pc of the virtual machine to fid. The virtual machine
;will start executing from the given function.
lostanza defn set-starting-func (vm:ref<VirtualMachine>, fid:ref<Int>) -> ref<False> :
  val pc = get(vm.vmtable.function-addresses,fid).value
  val stk:ptr<Stack> = untag(vm.vmstate.current-stack)
  stk.pc = pc * 4
  return false

;============================================================
;==================== Dispatch ==============================
;============================================================

lostanza defn branch-table (vm:ref<VirtualMachine>) -> ref<BranchTable> :
  return vm.vmtable.branch-table

;============================================================
;===================== Stack Traces =========================
;============================================================

;------------------------------------------------------------
;---------------------- Printing ----------------------------
;------------------------------------------------------------

lostanza defn print-stack-trace (stack:ptr<Stack>, vmtable:ref<VMTable>, livemap:ref<LiveMapTable>) -> ref<False> :
  ;Collect entries
  val buffer = collect-stack-trace-entries(stack, vmtable, livemap)

  ;Print out the entries
  print-stack-buffer(buffer)

  ;Return false
  return false

;Print the stack buffer
defn print-stack-buffer (buffer:Vector<StackTraceEntry>) -> False :
  do(print-stack-entry, buffer)

;Print a single stack trace entry.
defn print-stack-entry (e:StackTraceEntry) -> False :
  ;Print package and signature
  match(signature(e)) :
    (sig:String) : println(STANDARD-ERROR-STREAM, "  in %_/%_" % [package(e), sig])
    (sig:False) : println(STANDARD-ERROR-STREAM, "  in %_" % [package(e)])
  ;Print file information
  match(info(e)) :
    (info:FileInfo) : println(STANDARD-ERROR-STREAM, "    at %_" % [info])
    (f:False) : false  

;------------------------------------------------------------
;---------------------- Collecting --------------------------
;------------------------------------------------------------

lostanza defn collect-stack-trace (stack:ptr<Stack>, vmtable:ref<VMTable>, livemap:ref<LiveMapTable>) -> ptr<PackedStackTrace> :
  ;Collect all StackTraceEntry items.
  val buffer = collect-stack-trace-entries(stack, vmtable, livemap)

  ;Pack items into stable memory.
  val builder = StackTraceBuilder()
  add-entries(builder, buffer)
  
  ;Return false
  return pack(builder)

defn add-entries (b:StackTraceBuilder, es:Seqable<StackTraceEntry>) :
  do(add-entry{b, _}, es)

;------------------------------------------------------------
;-------------------- Common Utilities ----------------------
;------------------------------------------------------------

lostanza defn collect-stack-trace-entries (stack:ptr<Stack>,
                                           vmtable:ref<VMTable>,
                                           livemap:ref<LiveMapTable>) -> ref<Vector<StackTraceEntry>> :
  ;Accumulate all entries into a buffer.
  val buffer = Vector<StackTraceEntry>()
  
  ;Discover return addresses
  val end-sp = stack.stack-pointer
  labels :
    begin : goto loop(stack.frames)
    loop (sp:ptr<StackFrame>) :
      ;Store in return buffer
      ;if it exists in the file info table
      val ret = new Int{(sp.return / 4L) as int}
      match(get?(vmtable.trace-table, ret)) :
        (info:ref<StackTraceEntry>) : add(buffer, info)
        (info) : ()

      ;Continue if we're not at the end of the stack
      if sp < end-sp :
        val map-index = sp.liveness-map as int
        val stackmap = get(livemap, new Int{map-index})
        val num-slots = num-slots(stackmap).value
        val next-frame = addr(sp.slots[num-slots]) as ptr<StackFrame>
        goto loop(next-frame)

  ;Return the vector of entries in reverse order
  reverse!(buffer)
  return buffer
  

;============================================================
;==================== Heap/Stack Extension ==================
;============================================================

;This function is called by the C virtual machine when interpreting
;a GC_OPCODE instruction. It returns the new number of remaining bytes
;on the heap.
lostanza defn extend-heap (vm:ref<VirtualMachine>, size:long) -> long :
  ;Maximum heap size
  val maximum-heap-size = 4L * 1024L * 1024L * 1024L
  
  ;First run the garbage collector,
  collect-garbage(vm)

  ;Check whether we're still out of space.
  val vms = vm.vmstate
  val available-space = vms.heap-limit - vms.heap
  var desired-space:long = vms.heap-top + size - vms.heap  
  ;Increase the desired space if it is insufficient
  ;to satisfy the requested size.
  if available-space < desired-space :
    ;Double the desired space until it is sufficient.
    val requested-space = desired-space
    desired-space = available-space * 2
    while desired-space < requested-space :
      desired-space = desired-space * 2
  ;Increase the desired space if the usage ratio
  ;is too high.
  else :
    val usage-ratio = (desired-space as float) / (available-space as float)
    if usage-ratio > 0.5f :
      desired-space = available-space * 2
  ;Ensure that the desired space     
  desired-space = min(desired-space, maximum-heap-size)

  ;Increase the heap size if desired.
  if desired-space > available-space :
    vms.heap-limit = stz-memory-resize(vms.heap, available-space, desired-space)
    vms.free-limit = stz-memory-resize(vms.free, available-space, desired-space)

  ;Return the new amount of space remaining.
  return vms.heap-limit - vms.heap-top

lostanza defn ensure-heap-space (vm:ref<VirtualMachine>, size:long, num-objs:long) -> int :
  val total-size = size + num-objs * 8
  val vms = vm.vmstate
  if vms.heap-top + total-size > vms.heap-limit :
    val remaining = extend-heap(vm, total-size)
    if remaining < total-size : fatal("VM Out of Memory")
  return 0

;============================================================
;==================== Garbage Collector =====================
;============================================================

lostanza var TRACKER-CHAIN:ptr<LivenessTrackerObj>
lostanza defn collect-garbage (vm:ref<VirtualMachine>) -> long :
  ;Swap free with heap
  val vms = vm.vmstate
  val heap = vms.heap
  val heap-limit = vms.heap-limit
  val free = vms.free
  val free-limit = vms.free-limit
  vms.heap = free
  vms.heap-top = free
  vms.heap-limit = free-limit
  vms.free = heap
  vms.free-limit = heap-limit

  ;Initialize tracker chain
  TRACKER-CHAIN = null

  ;Scan global roots
  val globals:ptr<long> = vm.vmtable.globals.mem
  val roots = to-seq(roots(vm.vmtable.global-table))
  while empty?(roots) == false :
    val i = next(roots).value
    globals[i] = post-gc-object(globals[i], vm)

  ;Scan const roots
  val nconsts = vm.vmtable.consts.size / 8
  val consts:ptr<long> = vm.vmtable.consts.mem
  for (var i:int = 0, i < nconsts, i = i + 1) :
    consts[i] = post-gc-object(consts[i], vm)

  ;Scan stack roots
  vms.current-stack = post-gc-object(vms.current-stack, vm)
  vms.system-stack = post-gc-object(vms.system-stack, vm)

  ;Scan heap
  scan-heap(vm)

  ;Scan tracker chain
  scan-tracker-chain(TRACKER-CHAIN)

  ;Return
  return 0

lostanza defn object-size-on-heap (sz:long) -> long :
  val ceiled = (8L + sz + 7L) & -8L
  return max(ceiled, 16L)
lostanza defn object-size-on-heap (sz:ref<Int>) -> ref<Int> :
  return new Int{object-size-on-heap(sz.value) as int}

lostanza defn num-slots (f:ptr<StackFrame>, vm:ref<VirtualMachine>) -> int :
  val map = get(live-map-table(vm.linker), new Int{f.liveness-map as int})
  return num-slots(map).value

lostanza defn header (p:ptr<StackFrame>) -> ptr<StackFrameHeader> :
  return (p - sizeof(StackFrameHeader)) as ptr<StackFrameHeader>

lostanza defn scan-frames (frames:ptr<StackFrame>, f-end:ptr<StackFrame>, vm:ref<VirtualMachine>) -> int :
  if frames != null :
    val frameheader = header(frames)
    frameheader.mark = 1
    var f:ptr<StackFrame> = frames
    while f <= f-end :
      val map = get(live-map-table(vm.linker), new Int{f.liveness-map as int})
      val live-slots = live-slots(map)
      val num-live = length(live-slots).value
      for (var i:int = 0, i < num-live, i = i + 1) :
        val s = get(live-slots, new Int{i}).value
        f.slots[s] = post-gc-object(f.slots[s], vm)
      f = addr(f.slots[num-slots(map).value]) as ptr<StackFrame>
  return 0

lostanza defn scan-heap (vm:ref<VirtualMachine>) -> int :
  val vms = vm.vmstate
  var p:ptr<long> = vms.heap
  val class-table = vm.vmtable.class-table
  while p < vms.heap-top :
    p = scan-object(p, vm, class-table)
  return 0

lostanza deftype LivenessTrackerObj :
  tag: long
  var value: long
  var tail: ptr<LivenessTrackerObj>

lostanza defn scan-tracker-chain (tracker-chain:ptr<LivenessTrackerObj>) -> int :
  var t:ptr<LivenessTrackerObj> = tracker-chain
  while t != null :
    t.value = post-gc-weak-object(t.value)
    t = t.tail    
  return 0

lostanza defn post-gc-weak-object (ref:long) -> long :
  val tagbits = ref & 7L
  if tagbits == REF-TAG-BITS :
    val obj = (ref - REF-TAG-BITS) as ptr<long>
    val obj-tag = [obj]
    ;Case: Broken Heart
    if obj-tag == -1L :
      val heart = obj as ptr<BrokenHeartLayout>
      return heart.forward
    ;Case: Uncopied object
    else :
      return false-marker()
  else :
    return ref

lostanza defn scan-object (p:ptr<long>, vm:ref<VirtualMachine>, class-table:ref<ClassTable>) -> ptr<long> :
  ;p is [tag, cells ...]
  val tag = [p] as int
  val class = get(class-table, new Int{tag})
  match(class) :
    (class:ref<VMLeafClass>) :
      ;If it's a LivenessTracker, then extend the chain
      if tag == LIVENESS-TRACKER-TYPE.value :
        val t = p as ptr<LivenessTrackerObj>
        t.tail = TRACKER-CHAIN
        TRACKER-CHAIN = t
        return p + sizeof(LivenessTrackerObj)
      else :
        ;Scan the frames of a stack
        if tag == STACK-TYPE.value :
          val s = (p + 8) as ptr<Stack>
          scan-frames(s.frames, s.stack-pointer, vm)
        ;Get properties
        val size = object-size-on-heap(size(class).value)
        val roots = roots(class)
        val num-roots = length(roots).value
        ;Scan slots
        val obj = p as ptr<ObjectLayout>
        for (var i:int = 0, i < num-roots, i = i + 1) :
          val r = get(roots, new Int{i}).value
          obj.slots[r] = post-gc-object(obj.slots[r], vm)
        ;Return end of object
        return p + size
    (class:ref<VMArrayClass>) :
      ;Get properties
      val base-roots = base-roots(class)
      val item-roots = item-roots(class)
      val base-size = base-size(class).value
      val item-size = item-size(class).value
      val num-base-roots = length(base-roots).value
      val num-item-roots = length(item-roots).value
      ;Scan base roots
      val array = p as ptr<ObjectLayout>
      val len = array.slots[0]
      for (var i:int = 0, i < num-base-roots, i = i + 1) :
        val r = get(base-roots, new Int{i}).value
        array.slots[r] = post-gc-object(array.slots[r], vm)
      ;Scan item roots
      var items:ptr<long> = addr(array.slots) + base-size
      if num-item-roots > 0 :
        for (var n:long = 0, n < len, n = n + 1) :
          for (var i:int = 0, i < num-item-roots, i = i + 1) :
            val r = get(item-roots, new Int{i}).value
            items[r] = post-gc-object(items[r], vm)
          items = items + item-size
      ;Return end of array
      return p + object-size-on-heap(base-size + item-size * len)

lostanza defn post-gc-object (ref:long, vm:ref<VirtualMachine>) -> long :
  val vms = vm.vmstate
  val tagbits = ref & 7L
  if tagbits == REF-TAG-BITS :
    val obj = (ref - REF-TAG-BITS) as ptr<long>
    val obj-tag = [obj]
    ;Case: Broken Heart
    if obj-tag == -1L :
      val heart = obj as ptr<BrokenHeartLayout>
      return heart.forward
    ;Case: Uncopied object
    else :
      val obj* = tag(vms.heap-top)
      val class = get(vm.vmtable.class-table, new Int{obj-tag as int})
      copy-bytes-to-heap(obj, num-bytes(obj, class, vm), vms)
      set-broken-heart(obj, obj*)
      return obj*
  else :
    return ref

lostanza defn set-broken-heart (obj:ptr<long>, obj*:long) -> int :
  val heart = obj as ptr<BrokenHeartLayout>
  heart.tag = -1L
  heart.forward = obj*
  return 0

lostanza defn num-bytes (obj:ptr<?>, class:ref<VMClass>, vm:ref<VirtualMachine>) -> long :
  match(class) :
    (class:ref<VMLeafClass>) :
      return object-size-on-heap(size(class).value)
    (class:ref<VMArrayClass>) :
      val array = obj as ptr<ObjectLayout>
      val len = array.slots[0]
      val base-size = base-size(class).value
      val item-size = item-size(class).value
      return object-size-on-heap(base-size + len * item-size)

lostanza defn copy-bytes-to-heap (p:ptr<?>, n:long, vms:ptr<VMState>) -> int :
  val heap = vms.heap-top
  val nwords = n >>> 3
  val src = p as ptr<long>
  for (var i:long = 0, i < nwords, i = i + 1) :
    heap[i] = src[i]
  vms.heap-top = heap + n
  return 0

;============================================================
;==================== Liveness Detector =====================
;============================================================

lostanza var LIVENESS-BUFFER:ptr<BrokenHeartLayout>
lostanza var LIVENESS-BUFFER-TOP:ptr<BrokenHeartLayout>

lostanza defn mark-in-liveness-buffer (obj:ptr<ObjectLayout>) -> int :
  if obj.tag == -1L : fatal("Already marked!")
  LIVENESS-BUFFER-TOP.tag = obj.tag
  LIVENESS-BUFFER-TOP.forward = obj as long
  LIVENESS-BUFFER-TOP = LIVENESS-BUFFER-TOP + sizeof(BrokenHeartLayout)
  obj.tag = -1L
  return 0

lostanza defn mark-ref (ref:long) -> int :
  val tagbits = ref & 7L
  if tagbits == REF-TAG-BITS :
    val obj = (ref - REF-TAG-BITS) as ptr<ObjectLayout>
    ;If object is not already marked
    if obj.tag != -1L :
      mark-in-liveness-buffer(obj)
  return 0

lostanza defn mark-frames (frames:ptr<StackFrame>, f-end:ptr<StackFrame>, vm:ref<VirtualMachine>) -> int :
  if frames != null :
    var f:ptr<StackFrame> = frames
    while f <= f-end :
      val map = get(live-map-table(vm.linker), new Int{f.liveness-map as int})
      val live-slots = live-slots(map)
      val num-live = length(live-slots).value
      for (var i:int = 0, i < num-live, i = i + 1) :
        val s = get(live-slots, new Int{i}).value
        mark-ref(f.slots[s])
      f = addr(f.slots[num-slots(map).value]) as ptr<StackFrame>
  return 0

lostanza defn mark-object (p:ptr<long>, tag:int, vm:ref<VirtualMachine>, class-table:ref<ClassTable>) -> int :
  if tag != LIVENESS-TRACKER-TYPE.value :
    if tag == STACK-TYPE.value :
      val s = (p + 8) as ptr<Stack>
      mark-frames(s.frames, s.stack-pointer, vm)
    else :
      ;p is [tag, cells ...]
      val class = get(class-table, new Int{tag})
      match(class) :
        (class:ref<VMLeafClass>) :
          ;Get properties
          val size = size(class).value
          val roots = roots(class)
          val num-roots = length(roots).value
          ;Mark slots
          val obj = p as ptr<ObjectLayout>
          for (var i:int = 0, i < num-roots, i = i + 1) :
            val r = get(roots, new Int{i}).value
            mark-ref(obj.slots[r])
        (class:ref<VMArrayClass>) :
          ;Get properties
          val base-roots = base-roots(class)
          val item-roots = item-roots(class)
          val base-size = base-size(class).value
          val item-size = item-size(class).value
          val num-base-roots = length(base-roots).value
          val num-item-roots = length(item-roots).value
          ;Mark base roots
          val array = p as ptr<ObjectLayout>
          val len = array.slots[0]
          for (var i:int = 0, i < num-base-roots, i = i + 1) :
            val r = get(base-roots, new Int{i}).value
            mark-ref(array.slots[r])
          ;Mark item roots
          var items:ptr<long> = addr(array.slots) + base-size
          if num-item-roots > 0 :
            for (var n:long = 0, n < len, n = n + 1) :
              for (var i:int = 0, i < num-item-roots, i = i + 1) :
                val r = get(item-roots, new Int{i}).value
                mark-ref(items[r])
              items = items + item-size
  return 0

lostanza defn mark-liveness-buffer (vm:ref<VirtualMachine>) -> int :
  val class-table = vm.vmtable.class-table
  var buffer:ptr<BrokenHeartLayout> = LIVENESS-BUFFER
  while buffer < LIVENESS-BUFFER-TOP :
    mark-object(buffer.forward as ptr<long>, buffer.tag as int, vm, class-table)
    buffer = buffer + sizeof(BrokenHeartLayout)
  return 0

lostanza defn restore-tag (heart:ptr<BrokenHeartLayout>) -> int :
  val obj = heart.forward as ptr<ObjectLayout>
  obj.tag = heart.tag
  return 0

val EMPTY-TUPLE = []
public lostanza defn compute-live (vm:ref<VirtualMachine>, exclude:ref<Seqable<Symbol>>) -> ref<Tuple<Rec>> :
  ;If virtual machine is not yet initialized, then return empty tuple.
  if vm.core-loaded? == false :
    return EMPTY-TUPLE
    
  val vms = vm.vmstate
  ;Use free memory for liveness buffer
  ;(Guaranteed to be large enough)
  LIVENESS-BUFFER = vms.free as ptr<BrokenHeartLayout>
  LIVENESS-BUFFER-TOP = vms.free as ptr<BrokenHeartLayout>

  ;Scan global roots
  val globals:ptr<long> = vm.vmtable.globals.mem
  val roots = to-seq(roots(vm.vmtable.global-table, exclude))
  while empty?(roots) == false :
    val i = next(roots).value
    mark-ref(globals[i])

  ;Const roots do not affect liveness

  ;Scan stack roots
  mark-ref(vms.current-stack)
  mark-ref(vms.system-stack)

  ;Scan liveness buffer
  mark-liveness-buffer(vm)

  ;Get live set
  val live-recs = RecSet()
  for (var heart:ptr<BrokenHeartLayout> = LIVENESS-BUFFER,
       heart < LIVENESS-BUFFER-TOP,
       heart = heart + sizeof(BrokenHeartLayout)) :
    val tag = heart.tag as int
    if tag == FN-TYPE.value :
      val f = (heart.forward + 8L) as ptr<Function>
      val code = new Int{f.code as int}
      add-all(live-recs, function-dependencies(vm.vm-ids, code))
    else :
      val tag = new Int{tag}
      add-all(live-recs, class-dependencies(vm.vm-ids, tag))
    restore-tag(heart)

  ;Return ids
  return to-tuple(live-recs)

defn RecSet () :
  HashSet<Rec>(hash{id(_)}, {id(_) == id(_)})

;============================================================
;===================== Debugging ============================
;============================================================

lostanza defn dump-heap (vm:ref<VirtualMachine>) -> int :
  val vms = vm.vmstate
  return dump-heap(vms.heap, vms.heap-top, vm)

lostanza defn dump-heap (pstart:ptr<long>, pend:ptr<long>, vm:ref<VirtualMachine>) -> int :
  val stackrefs = Vector<Long>()
  call-c clib/printf("Heap:\n")
  var p:ptr<long> = pstart
  while p < pend :
    val tag = [p] as int
    if tag == STACK-TYPE.value :
      add(stackrefs, new Long{/tag(p)})
    val class = get(vm.vmtable.class-table, new Int{tag})
    match(class) :
      (class:ref<VMLeafClass>) :
        val obj = p as ptr<ObjectLayout>
        val size = size(class).value
        call-c clib/printf("  %p: [Object %d, size = %d]", /tag(p), tag, size)
        for (var i:long = 0, i < size, i = i + 8) :
          call-c clib/printf("  %lx", [p + 8 + i])
        call-c clib/printf("\n")
        ;Advance to next object
        p = p + object-size-on-heap(size)
      (class:ref<VMArrayClass>) :
        val array = p as ptr<ObjectLayout>
        val len = array.slots[0]
        val base-size = base-size(class).value
        val item-size = item-size(class).value
        val size = base-size + item-size * len
        call-c clib/printf("  %p: [Array %d, length = %ld, base-size = %d, item-size = %d]",
                           /tag(p), tag, len, base-size, item-size)
        for (var i:long = 0, i < size, i = i + 8) :
          call-c clib/printf("  %lx", [p + 8 + i])
        call-c clib/printf("\n")
        ;Advance to next object
        p = p + object-size-on-heap(size)
  ;Dump stacks
  for (var i:int = 0, i < length(stackrefs).value, i = i + 1) :
    val s = get(stackrefs, new Int{i})
    dump-stack(s.value, vm)
  return 0

lostanza defn dump-stack (stackref:long, vm:ref<VirtualMachine>) -> int :
  val stk:ptr<Stack> = untag(stackref)
  var f:ptr<StackFrame> = stk.frames
  val f-end = stk.stack-pointer
  call-c clib/printf("Stack %p:\n", stackref)
  if f != null :
    while f <= f-end :
      ;Get frame properties
      val map-index = new Int{f.liveness-map as int}
      if key?(live-map-table(vm.linker), map-index) == true :
        val map = get(live-map-table(vm.linker), map-index)
        val live-slots = live-slots(map)
        val num-slots = num-slots(map).value
        ;Print properties
        val num-live = length(live-slots).value
        var slot-i:int = 0
        call-c clib/printf("  %p: [StackFrame %ld, num-slots = %d]\n", f, f.liveness-map, num-slots)
        for (var i:int = 0, i < num-slots, i = i + 1) :
          if slot-i < num-live and live-slots.items[slot-i].value == i :
            call-c clib/printf("    %d: [%lx]\n", i, f.slots[i])
            slot-i = slot-i + 1
          else :
            call-c clib/printf("    %d: %lx\n", i, f.slots[i])
        ;Advance to next frame
        f = addr(f.slots[num-slots]) as ptr<StackFrame>
      else :
        call-c clib/printf("  %p: [BAD FRAME %x]\n", f, f.liveness-map)
        return 0
  return 0

lostanza defn print-tag (ref:long) -> int :
  call-c clib/printf("inspect tag of %p\n", ref)
  val tagbits = ref & 7L
  if tagbits == REF-TAG-BITS :
    val tag = [(ref - REF-TAG-BITS) as ptr<long>]
    if tag == FN-TYPE.value or tag == TYPE-TYPE.value :
      val f:ptr<Function> = untag(ref)
      call-c clib/printf("tagbits = %ld (REF), tag = %ld (FN/TYPE), code = %ld\n", tagbits, tag, f.code)
    else :
      call-c clib/printf("tagbits = %ld (REF), tag = %ld\n", tagbits, tag)
  else if tagbits == MARKER-TAG-BITS :
    val tag = ref >> 3L
    call-c clib/printf("tagbits = %ld (MARKER), tag = %ld\n", tagbits, tag)
  else if tagbits == INT-TAG-BITS :
    call-c clib/printf("tagbits = %ld (INT)\n", tagbits)
  else if tagbits == BYTE-TAG-BITS :
    call-c clib/printf("tagbits = %ld (BYTE)\n", tagbits)
  else if tagbits == CHAR-TAG-BITS :
    call-c clib/printf("tagbits = %ld (CHAR)\n", tagbits)
  else if tagbits == FLOAT-TAG-BITS :
    call-c clib/printf("tagbits = %ld (FLOAT)\n", tagbits)
  else :
    call-c clib/printf("Unrecognized tag bits.\n")
  return 0

lostanza defn function-addresses (vmt:ref<VMTable>) -> ref<StableIntArray> :
  return vmt.function-addresses

;============================================================
;================= Instruction Encoding =====================
;============================================================

defn EncodingResolver (ids:VMIds, branch-table:BranchTable, live-map-table:LiveMapTable) :
  new EncodingResolver :
    defmethod liveness-map (this, live:Tuple<Int>, num-locals:Int) :
      map-index(live-map-table, LivenessMap(live, num-locals))
    defmethod object-header-size (this) :
      8
    defmethod object-size-on-heap (this, sz:Int) :
      object-size-on-heap(sz) - 8
    defmethod stack-size (this) :
      SIZEOF-STACK
    defmethod dispatch-format (this, branches:Tuple<Tuple<TypeSet>>) :
      add(branch-table, DispatchFormat(branches))
    defmethod match-format (this, branches:Tuple<Tuple<TypeSet>>) :
      add(branch-table, MatchFormat(branches))
    defmethod method-format (this, multi:Int, num-header-args:Int, num-args:Int) :
      add(branch-table, MultiFormat(multi, num-header-args, num-args))
    defmethod marker (this, type:Int) : marker-int(type)
    defmethod void-marker (this) : void-marker-int()
    defmethod ref-offset (this) : REF-TAG-INT
    defmethod type-is-final? (this, n:Int) :
      match(class-rec(ids, n)) :
        (t:StructRec) : package(id(t)) == `core
        (t) : false
    defmethod marker? (this, n:Int) :
      val r = class-rec(ids,n) as StructRec
      empty?(base(r)) and items(r) is False
    defmethod tagbits (this, n:Int) :
      switch(n) :
        BYTE-TYPE : BYTE-TAG-INT
        CHAR-TYPE : CHAR-TAG-INT
        INT-TYPE : INT-TAG-INT
        FLOAT-TYPE : FLOAT-TAG-INT

;============================================================
;==================== Class Names ===========================
;============================================================

defn class-name (c:VMClass, ids:VMIds) -> String :
  match(class-rec(ids, id(c))) :
    (r:Rec) :
      to-string(name(id(r)))
    (_:False) :
      defn name! (n:Int) : name(id(class-rec(ids,n) as Rec))
      string-join(seq(name!, parents(c)), "&")

defn class-names (cs:Seqable<VMClass>, ids:VMIds) -> Seqable<KeyValue<Int,String>> :
  for c in cs seq :
    id(c) => class-name(c,ids)

;============================================================
;====================== Loading =============================
;============================================================

public defn load (vm:VirtualMachine, vmps:Collection<VMPackage>, keep-existing-globals?:True|False) :
  if not empty?(to-seq(vmps)) :
    ;Precondition
    ensure-core-loaded-first!(vm, vmps)

    ;Retrieve tables
    val vmt = vmtable(vm)
    val vm-ids = vm-ids(vm)
    val load-unit = load-packages(vm-ids, vmps)
    
    ;Load all packages
    for p in packages(load-unit) do :
      load-globals(vmt, globals(p), name(p), keep-existing-globals?)
      load-package-methods(branch-table(vm), name(p), methods(p))
    ;Load functions
    val encoding-resolver = EncodingResolver(vm-ids, branch-table(vm), live-map-table(linker(vm)))
    for f in funcs(load-unit) do :
      val ef = encode(func(f), encoding-resolver, backend(vm))
      load-function(vmt, id(f), ef)
    ;Load all classes into table  
    load-classes(vmt, classes(load-unit))
    load-class-names(vmt, class-names(classes(load-unit), vm-ids))
    ;Load datas and consts
    load-datas(vmt, datas(load-unit))
    load-consts(vmt, consts(load-unit))
    ;Load callbacks
    for c in callbacks(load-unit) do :
      load-callback(vmt, index(c), function-id(c))
    ;Update the virtual machine state
    update(branch-table(vm))
    update-vmstate(vm)

    ;If core has been loaded, then initialize the constants by running
    ;the initialize-constants function.
    run-bytecode(vm, INIT-CONSTS-FN) when core-loaded?(vm)

public defn unload (vm:VirtualMachine, ps:Collection<Symbol>) :
  val vmps = to-tuple $ for p in ps seq :
    val io = PackageIO(p, [], [], [])
    VMPackage(io, false, [], [], [], [], [], [], [], [])
  load(vm, vmps, false)    

defn ensure-core-loaded-first! (vm:VirtualMachine, vmps:Collection<VMPackage>) :
  if not core-loaded?(vm) :
    val package-names = to-tuple(seq(name, vmps))
    if not contains?(package-names, `core) :
      fatal("Cannot load packages %, before loading core." % [package-names])
lostanza defn core-loaded? (vm:ref<VirtualMachine>) -> ref<True|False> :
  return vm.core-loaded?
lostanza defn set-core-loaded? (vm:ref<VirtualMachine>, v:ref<True|False>) -> ref<False> :
  vm.core-loaded? = v
  return false

public defn init-package (vm:VirtualMachine, package:Symbol) -> True|False :
  val f = package-init(vm-ids(vm), package)
  match(f:Int) :
    val trace? = false
    if package == `core :
      run-bytecode(vm, f)
      set-core-loaded?(vm, true)      
      true
    else :
      launch-init(vm, f, trace?)
  else :
    true

lostanza defn launch-init (vm:ref<VirtualMachine>, fid:ref<Int>, trace?:ref<True|False>) -> ref<True|False> :
  val vms = vm.vmstate
  ;Create the closure representing the initialization function
  val closure-size = 8 + 8
  ensure-heap-space(vm, closure-size, 1)
  val closure:ptr<Function> = alloc-heap(vms, FN-TYPE.value, closure-size)
  closure.num-slots = 0L
  closure.code = fid.value
  ;Call closure using the launcher function
  vms.registers[0] = false-marker()
  vms.registers[1] = 1L
  vms.registers[2] = tag(closure - 8L)
  run-bytecode(vm, EXECUTE-TOPLEVEL-COMMAND-FN)
  ;Return whether successful or not
  val ret = vms.registers[0]
  if ret == false-marker() : return false
  else if ret == true-marker() : return true
  else : return fatal("Invalid return value from execute-toplevel-command.")

public lostanza defn clear-globals (vm:ref<VirtualMachine>) -> ref<False> :
  ;Scan global roots
  val globals:ptr<long> = vm.vmtable.globals.mem
  val roots = to-seq(roots(vm.vmtable.global-table))
  while empty?(roots) == false :
    val i = next(roots).value
    globals[i] = void-marker()
  return false

;============================================================
;==================== Utilities =============================
;============================================================
lostanza defn float-to-bits (x:float) -> int :
  return ($ls-prim bits x)

lostanza defn double-to-bits (x:double) -> long :
  return ($ls-prim bits x)

lostanza defn bits-to-float (x:int) -> float :
  return ($ls-prim fnum x)

lostanza defn bits-to-double (x:long) -> double :
  return ($ls-prim fnum x)

var delay : (() -> ?) -> False =
  fn (f) : fatal("Not within delay-actions block.")
defn delay-actions<?T> (f:() -> ?T) :
  val delays = Vector<(() -> ?)>()
  defn delay-action (action:() -> ?)  : add(delays, action)
  val result = let-var delay = delay-action : f()
  for d in delays do : d()
  clear(delays)
  result
