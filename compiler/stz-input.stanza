defpackage stz/input :
   import core
   import verse
   import stz/il-ir

;============================================================
;================== DECLARATION =============================
;============================================================

#use-overlay(stz/reader-lang)

defn flatten (es:List<IExp>) :
   for e in es map-append :
      match(e) :
         (e:IBegin) : exps(e)
         (e:ILSBegin) : comms(e)
         (e) : list(e)

defreader SEXP-TABLE :
   e -> IExp :
      ;Package Forms
      ($package name:e imports:e ...) :
         IPackage(name, imports, info)
      ($import name:e prefixes:e ...) :
         IImport(name, prefixes, info)
      ($prefix-of (names:e ...) p:e) :
         IPrefix(names, p, info)
      ($prefix p:e) :
         IPrefix(false, p, info)
      ($public e:e) :
         IPublic(e, info)
      ($protected e:e) :
         IProtected(e, info)
         
      ;Stanza Declaration Forms
      ($deftype name:e (args:e ...) parent:e) :
         IDefType(name, args, parent, info)
      ($def name:e type:e value:e) :
         IDef(name, type, value, info)
      ($defvar name:e type:e value:e) :
         IDefVar(name, type, value, info)      
      ($defn name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         IDefn(false, name, targs, a1, a2, args, body, info)
      ($defn* name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         IDefn(true, name, targs, a1, a2, args, body, info)         
      ($defmulti name:e (targs:e ...) (a1:e ...) a2:e) :
         IDefmulti(name, targs, a1, a2, info)
      ($defmethod name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         IDefmethod(false, name, false, targs, a1, a2, args, body, info)
      ($defmethod* name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         IDefmethod(true, name, false, targs, a1, a2, args, body, info)
         
      ;Stanza Expression Forms
      ($fn (a1:e ...) a2:e (args:e ...) body:e) :
         IFn(false, a1, a2, args, body, info)
      ($fn* (a1:e ...) a2:e (args:e ...) body:e) :
         IFn(true, a1, a2, args, body, info)
      ($multi f:e fs:e ...) :
         IMulti(List(f, fs), info)
      ($begin es:e ...) :
         val es* = flatten(es)
         if length(es*) == 1 : head(es*)
         else : IBegin(es*, info)
      ($match (es:e ...) bs:e ...) :
         IMatch(es, bs, info)
      ($branch (ts:e ...) (args:e ...) body:e) :
         IBranch(ts, args, body, info)
      ($new type:e methods:e ...) :
         INew(type, methods, info)
      ($as exp:e type:e) :
         ICast(exp, type, info)
      ($set name:e exp:e) :
         ISet(name, exp, info)
      ($do f:e args:e ...) :
         IDo(f, args, info)
      ($prim f:e args:e ...) :
         IPrim(f, args, info)
      ($call-ls f:e args:e ...) :
         ICallLS(f, args, info)
      ($tuple es:e ...) :
         ITuple(es, info)
      ($quote v:?) :
         ILiteral(unwrap-all(v), INone(info), info)
      ($none) :
         INone(info)
      x :
         IVar(x, info)
      v :
         ILiteral(v, INone(info), info)
         
      ;Stanza Type Forms   
      ($of name:e args:e ...) :
         IOf(name, args, info)
      ($and a:e b:e) :
         IAnd(a, b, info)
      ($or a:e b:e) :
         IOr(a, b, info)
      ($-> (a1:e ...) a2:e) :
         IArrow(a1, a2, info)
      ($cap x:e) :
         ICap(x, info)
      ($void) :
         IVoid(info)
      ($?) :
         IGradual(info)

      ;LoStanza Expressions
      ($ls-new type:e args:e ...) :
         ILSNew(type, args, info)
      ($ls-array type:e (args:e ...) length:e) :
         ILSArray(type, args, length, info)
      ($ls-struct type:e args:e ...) :
         ILSStruct(type, args, info)
      ($ls-addr exp:e) :
         ILSAddr(exp, info)
      ($ls-deref exp:e) :
         ILSDeref(exp, info)
      ($ls-slot exp:e i:e) :
         ILSSlot(exp, i, info)
      ($ls-field exp:e name:e) :
         ILSField(exp, name, info)
      ($ls-do f:e args:e ...) :
         ILSDo(f, args, info)
      ($ls-call-c f:e args:e ...) :
         ILSCallC(f, args, info)
      ($ls-prim f:e args:e ...) :
         ILSPrim(f, args, info)
      ($ls-sizeof type:e) :
         ILSSizeof(type, info)
      ($ls-tagof name:e) :
         ILSTagof(name, info)
      ($ls-as exp:e type:e) :
         ILSAs(exp, type, info)
      ($ls-letexp c:e e:e) :
         ILSLetExp(c, e, info)
      ($ls-and a:e b:e) :
         ILSAnd(a, b, info)
      ($ls-or a:e b:e) :
         ILSOr(a, b, info)

      ;Commands
      ($ls-set x:e y:e) :
         ILSSet(x, y, info)
      ($ls-labels (names:e ...) body:e) :
         ILSLabels(names, body, info)
      ($ls-label name:e) :
         ILSLabel(name, info)
      ($ls-goto name:e) :
         ILSGoto(name, info)
      ($ls-return e:e) :
         ILSReturn(e, info)
      ($ls-let c:e) :
         ILSLet(c, info)
      ($ls-if p:e c:e a:e) :
         ILSIf(p, c, a, info)
      ($ls-begin es:e ...) :
         val es* = flatten(es)
         if length(es*) == 1 : head(es*)
         else : ILSBegin(es*, info)
      ($ls-match (args:e ...) bs:e ...) :
         ILSMatch(args, bs, info)
      ($ls-branch (ts:e ...) (args:e ...) body:e) :
         ILSBranch(ts, args, body, info)
         
      ;Declarations   
      ($ls-def name:e type:e exp:e) :
         ILSDef(name, type, exp, info)
      ($ls-defvar name:e type:e exp:e) :
         ILSDefVar(name, type, exp, info)
      ($ls-deftype name:e (targs:e ...) parent:e (ts:e ...) tr:e (fs:e ...) fr:e) :
         ILSDefType(name, targs, parent, ts, tr, fs, fr, info)
      ($ls-defn name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         ILSDefn(false, name, targs, a1, a2, args, body, info)
      ($ls-defn* name:e (targs:e ...) (a1:e ...) a2:e (args:e ...) body:e) :
         ILSDefn(true, name, targs, a1, a2, args, body, info)
      ($ls-extern name:e type:e) :
         ILSExtern(name, type, info)
      ($ls-externfn name:e type:e) :
         ILSExternFn(name, type, info)

      ;Types
      ($ls-byte) :
         ILSByteT(info)
      ($ls-int) :
         ILSIntT(info)
      ($ls-long) :
         ILSLongT(info)
      ($ls-float) :
         ILSFloatT(info)
      ($ls-double) :
         ILSDoubleT(info)
      ($ls-?) :
         ILSUnknownT(info)
      ($ls-ptr t:e) :
         ILSPtrT(t, info)
      ($ls-ref t:e) :
         ILSRefT(t, info)
      ($ls-fn (a1:e ...) ar:e a2:e) :
         ILSFnT(a1, ar, a2, info)
         



;============================================================
;================== IMPLEMENTATION ==========================
;============================================================


;               Token Information Propagation
;               =============================

defn fill-info (form) :
   ;Find the first informative info
   defn first-info (a:FileInfo, b:FileInfo) :
      match(a) :
         (a:NoFileInfo) : b
         (a) : a
   defn first-info (x) :
      defn infos (x) :
         generate<FileInfo> :
            defn loop (x) :
               match(x) :
                  (x:Token) :
                     yield(info(x))
                     loop(item(x))
                  (x:List) : do(loop, x)
                  (x) : false
            loop(x)      
      reduce(first-info, FileInfo(), infos(x))

   ;Fill file information up tree
   defn fill-up (x, info:FileInfo) -> Token :
      match(x) :
         (x:Token) :
            fill-up(item(x), core/info(x))
         (x:List) :
            val x* = map(fill-up{_, FileInfo()}, x)
            val info* = first-info(info, first-info(x*))
            Token(x*, info*)
         (x) :
            Token(x, info)

   ;Fill file information down tree
   defn fill-down (x, info:FileInfo) -> Token :
      match(x) :
         (x:Token) :
            fill-down(item(x), first-info(core/info(x), info))
         (x:List) :
            val x* = map(fill-down{_, info}, x)
            Token(x*, info)
         (x) :
            Token(x, info)

   ;Driver
   fill-down(fill-up(form, FileInfo()), FileInfo())


;               FormTable Datastructures
;               ========================

defstruct FormTable :
   types: Tuple<KeyValue<Symbol, Tuple<Form>>>

definterface Form
defstruct TagForm <: Form :
   tag: Symbol
   fields: Tuple<Field>
   body: (Array, FileInfo) -> ?
defstruct SForm <: Form :
   body: (Symbol, FileInfo) -> ?
defstruct VForm <: Form :
   body: (?, FileInfo) -> ?

definterface Field
defstruct SField <: Field : (type: Symbol)
defstruct LField <: Field : (type: Symbol)
defstruct RField <: Field : (type: Symbol)   


;               Production Table Analysis
;               =========================

defstruct ProdTable :
   tagforms: HashTable<Symbol, TagForm>
   sform: False|SForm
   vform: False|VForm

defn tag? (t:ProdTable, x:Symbol) :
   key?(tagforms(t), x)

defn ProdTable (forms:Tuple<Form>) :
   val tagforms = HashTable<Symbol,TagForm>(symbol-hash)
   var sform = false
   var vform = false
   for f in forms do :
      match(f) :
         (f:TagForm) :
            if key?(tagforms, tag(f)) :
               error("Duplicate production for form ~." % [tag(f)])
            tagforms[tag(f)] = f
         (f:SForm) :
            error("Duplicate symbol form.") when sform != false
            sform = f   
         (f:VForm) :
            error("Duplicate value form.") when vform != false
            vform = f
   ProdTable(tagforms, sform, vform)


;                 Read Form to Input IR
;                 =====================

public defn read (form) :
   ;               Analyse productions
   ;               ===================
   val prod-tables = HashTable<Symbol, ProdTable>(symbol-hash)
   for entry in types(SEXP-TABLE) do :
      prod-tables[key(entry)] = ProdTable(value(entry))

   defn stanza-tag? (tag:Symbol) :
      for t in values(prod-tables) any? :
         tag?(t, tag)

   ;                   Error Buffer
   ;                   ============

   val read-errors = Vector<ReadError>()
   defn error! (e:ReadError) :
      add(read-errors, e)
      throw(e)

   defn read-prod (form, prod:Symbol) :
      if prod == `? : unwrap-all(form)
      else : read-prod(form, prod-tables[prod])
      
   defn read-prod (form, table:ProdTable) :
      ;Ensure proper form
      defn ensure-min-arity! (form:List, n:Int) :
         error!(MinArityError(form, n)) when (length(form) - 1) < n
      defn ensure-arity! (form:List, n:Int) :
         error!(ArityError(form, n)) when (length(form) - 1) != n

      ;Read form according to the field descriptors
      defn read-fields (fs:Tuple<Field>, form:List) :
         ;Ensure that x at position i is a list.
         defn list! (x, i:Int) :
            match(unwrap-token(x)) :
               (x:List) : x
               (x) : error!(NotListError(head(form), i, x))

         ;Ensure proper arity of form
         val n = length(fs)
         if any?({_ typeof RField}, fs) :
            ensure-min-arity!(form, n - 1)
         else :
            ensure-arity!(form, n)

         ;Read production and track whether it passed.
         var pass? = true
         defn read-prod? (form, prod:Symbol) :
            try :
               read-prod(form, prod)
            catch (e) :
               pass? = false
         defn read-prods? (form:List, prod:Symbol) :
            map(read-prod?{_, prod}, form)

         ;Read all values according to their field descriptors.
         val values = Array<?>(n)
         defn* loop (i:Int, form:List) :
            if i < n :
               values[i] = match(fs[i]) :
                  (f:SField) : read-prod?(head(form), type(f))
                  (f:LField) : read-prods?(list!(head(form), i), type(f))                     
                  (f:RField) : read-prods?(form, type(f))
               loop(i + 1, tail(form)) when fs[i] not-typeof RField
         loop(0, tail(form))

         ;Return read values, unless they did not pass reading
         if pass? : values
         else : throw(ReadError())

      ;Retrieve the tagform for a given form, or fail
      defn tagform-of (form:List, info:FileInfo) :
         if not empty?(form) :
            match(unwrap-token(head(form))) :
               (tag:Symbol) :
                  match(get?(tagforms(table), tag, false)) :
                     (t:TagForm) :
                        t
                     (t:False) :
                        if stanza-tag?(tag) : error!(InvalidContext(form, info))
                        else : error!(InvalidForm(form, info))
               (tag) : error!(InvalidForm(form, info))
         else : error!(InvalidForm(form, info))

      ;Dispatch based upon the type of the form
      defn r (form, info:FileInfo) :
         match(form) :
            (form:Token) :
               r(item(form), core/info(form))
            (form:List) :
               val f = tagform-of(form, info)
               val values = read-fields(fields(f), form)
               body(f)(values, info)
            (form:Symbol) :
               match(sform(table)) :
                  (f:SForm) : body(f)(form, info)
                  (f:False) : error!(InvalidContext(form, info))
            (form) :
               match(vform(table)) :
                  (f:VForm) : body(f)(form, info)
                  (f:False) : error!(InvalidContext(form, info))                  
      r(form, FileInfo())

   val prod1 = key(types(SEXP-TABLE)[0])
   try :
      read-prod(fill-info(form), prod1)
   catch (e:ReadError) :
      do(println, read-errors)
      throw(ReadError())

;============================================================
;================= Errors ===================================
;============================================================

definterface ReadError <: Exception
defn ReadError () :
   new ReadError :
      defmethod print (o:OutputStream, this) :
         print(o, "ReadError")

defn ReadError (msg, info) :
   new ReadError :
      defmethod print (o:OutputStream, this) :
         print(o, "~: ~" << [info, msg])

defn MinArityError (form:List, n:Int) :
   val info = info(head(form) as Token)
   ReadError{_, info} $
      "Stanza form ~ expects at least ~ arguments, but found only ~." << [
         head(form), n, length(form) - 1]

defn ArityError (form:List, n:Int) :
   val info = info(head(form) as Token)
   ReadError{_, info} $
      "Stanza form ~ expects ~ arguments, but found ~." << [
         head(form), n, length(form) - 1]

defn InvalidForm (form:List, info:FileInfo) :
   ReadError{_, info} $
      "Form ~ is not a valid Stanza core form." << [form]

defn NotListError (tag:Token, i:Int, x) :
   ReadError{_, info(tag)} $
      "Stanza form ~ expects a list in argument position ~, but received ~." << [tag, i, x]

defn InvalidContext (form, info:FileInfo) :
   ReadError{_, info} $
      "Inappropriate form ~ for in this context." << [form]

   
;============================================================      
;================= Syntax Restrictions ======================
;============================================================
;

#with-overlay(stz/check-lang) :
   public defcheck check (e:IExp) :
      ;======== Stanza Language ========
      pe*("package level form") :
         + pe
         custom{ensure-package(e)}
         
      pe("package level form") :
         IPackage: {name:v imports:(imp ...)}
         IBegin: {exps:(pe ...)}
         + te
   
      imp("import form") :
         IImport: {package:v prefix:(pr ...)}
   
      pr("prefix form") :
         IPrefix :
            if (names(e) == false) : {prefix:v}
            else : {names:(v ...) prefix:v}
   
      te("top level form") :
         IPublic: {exp:te}
         IProtected: {exp:te}
         IDefType: {class:v args:(v ...) parent:pt?}
         IDefmulti :
            {targs:(targ ...) a1:(ct? ...) a2:t?}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IDefmethod :
            {multi:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-no-this-param(e)}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IBegin: {exps:(te ...)}
         + tse
         + ltc
      tse("top level form") :
         + se*
         custom{ensure-impure(e)}

      se("scope level form") :
         + se*
         custom{ensure-body(e)}         
      se*("scope level form") :
         IDef: {name:b type:t? value:e}
         IDefVar: {name:v type:t? value:e?}
         IDefn :
            {name:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IBegin: {exps:(se*, se* ...)}
         + e
   
      e("expression") :
         IMulti: {funcs:(func func ...)}
         IBegin: {exps:(e e ...)}
         IMatch :
            {args:(e e ...) branches:(br br ...)}
            custom{ensure-branch-arity(e)}
         INew: {class:pt methods:(m ...)}
         IVar: ()
         ICast: {exp:e type:t}
         ISet: {name:v value:e}
         IDo: {func:farg args:(e ...)}
         IPrim: {function:farg, args:(e ...)}
         ILiteral: custom{ensure-literal(e)}
         ITuple: {exps:(e ...)}
         + func
   
      e?("optional expression") :
         INone: ()
         + e
   
      func("function expression") :
         IFn :
            {a1:(t? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
   
      farg("function argument") :
         IOf: {class:v args:(t ...)}
         + e

      br("branch expression") :
         IBranch :
            {types:(t? ...) args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), types(e), args(e))}
   
      m("method expression") :
         IDefmethod :
            {multi:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-this-param(e)}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
   
      b("binder expression") :
         IVar: ()
         ITuple: {exps:(b ...)}
   
      pt("parent type") :
         IOf: {class:v args:(t ...)}
         IAnd: {a:pt b:pt}
         IVar: ()
         
      pt?("optional parent type") :
         INone: ()
         + pt
   
      ct("type") :
         IOf: {class:v args:(ct ...)}
         IAnd: {a:ct b:ct}
         IOr: {a:ct b:ct}
         IArrow: {a1:(t ...) a2:ct}
         ICap: {name:v}
         IGradual: ()
         IVoid: ()
         IVar: ()
         ITuple: {exps:(ct ...)}
   
      ct?("optional type") :
         INone: ()
         + ct
   
      t("type") :
         + ct
         custom{ensure-no-cap(e)}
      t?("optional type") :
         INone: ()
         + t
   
      v("symbol") :
         IVar : ()

      targ("type argument") :
         ICap: {name:v}
         IVar: ()      
         
      ;======== LoStanza Language ========
      ltc("LoStanza top level command") :
         + ltc*
         custom{ensure-no-return(e)}
      ltc*("LoStanza top level command") :
         ILSDef: {name:v type:lt exp:le}
         ILSDefVar: {name:v type:lt exp:le?}
         ILSDefType :
             {name:v args:(v ...) parent:t? types:(lt ...) rtype:lt? fields:(v ...) rfield:rf}
             custom{ensure-matched-type-fields(info(e), types(e), fields(e))}
             custom{ensure-matched-rtype-rfield(info(e), rtype(e), rfield(e))}
         ILSExtern: {name:v type:lt}
         ILSExternFn: {name:v type:lft}
         ILSDefn :
            {name:v targs:(targ ...) a1:(lct ...) a2:lt args:(b ...) body:lc}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-return(body(e))}
         ILSBegin: {comms:(ltc* ...)}          
         ILSSet: {exp:lval value:le}
         ILSLet: {comm:lc}
         ILSLabels: {names:(v ...), body:lc}
         ILSIf: {pred:le conseq:lc alt:lc}
         ILSMatch: {args:(le le ...) branches:(lbr lbr ...)}
         + lcalls

      rf("LoStanza rest field") :
         INone: ()
         IVar: ()

      lc("LoStanza command") :   
         ILSDef: {name:v type:lt? exp:le}
         ILSDefVar: {name:v type:lt exp:le?}
         ILSBegin: {comms:(lc lc ...)}
         ILSSet: {exp:lval value:le}
         ILSLet: {comm:lc}
         ILSIf: {pred:le conseq:lc alt:lc}
         ILSMatch: {args:(le le ...) branches:(lbr lbr ...)}
         ILSLabels: {names:(v ...), body:lc}
         ILSLabel: {name:v}
         ILSGoto: {name:v}
         ILSReturn: {exp:le}
         + lcalls

      lbr("LoStanza branch") :
         ILSBranch:
            {types:(t? ...) args:(b ...) body:lc}
            custom{ensure-matched-a1-args(info(e), types(e), args(e))}
   
      le("LoStanza expression") :
         ILSNew: {type:t args:(le ...)}
         ILSArray: {type:t args:(le ...) length:le}
         ILSStruct: {type:t args:(le ...)}
         ILSAddr: {exp:lval}
         ILSDeref: {exp:le}
         ILSSlot: {exp:le index:le}
         ILSField: {exp:le name:v}
         ILSSizeof: {type:lt}
         ILSTagof: {name:v}
         ILSAs: {exp:le type:lt}
         ILSAnd: {a:le, b:le}
         ILSOr: {a:le, b:le}
         ILSLetExp: {comm:lc, exp:le}
         IVar: ()
         ILiteral: custom{ensure-ls-literal(e)}
         + lcalls
      lcalls("LoStanza call") :
         ILSDo: {func:lfarg args:(le ...)}
         ILSPrim: {op:lfarg args:(le ...)}
         ILSCallC: {func:le args:(le ...)}         

      lfarg("LoStanza function argument") :
         IOf: {class:v args:(t ...)}
         + le
   
      le?("optional LoStanza expression") :
         INone: ()
         + le
   
      lval("LoStanza lvalue expression") :
         IVar: ()
         ILSDeref: {exp:le}
         ILSSlot: {exp:le index:le}
         ILSField: {exp:le name:v}
   
      lt("LoStanza type") :
         ILSByteT: ()
         ILSIntT: ()
         ILSLongT: ()
         ILSFloatT: ()
         ILSDoubleT: ()
         ILSUnknownT: ()
         ILSPtrT: {type:lt}
         ILSRefT: {type:t}
         IVar: ()
         + lft
      lft("LoStanza function type") :
         ILSFnT: {a1:(lt ...) ar:lt? a2:lt}
         
      lt?("optional LoStanza type") :
         INone: ()
         + lt

      lct("LoStanza argument type") :
         ILSByteT: ()
         ILSIntT: ()
         ILSLongT: ()
         ILSFloatT: ()
         ILSDoubleT: ()
         ILSUnknownT: ()
         ILSPtrT: {type:lct}
         ILSRefT: {type:ct}
         ILSFnT: {a1:(lt ...) ar:lt? a2:lct}
         IVar: ()

defn var? (e:IExp, s:Symbol) :
   match(e) :
      (e:IVar) : name(e) == s
      (e) : false

;============================================================
;================== Error Checkers ==========================
;============================================================

defn ensure-package (e:IExp) :
   defn first-exp (e:IExp) :
      match(e) :
         (e:IBegin) : head(exps(e)) when not empty?(exps(e))
         (e) : e
   match(first-exp(e)) :
      (e:False|IPackage) : false
      (e:IExp) : lang/check/error!(info(e), "First expression in a program must be a package declaration.")

defn ensure-no-cap (e:IExp) :
   match(e) :
      (e:ICap) : lang/check/error!(info(e), "Capture variable ?~ cannot be used in this context." << [name(e)])
      (e) : do(ensure-no-cap, e)   

defn ensure-ordered-targs (es:List<IExp>) :
   defn loop* (es:List<IExp>) :
      if not empty?(es) :
         match(head(es)) :
            (e:ICap) :
               if not all?({_ typeof ICap}, es) :
                  lang/check/error!(info(e), "Capture variables must appear after all explicit type arguments.")
            (e) : loop*(tail(es))
   loop*(es)

defn ensure-capturing-position (targs:List<IExp>, a1:List<IExp>) :
   defn captures? (x:Symbol, e:IExp) :
      match(e) :
         (e:ICap) :
            match(name(e)) :
               (v:IVar) : name(v) == x
               (v) : false
         (e) : any?(captures?{x, _}, children(e))
   defn ensure-captured? (info:FileInfo, x:Symbol) :
      val captured? = any?(captures?{x, _}, a1)
      if not captured? :
         lang/check/error!(info, "Captured type argument ~ has no capturing position." << [x])
         
   val cargs = filter({_ typeof ICap}, targs) as Streamable<ICap>   
   for a in cargs do :
      val v = name(name(a) as IVar)
      ensure-captured?(info(a), v)

defn ensure-matched-a1-args (info:FileInfo, a1:List<IExp>, args:List<IExp>) :
   if length(a1) != length(args) :
      lang/check/error!(info, "The number of argument types do not match the number of arguments.")

defn ensure-no-this-param (e:IDefmethod) :
   if any?(var?{_, `this}, args(e)) :
      lang/check/error!(info(e), "Top level method cannot have a this argument.")

defn ensure-this-param (e:IDefmethod) :
   val n = count(var?{_, `this}, args(e))
   if n == 0 :
      lang/check/error!(info(e), "Instance method requires a this argument.")
   else if n > 1 :
      lang/check/error!(info(e), "Instance method cannot have multiple this arguments.")
   else :   
      val i = index-when(var?{_, `this}, args(e)) as Int
      if a1(e)[i] not-typeof INone :
         lang/check/error!(info(e), "Cannot give the this argument an explicit type.")

defn pure-expression? (e:IExp) :
   e typeof IFn|IMulti|INew|IVar|ILiteral|ITuple

defn ensure-impure (e:IExp) :
   if pure-expression?(e) :
      lang/check/error!(info(e), "Expected statement but received pure expression ~." << [e])
         
defn ensure-body (e:IExp) :
   val es = match(e) :
      (e:IBegin) : exps(e)
      (e) : list(e)

   do(ensure-impure, but-last(es))
   match(last(es)) :
      (e:IDef|IDefVar|IDefn) :
         lang/check/error!(info(e), "Declaration cannot be last expression in body." << [e])
      (e) : false

defn ensure-branch-arity (e:IMatch) :
   val bs = branches(e) as List<IBranch>
   val n = length(args(e))
   for b in bs do :
      if length(args(b)) != n :
         lang/check/error!(info(b), "The number of branch arguments does not match number of arguments given to match.")

defn ensure-literal (e:ILiteral) :
   defn lit? (v) :
      match(v) :
         (v:Int|Float|Symbol|String) : true
         (v:List) : all?(lit?, v)
         (v) : false
   if not lit?(value(e)) :
      lang/check/error!(info(e), "Value ~ is not a valid Stanza literal." << [value(e)])

defn ensure-matched-type-fields (info:FileInfo, ts:List<IExp>, fs:List<IExp>) :
   if length(ts) != length(fs) :
      lang/check/error!(info, "The number of field types does not match the number of fields.")

defn ensure-matched-rtype-rfield (info:FileInfo, t:IExp, f:IExp) :
   match(t, f) :
      (t:INone, f:INone) : false
      (t:INone, f) : lang/check/error!(info, "No type given for rest field ~." << [f])
      (t, f:INone) : lang/check/error!(info, "Type ~ given for rest field, but no rest field given." << [t])
      (t, f) : false

defn ensure-no-return (body:IExp) :
   match(body) :
      (body:ILSDefn) :
         false
      (body:ILSReturn) :
         lang/check/error!(info(body), "Invalid return statement in top level context.")
      (body) :
         do(ensure-no-return, body)

defn ensure-return (exp:IExp) :
   ;Apply function for every body in e
   defn do-last (f: IExp -> ?, e:IExp) :
      match(e) :
         (e:ILSBegin) :
            do-last(f, last(comms(e)))
         (e:ILSLet) :
            do-last(f, comm(e))
         (e:ILSLabels) :
            do-last(f, body(e))
         (e:ILSIf) :
            do-last(f, conseq(e))
            do-last(f, alt(e))
         (e:ILSMatch) :
            for b in branches(e) as List<ILSBranch> do :
               do-last(f, body(b))               
         (e) : f(e)

   ;Check last statement
   for e in exp do-last :
      match(e) :
         (e:ILSReturn|ILSGoto) : false
         (e) : lang/check/error!(info(e), "Return statement expected here for end of function body.")

defn ensure-ls-literal (e:ILiteral) :
   defn lit? (v) :
      v typeof Int|Float|String
   if not lit?(value(e)) :
      lang/check/error!(info(e), "Value ~ is not a valid LoStanza literal." << [value(e)])