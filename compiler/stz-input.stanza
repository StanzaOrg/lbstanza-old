;See License.txt for details about licensing.

defpackage stz/input :
  import core
  import collections
  import stz/il-ir
  import stz/dl-ir
  import stz/visibility

;============================================================
;======================== Driver ============================
;============================================================

public defn to-ipackages (form, default-imports:Tuple<IImport>) -> Tuple<IPackage> :
  val e = read-iexp(form)
  check(e)
  split-packages(e, default-imports)

public defn to-il (form) -> IExp :
  val e = read-iexp(form)
  check(e)
  e

;============================================================
;========== Exported Types from IPackage ====================
;============================================================

public defn exported-types (ps:Tuple<IPackage>) :
  val types = HashSet<TypeId>()
  for p in ps do :
    defn scan (e:IExp) :
      match(e) :
        (e:IVisibility) : scan(exp(e))
        (e:IBegin) : do(scan, exps(e))
        (e:IDefType|ILSDefType) : add(types, TypeId(name(p), name!(e)))
        (e) : false
    do(scan, exps(p))
  types

;============================================================
;==================== Reader ================================
;============================================================

defn Begin (e:IExp, es:List<IExp>, info:False|FileInfo) :
   Begin(cons(e, es), info)

defn name-args (e:IExp) -> [IExp, List<IExp>] :
   match(e) :
      (e:IOf) : [class(e), args(e)]
      (e) : [e, List()]

defn ls-name-args (e:IExp) -> [IExp, List<IExp>] :
   match(e) :
      (e:ILSOfT) : [class(e), args(e)]
      (e) : [e, List()]

#with-added-syntax(stz-reader-lang) :
   defreader read-iexp (e) -> IExp :
      ;Package Forms
      ($package name:e imports:e ...) :
         IDefPackage(name, imports, info)
      ($import name:e prefixes:e ...) :
         IImportExp(name, prefixes, info)
      ($prefix-of (names:e ...) p:e) :
         IPrefixExp(names, p, info)
      ($prefix p:e) :
         IPrefixExp(false, p, info)
      ($public e:e es:e ...) :
         val es* = Begin(e, es, info)
         IVisibility(es*, Public(), info)
      ($protected e:e es:e ...) :
         val es* = Begin(e, es, info)
         IVisibility(es*, Protected(), info)
      ($private e:e es:e ...) :
         val es* = Begin(e, es, info)
         IVisibility(es*, Private(), info)
      ;Stanza Declaration Forms
      ($doc string:e) :
        IDoc(string, info)
      ($deftype name:e parent:e children:e ...) :
         val [name*, args*] = name-args(name)
         IDefType(name*, args*, parent, children, info)
      ($defchild name:e parent:e) :
         val [name*, args*] = name-args(name)
         IDefChild(name*, args*, parent, info)
      ($def name:e type:e value:e) :
         IDef(name, type, value, info)
      ($defvar name:e type:e value:e) :
         IDefVar(name, type, value, info)      
      ($defn name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = name-args(name)
         val body = Begin(body0, bodyn, info)
         IDefn(false, name*, targs*, a1, a2, args, body, info)
      ($defn* name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = name-args(name)
         val body = Begin(body0, bodyn, info)
         IDefn(true, name*, targs*, a1, a2, args, body, info)         
      ($defmulti name:e (a1:e ...) a2:e) :
         val [name*, targs*] = name-args(name)
         IDefmulti(name*, targs*, a1, a2, info)
      ($defmethod name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = name-args(name)
         val body = Begin(body0, bodyn, info)
         IDefmethod(false, name*, false, targs*, a1, a2, args, body, info)
      ($defmethod* name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = name-args(name)
         val body = Begin(body0, bodyn, info)
         IDefmethod(true, name*, false, targs*, a1, a2, args, body, info)
         
      ;Stanza Expression Forms
      ($fn (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         IFn(false, a1, a2, args, body, info)
      ($fn* (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         IFn(true, a1, a2, args, body, info)
      ($multi f:e fs:e ...) :
         IMulti(cons(f, fs), info)
         
      ($begin es:e ...) :
         Begin(es, info)
      ($let e:e) :
         ILet(e, info)
      ($match (es:e ...) bs:e ...) :
         IMatch(es, bs, info)
      ($branch (args:e ...) (ts:e ...) body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         IBranch(ts, args, body, info)
      ($new type:e methods:e ...) :
         INew(type, methods, info)
      ($as exp:e type:e) :
         ICast(exp, type, info)
      ($as? exp:e type:e) :
         IUpCast(exp, type, info)
      ($set name:e exp:e) :
         ISet(name, exp, info)
      ($do f:e args:e ...) :
         IDo(f, args, info)
      ($prim f:e args:e ...) :
         IPrim(f, args, info)
      ($tuple es:e ...) :
         ITuple(es, info)
      ($quote v:?) :
         ILiteral(unwrap-all(v), INone(info), info)
      ($none) :
         INone(info)
      x :
         IVar(x, info)
      v :
         ILiteral(v, INone(info), info)
         
      ;Stanza Type Forms   
      ($of name:e args:e ...) :
         IOf(name, args, info)
      ($and a:e b:e) :
         IAnd(a, b, info)
      ($or a:e b:e) :
         IOr(a, b, info)
      ($-> (a1:e ...) a2:e) :
         IArrow(a1, a2, info)
      ($cap x:e) :
         ICap(x, info)
      ($void) :
         IVoid(info)
      ($?) :
         IGradual(info)

      ;LoStanza Expressions
      ($ls-new type:e args:e ...) :
         ILSNew(type, args, info)
      ($ls-struct type:e args:e ...) :
         ILSStruct(type, args, info)
      ($ls-addr exp:e) :
         ILSAddr(exp, false, info)
      ($ls-addr! exp:e) :
         ILSAddr(exp, true, info)         
      ($ls-deref exp:e) :
         ILSDeref(exp, info)
      ($ls-slot exp:e i:e) :
         ILSSlot(exp, i, info)
      ($ls-field exp:e name:e) :
         ILSField(exp, name, info)
      ($ls-do f:e args:e ...) :
         ILSDo(f, args, info)
      ($ls-call-c f:e args:e ...) :
         ILSCallC(f, args, info)
      ($ls-prim f:e args:e ...) :
         ILSPrim(f, args, info)
      ($ls-sizeof type:e) :
         ILSSizeof(type, info)
      ($ls-tagof name:e) :
         ILSTagof(name, info)
      ($ls-as exp:e type:e) :
         ILSAs(exp, type, info)
      ($ls-letexp e:e es:e ...) :
         val es* = flatten(cons(e, es))
         val empty = Begin(List(), info)
         if empty?(es*) : ILSLetExp(empty, empty, info)
         else : ILSLetExp(Begin(but-last(es*), info), last(es*), info)
      ($ls-and a:e b:e) :
         ILSAnd(a, b, info)
      ($ls-or a:e b:e) :
         ILSOr(a, b, info)

      ;Commands
      ($ls-set x:e y:e) :
         ILSSet(x, y, info)         
      ($ls-labels blocks:e ...) :
         ILSLabels(blocks, info)
      ($ls-block name:e (args:e ...) (ts:e ...) body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         ILSLabeledBlock(name, args, ts, body, info)
      ($ls-goto name:e args:e ...) :
         ILSGoto(name, args, info)         
      ($ls-return e:e) :
         ILSReturn(e, info)
      ($ls-let c:e cs:e ...) :
         val c* = Begin(c, cs, info)
         ILSLet(c*, info)
      ($ls-if p:e c:e a:e) :
         ILSIf(p, c, a, info)
      ($ls-match (args:e ...) bs:e ...) :
         ILSMatch(args, bs, info)
      ($ls-branch (args:e ...) (ts:e ...) body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         ILSBranch(ts, args, body, info)
      ($ls-func x:e) :
         ILSFn(x, info)
         
      ;Declarations   
      ($ls-def name:e type:e exp:e) :
         ILSDef(name, type, exp, info)
      ($ls-defvar name:e type:e exp:e) :
         ILSDefVar(name, type, exp, info)
      ($ls-deffield mut:e type:e name:e) :
         ILSDefField(mut, type, name, info)
      ($ls-deftype name:e parent:e (fs:e ...) fr:e) :
         val [name*, args*] = ls-name-args(name)
         ILSDefType(name*, args*, parent, fs, fr, info)
      ($ls-defn name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = ls-name-args(name)
         val body = Begin(body0, bodyn, info)
         ILSDefn(false, name*, targs*, a1, a2, args, body, info)
      ($ls-extern-fn name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val body = Begin(body0, bodyn, info)
         ILSExternFn(name, a1, a2, args, body, info)
      ($ls-defn* name:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [name*, targs*] = ls-name-args(name)
         val body = Begin(body0, bodyn, info)
         ILSDefn(true, name*, targs*, a1, a2, args, body, info)
      ($ls-defmethod multi:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [multi*, targs*] = ls-name-args(multi)
         val body = Begin(body0, bodyn, info)
         ILSDefmethod(false, multi*, targs*, a1, a2, args, body, info)
      ($ls-defmethod* multi:e (args:e ...) (a1:e ...) a2:e body0:e bodyn:e ...) :
         val [multi*, targs*] = ls-name-args(multi)
         val body = Begin(body0, bodyn, info)
         ILSDefmethod(true, multi*, targs*, a1, a2, args, body, info)
      ($ls-extern name:e type:e) :
         ILSExtern(name, type, info)

      ;Types
      ($ls-byte) :
         ILSByteT(info)
      ($ls-int) :
         ILSIntT(info)
      ($ls-long) :
         ILSLongT(info)
      ($ls-float) :
         ILSFloatT(info)
      ($ls-double) :
         ILSDoubleT(info)
      ($ls-?) :
         ILSUnknownT(info)
      ($ls-of name:e args:e ...) :
         ILSOfT(name, args, info)
      ($ls-ptr t:e) :
         ILSPtrT(t, info)
      ($ls-ref t:e) :
         ILSRefT(t, info)
      ($ls-fn (a1:e ...) ar:e a2:e) :
         ILSFnT(a1, ar, a2, info)

;============================================================
;================ Splitting into Packages ===================
;============================================================

defn split-packages (e:IExp, default-imports:Tuple<IImport>) :
  ;Flatten sequence of expressions
  defn flatten (e:IExp) :
    generate<IExp> :
      let loop (e:IExp = e) :
        match(e:IBegin) : do(loop, exps(e))
        else : yield(e)
  val eseq = flatten(e)
  
  ;Group packages
  val packages = Vector<IPackage>()
  while not empty?(eseq) :
    val pkg = match(peek(eseq)) :
      (e:IDefPackage) :
        next(eseq)
        val exps = to-list(take-while({_ is-not IDefPackage}, eseq))
        val imports = to-tuple $ seq(to-iimport, imports(e) as List<IImportExp>)
        IPackage(name!(e), imports, exps, info(e))
      (e:IExp) :
        val exps = to-list(take-while({_ is-not IDefPackage}, eseq))
        val info = info(head(exps))
        val name = gensym(`default)
        IPackage(name, default-imports, exps, info)
    add(packages, pkg)

  ;Return packages
  to-tuple(packages)

defn to-iimport (e:IImportExp) :
  defn to-iprefix (e:IPrefixExp) :
    val names = match(names(e)) :
      (ns:List<IExp>) : to-tuple(seq(name!, ns))
      (ns:False) : false
    val prefix = to-string(name!(prefix(e)))
    IPrefix(names, prefix)
  val package = name!(package(e))
  val prefix = to-tuple $ seq(to-iprefix, prefix(e) as List<IPrefixExp>)
  IImport(package, prefix, info(e))
      
;============================================================      
;================= Syntax Restrictions ======================
;============================================================

;                      Checker
;                      =======

#with-added-syntax(stz-check-lang) :
   public defcheck check (e:IExp) :
      ;======== Stanza Language ========         
      pe("package level form") :
         IDefPackage: {name:v imports:(imp ...)}
         ILoadPackage: ()
         IBegin: {exps:(pe ...)}
         + te
   
      imp("import form") :
         IImportExp: {package:v prefix:(pr ...)}
   
      pr("prefix form") :
         IPrefixExp :
            if (names(e) is False) : {prefix:v}
            else : {names:(v ...) prefix:v}
   
      te("top level form") :
         IVisibility: {exp:te}
         IDoc: ()
         IDefType :
            {class:v args:(v ...) parent:pt? children:(dc ...)}
            custom{ensure-wellformed-children(e)}
         IDefmulti :
            {name:v targs:(targ ...) a1:(ct? ...) a2:t?}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IDefmethod :
            {multi:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-no-this-param(e)}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IBegin: {exps:(te ...)}
         + tse
         + ltc
      tse("top level form") :
         + se*
         custom{ensure-impure(e)}

      dc("type child form") :
         IDefChild: {name:v args:(v ...) parent:cpt}
      cpt("child parent type") :
         IOf: {class:v args:(t ...)}
         IVar: ()         

      se("scope level form") :
         + se*
         custom{ensure-body(e)}         
      se*("scope level form") :
         IDef: {name:b type:t? value:e}
         IDefVar: {name:v type:t? value:e?}
         IDefn :
            {name:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         IBegin: {exps:(se*, se* ...)}
         + e
   
      e("expression") :
         ILet: {exp:se}
         IMulti: {funcs:(func func ...)}
         IBegin: {exps:(e e ...)}
         IMatch :
            {args:(e e ...) branches:(br br ...)}
            custom{ensure-branch-arity(e)}
         INew: {class:pt methods:(m ...)}
         IVar: ()
         ICast: {exp:e type:t}
         IUpCast: {exp:e type:t}
         ISet: {name:v value:e}
         IDo: {func:farg args:(e ...)}
         IPrim: {op:v, args:(e ...)}
         ILiteral: custom{ensure-literal(e)}
         ITuple: {exps:(e ...)}
         + func
   
      e?("optional expression") :
         INone: ()
         + e
   
      func("function expression") :
         IFn :
            {a1:(t? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
   
      farg("function argument") :
         IOf: {class:v args:(t ...)}
         + e

      br("branch expression") :
         IBranch :
            {types:(t? ...) args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), types(e), args(e))}
   
      m("method expression") :
         IDefmethod :
            {multi:v targs:(targ ...) a1:(ct? ...) a2:t? args:(b ...) body:se}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-this-param(e)}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
   
      b("binder expression") :
         IVar: ()
         ITuple: {exps:(b ...)}
   
      pt("parent type") :
         IOf: {class:v args:(t ...)}
         IAnd: {a:pt b:pt}
         IVar: ()
         
      pt?("optional parent type") :
         INone: ()
         + pt
   
      ct("type") :
         IOf: {class:v args:(ct ...)}
         IAnd: {a:ct b:ct}
         IOr: {a:ct b:ct}
         IArrow: {a1:(t ...) a2:ct}
         ICap: {name:v}
         IGradual: ()
         IVoid: ()
         IVar: ()
         ITuple: {exps:(ct ...)}
   
      ct?("optional type") :
         INone: ()
         + ct
   
      t("type") :
         + ct
         custom{ensure-no-cap(e)}
      t?("optional type") :
         INone: ()
         + t
   
      v("symbol") :
         IVar : ()

      targ("type argument") :
         ICap: {name:v}
         IVar: ()      
         
      mut?("mutability field") :
         ILiteral : custom{ensure-boolean(e)}

      ldf("lostanza deffield") :
         ILSDefField: {mut:mut? name:v type:lt}

      ldf?("optional lostanza deffield") :
         INone: ()
         + ldf

      ;======== LoStanza Language ========
      ltc("LoStanza top level command") :
         + ltc*
         custom{ensure-no-return(e)}
      ltc*("LoStanza top level command") :
         IBegin: {exps:(ltc* ...)}          
         ILSDef: {name:v type:lt exp:le}
         ILSDefVar: {name:v type:lt exp:le?}
         ILSDefField : {mut:mut? type:lt name:v}
         ILSDefType :
            {name:v args:(v ...) parent:t? fields:(ldf ...) rfield:ldf?}
         ILSExtern: {name:v type:lt}
         ILSDefn :
            {name:v targs:(targ ...) a1:(lct ...) a2:lt args:(v ...) body:lc}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-return(body(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
         ILSExternFn :
            {name:v a1:(lt ...) a2:lt args:(v ...) body:lc}
            custom{ensure-return(body(e))}
         ILSDefmethod :
            {multi:v targs:(targ ...) a1:(lct ...) a2:lt args:(v ...) body:lc}
            custom{ensure-matched-a1-args(info(e), a1(e), args(e))}
            custom{ensure-ordered-targs(targs(e))}
            custom{ensure-return(body(e))}
            custom{ensure-capturing-position(targs(e), a1(e))}
            
         ILSSet: {exp:lval value:le}
         ILSLet: {comm:lc}
         ILSLabels:
            {blocks:(blk ...)}
            custom{ensure-entry-block(info(e), blocks(e))}
         ILSIf: {pred:le conseq:lc alt:lc}
         ILSMatch: {args:(le le ...) branches:(lbr lbr ...)}
         + lcalls

      rf("LoStanza rest field") :
         INone: ()
         IVar: ()

      blk("LoStanza labeled block") :
         ILSLabeledBlock:
            {name:v args:(v ...) types:(lt ...) body:lc}
            custom{ensure-matched-a1-args(info(e), types(e), args(e))}

      lc("LoStanza command") :   
         IBegin: {exps:(lc ...)}
         ILSDef: {name:v type:lt? exp:le}
         ILSDefVar: {name:v type:lt exp:le?}
         ILSSet: {exp:lval value:le}
         ILSLet: {comm:lc}
         ILSIf: {pred:le conseq:lc alt:lc}
         ILSMatch: {args:(le le ...) branches:(lbr lbr ...)}
         ILSLabels:
            {blocks:(blk ...)}
            custom{ensure-entry-block(info(e), blocks(e))}
         ILSGoto: {name:v, args:(le ...)}
         ILSReturn: {exp:le}
         + lcalls

      lbr("LoStanza branch") :
         ILSBranch:
            {types:(lbt? ...) args:(v ...) body:lc}
            custom{ensure-matched-a1-args(info(e), types(e), args(e))}
   
      le("LoStanza expression") :
         ILSNew: {type:lclass args:(le ...)}
         ILSStruct: {type:lclass args:(le ...)}
         ILSAddr: {exp:lval}
         ILSDeref: {exp:le}
         ILSSlot: {exp:le index:le}
         ILSField: {exp:le name:v}
         ILSSizeof: {type:lt}
         ILSTagof: {name:v}
         ILSAs: {exp:le type:lt}
         ILSAnd: {a:le, b:le}
         ILSOr: {a:le, b:le}
         ILSLetExp: {comm:lc, exp:le}
         ILSFn: {ref:v}
         IVar: ()
         ILiteral: custom{ensure-ls-literal(e)}
         + lcalls
      lcalls("LoStanza call") :
         ILSDo: {func:lfarg args:(le ...)}
         ILSPrim: {op:v args:(le ...)}
         ILSCallC: {func:le args:(le ...)}

      lfarg("LoStanza function argument") :
         ILSOfT: {class:v args:(t ...)}
         + le
   
      le?("optional LoStanza expression") :
         INone: ()
         + le
   
      lval("LoStanza lvalue expression") :
         IVar: ()
         ILSDeref: {exp:le}
         ILSSlot: {exp:le index:le}
         ILSField: {exp:le name:v}

      lclass("LoStanza object type") :
         ILSOfT: {class:v, args:(t ...)}
         IVar: ()

      lct("LoStanza argument type") :
         ILSByteT: ()
         ILSIntT: ()
         ILSLongT: ()
         ILSFloatT: ()
         ILSDoubleT: ()
         ILSUnknownT: ()
         ILSOfT: {class:v args:(ct ...)}
         ILSPtrT: {type:lct}
         ILSRefT: {type:ct}
         ILSFnT: {a1:(lt ...) ar:lt? a2:lct}
         IVar: ()
      lt("LoStanza type") :
         + lct
         custom{ensure-no-cap(e)}
      lt?("optional LoStanza type") :
         INone: ()
         + lt
      lbt?("LoStanza branch type") :
         ILSRefT: {type:t}
         INone: ()

defn var? (e:IExp, s:Symbol) :
   match(e) :
      (e:IVar) : name(e) == s
      (e) : false

;                   Error Checkers
;                   ==============

defn ensure-no-cap (e:IExp) :
   match(e) :
      (e:ICap) : lang/check/error!(info(e), "Capture variable ?%_ cannot be used in this context." % [name(e)])
      (e) : do(ensure-no-cap, e)   

defn ensure-wellformed-children (e:IDefType) :
   if parent(e) is INone :
      for c in children(e) do :
         match(c) :
            (c:IDefChild) :
               val p = match(parent(c)) :
                  (p:IVar) : p
                  (p:IOf) : class(p)
                  (p) : false
               match(p, class(e)) :
                  (p:IVar, c:IVar) :
                     if name(p) != name(c) :
                        lang/check/error!(info(e), "Child declaration for type %_ cannot have %_ as specified parent." % [
                           class(e), p])
                  (p, c) : false
            (c) : false         
   else if not empty?(children(e)) :
      lang/check/error!(info(e), "Type with parent cannot have child type declarations.")

defn ensure-ordered-targs (es:List<IExp>) :
   defn loop* (es:List<IExp>) :
      if not empty?(es) :
         match(head(es)) :
            (e:ICap) :
               if not all?({_ is ICap}, es) :
                  lang/check/error!(info(e), "Capture variables must appear after all explicit type arguments.")
            (e) : loop*(tail(es))
   loop*(es)

defn ensure-capturing-position (targs:List<IExp>, a1:List<IExp>) :
   defn captures? (x:Symbol, e:IExp) :
      match(e) :
         (e:ICap) : var?(name(e), x)
         (e) : any?(captures?{x, _}, subexps(e))
   defn ensure-captured? (info:False|FileInfo, x:Symbol) :
      val captured? = any?(captures?{x, _}, a1)
      if not captured? :
         lang/check/error!(info, "Captured type argument %_ has no capturing position." % [x])
         
   val cargs = filter({_ is ICap}, targs) as Seqable<ICap>   
   for a in cargs do :
      val v = name(name(a) as IVar)
      ensure-captured?(info(a), v)

defn ensure-matched-a1-args (info:False|FileInfo, a1:List<IExp>, args:List<IExp>) :
   if length(a1) != length(args) :
      lang/check/error!(info, "The number of argument types do not match the number of arguments.")

defn ensure-no-this-param (e:IDefmethod) :
   if any?(var?{_, `this}, args(e)) :
      lang/check/error!(info(e), "Top level method cannot have a this argument.")

defn ensure-this-param (e:IDefmethod) :
   val n = count(var?{_, `this}, args(e))
   if n == 0 :
      lang/check/error!(info(e), "Instance method requires a this argument.")
   else if n > 1 :
      lang/check/error!(info(e), "Instance method cannot have multiple this arguments.")

defn pure-expression? (e:IExp) :
   e is IFn|IMulti|INew|IVar|ILiteral|ITuple

defn ensure-impure (e:IExp) :
   if pure-expression?(e) :
      lang/check/error!(info(e), "Expression %~ has no effect and is invalid in this context." % [e])
         
defn ensure-body (e:IExp) :
   val es = match(e) :
      (e:IBegin) : exps(e)
      (e) : List(e)
   if empty?(es) :
      lang/check/error!(info(e), "No expressions in body.")
   else :
      do(ensure-impure, but-last(es))
      match(last(es)) :
         (e:IDef|IDefVar|IDefn) :
            lang/check/error!(info(e), "Declaration cannot be last expression in body.")
         (e) : false

defn ensure-branch-arity (e:IMatch) :
   val bs = branches(e) as List<IBranch>
   val n = length(args(e))
   for b in bs do :
      if length(args(b)) != n :
         lang/check/error!(info(b), "The number of branch arguments does not match number of arguments given to match.")

defn ensure-literal (e:ILiteral) :
   defn lit? (v) :
      match(v) :
         (v:Byte|Char|Int|Long|Float|Double|String|Symbol|True|False) : true
         (v:List) : all?(lit?, v)
         (v) : false
   if not lit?(value(e)) :
      lang/check/error!(info(e), "Value %~ is not a valid Stanza literal." % [value(e)])

defn ensure-no-return (body:IExp) :
   match(body) :
      (body:ILSDefn|ILSDefmethod|ILSExternFn) :
         false
      (body:ILSReturn) :
         lang/check/error!(info(body), "Invalid return statement in top level context.")
      (body) :
         do(ensure-no-return, body)

defn ensure-return (exp:IExp) :
   ;Apply function for every body in e
   defn do-last (f: IExp -> ?, e:IExp) :
      match(e) :
         (e:IBegin) :
            if empty?(exps(e)) : f(e)
            else : do-last(f, last(exps(e)))
         (e:ILSLet) :
            do-last(f, comm(e))
         (e:ILSLabels) :
            val bs = filter({_ is ILSLabeledBlock}, blocks(e))
            for b in bs as Seq<ILSLabeledBlock> do :
               do-last(f, body(b))
         (e:ILSIf) :
            do-last(f, conseq(e))
            do-last(f, alt(e))
         (e:ILSMatch) :
            val bs = filter({_ is ILSBranch}, branches(e))
            for b in bs as Seq<ILSBranch> do :
               do-last(f, body(b))
         (e) : f(e)

   ;Check last statement
   for e in exp do-last :
      match(e) :
         (e:ILSReturn|ILSGoto) : false
         (e) : lang/check/error!(info(e), "Return statement expected here for end of function body.")

defn ensure-entry-block (info:False|FileInfo, bs:List<IExp>) :
   if not empty?(bs) :
      match(head(bs)) :
         (b:ILSLabeledBlock) :
            if not empty?(args(b)) :
               lang/check/error!(info, "First block in labeled scope cannot take arguments.")
         (b) : false

defn ensure-ls-literal (e:ILiteral) :
   defn lit? (v) :
      v is Byte|Char|Int|Long|Float|Double|String|True|False
   if not lit?(value(e)) :
      lang/check/error!(info(e), "Value %~ is not a valid LoStanza literal." % [value(e)])

defn ensure-boolean (e:ILiteral) :
   if value(e) is-not True|False :
      lang/check/error!(info(e), "Value %~ is not a boolean." % [value(e)])
