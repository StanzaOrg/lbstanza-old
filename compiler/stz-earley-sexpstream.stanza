defpackage stz/earley-sexp-stream :
  import core
  import collections
  
;============================================================
;=================== SExpStream Interface ===================
;============================================================

;+[SExpStream Definition]
public deftype SExpStream
public defmulti peek (s:SExpStream) -> SExpToken
public defmulti advance (s:SExpStream, expand-list?:True|False) -> False
public defmulti advance-rest (s:SExpStream) -> False
public defmulti insert-wildcard (s:SExpStream) -> False
public defmulti insert-list (s:SExpStream) -> False
public defmulti info (s:SExpStream) -> FileInfo|False
public defmulti inputlist (s:SExpStream) -> Vector<SExpToken>
public defmulti infolist (s:SExpStream) -> Vector<FileInfo|False>

;============================================================
;====================== SExpTokens ==========================
;============================================================

;+[SExpToken Definition]
public deftype SExpToken
public defstruct SExpForm <: SExpToken :
  form
  list:List
with:
  printer => true
public defstruct SExpWildcard <: SExpToken
public defstruct SExpListEnd <: SExpToken
public defstruct EndOfInput <: SExpToken

public defn list (t:SExpListEnd) :
  List()

;============================================================
;====================== Implementation ======================
;============================================================

public defn SExpStream (input:List) :
  val stack = Vector<List>()
  var current:List = List(input)
  var info:FileInfo|False = false
  val inputlist = Vector<SExpToken>()
  val infolist = Vector<FileInfo|False>()

  defn update-info () :
    if not empty?(current) :
      val t = head(current)
      match(t:Token) :
        info = /info(t)

  defn peek-stream () :
    if empty?(current) :
      if empty?(stack) : EndOfInput()
      else : SExpListEnd()
    else :
      match(head(current)) :
        (t:SExpWildcard) : t
        (t) : SExpForm(t, current)

  defn save-peek () :
    add(inputlist, peek-stream())
    add(infolist, info)

  defn ensure-not-empty! () :
    fatal("No more tokens.") when empty?(current) and empty?(stack)

  defn advance-stream (expand-list?:True|False) :
    ensure-not-empty!()
    if empty?(current) :
      current = pop(stack)
    else :
      val expand? = expand-list? and
                    unwrap-token(head(current)) is List
      if expand? :
        add(stack, tail(current))
        current = unwrap-token(head(current)) as List
      else :
        current = tail(current)
    update-info()

  update-info()
  new SExpStream :
    defmethod info (this) :
      info
    defmethod peek (this) :    
      peek-stream()
    defmethod advance (this, expand-list?:True|False) :
      save-peek()
      advance-stream(expand-list?)
    defmethod advance-rest (this) :
      ensure-not-empty!()
      save-peek()
      current = List()
    defmethod insert-wildcard (this) :
      ensure-not-empty!()
      current = cons(SExpWildcard(), current)
    defmethod insert-list (this) :
      ensure-not-empty!()
      current = cons(List(), current)
    defmethod inputlist (this) :
      inputlist
    defmethod infolist (this) :
      infolist
;/[SExpStream Definition]