defpackage stz/reg-alloc-ports :
  import core
  import collections
  import stz/printing-utils
  import stz/reg-alloc-ir

;- usage-position: The first position at which this
;  variable is used. 0 indicates the first instruction
;  of the current block.
;- pref-reg: Based upon the code downstream, the preferred
;  register that the port value should be stored in.
public defstruct Port :
  id:Int
  usage-position:Int
  prefs:PortPref|False with:
    default => false
    updater => sub-prefs
  status: PortStatus|False with:
    default => false
    updater => sub-status

;Indicates the actual save/load/reg status of the port
;based on calculations from code upstream.
public defstruct PortStatus :
  saved?:True|False
  loaded:Location|False with: (updater => sub-loaded)

;Indicates the save, load, and register preferences of a port.
public defstruct PortPref <: Hashable&Equalable :
  save-pref: SavePref
  load-pref: LoadPref
  reg-pref: Register|False with: (updater => sub-reg-pref)
with:
  hashable => true
  equalable => true

public defenum SavePref :
  ;The variable is preferred to be eagerly saved. The
  ;variable is live across a clear-registers operation.
  ;The earlier you save it, the less register pressure there is.
  PreferSave

  ;The variable is not preferred to be saved.
  ;The variable is killed so there's no point
  ;in saving it.
  NoPreferSave

  ;The variable is preferred to be saved, if the
  ;block successors prefer for it to be saved.
  ;(Used only during port preference algorithm.)
  PreferSaveIfSuccessor

public defenum LoadPref :
  ;The variable is preferred to be eagerly unloaded.
  ;The variable crosses a clear-registers operation
  ;before it is ever used.
  PreferUnload

  ;The variable is not preferred to be unloaded.
  ;It is used as in an operation so it is unloaded, it
  ;will need to be loaded again before the operation.
  NoPreferUnload 

  ;The variable is preferred to be unloaded if the
  ;block successors prefer for it to be unloaded.
  ;(Used only during port preference algorithm.)
  PreferUnloadIfSuccessor

;============================================================
;=================== Printers ===============================
;============================================================

defmethod print (o:OutputStream, p:Port) :
  val items = [
    simple-field("usage-position", usage-position(p))
    falseable-field(prefs(p))
    falseable-field("status", status(p))]
  print(o, "port V%_ (%_)" % [id(p), comma-field-list(items)])

defmethod print (o:OutputStream, p:PortPref) :
  val items = [
    save-pref(p)
    load-pref(p)
    falseable-field("reg-pref", reg-pref(p))]
  print(o, "PortPref(%_)" % [comma-field-list(items)])

defmethod print (o:OutputStream, s:PortStatus) :
  val items = [
    bool-flag-field("saved", saved?(s))
    falseable-field("loaded", loaded(s))]
  print(o, "PortStatus(%_)" % [comma-field-list(items)])

;============================================================
;================== Convenient Accessors ====================
;============================================================

;Assume preference has been calculated.
;Return the preferred register.
public defn reg-pref (p:Port) -> Register|False :
  reg-pref(prefs(p) as PortPref)

;Substitute the new preferred register into the port.
public defn sub-reg-pref (p:Port, r:Register|False) -> Port :
  val prefs = prefs(p) as PortPref
  val new-prefs = sub-reg-pref(prefs, r)
  sub-prefs(p, new-prefs)

;Returns true if the given port is loaded into a register.
public defn loaded? (p:Port) -> True|False :
  val s = status(p) as PortStatus
  loaded(s) is Location

;Returns the location that a port was loaded to.
public defn location (p:Port) -> Location :
  val s = status(p) as PortStatus
  loaded(s) as Location

;Substitute the location for a port.
public defn sub-location (p:Port, l:Location|False) -> Port  :
  val new-status = sub-loaded(status(p) as PortStatus, l)
  sub-status(p, new-status)

;Returns true if the given port is saved.
public defn saved? (p:Port) -> True|False :
  val s = status(p) as PortStatus
  saved?(s)