defpackage stz/absolute-info :
  import core

;Represents an absolute location in a file.
public defstruct AbsoluteFileInfo <: Hashable & Equalable :
  base:String with: (updater => sub-base)
  filename:String
  line:Int
  column:Int
with:
  hashable => true
  equalable => true
  printer => true

;Create an AbsoluteFileInfo from a FileInfo and a base directory.
public defn AbsoluteFileInfo (info:FileInfo, base:String) -> AbsoluteFileInfo :
  AbsoluteFileInfo(base, filename(info), line(info), column(info))

;Convenience: Adding a base directory if FileInfo is unknown results
;in an AbsoluteFileInfo that is still unknown.
public defn AbsoluteFileInfo (info:False, base:String) -> False :
  false

;Return the FileInfo contained in the AbsoluteFileInfo.
public defn info (info:AbsoluteFileInfo) -> FileInfo :
  FileInfo(filename(info), line(info), column(info))

;Return the FileInfo if not false.
public defn info? (abs-info:AbsoluteFileInfo|False) -> FileInfo|False :
  match(abs-info:AbsoluteFileInfo) : info(abs-info)