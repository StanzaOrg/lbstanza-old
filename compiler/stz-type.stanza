defpackage stz/type :
   import core
   import verse
   import stz/il-ir
   import stz/tl-ir
   import stz/namemap
   import stz/infer
   import stz/type-calculus
   import stz/algorithms
   import stz/utils
   import stz/ids

#use-overlay(stz/attempt-lang)

;============================================================
;====================== Driver ==============================
;============================================================

public defn type-program (nm:NameMap, e:IExp) :
   val prog = to-tprog(nm, e)
   set-current-hierarchy(hier(prog))
   println("Infer types")
   val prog* = infer-types(prog)
   println(prog*)
   println("Report errors")
   report-errors(nm, prog*)
   prog*

;============================================================
;=============== Conversion to Type IR ======================
;============================================================

definterface Assert
defstruct CorrectTArity <: Assert: (info:FileInfo, n:Int, targs:List<Type>)

defn to-tprog (nm:NameMap, e:IExp) :
   ;     State
   ;     =====
   val accum = Vector<TComm>()
   val asserts = Vector<Assert>()
   val errors = Vector<?>()
   val hier = THierarchy()

   ;     Utilities
   ;     =========
   val #TN = TNone()
   val #LTN = NoneT()
   val #CN = CNone()
   val #SN = SNone()
   val #IN = INone(FileInfo())
   defn #name (n:Int) : name(nm[n] as VarEntry)
   defn #info (n:Int) : info(nm[n] as VarEntry)
   defn #pr (t:Type) : printable(nm, t)
   defn #pr (ts:Streamable<Type>) : stream(#pr, ts)
   defn cms (xs:Streamable) : join(xs, ", ")
   defn add-comm (c:TComm) : add(accum, c)
   defn add-comm (cs:List<TComm>) : do(add-comm, cs)
   defn name! (e:IExp) : name(e as IVar)
   defn n! (e:IExp) : n(e as VarN)
   defn n! (es:List<IExp>) : map(n!, es)
   defn #comm (es:List<IExp>) : do(#comm, es)
   defn #type (es:List<IExp>) : map(#type, es)
   defn #lstype (es:List<IExp>) : map(#lstype, es)
   defn #exp (es:List<IExp>) : map(#exp, es)
   defn #lsexp (es:List<IExp>) : map(#lsexp, es)
   defn #lscomm (es:List<IExp>) : map(#lscomm, es)
   defn #defn (defs:List<IDefn>) : map(#defn, defs)
   defn #defmethod (defs:List<IDefmethod>) : map(#defmethod, defs)

   defn split-targs (es:List<IExp>) :
      val targs = Vector<Int>()
      val cargs = Vector<Int>()
      for e in es do :
         match(e) :
            (e:VarN) : add(targs, n(e))
            (e:ICap) : add(cargs, n!(name(e)))
      [to-list(targs), to-list(cargs)]

   ;     Transformers
   ;     ============
   defn #comm (e:IExp) -> False :
      match(e) :
         (e:IBegin) :
            #comm(exps(e))
         (e:IPublic) :
            #comm(exp(e))
         (e:IProtected) :
            #comm(exp(e))
         (e:IDefType) :
            add(hier, HSHEntry(c, args*, p)) where :
               val c = n!(class(e))
               val args* = n!(args(e))
               val p = #type(parent(e))
         (e:IDef) :
            add-comm(destructure-def(e))
         (e:IDefVar) :
            add-comm(TDefVar(n, t, v)) where :
               val n = n!(name(e))
               val t = #type(type(e))
               val v = #exp(value(e)) when value(e) not-typeof INone
         (e:IDefn) :
            add-comm(#defn(e))
         (e:IDefmulti) :
            add-comm(TDefmulti(n, targs, cargs, a1*, a2*)) where :
               val n = n!(name(e))
               val [targs, cargs] = split-targs(targs(e))
               val a1* = #type(a1(e))
               val a2* = #type(a2(e))
         (e:IDefmethod) :
            add-comm(#defmethod(e))
         (e:IFn|IMulti|IBegin|IMatch|IBranch|INew|
            VarN|ICast|ISet|IDo|IPrim|ILiteral|ITuple)  :
            add-comm(TInit(#exp(e)))
         (e:ILSDef) :
            add-comm(TLDef(n!(name(e)), #lstype(type(e)), #lsexp(exp(e))))
         (e:ILSDefVar) :
            val v = #lsexp(exp(e)) when exp(e) not-typeof INone
            add-comm(TLDefVar(n!(name(e)), #lstype(type(e)), v))
         (e:ILSDefType) :
            add(hier, LSHEntry(c, args*, p, fs, rf)) where :
               val c = n!(name(e))
               val args* = n!(args(e))
               val p = #type(parent(e))
               val fs = map(#field, fields(e), types(e))
               val rf = #field?(rfield(e), rtype(e))
         (e:ILSExtern) :
            add-comm(TExtern(n!(name(e)), #lstype(type(e))))
         (e:ILSDefn) :
            val [targs, cargs] = split-targs(targs(e))
            val a1* = #lstype(a1(e))
            val a2* = #lstype(a2(e))
            val body* = #lscomm(body(e))
            add-comm(TLDefn(tail?(e), n!(name(e)), targs, cargs, a1*, a2*, n!(args(e)), body*))
         (e:ILSSet|ILSLabels|ILSLabel|ILSGoto|ILSReturn|ILSLet|
            ILSIf|ILSMatch|ILSBranch|ILSDef|ILSDefVar) :
            add-comm(TLInit(#lscomm(e)))

   defn #field (name:IExp, type:IExp) :
      val n = n!(name)
      Field(n!(name), #name(n), #lstype(type))
   defn #field? (name:IExp, type:IExp) :
      #field(name, type) when name not-typeof INone

   defn #type (e:IExp) -> Type :
      match(e) :
         (e:ITuple) : TTuple(#type(exps(e)))
         (e:IOf) :
            val n = n!(class(e))
            val targs = #type(args(e))
            add(asserts, CorrectTArity(info(e), n, targs))
            TOf(n, TTuple(targs))
         (e:IAnd) : TAnd(#type(a(e)), #type(b(e)))
         (e:IOr) : TOr(#type(a(e)), #type(b(e)))
         (e:IArrow) : TArrow(TTuple(#type(a1(e))), #type(a2(e)))
         (e:ICap) : TCap(n!(name(e)))
         (e:IGradual) : TGradual()
         (e:IVoid) : TBot()
         (e:INone) : TNone()
         (e:VarN) : TVar(n(e))
         (e:Raw) : TOf(n!(class(e)), TGradual())

   defn #exp (e:IExp) -> TExp :
      match(e) :
         (e:IFn) :
            TFn(tail?(e), a1*, a2*, ns, body, #TN, #TN, #CN) where :
               val a1* = #type(a1(e))
               val a2* = #type(a2(e))
               val [ns, body] = destructure-args(args(e), body(e))
         (e:IMulti) :
            TMulti(fs, #TN, #TN, #CN) where :
               val fs = #exp(funcs(e)) as List<TFn>
         (e:IBegin) :
            if empty?(tail(exps(e))) :
               #exp(head(exps(e)))
            else :
               TSeq(a*, b*, #TN, #TN, #CN) where :
                  val a* = #exp(head(exps(e)))
                  val binfo = info(exps(e)[1])
                  val b* = #exp(IBegin(tail(exps(e)), binfo))
         (e:IMatch) :
            TMatch(args*, brs*, #TN, #TN, #CN) where :
               val args* = #exp(args(e))
               val brs* = #exp(branches(e)) as List<TBranch>
         (e:IBranch) :
            TBranch(ns, ts, body, #TN, #TN, #CN) where :
               val ts = #type(types(e))
               val [ns, body] = destructure-args(args(e), body(e))
         (e:INew) :
            TNew(c, ms, #TN, #TN, #CN) where :
               val c = #type(class(e))
               val ms = #defmethod(methods(e) as List<IDefmethod>)
         (e:ISet) :
            TSet(r, v, #TN, #TN, #CN) where :
               val r = #exp(name(e)) as TRef
               val v = #exp(value(e))
         (e:IDo) :
            TDo(f*, args*, #TN, #TN, #CN) where :
               val f* = #farg(func(e))
               val args* = #exp(args(e))
         (e:IPrim) :
            TPrim(n, args*, #TN, #TN, #CN) where :
               val n = name!(function(e))
               val args* = #exp(args(e))
         (e:ILiteral) :
            TLiteral(value(e), #TN, #TN, #CN)
         (e:ITuple) :
            TTupleExp(#exp(exps(e)), #TN, #TN, #CN)
         (e:VarN) :
            TRef(n(e), #TN, #TN, #CN)
         (e:Mix) :
            TMix(#SN, #exp(exps(e)), #TN, #TN, #CN)
         (e:Let) :
            destructure-let(e)
         (e:LetRec) :
            TLetRec(defns*, body*, #TN, #TN, #CN) where :
               val defns* = #defn(defns(e) as List<IDefn>)
               val body* = #exp(body(e))
         (e) :
            error("Unsupported expression: ~" % [e])

   defn #farg (e:IExp) -> TExp :
      match(e) :
         (e:IOf) :
            if empty?(args(e)) :
               #exp(class(e))
            else :
               val ts = #type(args(e))
               defn #mix (e:IExp) :
                  match(e) :
                     (e:VarN) :
                        val ref = TRef(n(e), #TN, #TN, #CN)
                        TCurry(ref, ts, List(), #TN, #TN, #CN)
                     (e:Mix) :
                        TMix(#SN, map(#mix, exps(e)), #TN, #TN, #CN)
               #mix(class(e))
         (e) :
            #exp(e)

   defn #defn (def:IDefn) :
      TDefn(tail?(def), n, targs, cargs, a1*, a2*, args*, body*) where :
         val n = n!(name(def))
         val [targs, cargs] = split-targs(targs(def))
         val a1* = #type(a1(def))
         val a2* = #type(a2(def))
         val [args*, body*] = destructure-args(args(def), body(def))

   defn #defmethod (def:IDefmethod) :
      TDefmethod(tail?(def), n, m, thisn(def), targs, cargs, a1*, a2*, args*, body*) where :
         val n = fresh-id()
         val m = #exp(multi(def))
         val a1* = #type(a1(def))
         val a2* = #type(a2(def))
         val [targs, cargs] = split-targs(targs(def))
         val [args*, body*] = destructure-args(args(def), body(def))

   defn #lsexp (e:IExp) -> LSExp :
      match(e) :
         (e:ILSNew) :
            val [n, targ] = #lsclass(type(e))
            LSNew(n, targ, #lsexp(args(e)), #LTN, #LTN, #CN)
         (e:ILSArray) :
            val [n, targ] = #lsclass(type(e))
            LSArray(n, targ, #lsexp(args(e)), #lsexp(length(e)), #LTN, #LTN, #CN)
         (e:ILSStruct) :
            val [n, targ] = #lsclass(type(e))
            LSStruct(n, targ, #lsexp(args(e)), #LTN, #LTN, #CN)
         (e:ILSAddr) :
            LSAddr(#lsexp(exp(e)), #LTN, #LTN, #CN)
         (e:ILSDeref) :
            LSDeref(#lsexp(exp(e)), #LTN, #LTN, #CN)
         (e:ILSSlot) :
            LSSlot(#lsexp(exp(e)), #lsexp(index(e)), #LTN, #LTN, #CN)
         (e:ILSField) :
            LSField(#lsexp(exp(e)), name!(name(e)), #LTN, #LTN, #CN)
         (e:ILSDo) :
            LSDo(f*, args*, #LTN, #LTN, #CN) where :
               val f* = #lsfarg(func(e))
               val args* = #lsexp(args(e))
         (e:ILSCallC) :
            LSCallC(#lsexp(func(e)), #lsexp(args(e)), #LTN, #LTN, #CN)
         (e:ILSPrim) :
            val [op, targs] = #lprim(op(e))
            LSPrim(op, targs, #lsexp(args(e)), #LTN, #LTN, #CN)
         (e:ILSSizeof) :
            LSSizeof(#lstype(type(e)), #LTN, #LTN, #CN)
         (e:ILSTagof) :
            LSTagof(n!(name(e)), #LTN, #LTN, #CN)
         (e:ILSAs) :
            LSCast(#lsexp(exp(e)), #lstype(type(e)), #LTN, #LTN, #CN)
         (e:ILSAnd) :
            LSAnd(#lsexp(a(e)), #lsexp(b(e)), #LTN, #LTN, #CN)
         (e:ILSOr) :
            LSOr(#lsexp(a(e)), #lsexp(b(e)), #LTN, #LTN, #CN)
         (e:ILSLetExp) :
            LSLetExp(#lscomm(comm(e)), #lsexp(exp(e)), #LTN, #LTN, #CN)
         (e:ILSFn) :
            LSFn(#exp(ref(e)), #LTN, #LTN, #CN)
         (e:VarN) :
            LSVar(n!(e), #LTN, #LTN, #CN)
         (e:ILiteral) :
            LSLiteral(value(e), #LTN, #LTN, #CN)

   defn #lscomm (e:IExp) -> LSComm :
      match(e) :
         (e:IBegin) :
            val a = #lscomm(head(exps(e)))
            val rest = tail(exps(e))
            if empty?(rest) : a
            else : LSSeq(a, #lscomm(IBegin(rest, FileInfo())))
         (e:ILSSet) :
            LSSet(#lsexp(exp(e)), #lsexp(value(e)))
         (e:ILSLabels) :
            LSLabels(n!(names(e)), #lscomm(body(e)))
         (e:ILSLabel) :
            LSLabel(n!(name(e)))
         (e:ILSGoto) :
            LSGoto(n!(name(e)))
         (e:ILSReturn) :
            LSReturn(#lsexp(exp(e)))
         (e:ILSLet) :
            #lscomm(comm(e))
         (e:ILSIf) :
            LSIf(#lsexp(pred(e)), #lscomm(conseq(e)), #lscomm(alt(e)))
         (e:ILSMatch) :
            val args* = #lsexp(args(e))
            val bs = #lscomm(branches(e)) as List<LSBranch>
            LSMatch(args*, bs)
         (e:ILSBranch) :
            LSBranch(n!(args(e)), #lstype(types(e)), #lscomm(body(e)))
         (e:ILSDef) :
            LSDef(n!(name(e)), #lstype(type(e)), #lsexp(exp(e)))
         (e:ILSDefVar) :
            val v = #lsexp(exp(e)) when exp(e) not-typeof INone
            LSDefVar(n!(name(e)), #lstype(type(e)), v)

   defn #lsclass (e:IExp) :
      match(e) :
         (e:ILSOfT) : [n!(class(e)), TTuple(#type(args(e)))]
         (e:VarN) : [n!(e), TGradual()]

   defn #lprim (e:IExp) :
      match(e) :
         (e:ILSOfT) : [name!(class(e)), #type(args(e))]
         (e:IVar) : [name!(e), List()]

   defn #lsfarg (e:IExp) :
      match(e) :
         (e:ILSOfT) :
            if empty?(args(e)) :
               #lsexp(class(e))
            else :
               val ts = #type(args(e))
               defn #mix (e:IExp) :
                  match(e) :
                     (e:VarN) :
                        val ref = LSVar(n(e), #LTN, #LTN, #CN)
                        LSCurry(ref, ts, List(), #LTN, #LTN, #CN)
                     (e:Mix) :
                        LSMix(#SN, map(#mix, exps(e)), #LTN, #LTN, #CN)
               #mix(class(e))
         (e) :
            #lsexp(e)

   defn #lstype (e:IExp) :
      match(e) :
         (e:ILSByteT) : ByteT()
         (e:ILSIntT) : IntT()
         (e:ILSLongT) : LongT()
         (e:ILSFloatT) : FloatT()
         (e:ILSDoubleT) : DoubleT()
         (e:ILSUnknownT) : UnknownT()
         (e:ILSOfT) :
            val n = n!(class(e))
            val targs = #type(args(e))
            add(asserts, CorrectTArity(info(e), n, targs))
            StructT(n, TTuple(targs))
         (e:ILSPtrT) : PtrT(#lstype(type(e)))
         (e:ILSRefT) : RefT(#type(type(e)))
         (e:ILSFnT) : FnT(#lstype(a1(e)), #lstype(ar(e)), #lstype(a2(e)))
         (e:Raw) : StructT(n!(class(e)), TGradual())
         (e:INone) : NoneT()
         (e) : error("Not a LoStanza type: ~" % [e])

   ;     Nested Tuple Destructuring
   ;     ==========================
   defn destructure (args:List<IExp>, value:TExp) -> List<KeyValue<List<Int>, TExp>> :
      val [ns, bs] = destructure(args)
      List(ns => value, bs)

   defn destructure (args:List<IExp>) -> [List<Int>, List<KeyValue<List<Int>, TExp>>] :
      var bindings = List()
      defn #n (x:IExp) -> Int :
         match(x) :
            (x:ITuple) :
               val n = fresh-id()
               bindings = List(entry, bindings) where :
                  val exps* = map(#n, exps(x))
                  val entry = exps* => TRef(n, #TN, #TN, #CN)
               n
            (x) :
               n!(x)
      val ns = map(#n, args)
      [ns, bindings]

   defn destructure-args (args:List<IExp>, fbody:IExp) -> [List<Int>, TExp] :
      val [ns, bs] = destructure(args)
      var body = #exp(fbody)
      for b in bs do :
         body = TLetTuple(ns, ts, v, body, #TN, #TN, #CN) where :
            val [ns, v] = [key(b), value(b)]
            val ts = map({#TN}, ns)
      [ns, body]

   defn destructure-def (def:IDef) -> List<TDef|TDefTuple> :
      match(name(def), type(def), value(def)) :
         (name:ITuple, type:INone, v) :
            val bs = destructure(exps(name), #exp(v))
            for b in bs map :
               TDefTuple(ns, ts, v) where :
                  val [ns, v] = [key(b), value(b)]
                  val ts = map({#TN}, ns)
         (name:ITuple, type, v) :
            val tmp = VarN(fresh-id(), info(name))
            append(
               destructure-def(IDef(tmp, type, v, info(name)))
               destructure-def(IDef(name, #IN, tmp, info(name))))
         (name, type, v) :
            list(TDef(n, t*, v*)) where :
               val n = n!(name)
               val t* = #type(type)
               val v* = #exp(v)

   defn destructure-let (lete:Let) -> TExp :
      match(def(lete)) :
         (def:IDefVar) :
            val n = n!(name(def))
            val t = #type(type(def))
            val e = #exp(value(def))
            val b = #exp(body(lete))
            TLetVar(n, t, e, b, #TN, #TN, #CN)
         (def:IDef) :
            defn loop (defs:List<TDef|TDefTuple>) :
               if empty?(defs) :
                  #exp(body(lete))
               else :
                  val body = loop(tail(defs))
                  match(head(defs)) :
                     (e:TDef) : TLet(n(e), type(e), value(e), body, #TN, #TN, #CN)
                     (e:TDefTuple) : TLetTuple(ns(e), types(e), value(e), body, #TN, #TN, #CN)
            loop(destructure-def(def))

   ;     Error Reporting
   ;     ===============
   ;Check hierarchical cycles
   defn check-cycles () :
      defn classes (t:Type) -> List<Int> :
         match(t) :
            (t:TOf) : list(n(t))
            (t:TAnd) : append(classes(a(t)), classes(b(t)))
            (t:TNone) : list()
      val components = strong-components $
         for entry in hier stream :
            n(entry) => classes(parent(entry))
      val cycles =
         {_ as Stream<List<Int>>} $
         filter({_ typeof List}, components)
      for c in cycles do :
         defn msg (ns:List<Int>) :
            join{_, "\n"} $
            for n in ns stream :
               "   ~ at ~" << [#name(n), #info(n)]
         add(errors, "Illegal cycle in type hierarchy. The following types are parents of each other.\n~@" << [
            msg(c)])

   ;Check type arities
   defn check-arities () :
      for assert in asserts do :
         match(assert) :
            (a:CorrectTArity) :
               val arity = length(args(hier[n(a)]))
               if length(targs(a)) != arity :
                  add(errors, "~: Incorrect number of type parameters for type ~. Expecting ~ but given ~@." << [
                     info(a), #name(n(a)), arity, cms(#pr(targs(a)))])

   ;Report errors
   defn report-errors () :
      check-cycles()
      check-arities()
      if not empty?(errors) :
         do(println, errors)
         throw(TypeError())

   ;     Driver
   ;     ======
   #comm(e)
   report-errors()
   TProg(hier, to-list(accum))


;============================================================
;============== Generate Type Variables =====================
;============================================================
defstruct TupleF : ()
defstruct SubF : (ti:Type, t:Type)
defstruct ArgsF : ()
defstruct RetF : (n:Int)


defn infer-types (prog:TProg) :
   ;======== Environment ========
   val env = HashTable<Int,Type>({_})
   val ls-env = HashTable<Int, LSType>({_})
   val req-env = HashTable<Int, TUVar>({_})
   val constraints = Vector<Constraint>()


   ;          ==================================
   ;          ==   Type Variable Generation   ==
   ;          ==================================

   val #? = TGradual()
   val #ls? = UnknownT()
   val #fresh = Fresh()
   defn addc (c:Constraint) : add(constraints, c)
   defn #freshv () : TUVar(fresh-id())
   defn #freshv (n:Int) : map(#freshv{}, 0 to n)
   defn #ls-freshv () : UVarT(fresh-id())
   defn #ls-freshv (n:Int) : map(#ls-freshv{}, 0 to n)
   defn #or (x:Type, y:Type) : y when x typeof TUVar|TNone else x
   defn #or (x:Type, f:Fresh) : #freshv() when x typeof TUVar|TNone else x
   defn #or (xs:List<Type>, y:Type|Fresh) : map(#or{_, y}, xs)
   defn #or (xs:List<Type>, ys:List<Type>) : map(#or, xs, ys)
   defn #or (x:LSType, y:LSType) : y when x typeof UVarT|NoneT else x
   defn #or (xs:List<LSType>, ys:List<LSType>) : map(#or, xs, ys)
   defn set-req-env (n:Int, t:TUVar) : req-env[n] = t
   defn set-env (n:Int, t:Type) : env[n] = t
   defn set-env (ns:List<Int>, y:Type) : do(set-env{_, y}, ns)
   defn set-env (ns:List<Int>, ts:List<Type>) : do(set-env, ns, ts)
   defn set-ls-env (n:Int, t:LSType) : ls-env[n] = t
   defn set-ls-env (ns:List<Int>, ts:List<LSType>) : do(set-ls-env, ns, ts)
   defn bexp (es:List<TExp>, c:Context) : map(bexp{_, c}, es)
   defn bcomm<?T> (cs:List<?T&TComm>) : map(bcomm, cs)
   defn bmulti (es:List<TExp>, targs:List<TVar>, c:Context) : map(bmulti{_, targs, c}, es)
   defn #types (es:List<TExp>) : map(type, es)
   defn #types (es:List<LSExp>) : map(type, es)
   defn #exp-types (es:List<TExp>) : map(exp-type, es)
   defn #exp-types (es:List<LSExp>) : map(exp-type, es)

   defn env-a1 (n:Int) :
      defn* loop (t:Type) :
         match(t) :
            (t:TPoly) : loop(func(t))
            (t:TArrow) : types(a(t) as TTuple)
      loop(env[n])

   defn env-a2 (n:Int) :
      defn* loop (t:Type) :
         match(t) :
            (t:TPoly) : loop(func(t))
            (t:TArrow) : b(t)
      loop(env[n])

   defn wrap-poly (t:TArrow, targs:List<Int>, cargs:List<Int>) :
      if empty?(targs) and empty?(cargs) : t
      else : TPoly(targs, cargs, t)

   defn fn-type (c:TDefn|TDefmethod) :
      val t = TArrow(TTuple(a1*), a2*) where :
         val a1* = #or(a1(c), #?)
         val a2* = #or(a2(c), #fresh)
      wrap-poly(t, targs(c), cargs(c))

   defn fn-type (c:TDefmulti) :
      val t = TArrow(TTuple(a1*), a2*) where :
         val a1* = #or(a1(c), #?)
         val a2* = #or(a2(c), #?)
      wrap-poly(t, targs(c), cargs(c))

   defn fn-type (c:TLDefn) :
      val t = FnT(a1(c), NoneT(), a2(c))
      if empty?(targs(c)) and empty?(cargs(c)) : t
      else : PolyT(targs(c), cargs(c), t)

   defn to-hs-type (t:LSType) :
      match(t) :
         (t:RefT) : type(t)
         (t) : TNone()

   ;======== Specialized Type Constructors ========
   defn lit-type (v) :
      match(v) :
         (v:False) : TOf(FALSE-TYPE-ID, TTuple(List()))
         (v:True) : TOf(TRUE-TYPE-ID, TTuple(List()))
         (v) : error("Literal type ~ not yet implemented." % [v])
      ;defn base (n:Int) : TOf(n, TTuple(List()))
      ;defn raw (n:Int) : TOf(n, TGradual())
      ;match(v) :
      ;   (v:False) : base(False(hier(prog)))
      ;   (v:Int) : base(Int(hier(prog)))
      ;   (v:String) : base(String(hier(prog)))
      ;   (v:Symbol) : base(Symbol(hier(prog)))
      ;   (v:List) : raw(List(hier(prog)))

   defn prim-type (f:Symbol) :
      error("Primitive type not yet implemented.")

   ;====== Environment Queries ======
   ;Return all possible types in e
   defn all-types (e:TExp) -> List<Type> :
      match(e) :         
         (e:TLet) : all-types(body(e))
         (e:TLetVar) : all-types(body(e))
         (e:TLetTuple) : all-types(body(e))
         (e:TLetRec) : all-types(body(e))
         (e:TSeq) : all-types(b(e))
         (e:TBranch) : all-types(body(e))
         (e:TMatch) : map-append(all-types, branches(e))
         (e:TMix) : map-append(all-types, exps(e))
         (e) : list(exp-type(e))

   defn all-types (e:LSExp) -> List<LSType> :
      match(e) :
         (e:LSLetExp) : all-types(exp(e))
         (e:LSMix) : map-append(all-types, exps(e))
         (e:LSFn) : map(RefT, all-types(ref(e)))
         (e) : list(exp-type(e))

   ;======== Build top level environment ========
   defn build-environment () :
      for c in comms(prog) do :
         match(c) :
            (c:TDef) :
               val t = #or(type(c), #fresh)
               set-env(n(c), t)
               set-ls-env(n(c), RefT(t))
            (c:TDefTuple) :
               val ts = map(#freshv{}, ns(c))
               set-env(ns(c), ts)
               set-ls-env(ns(c), map(RefT, ts))
            (c:TDefVar) :
               val t = #or(type(c), #fresh)
               set-env(n(c), t)
               set-ls-env(n(c), RefT(t))
            (c:TDefn) :
               val t = fn-type(c)
               set-env(n(c), t)
               set-ls-env(n(c), to-ls-fn(t))
            (c:TDefmulti) :
               val t = fn-type(c)
               set-env(n(c), t)
               set-ls-env(n(c), to-ls-fn(t))
            (c:TDefmethod) :
               set-env(n(c), fn-type(c))
            (c:TInit) :
               false
            (c:TLDef) :
               set-ls-env(n(c), type(c))
               set-env(n(c), to-hs-type(type(c)))
            (c:TLDefVar) :
               set-ls-env(n(c), type(c))
               set-env(n(c), to-hs-type(type(c)))
            (c:TLDefn) :
               val t = fn-type(c)
               set-ls-env(n(c), t)
               set-env(n(c), to-hs-fn(t))
            (c:TExtern) :
               set-ls-env(n(c), type(c))
            (c:TLInit) :
               false

   ;======== Productions ========
   defn bcomm<?T> (c:?T&TComm) -> T :
      {_ as T&TComm} $ match(c) :
         (c:TDef) :
            val v* = bexp(value(c), CExp(#or(type(c), #?)))
            val t* = env[n(c)]
            #equal(t*, type(v*))
            TDef(n(c), t*, v*)
         (c:TDefTuple) :
            val v* = bexp(value(c), CExp(#?))
            val ts* = map({env[_] as TUVar}, ns(c))
            #downto(ts*, type(v*), TupleF())
            TDefTuple(ns(c), ts*, v*)
         (c:TDefVar) :
            val t* = env[n(c)]
            match(value(c)) :
               (v:False) :
                  TDefVar(n(c), t*, false)
               (v:TExp) :               
                  val v* = bexp(v, CExp(#or(type(c), #?)))
                  #super(t*, type(v*))
                  TDefVar(n(c), t*, v*)
         (c:TDefn) :
            val a1* = env-a1(n(c))
            val a2* = env-a2(n(c))
            set-env(args(c), map(uncap, a1*))
            val body* = bexp(body(c), CExp(#or(a2(c), #?)))
            #equal(a2*, type(body*))
            TDefn(tail?(c), n(c), targs(c), cargs(c), a1*, a2*, args(c), body*)
         (c:TDefmulti) :
            val a1* = env-a1(n(c))
            val a2* = env-a2(n(c))
            TDefmulti(n(c), targs(c), cargs(c), a1*, a2*)
         (c:TDefmethod) :
            val a1* = env-a1(n(c))
            val a2* = env-a2(n(c))
            val t* = TArrow(uncap(TTuple(a1*)), a2*)
            val m* = bmulti(multi(c), map(TVar, targs(c)), CMulti(t*))
            set-env(args(c), map(uncap, a1*))
            val body* = bexp(body(c), CExp(#or(a2(c), #?)))
            #equal(a2*, type(body*))               
            TDefmethod(tail?(c), n(c), m*, thisn(c), targs(c), cargs(c), a1*, a2*, args(c), body*)
         (c:TInit) :
            TInit(bexp(exp(c), CExp(#?)))
         (c:TLDef) :
            val e* = blsexp(exp(c), CLSExp(type(c)), NoneT())
            TLDef(n(c), type(c), e*)
         (c:TLDefVar) :
            val e* = blsexp(exp(c) as LSExp, CLSExp(type(c)), NoneT()) when exp(c) != false
            TLDefVar(n(c), type(c), e*)
         (c:TLDefn) :
            set-ls-env(args(c), map(uncap, a1(c)))
            val body* = blscomm(body(c), a2(c))
            TLDefn(tail?(c), n(c), targs(c), cargs(c), a1(c), a2(c), args(c), body*)
         (c:TExtern) :
            c
         (c:TLInit) :
            TLInit(blscomm(comm(c), NoneT()))

   defn bmulti (e:TExp, targs*:List<TVar>, c:Context) -> TExp :
      defn curry (e:TExp) :
         match(e) :
            (e:TRef) : TCurry(e, targs*, List(), TNone(), TNone(), CNone())
            (e:TMix) : map(curry, e)
      bexp(curry(e), c)      

   defn bfn (e:TFn, c:Context) -> TFn :
      ;Type variables for arguments
      val a1* = #or(a1(e), #fresh)
      val req-a1* = #or(a1(e), #fresh)
      set-env(args(e), a1*)
      for (x in args(e), t* in req-a1*) do :
         set-req-env(x, t* as TUVar) when t* typeof TUVar
      ;Type variables for return
      val a2* = #or(a2(e), #freshv())
      val t* = TArrow(TTuple(a1*), a2*)
      val exp-t* = TArrow(TTuple(req-a1*), a2*)
      val body* = bexp(body(e), CExp(#or(a2(e), #freshv())))
      ;Constraints
      #equal(a2*, type(body*))
      ;Return new struct
      TFn(tail?(e), a1*, a2*, args(e), body*, t*, exp-t*, c)
      
   defn capfn (f:TFn, c:Context, ft:Type) :
      match(c) :
         (c:CExp) :
            ;Expected return type
            val exp-a2 = type(ctxt(body(f)) as CExp)
            #upto(exp-a2, type(c), SubF(a2(f), ft))
            ;Infer argument types
            for t in a1(f) do :
               #upto(t, type(c), SubF(t, ft))
         (c:CDo) :
            ;Expected return type
            val exp-a2 = type(ctxt(body(f)) as CExp)
            #equal(exp-a2, ret(c))
            ;Infer argument types
            if length(args(f)) == length(args(c)) :
               #equal(a1(f), args(c))      

   defn bexp (e:TExp, ctxt:Context) -> TExp :
      val c = ctxt as CMulti|CDo|CExp
      match(e) :
         (e:TLet) :
            val v* = bexp(value(e), CExp(#or(ntype(e), #?)))
            val t* = #or(ntype(e), type(v*))
            set-env(n(e), t*)
            val body* = bexp(body(e), c)
            TLet(n(e), t*, v*, body*, type(body*), exp-type(body*), c)
         (e:TLetVar) :
            val t* = #or(ntype(e), #fresh)
            set-env(n(e), t*)
            val body* = bexp(body(e), c)            
            match(value(e)) :
               (v:False) :
                  TLetVar(n(e), t*, false, body*, type(body*), exp-type(body*), c)
               (v:TExp) :
                  val v* = bexp(v, CExp(t*))
                  #super(t*, type(v*))
                  TLetVar(n(e), t*, v*, body*, type(body*), exp-type(body*), c)
         (e:TLetTuple) :
            val ts* = #freshv(length(ns(e)))
            val v* = bexp(value(e), CExp(#?))
            set-env(ns(e), ts*)
            val body* = bexp(body(e), c)
            #downto(ts*, type(v*), TupleF())
            TLetTuple(ns(e), ts*, v*, body*, type(body*), exp-type(body*), c)
         (e:TLetRec) :
            ;Define types for functions
            for def in defns(e) do :
               set-env(n(def), fn-type(def))
            val defns* = map(bcomm, defns(e))
            val body* = bexp(body(e), c)
            TLetRec(defns*, body*, type(body*), exp-type(body*), c)
         (e:TFn) :
            val f = bfn(e, c)
            capfn(f, c, type(f))
            f
         (e:TMulti) :
            val fs = map(bfn{_, c}, funcs(e))
            val mt = reduce(TAnd, #types(fs))
            val exp-mt = reduce(TAnd, #exp-types(fs))
            for f in fs do : capfn(f, c, mt)
            TMulti(fs, mt, exp-mt, c)
         (e:TSeq) :
            val a* = bexp(a(e), CExp(#?))
            val b* = bexp(b(e), c)
            TSeq(a*, b*, type(b*), exp-type(b*), c)
         (e:TMatch) :
            val args* = bexp(args(e), CExp(#?))
            val branches* = for b in branches(e) map :
               TBranch(args(b), atypes*, body*, type(body*), exp-type(body*), c) where :
                  val atypes* = #or(atypes(b), #types(args*))
                  set-env(args(b), atypes*)
                  val body* = bexp(body(b), c)
            val t* = reduce(TOr, #types(branches*))               
            val exp-t* = reduce(TOr, #exp-types(branches*))               
            TMatch(args*, branches*, t*, exp-t*, c)
         (e:TNew) :
            val ms* = bcomm(methods(e))
            TNew(class(e), ms*, class(e), class(e), c)
         (e:TCurry) :
            val t = env[n(ref(e))]
            val e* = TRef(n(ref(e)), t, t, c)
            attempt :
               val [cargs*, t*, rt*] = #instantiate(t, targs(e), type(c))
               TCurry(e*, targs(e), cargs*, t*, rt*, c)
            else :
               TCurry(e*, targs(e), List(), TNone(), TNone(), c)
         (e:TRef) :         
            val t = env[n(e)]
            val e* = TRef(n(e), t, t, c)
            val rt = get?(req-env, n(e), TNone())
            #super(rt, type(c))
            attempt :
               val [cargs*, t*, rt*] = #instantiate(t, List(), type(c))
               TCurry(e*, List(), cargs*, t*, rt*, c)
            else : e*
         (e:TCast) :
            val exp* = bexp(exp(e), CExp(#?))
            TCast(exp*, targ(e), targ(e), targ(e), c)
         (e:TSet) :
            val ref* = bexp(ref(e), CExp(#?)) as TRef
            val v* = bexp(value(e), CExp(type(ref*)))
            #super(type(ref*), type(v*))
            TSet(ref*, v*, lit-type(false), lit-type(false), c)
         (e:TDo) :
            ;Variables
            val exp-a1* = #freshv(length(args(e)))
            val args* = map(bexp{_, CExp(_)}, args(e), exp-a1*)
            val fc = CDo(#types(args*), type(c), map(all-types, args*))
            val f* = bexp(func(e), fc)
            ;Expected argument types
            #downto(exp-a1*, exp-type(f*), ArgsF())
            ;Infer return type
            val a2* = #freshv()
            #downto(a2*, type(f*), RetF(length(args(e))))
            TDo(f*, args*, a2*, a2*, c)
         (e:TPrim) :
            val args* = bexp(args(e), CExp(#?))
            val t* = prim-type(function(e))
            TPrim(function(e), args*, t*, t*, c)
         (e:TLiteral) :
            val vt = lit-type(value(e))
            TLiteral(value(e), vt, vt, c)
         (e:TTupleExp) :
            val exp-ts* = #freshv(length(exps(e)))
            val exps* = map(bexp{_, CExp(_)}, exps(e), exp-ts*)
            val t* = TTuple(#types(exps*))
            val exp-t* = TTuple(#exp-types(exps*))
            #upto(exp-ts*, type(c), TupleF())            
            TTupleExp(exps*, t*, exp-t*, c)
         (e:TMix) :
            val sv = SVar(fresh-id())
            val exps* = map(bexp{_, c}, exps(e))
            val t* = #freshv()
            val ct* = #freshv()
            match(c) :
               (c:CExp) :
                  #partof(sv, #exp-types(exps*), type(c))
                  #mix(t*, sv, #types(exps*))
                  #mix(ct*, sv, #exp-types(exps*))
               (c:CDo) :
                  #callable(sv, #exp-types(exps*), all-args(c))
                  #mix(t*, sv, #types(exps*))
                  #mix(ct*, sv, #exp-types(exps*))
               (c:CMulti) :
                  #select-multi(sv, type(c), #types(exps*))
                  #pick(t*, sv, #types(exps*))
            TMix(sv, exps*, t*, ct*, c)

   defn blscomm (c:LSComm, rett:LSType) -> LSComm :
      match(c) :
         (c:LSCall) :
            LSCall(blsexp(exp(c), CLSExp(#ls?), rett))
         (c:LSSet) :
            LSSet(exp*, v*) where :
               val exp* = blsexp(exp(c), CLSExp(#ls?), rett)
               val v* = blsexp(value(c), CLSExp(type(exp*)), rett)
         (c:LSLabels) :
            LSLabels(ns(c), blscomm(body(c), rett))
         (c:LSLabel) :
            c
         (c:LSGoto) :
            c
         (c:LSReturn) :
            LSReturn(blsexp(exp(c), CLSExp(rett), rett))
         (c:LSDef) :
            LSDef(n(c), t*, v*) where :
               val v* = blsexp(value(c), CLSExp(#or(type(c), #ls?)), rett)
               val t* = #or(type(c), type(value(c)))
         (c:LSDefVar) :
            LSDefVar(n(c), type(c), v*) where :
               val v* = blsexp(value(c) as LSExp, CLSExp(type(c)), rett) when value(c) != false
         (c:LSSeq) :
            LSSeq(blscomm(a(c), rett), blscomm(b(c), rett))
         (c:LSIf) :
            LSIf(pred*, conseq*, alt*) where :
               val pred* = blsexp(pred(c), CLSExp(LongT()), rett)
               val conseq* = blscomm(conseq(c), rett)
               val alt* = blscomm(alt(c), rett)
         (c:LSMatch) :
            val args* = map(blsexp{_, CLSExp(#ls?), rett}, args(c))
            val bs* = for b in branches(c) map :
               LSBranch(args(b), atypes*, body*) where :
                  val atypes* = #or(atypes(b), #types(args*))
                  set-ls-env(args(b), atypes*)
                  val body* = blscomm(body(b), rett)
            LSMatch(args*, bs*)

   defn blsfields (n:Int, es:List<LSExp>, rett:LSType) :
      val he = current-hierarchy()[n] as LSHEntry
      map(blsexp{_, CLSExp(type(_)), rett}, es, fields(he))

   defn blsexp (e:LSExp, ctxt:Context, rett:LSType) -> LSExp :
      val c = ctxt as CLSDo|CLSExp
      match(e) :
         (e:LSVar) :
            val t = ls-env[n(e)]
            val e* = LSVar(n(e), t, t, c)
            attempt :
               val [cargs*, t*, rt*] = #instantiate(t, List(), type(c))
               LSCurry(e*, List(), cargs*, t*, rt*, c)
            else : e*
         (e:LSNew) :
            val t = RefT(TOf(n(e), targ(e)))
            val args* = blsfields(n(e), args(e), rett)
            LSNew(n(e), targ(e), args*, t, t, c)
         (e:LSArray) :
            val t = RefT(TOf(n(e), targ(e)))
            val args* = blsfields(n(e), args(e), rett)
            val len* = blsexp(length(e), CLSExp(LongT()), rett)
            LSArray(n(e), targ(e), args*, len*, t, t, c)
         (e:LSStruct) :
            val t = StructT(n(e), targ(e))
            val args* = blsfields(n(e), args(e), rett)
            LSStruct(n(e), targ(e), args*, t, t, c)
         (e:LSAddr) :
            val c = CLSExp(#deptr(type(c)))
            val exp* = blsexp(exp(e), c, rett)
            LSAddr(exp*, PtrT(type(exp*)), PtrT(exp-type(exp*)), c)
         (e:LSDeref) :
            val c = CLSExp(#ls?)
            val exp* = blsexp(exp(e), c, rett)
            LSDeref(exp*, #deref(type(exp*)), #deref(exp-type(exp*)), c)
         (e:LSSlot) :
            val exp* = blsexp(exp(e), CLSExp(PtrT(type(c))), rett)
            val ind* = blsexp(index(e), CLSExp(LongT()), rett)
            LSSlot(exp*, ind*, #deref(type(exp*)), #deref(exp-type(exp*)), c)
         (e:LSField) :
            val exp* = blsexp(exp(e), CLSExp(#ls?), rett)
            val t* = #field(type(exp*), name(e))
            LSField(exp*, name(e), t*, t*, c)
         (e:LSDo) :
            ;Variables
            val exp-a1* = #ls-freshv(length(args(e)))
            val args* = map(blsexp{_, CLSExp(_), rett}, args(e), exp-a1*)
            val fc = CLSDo(#types(args*), type(c), map(all-types, args*))
            val f* = blsexp(func(e), fc, rett)
            ;Expected argument types
            #args(exp-a1*, exp-type(f*))
            ;Infer return type
            val t* = #ret(type(f*))
            LSDo(f*, args*, t*, t*, c)
         (e:LSCallC) :
            ;Variables
            val exp-a1* = #ls-freshv(length(args(e)))
            val args* = map(blsexp{_, CLSExp(_), rett}, args(e), exp-a1*)
            val fc = CLSDo(#types(args*), type(c), map(all-types, args*))
            val f* = blsexp(func(e), fc, rett)
            ;Expected argument types
            #args(exp-a1*, exp-type(f*))
            ;Infer return type
            val t* = #ret(type(f*))
            LSCallC(f*, args*, t*, t*, c)            
         (e:LSPrim) :
            error("Not yet implemented.")
         (e:LSSizeof) :
            LSSizeof(targ(e), LongT(), LongT(), c)
         (e:LSTagof) :
            LSTagof(n(e), LongT(), LongT(), c)
         (e:LSCast) :
            val exp* = blsexp(exp(e), CLSExp(#ls?), rett)
            LSCast(exp*, targ(e), targ(e), targ(e), c)
         (e:LSLiteral) :
            error("Not yet implemented.")
         (e:LSAnd) :
            defn blong (e:LSExp) : blsexp(e, CLSExp(LongT()), rett)
            LSAnd(blong(a(e)), blong(b(e)), LongT(), LongT(), c)
         (e:LSOr) : 
            defn blong (e:LSExp) : blsexp(e, CLSExp(LongT()), rett)
            LSOr(blong(a(e)), blong(b(e)), LongT(), LongT(), c)
         (e:LSLetExp) :
            val comm* = blscomm(comm(e), rett)
            val exp* = blsexp(exp(e), c, rett)
            LSLetExp(comm*, exp*, type(exp*), exp-type(exp*), c)
         (e:LSMix) :
            val sv = SVar(fresh-id())
            val exps* = map(blsexp{_, c, rett}, exps(e))
            val t* = #ls-freshv()
            val ct* = #ls-freshv()
            match(c) :
               (c:CLSExp) :
                  #select(sv, #exp-types(exps*), type(c))
                  #pick(t*, sv, #types(exps*))
                  #pick(ct*, sv, #exp-types(exps*))
               (c:CLSDo) :
                  #callable(sv, #exp-types(exps*), all-args(c))
                  #mix(t*, sv, #types(exps*))
                  #mix(ct*, sv, #exp-types(exps*))
            LSMix(sv, exps*, t*, ct*, c)
         (e:LSCurry) :
            val t = ls-env[n(ref(e))]
            val e* = LSVar(n(ref(e)), t, t, c)
            attempt :
               val [cargs*, t*, exp-t*] = #instantiate(t, targs(e), type(c))
               LSCurry(e*, targs(e), cargs*, t*, exp-t*, c)
            else :
               LSCurry(e*, targs(e), List(), NoneT(), NoneT(), c)
         (e:LSFn) :
            val ref* = bexp(ref(e), CExp(#ref-type(type(c))))
            LSFn(ref*, RefT(type(ref*)), RefT(exp-type(ref*)), c)


   ;             ===============================
   ;             ==   Constraint Generation   ==
   ;             ===============================

   ;===== Constraint Upward and Downward Propagators =====
   ;Propagate type upwards towards a subform
   defn #upto (r:Type, w:Type, f:SubF) :
      ;Substitute TUVar(n*) with t*, and all other TUVar with ?
      defn sub (t:Type, n*:Int, t*:Type) :
         match(t) :
            (t:TUVar) :
               if n(t) == n* : t*
               else : #?
            (t) : map(sub{_, n*, t*}, t)
      match(r, ti(f)) :
         (r:TUVar, ti:TUVar) :
            val t* = sub(t(f), n(ti), TCap(n(r)))
            #cap(r, t*, w)
         (r, ti) :
            false

   ;Propagate type upwards towards elements of tuple form
   defn #upto (rs:List<TUVar>, w:Type, f:TupleF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         #cap(r, #tuple-cap(r, i, n), w)

   ;Propagate type downwards towards elements of tuple form
   defn #downto (rs:List<TUVar>, w:Type, f:TupleF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         #cap(r, w, #tuple-cap(r, i, n))

   ;Propagate type downwards towards arguments of function
   defn #downto (rs:List<TUVar>, w:Type, f:ArgsF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         val a1 = #tuple-cap(r, i, n)
         #cap(r, w, TArrow(a1, #?))

   ;Propagate type downwards towards return of function
   defn #downto (r:TUVar, w:Type, f:RetF) :
      val a = TTuple(map({#?}, 0 to n(f)))
      val b = TCap(n(r))
      #cap(r, w, TArrow(a, b))

   ;===== Constraint Generation Utilities =====
   ;Capture i'th element in m element tuple
   defn #tuple-cap (t:TUVar, i:Int, m:Int) :
      TTuple $ for j in 0 to m map :
         if j == i : TCap(n(t))
         else : #?
         
   ;Create supertype constraint
   defn #super (a:Type, b:Type) :
      match(a, b) :
         (a:TUVar, b) : addc(SuperConstraint(n(a), b))
         (a, b) : false

   ;Create equality constraint
   defn #equal (ats:List<Type>, bts:List<Type>) :
      do(#equal, ats, bts)

   defn #equal (a:Type, b:Type) :
      match(a) :
         (a:TUVar) : addc(EqualConstraint(n(a), b))
         (a) : false
         
   defn #equal (a:LSType, b:LSType) :
      match(a) :
         (a:UVarT) : addc(LSEqualConstraint(n(a), b))
         (a) : false

   ;Create capture constraint
   defn #cap (r:TUVar, x:Type, y:Type) :
      addc(FlowConstraint(n(r), x, y))

   defn #cap (r:TUVar, x:LSType, y:LSType) :
      addc(LSFlowConstraint(n(r), x, y))

   ;Create a partof constraint
   defn #partof (s:SVar, ats:List<Type>, bt:Type) :
      addc(PartofConstraint(n(s), ats, bt))

   defn #partof (s:SVar, ats:List<LSType>, args:List<LSType>) :
      addc(LSPartofConstraint(n(s), ats, args))

   ;Create a callable constraint
   defn #callable (s:SVar, ats:List<Type>, args:List<List<Type>>) :
      addc(CallableConstraint(n(s), ats, args))
      
   defn #callable (s:SVar, ats:List<LSType>, args:List<List<LSType>>) :
      addc(LSCallableConstraint(n(s), ats, args))

   ;Create a selection constraint
   defn #select (s:SVar, ats:List<LSType>, bt:LSType) :
      addc(LSSelConstraint(n(s), ats, bt))

   ;Create a method selection constraint
   defn #select-multi (s:SVar, at:TArrow, bts:List<Type>) :
      addc(SelMultiConstraint(n(s), at, bts))

   ;Create a method pick constraint
   defn #pick (t:TUVar, s:SVar, ys:List<Type>) :
      addc(PickConstraint(n(t), n(s), ys))

   defn #pick (t:UVarT, s:SVar, ys:List<LSType>) :
      addc(LSPickConstraint(n(t), n(s), ys))

   ;Create a substitution constraint
   defn #sub (t:Type, env:List<KeyValue<Int,Type>>) :
      val r = TUVar(fresh-id())
      addc(SubConstraint(n(r), t, env))
      r

   ;Create a mixture constraint
   defn #mix (t:TUVar, s:SVar, ats:List<Type>) :
      addc(MixConstraint(n(t), n(s), ats))

   defn #mix (t:UVarT, s:SVar, ats:List<LSType>) :
      addc(LSMixConstraint(n(t), n(s), ats))

   ;Isolate a single capture variable
   defn isolate-cap (n*:Int, t:Type) :
      match(t) :
         (t:TCap) :
            if n(t) == n* : t
            else : TUVar(n(t))
         (t) : map(isolate-cap{n*, _}, t)
         
   defn isolate-cap (n*:Int, t:LSType) :
      match(t) :
         (t:RefT) : RefT(isolate-cap(n*, type(t)))
         (t) : map(isolate-cap{n*, _}, t)

   ;Relax captures in a type
   defn relax-cap (t:Type) :
      match(t) :
         (t:TCap) : TGradual()
         (t) : map(relax-cap, t)
         
   defn relax-cap (t:LSType) :
      match(t) :
         (t:RefT) : RefT(relax-cap(type(t)))
         (t) : map(relax-cap, t)

   ;Instantiate a polymorphic type with the given type arguments
   defn #instantiate (t:Type, targs*:List<Type>, w:Type) :
      match(t) :
         (t:TPoly) :
            fail() when length(targs(t)) != length(targs*)
            val cargs* = #freshv(length(cargs(t)))
            val env = map(KeyValue,
                          append(targs(t), cargs(t))
                          append(targs*, cargs*))
            val a1* = sub(uncap(a(func(t))), env)
            val a2* = #sub(b(func(t)), env)
            val t* = TArrow(a1*, a2*)
            val cap-a1* = sub(a(func(t)), env)
            val cap-t* = TArrow(cap-a1*, a2*)
            for v in cargs* do :
               #cap(v, isolate-cap(n(v), cap-t*), w)
            [cargs*, t*, relax-cap(cap-t*)]
         (t) : fail()

   defn #instantiate (t:LSType, targs*:List<Type>, w:LSType) :
      match(t) :
         (t:PolyT) :
            fail() when length(targs(t)) != length(targs*)
            val cargs* = #freshv(length(cargs(t)))
            val env = map(KeyValue,
                          append(targs(t), cargs(t))
                          append(targs*, cargs*))
            val a* = map(sub{uncap(_), env}, a(func(t)))
            val r* = sub(uncap(r(func(t))), env)
            val b* = sub(b(func(t)), env)
            val t* = FnT(a*, r*, b*)            
            val cap-a* = map(sub{_, env}, a(func(t)))
            val cap-r* = sub(r(func(t)), env)
            val cap-t* = FnT(cap-a*, cap-r*, b*)
            for v in cargs* do :
               #cap(v, isolate-cap(n(v), cap-t*), w)
            [cargs*, t*, relax-cap(cap-t*)]
         (t) : fail()

   ;Create a deptr constraint
   defn #deptr (t:LSType) :
      val r = #ls-freshv()
      addc(DeptrConstraint(n(r), t))
      r

   ;Create a deref constraint
   defn #deref (t:LSType) :
      val r = #ls-freshv()
      addc(DerefConstraint(n(r), t))
      r

   ;Create a field constraint
   defn #field (t:LSType, name:Symbol) :
      val r = #ls-freshv()
      addc(FieldConstraint(n(r), t, name))
      r

   ;Create an arguments constraint
   defn #arg (r:UVarT, t:LSType, i:Int, arity:Int) :
      addc(ArgConstraint(n(r), t, i, arity))
   defn #args (rs:List<UVarT>, t:LSType) :
      val n = length(rs)
      for (r in rs, i in 0 to false) do :
         #arg(r, t, i, n)

   ;Create a return constraint
   defn #ret (t:LSType) :
      val r = #ls-freshv()
      addc(RetConstraint(n(r), t))
      r

   ;Create a ref constraint
   defn #ref-type (t:LSType) :
      val r = #freshv()
      addc(RefTypeConstraint(n(r), t))
      r
      

   ;             ======================
   ;             ==   Fill Results   ==
   ;             ======================
   
   defn fill (prog:TProg, results:SolveResults) :
      defn fill-ctxt (c:Context) -> Context :
         map(fill-t, c)
      defn fill-s (s:Selection) -> Selection :
         match(s) :
            (s:SVar) : sels(results)[n(s)]
            (s) : s
      defn fill-t (t:Type) -> Type :
         match(t) :
            (t:TUVar) : get?(types(results), n(t), t)
            (t) : map(fill-t, t)
      defn fill-e (e:TExp) -> TExp :
         map{fill-t, _} $
         map{fill-e, _} $
         map{fill-c, _} $
         map{fill-ctxt, _} $
         map{fill-s, _} $
         e
      defn fill-c (c:TComm) -> TComm :
         map{fill-e, _} $
         map{fill-t, _} $
         c

      TProg(hier(prog), map(fill-c, comms(prog)))


   ;             ================
   ;             ==   Driver   ==
   ;             ================

   build-environment()
   val prog* = TProg(hier(prog), bcomm(comms(prog)))
   println("Generated type variables")
   println(prog*)
   val results = solve(constraints)
   fill(prog*, results)

;============================================================
;=================== Print a Type ===========================
;============================================================
defn cms (xs:Streamable) : join(xs, ", ")

defn printable (nm:NameMap, t:Type) :
   defn #name (n:Int) : name(nm[n] as VarEntry)
   defn #carg (n:Int) : "?~" << [#name(n)]
   defn lvl (t:Type) :
      match(t) :
         (t:TArrow|TPoly) : 0
         (t:TOr) : 1
         (t) : 2

   defn p0 (t:Type) : pt(t, 0)
   defn p0 (ts:List<Type>) : stream(p0, ts)
   defn pt (t:Type, l:Int) :
      defn pn (t2:Type) : pt(t2, lvl(t))
      defn p+ (t2:Type) : pt(t2, lvl(t) + 1)

      val r = match(t) :
         (t:TArrow) :
            match(a(t)) :
               (a:TTuple) :
                  if length(types(a)) == 1 : "~ -> ~" << [p+(head(types(a))), pn(b(t))]
                  else : "(~@) -> ~" << [cms(p0(types(a))), pn(b(t))]
               (a:TGradual) : "(? ...) -> ~" << [pn(b(t))]
         (t:TPoly) :
            val ts = concat(stream(#name, targs(t)), stream(#carg, cargs(t)))
            val f = pn(func(t))
            "<~@> . ~" << [cms(ts), f]
         (t:TOr) :
            "~|~" << [pn(a(t)), pn(b(t))]
         (t:TAnd) :
            "~&~" << [pn(a(t)), pn(b(t))]
         (t:TOf) :
            match(type(t)) :
               (ta:TTuple) :
                  if empty?(types(ta)) : #name(n(t))
                  else : "~<~@>" << [#name(n(t)), cms(p0(types(ta)))]
               (ta:TGradual) :
                  #name(n(t))
         (t:TGradual) :
            "?"
         (t:TBot) :
            "Void"
         (t:TVar) :
            #name(n(t))
         (t:TUVar) :
            "UK"
         (t:TCap) :
            #carg(n(t))
         (t:TNone) :
            "none"
         (t:TTuple) :
            "[~@]" << [cms(p0(types(t)))]
      if lvl(t) < l : "(~)" << [r]
      else : r
   p0(t)

defn printable (nm:NameMap, t:LSType) :
   defn #pr (t:LSType) : printable(nm, t)
   defn #pr (t:Type) : printable(nm, t)
   defn #pr (ts:Streamable<LSType>) : stream(#pr, ts)
   defn #pr (ts:Streamable<Type>) : stream(#pr, ts)
   defn #ap (ts:Streamable<LSType>, t:LSType) : concat(ts, [t])
   defn #name (n:Int) : name(nm[n] as VarEntry)
   match(t) :
      (t:ByteT) : "byte"
      (t:IntT) : "int"
      (t:LongT) : "long"
      (t:FloatT) : "float"
      (t:DoubleT) : "double"
      (t:UnknownT) : "?"
      (t:PtrT) : "ptr<~>" << [#pr(type(t))]
      (t:RefT) : "ref<~>" << [#pr(type(t))]
      (t:FnT) :
         match(r(t)) :
            (r:NoneT) : "(~@) -> ~" << [cms(#pr(a(t))), #pr(b(t))]
            (r:LSType) : "(~@ ...) -> ~" << [cms(#pr(#ap(a(t), r))), #pr(b(t))]
      (t:StructT) :
         match(type(t)) :
            (ta:TTuple) :
               if empty?(types(ta)) : #name(n(t))
               else : "~<~@>" << [#name(n(t)), cms(#pr(types(ta)))]
            (ta:TGradual) :
               #name(n(t))

;============================================================
;=================== Report Errors ==========================
;============================================================

defn report-errors (nm:NameMap, prog:TProg) :
   ;======== State ========
   val errors = Vector<Vector<?>>()

   ;======== Utilities ========
   val #? = TGradual()
   defn unknown? (x:True|False|Maybe|Unknown) : x typeof Unknown
   defn not-st? (x:Type, y:Type) : subtype?(x, y) == false
   defn not-st? (x:TExp, y:Type) : not-st?(type(x), y)
   defn not-st? (x:Type, y:TExp) : not-st?(x, type(y))
   defn not-st? (x:TExp, y:TExp) : not-st?(type(x), type(y))
   defn cms (xs:Streamable) : join(xs, ", ")
   defn #pr (t:Type) : printable(nm, t)
   defn #pr (ts:Streamable<Type>) : stream(#pr, ts)
   defn #types (es:List<TExp>) : map(type, es)

   defn add-error (p:Int, msg) :
      while length(errors) < p + 1 :
         add(errors, Vector<?>())
      add(errors[p], msg)

   defn unresolved? (t:Type) :
      match(t) :
         (t:TUVar|TNone) : true
         (t) : any?(unresolved?, children(t))

   defn resolved-pr (x) :
      match(x) :
         (xs:Streamable) : reduce(max{_, resolved-pr(_)}, 0, x)
         (x:Type) : 1 when unresolved?(x) else 0
         (x:TExp) : resolved-pr(type(x))

   defn all-pairs<?T> (xs:Streamable<?T>) :
      val xs* = to-array(xs)
      generate<[T, T]> :
         for i in 0 to length(xs*) do :
            for j in (i + 1) to length(xs*) do :
               yield([xs*[i], xs*[j]])

   defn has-name? (n:Int) :
      key?(nm,n) and (nm[n] typeof VarEntry)

   defn #name (n:Int) :
      name(nm[n] as VarEntry)

   defn not-sm? (x:Type, y:Type) :
      match(x, y) :
         (x:TArrow, y:TArrow) : submethod?(x, y) == false
         (x, y) : false

   defn #select<?T> (xs:List<?T>, ss:List) :
      to-list $ for (x in xs, s in ss) filter :
         s != false

   defn tarity (t:Type) :
      match(t) :
         (t:TArrow) : 0
         (t:TPoly) : length(targs(t))

   ;======== Phase 1: Error Detection ========
   defn check (c:TComm) :
      ;Check constraints
      match(c) :
         (c:TDef) :
            no-inferred-type(c) when unresolved?(type(c))
            bad-assign(c) when not-st?(value(c), type(c))
         (c:TDefTuple) :
            val t? = TTuple(map({#?}, types(c)))
            val t = TTuple(types(c))
            not-tuple(c) when not-st?(value(c), t?)
            no-inferred-type(c) when any?(unresolved?, types(c))
            bad-assign(c) when not-st?(value(c), t)
         (c:TDefVar) :
            no-inferred-type(c) when unresolved?(type(c))
            if value(c) != false :
               bad-assign(c) when not-st?(value(c) as TExp, type(c))
         (c:TDefn) :
            no-inferred-ret(c) when unresolved?(a2(c))
            bad-ret(c) when not-st?(body(c), a2(c))
         (c:TDefmethod) :
            val t = type(ctxt(multi(c)) as CMulti)
            val tm = type(multi(c))
            check-multi(multi(c))
            bad-method(c) when not-sm?(t, tm)
            no-inferred-ret(c) when unresolved?(a2(c))
            bad-ret(c) when not-st?(body(c), a2(c))
         (c) :
            false
      ;Check subexpressions
      match(c) :
         (c:TDefmethod) : check(body(c))
         (c) : do(check{_:TExp}, c)

   defn check-multi (e:TExp) :
      match(e) :
         (e:TRef) :
            false
         (e:TMix) :
            val exps* = #select(exps(e), sels(sel(e) as SSel))
            val n = length(exps*)
            do(check-multi, exps*)
            no-method(e) when n == 0
            amb-method(e) when n > 1
         (e:TCurry) :
            check(e)

   defn check (e:TExp) :
      ;Check constraints
      match(e) :
         (e:TLet) :
            no-inferred-type(e) when unresolved?(ntype(e))
            bad-assign(e) when not-st?(value(e), ntype(e))
         (e:TLetVar) :
            no-inferred-type(e) when unresolved?(ntype(e))
            if value(e) != false :
               bad-assign(e) when not-st?(value(e) as TExp, ntype(e))
         (e:TLetTuple) :
            val t? = TTuple(map({#?}, ntypes(e)))
            val t = TTuple(ntypes(e))
            not-tuple(e) when not-st?(value(e), t?)
            no-inferred-type(e) when any?(unresolved?, ntypes(e))
            bad-assign(e) when not-st?(value(e), t)
         (e:TFn) :
            no-inferred-args(e) when any?(unresolved?, a1(e))
            no-inferred-ret(e) when unresolved?(a2(e))
            bad-ret(e) when not-st?(body(e), a2(e))
         (e:TMatch) :
            if none?(unresolved?{type(_)}, args(e)) :
               for b in branches(e) do :
                  if any?({not overlap?(type(_), _)}, args(e), atypes(b)) :
                     bad-branch(e, b)
         (e:TCast) :
            if not unresolved?(type(exp(e))) :
               if not overlap?(type(exp(e)), targ(e)) :
                  bad-cast(e)
         (e:TSet) :
            bad-set(e) when not-st?(value(e), ref(e))
         (e:TDo) :
            val t? = TArrow(#?, #?)
            val t = TArrow(TTuple(#types(args(e))), #?)
            not-function(e) when not-st?(func(e), t?) else
            bad-call(e) when not-st?(func(e), t)
         (e:TMix) :
            val ss = sels(sel(e) as SSel)
            if all?({_ typeof False}, ss) :
               no-mix(e)
            else if any?(unknown?, ss) :
               amb-mix(e)
            else :            
               val es = #select(exps(e), ss)
               if none?(unresolved?, #types(es)) :
                  val disjoint = let :
                     for [x, y] in all-pairs(#types(es)) all? :
                        disjoint-fns?(x as TArrow, y as TArrow, type(ctxt(e) as CExp|CDo))
                  amb-mix(e) when not disjoint
         (e:TCurry) :
            val n = tarity(type(ref(e)))
            bad-tarity(e) when n != length(targs(e))
            no-capture(e) when any?(unresolved?, cargs(e))
         (e) :
            false

      ;Check subexpressions
      match(e) :
         (e:TMix) :
            for (e in exps(e), s in sels(sel(e) as SSel)) do :
               check(e) when s typeof True|Maybe
         (e:TCurry) :
            false
         (e) :
            do(check{_:TExp}, e)
            do(check{_:TComm}, e)

   ;======== Phase 2: Report Creation ========
   defn name? (x:Int|List<Int>, str:String, default:String) :
      match(x) :
         (xs:List<Int>) :
            if all?(has-name?, xs) : str << [cms(stream(#name, xs))]
            else : default
         (x:Int) :
            if has-name?(x) : str << [#name(x)]
            else : default
   defn def-name (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      match(c) :
         (c:TDef) : name?(n(c), "global value ~", "global value")
         (c:TDefVar) : name?(n(c), "global variable ~", "global variable")
         (c:TDefTuple) : name?(ns(c), "global values ~@", "tuple elements")
         (c:TLet) : name?(n(c), "value ~", "value")
         (c:TLetVar) : name?(n(c), "variable ~", "variable")
         (c:TLetTuple) : name?(ns(c), "values ~@", "tuple elements")
   defn no-inferred-type (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      val p = match(value(c)) :
         (v:TExp) : resolved-pr(type(v))
         (v:False) : 0
      add-error(p, "Could not infer type of ~." << [def-name(c)])

   defn def-type (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      match(c) :
         (c:TLetVar|TLet) : ntype(c)
         (c:TDef|TDefVar) : type(c)
         (c:TLetTuple) : TTuple(ntypes(c))
         (c:TDefTuple) : TTuple(types(c))
   defn bad-assign (c:TDef|TDefVar|TLet|TLetVar|TLetTuple|TDefTuple) :
      val name = def-name(c)
      val t = def-type(c)
      val p = resolved-pr([t, value(c) as TExp])
      add-error(p, "Cannot assign expression of type ~ to ~ with declared type ~." <<
         [#pr(type(value(c) as TExp)), name, #pr(t)])

   defn not-tuple (c:TDefTuple|TLetTuple) :
      val n = length(ns(c))
      val p = resolved-pr(value(c))
      add-error(p, "Cannot deconstruct expression of type ~ into tuple of length ~." <<
         [#pr(type(value(c))), n])

   defn no-inferred-args (e:TFn) :
      val w = type(ctxt(e) as CExp|CDo)
      for (a in args(e), t in a1(e), i in 0 to false) do :
         if unresolved?(t) :
            val name = name?(a, "argument ~", "argument ~" % [i])
            add-error{0, _} $
               if unresolved?(w) : "Could not infer type for ~." << [name]
               else : "Could not infer type for ~. Function used in context expecting type ~." << [name, #pr(w)]

   defn no-inferred-ret (e:TFn|TDefn|TDefmethod) :
      val p = resolved-pr([a1(e), body(e)])
      val name = match(e) :
         (e:TFn) : "anonymous function"
         (e:TDefn) : name?(n(e), "function ~", "function")
         (e:TDefmethod) : "method"
      add-error(p, "Could not infer return type for ~." << [name])

   defn bad-ret (f:TDefn|TDefmethod|TFn) :
      val name = match(f) :
         (f:TDefn) : name?(n(f), "function ~", "function")
         (f:TDefmethod) : "method"
         (f:TFn) : "anonymous function"
      defn loop (e:TExp) :
         match(e) :
            (e:TLet|TLetVar|TLetTuple|TLetRec|TBranch) : loop(body(e))
            (e:TMatch) : do(loop, branches(e))
            (e) :
               val p = resolved-pr([a2(f), e])
               if not-st?(e, a2(f)) :
                  add-error(p, "Cannot return an expression of type ~ for ~ with declared return type ~." << [
                     #pr(type(e)), name, #pr(a2(f))])
      loop(body(f))

   defn bad-branch (e:TMatch, b:TBranch) :
      val bargs = stream({[_, _]}, args(b), atypes(b))
      for (a in args(e), [ba, bt] in bargs, i in 0 to false) do :
         if not overlap?(type(a), bt) :
            val name = name?(ba, "argument ~", "argument ~" % [i])
            add-error(0, "Branch type ~ for ~ cannot match against match argument of type ~." << [
               #pr(bt), name, #pr(type(a))])

   defn bad-cast (e:TCast) :
      add-error(0, "Cannot cast expression of type ~ to type ~." << [
         #pr(type(exp(e))), #pr(targ(e))])

   defn bad-set (e:TSet) :
      val p = resolved-pr([ref(e), value(e)])
      val name = name?(n(ref(e)), "variable ~", "variable")
      add-error(p, "Cannot assign expression of type ~ to ~ with declared type ~." << [
         #pr(type(value(e))), name, #pr(type(ref(e)))])

   defn not-function (e:TDo) :
      val p = resolved-pr(func(e))
      val name = match(func(e)) :
         (f:TRef) : name?(n(f), "Value ~", "Value")
         (f) : "Expression"
      add-error(p, "~ of type ~ cannot be called as a function." << [
         name, #pr(type(func(e)))])

   defn bad-call (e:TDo) :
      val p = resolved-pr([func(e), args(e)])
      val name = get-name(func(e)) where :
         defn get-name (e:TExp) :
            match(e) :
               (e:TRef) : name?(n(e), "function ~", "function")
               (e:TMix) : get-name(head(exps(e)))
               (e:TCurry) : get-name(ref(e))
               (e) : "expression"
      add-error(p, "Cannot call ~ of type ~ with arguments of type ~@" << [
         name, #pr(type(func(e))), cms(map(#pr{type(_)}, args(e)))])

   defn get-ref (e:TExp) :
      match(e) :
         (e:TRef) : e
         (e:TCurry) : ref(e)
   defn possibilities (exps:Streamable<TExp>) :
      join{_, "\n"} $ for e in exps stream :
         val r = get-ref(e)
         "   ~: ~" << [#name(n(r)), #pr(type(r))]
   defn no-mix (e:TMix) :
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      match(ctxt(e)) :
         (c:CDo) :
            val p = resolved-pr([exps(e), args(c)])
            add-error(p, "No appropriate function ~ for arguments of type ~@. Possibilities are:\n~@" << [
               name, cms(#pr(args(c))), possibilities(exps(e))])
         (c:CExp) :
            val p = resolved-pr([exps(e), type(c)])
            add-error(p, "No appropriate function ~ for expected type ~. Possibilities are:\n~@" << [
               name, #pr(type(c)), possibilities(exps(e))])
   defn amb-mix (e:TMix) :
      val exps* = to-list $
         for (e in exps(e), s in sels(sel(e) as SSel)) filter :
            s != false
      val name = #name(n(r)) where :
         val r = get-ref(head(exps*))
      match(ctxt(e)) :
         (c:CDo) :
            val p = resolved-pr([map(get-ref, exps*), args(c)])
            add-error(p, "Ambiguous call to overloaded function ~ with arguments of type ~@. Possibilities are:\n~@" << [
               name, cms(#pr(args(c))), possibilities(exps*)])
         (c:CExp) :
            val p = resolved-pr([map(get-ref, exps*), type(c)])
            add-error(p, "Ambiguous reference to overloaded function ~ for expected type ~. Possibilities are:\n~@" << [
               name, #pr(type(c)), possibilities(exps*)])

   defn no-method (e:TMix) :
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      val t = type(ctxt(e) as CMulti)
      val p = resolved-pr([exps(e), t])
      add-error(p, "No appropriate multi ~ for method of type ~@. Possibilities are:\n~@" << [
         name, #pr(t), possibilities(exps(e))])

   defn amb-method (e:TMix) :
      val ss = sels(sel(e) as SSel)
      val exps* = #select(exps(e), ss)
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      val t = type(ctxt(e) as CMulti)
      val p = resolved-pr([exps*, t])
      add-error(p, "Multiple appropriate multis ~ for method of type ~@. Possibilities are:\n~@" << [
         name, #pr(t), possibilities(exps*)])

   defn bad-method (c:TDefmethod) :
      val name = get-name(multi(c)) where :
         defn get-name (e:TExp) :
            match(e) :
               (e:TMix) : get-name(head(exps(e)))
               (e:TCurry) : get-name(ref(e))
               (e:TRef) : #name(n(e))
      val t = type(ctxt(multi(c)) as CMulti)
      val p = resolved-pr([t, type(multi(c))])
      add-error(p, "Cannot attach method of type ~ to multi ~ of type ~." << [
         #pr(t), name, #pr(type(multi(c)))])

   defn bad-tarity (e:TCurry) :
      match(type(ref(e))) :
         (t:TPoly) :
            add-error(0, "Function ~ of type ~ requires ~ type arguments, but received ~@." << [
               #name(n(ref(e))), #pr(type(ref(e))), cms(#pr(targs(e)))])
         (t:TArrow) :
            add-error(0, "Function ~ of type ~ does not accept type arguments, but received ~@." << [
               #name(n(ref(e))), #pr(type(ref(e))), cms(#pr(targs(e)))])

   defn no-capture (e:TCurry) :
      val rt = type(ref(e)) as TPoly
      for (a in cargs(rt), t in cargs(e)) do :
         if unresolved?(t) :
            val a-name = name?(a, "argument ~", "argument")
            val r-name = #name(n(ref(e)))
            match(ctxt(e)) :
               (c:CExp|CMulti) :
                  val p = resolved-pr(type(c))
                  add-error(p, "Could not capture ~ for function ~ of type ~. Expected type is ~." << [
                     a-name, r-name, #pr(rt), #pr(type(c))])
               (c:CDo) :
                  val p = resolved-pr(args(c))
                  add-error(p, "Could not capture ~ for function ~ of type ~. Called with arguments of type ~@." << [
                     a-name, r-name, #pr(rt), cms(#pr(args(c)))])

   ;======== Driver ========
   do(check, comms(prog))
   for i in 0 to length(errors) do :
      if not empty?(errors[i]) :
         do(println, errors[i])
         throw(TypeError())

;;============================================================
;;============== LoStanza Type Inference =====================
;;============================================================
;
;defn infer-lostanza-types (nm:NameMap, prog:TProg) :
;   ;====== State ======
;   val env = HashTable<Int,False|LSType>({_})
;   val structs = HashTable<Int,LSDefStruct>({_})
;   val errors = Vector<?>()
;
;   ;====== Utilities ======
;   defn set-env (n:Int, t:LSType) :
;      env[n] = t
;
;   defn set-env (ns:List<Int>, ts:List<LSType>) :
;      do(set-env, ns, ts)
;
;   defn do-ls (f:LSTComm -> ?, prog:TProg) :
;      for c in comms(prog) do :
;         match(c) :
;            (c:TLoStanza) : f(comm(c))
;            (c) : false
;
;   defn map-ls (f:LSTComm -> LSTComm, prog:TProg) :
;      val comms* = for c in comms(prog) map :
;         match(c) :
;            (c:TLoStanza) : TLoStanza(f(comm(c)))
;            (c) : c
;      TProg(hier(prog), comms*)
;
;   defn issue-errors () :
;      if not empty?(errors) :
;         do(println, errors)
;         throw(TypeError())
;
;   defn ensure!<T> (x:T|False) :
;      match(x) :
;         (x:False) : fail()
;         (x:T) : x
;
;   defn type! (e:LSExp) : ensure!<LSType>(type(e))
;   defn type! (c:LSLetVar) : ensure!<LSType>(type(c))
;   defn value! (e:LSDefVar|LSLetVar) : ensure!<LSExp>(value(e))
;
;   defn deref (e:LSExp) :
;      attempt :
;         match(type!(e)) :
;            (t:RefT|PtrT) : deref(LSDeref(e, type(t)))
;            (t) : fail()
;      else : e
;
;   defn struct-type! (t:StructT, fname:Symbol) :
;      label<LSType> return :
;         val s = structs[n(t)]
;         match(rfield(s)) :
;            (f:StructField) :
;               if name(f) == fname :
;                  return(PtrT(type(f)))
;            (f:False) : false
;         for f in fields(s) do :
;            return(type(f)) when name(f) == fname
;         fail()
;
;   defn lvalue? (e:LSExp) :
;      match(e) :
;         (e:LSVar|LSDeref|LSSlot) : true
;         (e:LSField) : lvalue?(exp(e))
;
;   ;====== Phase 1: Build Global Environment ======
;   defn build-env (c:LSTComm) :
;      match(c) :
;         (c:LSDefVar) : set-env(n(c), type(c))
;         (c:LSDefStruct) : structs[n(c)] = c
;         (c:LSExtern) : set-env(n(c), type(c))
;         (c:LSExternFn) : set-env(n(c), type(c))
;         (c:LSDefn) : set-env(n(c), FnT(a1(c), false, a2(c)))
;         (c) : false
;
;   ;====== Phase 2: Inference ======
;   defn infer-tc (c:LSTComm) -> LSTComm :
;      match(c) :
;         (c:LSDefVar) :
;            val c* = map(infer-e, c)
;            attempt :
;               val tau = type!(value!(c*))
;               bad-def(c*) when not subtype?(tau, type(c*))
;            c*
;         (c:LSDefn) :
;            set-env(args(c), a1(c))
;            map(infer-c{_, a2(c)}, c)
;         (c:LSTopComm) :
;            map(infer-c{_, false}, c)
;         (c:LSDefStruct|LSExtern|LSExternFn) :
;            c
;
;   defn infer-c (c:LSComm, ret:False|LSType) -> LSComm :
;      defn ret! () : ret as LSType
;      match(c) :
;         (#c:LSTCall) :
;            val c = map(infer-e, #c)
;            attempt :
;               val tau = type!(exp(c))
;               bad-ret(c, ret!()) when not subtype?(tau, ret!())
;            c
;         (#c:LSSet) :
;            val c = map(infer-e, #c)
;            attempt :
;               val tau = type!(exp(c))
;               not-lvalue(c) when not lvalue?(exp(c))
;               val tau* = type!(value(c))
;               bad-set(c) when not subtype?(tau*, tau)
;            c
;         (#c:LSReturn) :
;            val c = map(infer-e, #c)
;            attempt :
;               val tau = type!(exp(c))
;               bad-ret(c, ret!()) when not subtype?(tau, ret!())
;            c
;         (c:LSLetVar) :
;            val v* = infer-e(value(c))
;            val type* = match(type(c)) :
;               (t:LSType) : t
;               (t:False) : type(v* as LSExp)
;            env[n(c)] = type*
;            val body* = infer-c(body(c), ret)
;            val c* = LSLetVar(n(c), type*, v*, body*)
;            attempt :
;               val tau = type!(value!(c*))
;               bad-let(c*) when not subtype?(tau, type!(c))
;            c*
;         (c:LSSeq) :
;            map(infer-c{_, ret}, c)
;         (#c:LSIf) :
;            val c = map(infer-c{_, ret}, map(infer-e, #c))
;            attempt :
;               val tau = type!(pred(c))
;               bad-pred(c) when not subtype?(tau, LongT())
;            c
;         (c:LSMatch) :
;            val arg* = infer-e(arg(c))
;            val br* = for b in branches(c) map :
;               env[n(b)] = type(b)
;               val body* = infer-c(body(b), ret)
;               LSBranch(n(b), type(b), body*)
;            val c* = LSMatch(arg*, br*)
;            attempt :
;               val t = type!(arg*)
;               val reft = RefT(UnknownT())
;               bad-match(c*) when not subtype?(t, reft)
;            c*
;         (c:LSCall|LSLabel|LSGoto) :
;            map(infer-e, c)
;
;   defn infer-e (e:False) -> False :
;      false
;
;   defn infer-e (e:LSExp) -> LSExp :
;      match(map(infer-e, e)) :
;         (e:LSVar) :
;            LSVar(n(e), env[n(e)])
;         (e:LSRef) :
;            LSRef(n(e), args(e), RefT(StructT(n(e))))
;         (e:LSRefArray) :
;            attempt :
;               val tau = type!(length(e))
;               bad-length(e) when not subtype?(tau, LongT())
;            LSRefArray(n(e), length(e), args(e), RefT(StructT(n(e))))
;         (e:LSStruct) :
;            LSStruct(n(e), args(e), StructT(n(e)))
;         (e:LSPtr) :
;            val type* = attempt :
;               val tau = type!(exp(e))
;               not-lvalue(e) when not lvalue?(exp(e))
;               PtrT(tau)
;            LSPtr(exp(e), type*)
;         (e:LSDeref) :
;            val type* = attempt :
;               match(type!(exp(e))) :
;                  (t:RefT) : type(t)
;                  (t:PtrT) : type(t)
;                  (t) : (bad-deref(e), fail())
;            LSDeref(exp(e), type*)
;         (e:LSSlot) :
;            val type* = attempt :
;               match(type!(exp(e))) :
;                  (t:PtrT) : type(t)
;                  (t) : (bad-slot(e), fail())
;            attempt :
;               val tau = type!(index(e))
;               bad-index(e) when not subtype?(tau, LongT())
;            LSSlot(exp(e), index(e), type*)
;         (e:LSField) :
;            val exp* = deref(exp(e))
;            val type* = attempt :
;               val tau = type!(exp*)
;               match(tau) :
;                  (tau:StructT) :
;                     attempt : struct-type!(tau, name(e))
;                     else : (no-field(e), fail())
;                  (tau) : (bad-field(e), fail())
;            LSField(exp*, name(e), type*)
;         (e:LSDo) :
;            val type* = attempt :
;               match(type!(func(e))) :
;                  (tau:FnT) :
;                     val ts = map(type!, args(e))
;                     bad-call(e) when not subtype?(ts, false, a(tau), r(tau))
;                     b(tau)
;                  (tau) :
;                     not-func(e)
;                     fail()
;            LSDo(func(e), args(e), type*)
;         (e:LSCallC) :
;            val type* = attempt :
;               match(type!(func(e))) :
;                  (tau:FnT) :
;                     val ts = map(type!, args(e))
;                     bad-call(e) when not subtype?(ts, false, a(tau), r(tau))
;                     b(tau)
;                  (tau) :
;                     not-func(e)
;                     fail()
;            LSCallC(func(e), args(e), type*)
;         (e:LSSizeof) :
;            LSSizeof(argtype(e), LongT())
;         (e:LSTagof) :
;            LSTagof(n(e), LongT())
;         (e:LSCast) :
;            attempt :
;               val tau = type!(exp(e))
;               bad-cast(e) when not castable?(tau, type!(e))
;            e
;         (e:LSValue) :
;            e
;         (e:LSAnd) :
;            defn long? (e:LSExp) : subtype?(type!(e), LongT())
;            attempt: bad-and-a(e) when not long?(a(e))
;            attempt: bad-and-b(e) when not long?(b(e))
;            LSAnd(a(e), b(e), LongT())
;         (e:LSOr) :
;            defn long? (e:LSExp) : subtype?(type!(e), LongT())
;            attempt: bad-or-a(e) when not long?(a(e))
;            attempt: bad-or-b(e) when not long?(b(e))
;            LSOr(a(e), b(e), LongT())
;
;   ;====== Phase 3: Report Generation ======
;   defn cms (xs:Streamable) : join(xs, ", ")
;   defn has-name? (n:Int) : key?(nm,n) and (nm[n] typeof VarEntry)
;   defn #name (n:Int) : name(nm[n] as VarEntry)
;   defn #pr (t:LSType) : printable(nm, t)
;   defn #pr (ts:Streamable<LSType>) : stream(#pr, ts)
;
;   defn name? (x:Int|List<Int>, format:String, default:String) :
;      match(x) :
;         (xs:List<Int>) :
;            if all?(has-name?, xs) : format << [cms(stream(#name, xs))]
;            else : default
;         (x:Int) :
;            if has-name?(x) : format << [#name(x)]
;            else : default
;
;   defn name? (e:LSExp, format:String, default:String) :
;      match(e) :
;         (e:LSVar) : name?(n(e), format, default)
;         (e) : default
;
;   defn bad-def (e:LSDefVar) :
;      val name = name?(n(e), "Variable ~", "Variable")
;      add(errors, "~ of declared type ~ cannot be initialized with expression of type ~." << [
;         name, #pr(type(e)), #pr(type!(value!(e)))])
;
;   defn bad-let (c:LSLetVar) :
;      val name = name?(n(c), "Variable ~", "Variable")
;      add(errors, "~ of declared type ~ cannot be initialized with expression of type ~." << [
;         name, #pr(type!(c)), #pr(type!(value!(c)))])
;
;   defn bad-ret (c:LSTCall|LSReturn, ret:LSType) :
;      add(errors, "Cannot return expression of type ~ from function with declared type ~." << [
;         #pr(type!(exp(c))), #pr(ret)])
;
;   defn bad-set (c:LSSet) :
;      val name = name?(exp(c), "variable ~", "location")
;      add(errors, "Cannot assign expression of type ~ to ~ of type ~." << [
;         #pr(type!(value(c))), name, #pr(type!(exp(c)))])
;
;   defn bad-pred (c:LSIf) :
;      add(errors, "Expression of type ~ cannot be used as predicate to if statement." << [
;         #pr(type!(pred(c)))])
;
;   defn bad-match (c:LSMatch) :
;      add(errors, "Expression of type ~ cannot be used as argument to match statement." << [
;         #pr(type!(arg(c)))])
;
;   defn bad-length (e:LSRefArray) :
;      add(errors, "Expression of type ~ cannot be used as length." << [
;         #pr(type!(length(e)))])
;
;   defn bad-index (e:LSSlot) :
;      add(errors, "Expression of type ~ cannot be used as index." << [
;         #pr(type!(index(e)))])
;
;   defn not-lvalue (e:LSPtr) :
;      add(errors, "Cannot create pointer to non-lvalue expression.")
;
;   defn not-lvalue (c:LSSet) :
;      add(errors, "Cannot assign to non-lvalue expression.")
;
;   defn bad-deref (e:LSDeref) :
;      add(errors, "Cannot dereference expression of type ~." << [#pr(type!(exp(e)))])
;
;   defn bad-slot (e:LSSlot) :
;      add(errors, "Cannot access indexed slot in expression of type ~." << [#pr(type!(exp(e)))])
;
;   defn no-field (e:LSField) :
;      add(errors, "Expression of type ~ has no field named ~." << [
;         #pr(type!(exp(e))), name(e)])
;
;   defn bad-field (e:LSField) :
;      add(errors, "Cannot access named field ~ in expression of type ~." << [
;         name(e), #pr(type!(exp(e)))])
;
;   defn bad-call (e:LSDo|LSCallC) :
;      val name = name?(func(e), "Function ~", "Function")
;      add(errors, "~ of type ~ cannot be called with arguments of type ~@." << [
;         name, #pr(type!(func(e))), cms(#pr(map(type!, args(e))))])
;
;   defn not-func (e:LSDo|LSCallC) :
;      val name = name?(func(e), "Value ~", "Expression")
;      add(errors, "~ of type ~ cannot be called as a function." << [
;         name, #pr(type!(func(e)))])
;
;   defn bad-cast (e:LSCast) :
;      add(errors, "Expression of type ~ cannot be cast to ~." << [
;         #pr(type!(exp(e))), #pr(type!(e))])
;
;   defn bad-and-a (e:LSAnd) :
;      add(errors, "Expression of type ~ cannot be used as operand to and." << [
;         #pr(type!(a(e)))])
;
;   defn bad-and-b (e:LSAnd) :
;      add(errors, "Expression of type ~ cannot be used as operand to and." << [
;         #pr(type!(b(e)))])
;
;   defn bad-or-a (e:LSOr) :
;      add(errors, "Expression of type ~ cannot be used as operand to or." << [
;         #pr(type!(a(e)))])
;
;   defn bad-or-b (e:LSOr) :
;      add(errors, "Expression of type ~ cannot be used as operand to or." << [
;         #pr(type!(b(e)))])
;
;   ;====== Driver ======
;   do-ls(build-env, prog)
;   val prog* = map-ls(infer-tc, prog)
;   issue-errors()
;   prog*
;
;
;;============================================================
;;=============== General Utilities ==========================
;;============================================================
defstruct Fresh

defstruct TypeError <: Exception

defn uncap (t:Type) -> Type :
   match(t) :
      (t:TCap) : TVar(n(t))
      (t) : map(uncap, t)

defn uncap (t:LSType) -> LSType :
   match(t) :
      (t:RefT) : RefT(uncap(type(t)))
      (t) : map(uncap, t)

defn map<?T> (f:Int -> ?T, r:Range) :
   to-list(stream(f, r))
