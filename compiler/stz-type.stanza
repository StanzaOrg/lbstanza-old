defpackage stz/type :
   import core
   import verse
   import stz/il-ir
   import stz/tl-ir
   import stz/namemap
   import stz/infer
   import stz/type-calculus
   import stz/algorithms

;============================================================
;====================== Driver ==============================
;============================================================

public defn type-program (nm:NameMap, e:IExp) :
   val prog = to-tprog(nm, e)
   set-current-hierarchy(hier(prog))
   println("Infer types")
   val prog* = infer-types(prog)
   println(prog*)
   println("Report errors")
   report-errors(nm, prog*)   
   prog*

;============================================================
;=============== Conversion to Type IR ======================
;============================================================

definterface Assert
defstruct CorrectTArity <: Assert : (info:FileInfo, type: TOf)

defn to-tprog (nm:NameMap, e:IExp) :
   ;======== State ========
   val comms = Vector<TComm>()
   val asserts = Vector<Assert>()
   val errors = Vector<?>()
   val hier = THierarchy $ [
      `False => resolve-type(nm, `core, `False)
      `Int => resolve-type(nm, `core, `Int)
      `String => resolve-type(nm, `core, `String)
      `Symbol => resolve-type(nm, `core, `Symbol)
      `List => resolve-type(nm, `core, `List)
      `Tuple => resolve-type(nm, `core, `Tuple)]
      
   ;======== Utilities ========
   val #TN = TNone()
   val #CN = CNone()
   val #SN = SNone()
   val #IN = INone(FileInfo())
   defn #name (n:Int) : name(nm[n] as VarEntry)
   defn #info (n:Int) : info(nm[n] as VarEntry)
   defn #pr (t:Type) : printable(nm, t)
   defn #pr (ts:Streamable<Type>) : stream(#pr, ts)
   defn cms (xs:Streamable) : join(xs, ", ")
   defn add-comm (c:TComm) : add(comms, c)
   defn add-comm (cs:List<TComm>) : do(add-comm, cs)
   defn n! (es:List<IExp>) : map(n!, es)
   defn #comm (es:List<IExp>) : do(#comm, es)
   defn #type (es:List<IExp>) : map(#type, es)
   defn #exp (es:List<IExp>) : map(#exp, es)
   defn #farg (es:List<IExp>, ts:List<Type>) : map(#farg{_ ts}, es)
   defn #defn (defs:List<IDefn>) : map(#defn, defs)
   defn #defmethod (defs:List<IDefmethod>) : map(#defmethod, defs)
      
   defn n! (e:IExp) :
      match(e) :
         (e:VarN) : n(e)
         (e:LSVarN) : n(e)

   defn split-targs (es:List<IExp>) :
      val targs = Vector<Int>()
      val cargs = Vector<Int>()
      for e in es do :
         match(e) :
            (e:VarN) : add(targs, n(e))
            (e:ICap) : add(cargs, n!(name(e)))
      [to-list(targs), to-list(cargs)]

   ;======== Transformers ========
   defn #comm (e:IExp) -> False :
      match(e) :
         (e:IBegin) :
            #comm(exps(e))
         (e:IPublic) :
            #comm(exp(e))
         (e:IProtected) :
            #comm(exp(e))
         (e:IDefType) :
            add(hier, c, args*, p) where :
               val c = n!(class(e))
               val args* = n!(args(e))
               val p = #type(parent(e))
         (e:IDef) :
            add-comm(destructure-def(e))
         (e:IDefVar) :
            add-comm(TDefVar(n, t, v)) where :
               val n = n!(name(e))
               val t = #type(type(e))
               val v = #exp(value(e))
         (e:IDefn) :
            add-comm(#defn(e))
         (e:IDefmulti) :
            add-comm(TDefmulti(n, targs, cargs, a1*, a2*)) where :
               val n = n!(name(e))
               val [targs, cargs] = split-targs(targs(e))
               val a1* = #type(a1(e))
               val a2* = #type(a2(e))
         (e:IDefmethod) :
            add-comm(#defmethod(e))
         (e:ILoStanza) :
            false
         (e) :
            add-comm(TExpComm(#exp(e)))

   defn #type (e:IExp) -> Type :
      match(e) :
         (e:ITuple) : TTuple(#type(exps(e)))
         (e:IOf) :            
            val t = TOf(n!(class(e)), TTuple(#type(args(e))))
            add(asserts, CorrectTArity(info(e), t))
            t
         (e:IAnd) : TAnd(#type(a(e)), #type(b(e)))
         (e:IOr) : TOr(#type(a(e)), #type(b(e)))
         (e:IArrow) : TArrow(TTuple(#type(a1(e))), #type(a2(e)))
         (e:ICap) : TCap(n!(name(e)))
         (e:IGradual) : TGradual()
         (e:IVoid) : TBot()
         (e:INone) : TNone()
         (e:VarN) : TVar(n(e))
         (e:Raw) : TOf(n!(class(e)), TGradual())      

   defn #exp (e:IExp) -> TExp :
      match(e) :
         (e:IFn) :
            TFn(tail?(e), a1*, a2*, ns, body, #TN, #CN) where :
               val a1* = #type(a1(e))
               val a2* = #type(a2(e))
               val [ns, body] = destructure-args(args(e), body(e))
         (e:IMulti) :
            TMulti(fs, #TN, #CN) where :
               val fs = #exp(funcs(e)) as List<TFn>
         (e:IBegin) :
            if empty?(tail(exps(e))) :
               #exp(head(exps(e)))
            else :
               TSeq(a*, b*, #TN, #CN) where :
                  val a* = #exp(head(exps(e)))
                  val binfo = info(exps(e)[1])
                  val b* = #exp(IBegin(tail(exps(e)), binfo))
         (e:IMatch) :
            TMatch(args*, brs*, #TN, #CN) where :
               val args* = #exp(args(e))
               val brs* = #exp(branches(e)) as List<TBranch>
         (e:IBranch) :
            TBranch(ns, ts, body, #TN, #CN) where :
               val ts = #type(types(e))
               val [ns, body] = destructure-args(args(e), body(e))            
         (e:INew) :
            TNew(ms, t, #CN) where :
               val ms = #defmethod(methods(e) as List<IDefmethod>)
               val t = #type(class(e))
         (e:ISet) :
            TSet(r, v, #TN, #CN) where :
               val r = #exp(name(e)) as TRef
               val v = #exp(value(e))
         (e:IDo) :
            TDo(f*, args*, #TN, #CN) where :
               val f* = match(func(e)) :
                  (f:IOf) :
                     if empty?(args(f)) : #exp(class(f))
                     else : #farg(class(f), #type(args(f)))
                  (f) : #exp(f)
               val args* = #exp(args(e))            
         (e:IPrim) :
            TPrim(n, args*, #TN, #CN) where :
               val n = name(function(e) as IVar)
               val args* = #exp(args(e))
         (e:ICallLS) :
            TCallLS(n, targs, args*, #TN, #CN) where :
               val [n, targs] = match(function(e)) :
                  (f:IOf) : [n!(class(f)), #type(args(f))]
                  (f) : [n!(f), List()]
               val args* = #exp(args(e))
         (e:ILiteral) :
            TLiteral(value(e), #TN, #CN)
         (e:ITuple) :
            TTupleExp(#exp(exps(e)), #TN, #CN)
         (e:INone) :
            TElide(#TN, #CN)
         (e:VarN) :
            TRef(n(e), #TN, #CN)
         (e:Mix) :
            TMix(SNone(), #exp(exps(e)), #TN, #CN)
         (e:Let) :
            destructure-let(e)
         (e:LetRec) :
            TLetRec(defns*, body*, #TN, #CN) where :
               val defns* = #defn(defns(e) as List<IDefn>)
               val body* = #exp(body(e))
         (e) :
            error("Unsupported expression: ~" % [e])

   defn #farg (e:IExp, ts:List<Type>) -> TExp :
      match(e) :
         (e:VarN) :
            TArgExp(ref, ts, List(), #TN, #CN) where :
               val ref = TRef(n(e), #TN, #CN)
         (e:Mix) :
            TMix(#SN, exps*, #TN, #CN) where :
               val exps* = #farg(exps(e), ts)

   defn #defn (def:IDefn) :
      TDefn(tail?(def), n, targs, cargs, a1*, a2*, args*, body*) where :
         val n = n!(name(def))               
         val [targs, cargs] = split-targs(targs(def))
         val a1* = #type(a1(def))
         val a2* = #type(a2(def))
         val [args*, body*] = destructure-args(args(def), body(def))

   defn #defmethod (def:IDefmethod) :
      TDefmethod(tail?(def), n, m, thisn(def), targs, cargs, a1*, a2*, args*, body*) where :
         val n = fresh-n()
         val m = #exp(multi(def))
         val a1* = #type(a1(def))
         val a2* = #type(a2(def))
         val [targs, cargs] = split-targs(targs(def))
         val [args*, body*] = destructure-args(args(def), body(def))

   ;======== Destructuring ========
   defn destructure (args:List<IExp>, value:TExp) -> List<KeyValue<List<Int>, TExp>> :
      val [ns, bs] = destructure(args)
      List(ns => value, bs)
      
   defn destructure (args:List<IExp>) -> [List<Int>, List<KeyValue<List<Int>, TExp>>] :
      var bindings = List()
      defn #n (x:IExp) -> Int :
         match(x) :
            (x:ITuple) :
               val n = fresh-n()
               bindings = List(entry, bindings) where :
                  val exps* = map(#n, exps(x))
                  val entry = exps* => TRef(n, #TN, #CN)
               n   
            (x) :
               n!(x)
      val ns = map(#n, args)
      [ns, bindings]

   defn destructure-args (args:List<IExp>, fbody:IExp) -> [List<Int>, TExp] :
      val [ns, bs] = destructure(args)
      var body = #exp(fbody)
      for b in bs do :
         body = TLetTuple(ns, ts, v, body, #TN, #CN) where :
            val [ns, v] = [key(b), value(b)]
            val ts = map({#TN}, ns)         
      [ns, body]

   defn destructure-let (lete:Let) -> TExp :
      defn loop (name:IExp, type:IExp, v:IExp) :
         match(name, type) :
            (name:ITuple, type:INone) :
               var body = #exp(body(lete))
               for b in destructure(exps(name), #exp(v)) do :
                  body = TLetTuple(ns, ts, v, body, #TN, #CN) where :
                     val [ns, v] = [key(b), value(b)]
                     val ts = map({#TN}, ns)                  
               body
            (name:ITuple, type) :
               TLet(n, t*, v*, body*, #TN, #CN) where :
                  val n = fresh-n()
                  val t* = #type(type)
                  val v* = #exp(v)
                  val body* = loop(name, #IN, VarN(n, info(name)))               
            (name, type) :
               TLet(n!(name), t*, v*, body*, #TN, #CN) where :
                  val t* = #type(type)
                  val v* = #exp(v)
                  val body* = #exp(body(lete))               
      loop(name(d), type(d), value(d)) where :
         val d = def(lete) as IDef

   defn destructure-def (def:IDef) -> List<TDef|TDefTuple> :
      defn loop (name:IExp, type:IExp) :
         match(name, type) :
            (name:ITuple, type:INone) :
               val bs = destructure(exps(name), #exp(value(def)))
               for b in bs map :
                  TDefTuple(ns, ts, v) where :
                     val [ns, v] = [key(b), value(b)]
                     val ts = map({#TN}, ns)                  
            (name:ITuple, type) :
               List(TDef(n, t*, v*), rest*) where :
                  val n = fresh-n()
                  val t* = #type(type)
                  val v* = #exp(value(def))
                  val rest* = loop(name, #IN)
            (name, type) :
               list(TDef(n, t*, v*)) where :
                  val n = n!(name)
                  val t* = #type(type)
                  val v* = #exp(value(def))
      loop(name(def), type(def))

   ;======== Report Errors ========
   ;Check hierarchical cycles
   defn check-cycles () :
      defn classes (t:Type) -> List<Int> :
         match(t) :
            (t:TOf) : list(n(t))
            (t:TAnd) : append(classes(a(t)), classes(b(t)))
            (t:TNone) : list()
      val components = strong-components $
         for entry in hier stream :
            n(entry) => classes(parent(entry))
      val cycles = {to-list(_) as List<List<Int>>} $
         for c in components filter :
            c typeof List
      for c in cycles do :
         defn msg (ns:List<Int>) :
            join{_, "\n"} $
            for n in ns stream :
               "   ~ at ~" << [#name(n), #info(n)]
         add(errors, "Illegal cycle in type hierarchy. The following types are parents of each other.\n~@" << [
            msg(c)])

   ;Check type arities
   defn check-arities () :
      for assert in asserts do :
         match(assert) :
            (a:CorrectTArity) :
               val n = n(type(a))
               val arity = length(args(hier[n]))
               val targs = types(type(type(a)) as TTuple)
               if length(targs) != arity :
                  add(errors, "~: Incorrect number of type parameters for type ~. Expecting ~ but given ~@." << [
                     info(a), #name(n), arity, cms(#pr(targs))])

   ;Report errors
   defn report-errors () :
      check-cycles()
      check-arities()
      if not empty?(errors) :
         do(println, errors)
         throw(TypeError())

   ;======== Driver ========
   #comm(e)
   report-errors()
   TProg(hier, to-list(comms))   


;============================================================
;============== Generate Type Variables =====================
;============================================================
defn generate-type-vars (prog:TProg) :
   ;======== Environment ========
   val env = HashTable<Int,Type>({_})

   ;======== Utilities ========
   val #? = TGradual()
   val #fresh = Fresh()
   defn #freshv () : TUVar(fresh-n())
   defn #freshv (n:Int) : map(#freshv{}, 0 to n)
   defn #or (x:Type, y:Type) : y when x typeof TUVar|TNone else x
   defn #or (x:Type, f:Fresh) : #freshv() when x typeof TUVar|TNone else x
   defn #or (xs:List<Type>, y:Type|Fresh) : map(#or{_, y}, xs)
   defn #or (xs:List<Type>, ys:List<Type>) : map(#or, xs, ys)
   defn set-env (n:Int, t:Type) : env[n] = t
   defn set-env (n:Int, f:Fresh) : env[n] = #freshv()
   defn set-env (ns:List<Int>, y:Type|Fresh) : do(set-env{_, y}, ns)
   defn set-env (ns:List<Int>, ts:List<Type>) : do(set-env, ns, ts)   
   defn bexp (es:List<TExp>, c:Context) : map(bexp{_, c}, es)
   defn bcomm<?T> (cs:List<?T&TComm>) : map(bcomm, cs)
   defn bmulti (es:List<TExp>, targs:List<TVar>, c:Context) : map(bmulti{_, targs, c}, es)
   defn #types (es:List<TExp>) : map(type, es)
   
   defn env-a1 (n:Int) :
      defn* loop (t:Type) :
         match(t) :
            (t:TPoly) : loop(func(t))
            (t:TArrow) : types(a(t) as TTuple)
      loop(env[n])

   defn env-a2 (n:Int) :
      defn* loop (t:Type) :
         match(t) :
            (t:TPoly) : loop(func(t))
            (t:TArrow) : b(t)
      loop(env[n])

   defn fn-type (c:TDefmulti|TDefn|TDefmethod) :
      val t = TArrow(TTuple(a1*), a2*) where :
         val a1* = #or(a1(c), #?)
         val a2* = #or(a2(c), #fresh)
      if empty?(targs(c)) and empty?(cargs(c)) : t
      else : TPoly(targs(c), cargs(c), t)

   ;======== Specialized Type Constructors ========
   defn lit-type (v) :
      defn base (n:Int) : TOf(n, TTuple(List()))
      defn raw (n:Int) : TOf(n, TGradual())
      match(v) :
         (v:False) : base(False(hier(prog)))
         (v:Int) : base(Int(hier(prog)))
         (v:String) : base(String(hier(prog)))
         (v:Symbol) : base(Symbol(hier(prog)))
         (v:List) : raw(List(hier(prog)))

   defn prim-type (f:Symbol) :
      error("Primitive type not yet implemented.")


   ;======== Build top level environment ========
   defn build-environment () :
      for c in comms(prog) do :
         match(c) :
            (c:TDef) : set-env(n(c), #or(type(c), #fresh))
            (c:TDefTuple) : set-env(ns(c), #fresh)
            (c:TDefVar) : set-env(n(c), #or(type(c), #fresh))
            (c:TDefn) : set-env(n(c), fn-type(c))
            (c:TDefmulti) : set-env(n(c), fn-type(c))
            (c:TDefmethod) : set-env(n(c), fn-type(c))
            (c:TExpComm) : false
         
   ;======== Productions ========
   defn bcomm<?T> (c:?T&TComm) -> T :
      {_ as T&TComm} $ match(c) :
         (c:TDef) :
            TDef(n(c), t*, v*) where :
               val v* = bexp(value(c), CExp(#or(type(c), #?)))
               val t* = env[n(c)]
         (c:TDefTuple) :
            TDefTuple(ns(c), ts*, v*) where :
               val v* = bexp(value(c), CExp(#?))
               val ts* = map({env[_]}, ns(c))            
         (c:TDefVar) :
            TDefVar(n(c), t*, v*) where :
               val v* = bexp(value(c), CExp(#or(type(c), #?)))
               val t* = env[n(c)]            
         (c:TDefn) :
            set-env(args(c), map(uncap, env-a1(n(c))))
            TDefn(tail?(c), n(c), targs(c), cargs(c), a1*, a2*, args(c), body*) where :
               val body* = bexp(body(c), CExp(#or(a2(c), #?)))
               val a1* = env-a1(n(c))
               val a2* = env-a2(n(c))
         (c:TDefmulti) :
            TDefmulti(n(c), targs(c), cargs(c), a1*, a2*) where :
               val a1* = env-a1(n(c))
               val a2* = env-a2(n(c))               
         (c:TDefmethod) :
            set-env(args(c), map(uncap, env-a1(n(c))))
            TDefmethod(tail?(c), n(c), m*, thisn(c), targs(c), cargs(c), a1*, a2*, args(c), body*) where :
               val a1* = env-a1(n(c))
               val a2* = env-a2(n(c))
               val t* = uncap(TArrow(TTuple(a1*), a2*))
               val m* = bmulti(multi(c), map(TVar, targs(c)), CMulti(t*))        
               val body* = bexp(body(c), CExp(#or(a2(c), #?)))
         (c:TExpComm) :
            TExpComm(bexp(exp(c), CExp(#?)))
            
   defn bmulti (e:TExp, targs*:List<TVar>, c:Context) -> TExp :
      match(e) :
         (e:TRef) :
            val t = env[n(e)]
            val e* = TRef(n(e), t, c)
            val cargs* = match(t) :
               (t:TPoly) : #freshv(length(cargs(t)))
               (t:TArrow) : List()
            if empty?(targs*) and empty?(cargs*) : e*
            else : TArgExp(e*, targs*, cargs*, #freshv(), c)            
         (e:TMix) :
            TMix(sel*, exps*, #freshv(), c) where :
               val sel* = SVar(fresh-n())
               val exps* = bmulti(exps(e), targs*, c)

   defn bexp (e:TExp, c:Context) -> TExp :
      match(e) :
         (e:TLet) :
            TLet(n(e), type(v*), v*, body*, type(body*), c) where :
               val v* = bexp(value(e), CExp(#or(ntype(e), #?)))
               set-env(n(e), type(v*))
               val body* = bexp(body(e), c)
         (e:TLetVar) :
            TLetVar(n(e), t*, v*, body*, type(body*), c) where :
               val t* = #or(ntype(e), #fresh)
               val v* = bexp(value(e), CExp(t*))
               set-env(n(e), t*)
               val body* = bexp(body(e), c)
         (e:TLetTuple) :
            TLetTuple(ns(e), ts*, v*, body*, type(body*), c) where :
               val ts* = #or(ntypes(e), #fresh)
               val v* = bexp(value(e), CExp(#?))
               set-env(ns(e), ts*)
               val body* = bexp(body(e), c)
         (e:TLetRec) :
            TLetRec(defns*, body*, type(body*), c) where :
               for def in defns(e) do :
                  set-env(n(def), fn-type(def))
               val defns* = for d in defns(e) map :
                  TDefn(tail?(d), n(d), targs(d), cargs(d), a1*, a2*, args(d), body*) where :
                     val a1* = env-a1(n(d))
                     val a2* = env-a2(n(d))
                     set-env(args(d), a1*)
                     val body* = bexp(body(d), CExp(#or(a2(d), #?)))
               val body* = bexp(body(e), c)
         (e:TFn) :
            TFn(tail?(e), a1*, a2*, args(e), body*, t*, c) where :
               val a1* = #or(a1(e), #fresh)
               val a2* = #or(a2(e), #fresh)
               val t* = TArrow(TTuple(a1*), a2*)
               set-env(args(e), a1*)
               val body* = bexp(body(e), CExp(#or(a2(e), #fresh)))
         (e:TMulti) :
            TMulti(funcs*, type*, c) where :
               val funcs* = bexp(funcs(e), c) as List<TFn>
               val type* = reduce(TAnd, #types(funcs*))
         (e:TSeq) :
            TSeq(a*, b*, type(b*), c) where :
               val a* = bexp(a(e), CExp(#?))
               val b* = bexp(b(e), c)
         (e:TMatch) :         
            TMatch(args*, branches*, #freshv(), c) where :
               val args* = bexp(args(e), CExp(#?))
               val branches* = for b in branches(e) map :
                  TBranch(args(b), atypes*, body*, type(body*), c) where :
                     val atypes* = #or(atypes(b), #types(args*))
                     set-env(args(b), atypes*)
                     val body* = bexp(body(b), c)
         (e:TNew) :
            TNew(ms*, type(e), c) where :
               val ms* = bcomm(methods(e))
         (e:TArgExp) :
            TArgExp(e*, targs(e), cargs*, #freshv(), c) where :
               val t = env[n(ref(e))]
               val e* = TRef(n(ref(e)), t, c)
               val cargs* = match(t) :
                  (t:TPoly) : #freshv(length(cargs(t)))
                  (t) : List()
         (e:TRef) :
            val t = env[n(e)]
            val e* = TRef(n(e), t, c)
            match(t) :
               (t:TPoly) :
                  TArgExp(e*, List(), #freshv(n), #freshv(), c) where :
                     val n = length(cargs(t))
               (t) : e*
         (e:TCast) :
            TCast(exp*, type(e), c) where :
               val exp* = bexp(exp(e), CExp(#?))
         (e:TSet) :
            TSet(ref*, v*, lit-type(false), c) where :
               val ref* = bexp(ref(e), CExp(#?)) as TRef
               val v* = bexp(value(e), CExp(type(ref*)))
         (e:TDo) :         
            TDo(f*, args*, #freshv(), c) where :
               val args* = map(bexp{_, CExp(#freshv())}, args(e))
               val ret* = match(c) :
                  (c:CExp) : type(c)
                  (c:CDo) : TArrow(TTuple(args(c)), ret(c))
               val f* = bexp(func(e), CDo(#types(args*), ret*, #freshv()))
         (e:TPrim) :
            TPrim(function(e), args*, t*, c) where :
               val args* = bexp(args(e), CExp(#?))
               val t* = prim-type(function(e))
         (e:TCallLS) :
            TCallLS(function(e), targs(e), args*, #?, c) where :
               val args* = bexp(args(e), CExp(#?))
         (e:TLiteral) :
            TLiteral(value(e), lit-type(value(e)), c)
         (e:TTupleExp) :
            TTupleExp(exps*, t*, c) where :
               val exps* = map(bexp{_, CExp(#freshv())}, exps(e))
               val t* = TTuple(#types(exps*))
         (e:TMix) :
            TMix(sel*, exps*, #freshv(), c) where :
               val sel* = SVar(fresh-n())
               val exps* = match(c) :
                  (c:CExp) : bexp(exps(e), c)
                  (c:CDo) : bexp(exps(e), CDo(args(c), ret(c), TNone()))
         (e:TElide) :
            e

   ;Return new program
   build-environment()
   TProg(hier(prog), bcomm(comms(prog)))


;============================================================
;============== Generate Type Constraints ===================
;============================================================
defstruct TupleF : ()
defstruct SubF : (ti:Type, t:Type)
defstruct ArgsF : ()
defstruct RetF : (n:Int)

defn infer-types (prog:TProg) :
   ;======== State ========
   val exp-env = HashTable<Int,Type>({_})
   val constraints = Vector<Constraint>()
   
   ;======== Utilities ========
   val #? = TGradual()
   val #fresh = Fresh()
   defn expected (n:Int) : get?(exp-env, n, false)
   defn set-expected (n:Int, t:Type) : exp-env[n] = t
   defn set-expected (ns:List<Int>, ts:List<Type>) : do(set-expected, ns, ts)
   defn addc (c:Constraint) : add(constraints, c)   
   defn #or (x:Type, f:Fresh) : TUVar(fresh-n()) when x typeof TUVar else x
   defn #or (xs:List<Type>, f:Fresh) : map(#or{_, f}, xs)
   defn #ctxts (es:List<TExp>) : map(ctxt, es)
   defn #types (es:List<TExp>) : map(type, es)
   defn #types (cs:List<CExp>) : map(type, cs)
   defn #super (a:False, b:Type) : false
   defn #equal (ats:List<Type>, bts:List<Type>) : do(#equal, ats, bts)
   defn #equal (ats:List<Type>, b:Type) : do(#equal{_, b}, ats)
   defn #flow (rs:List<TUVar>, t:Type, w:Type) : do(#flow{_, t, w}, rs)
   defn gen-exp (es:Streamable<TExp>) : do(gen-exp, es)
   defn gen-comm (cs:Streamable<TComm>) : do(gen-comm, cs)

   defn #tuple-cap (t:TUVar, i:Int, m:Int) :
      TTuple $ for j in 0 to m map :
         if j == i : TCap(n(t))
         else : #?

   defn #targ-apply (t*:Type, targs*:List<Type>, cargs*:List<TUVar>) :
      label<Type> return :
         match(t*) :
            (t*:TPoly) :
               if length(targs(t*)) != length(targs*) :
                  return(TNone())
               val env = map(KeyValue, ns, ts) where :
                  val ns = append(targs(t*), cargs(t*))
                  val ts = append(targs*, cargs*)
               defn sub (t:Type) :
                  match(t) :
                     (t:TCap) : TCap(n(lookup!(env, n(t)) as TUVar))
                     (t:TVar) : lookup(env, n(t), t)
                     (t) : map(sub, t)
               TArrow(a*, b*) where :
                  val a* = sub(a(func(t*)))
                  val b* = #sub(b(func(t*)), env)
            (t*) :
               return(TNone())   
      
   ;======== Constraint Generation Language ========
   ;Propagate type upwards towards a subform
   defn #upto (r:Type, w:Type, f:SubF) :
      defn sub (t:Type, n*:Int, t*:Type) :
         match(t) :
            (t:TUVar) : 
               if n(t) == n* : t*
               else : #?
            (t) : map(sub{_, n*, t*}, t)            
      match(r, ti(f)) :
         (r:TUVar, ti:TUVar) :
            val t* = sub(t(f), n(ti), TCap(n(r)))
            #cap(r, t*, w)
         (r, ti) :
            false

   ;Propagate type upwards towards elements of tuple form
   defn #upto (rs:List<TUVar>, w:Type, f:TupleF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         #cap(r, #tuple-cap(r, i, n), w)
   
   ;Propagate type downwards towards arguments of function
   defn #downto (rs:List<TUVar>, w:Type, f:ArgsF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         val a1 = #tuple-cap(r, i, n)
         #cap(r, w, TArrow(a1, #?))

   ;Propagate type downwards towards return of function   
   defn #downto (r:TUVar, w:Type, f:RetF) :
      val a = TTuple(map({#?}, 0 to n(f)))
      val b = TCap(n(r))
      #cap(r, w, TArrow(a, b))

   ;Propagate type downwards towards elements of tuple form
   defn #downto (rs:List<TUVar>, w:Type, f:TupleF) :
      val n = length(rs)
      for (r in rs, i in 0 to n) do :
         #cap(r, w, #tuple-cap(r, i, n))

   ;Create supertype constraint
   defn #super (a:Type, b:Type) :
      match(a, b) :
         (a, b:TNone) : false
         (a:TUVar, b) : addc(SuperConstraint(n(a), b))
         (a, b) : false

   ;Create equality constraint
   defn #equal (a:Type, b:Type) :
      match(a) :
         (a:TUVar) : addc(EqualConstraint(n(a), b))
         (a) : false
   
   ;Create capture constraint
   defn #cap (r:TUVar, x:Type, y:Type) :
      addc(FlowConstraint(n(r), x, y))

   ;Create a selection constraint
   defn #select (s:SVar, ats:List<Type>, bt:Type) :
      addc(SelConstraint(n(s), ats, bt))

   ;Create a method selection constraint
   defn #select-method (s:SVar, at:Type, bts:List<Type>) :
      addc(SelMethodConstraint(n(s), at, bts))

   ;Create a method pick constraint
   defn #pick (t:TUVar, s:SVar, ys:List<Type>) :
      addc(PickConstraint(n(t), n(s), ys))

   ;Create a substitution constraint
   defn #sub (t:Type, env:List<KeyValue<Int,Type>>) :
      val r = TUVar(fresh-n())
      addc(SubConstraint(n(r), t, env))
      r

   ;Create a mixture constraint
   defn #mix (t:TUVar, s:SVar, ats:List<Type>) :
      addc(MixConstraint(n(t), n(s), ats))
   
   ;Create a threshold constraint
   defn #thresh (s:SVar) :
      val r = SVar(fresh-n())
      addc(ThresholdConstraint(n(r), n(s)))
      r
   
   ;Create a flow constraint
   defn #flow (r:TUVar, t:Type, w:Type) :
      ;Isolate a single capture variable
      defn isolate (n*:Int, t:Type) :
         match(t) :
            (t:TCap) :
               if n(t) == n* : t
               else : TUVar(n(t))
            (t) : map(isolate{n*, _}, t)
      addc(FlowConstraint(n(r), t*, w)) where :      
         val t* = isolate(n(r), t)

   ;Set expected type of a context
   defn #expect (c:Context, t:Type) :
      match(c) :
         (c:CDo) : #super(exp(c), t)
         (c) : false  

   ;======== Productions ========
   ;Constraint Generation for Commands
   defn gen-comm (c:TComm) :
      ;Constraints for subexpressions
      gen-exp $ match(c) :
         (c:TDef|TDefTuple|TDefVar) : value(c)
         (c:TDefn) : body(c)
         (c:TDefmethod) : [multi(c), body(c)]
         (c:TExpComm) : exp(c)
         (c:TDefmulti) : []
      ;Additional constraints
      match(c) :
         (c:TDef) : #equal(type(c), type(value(c)))
         (c:TDefTuple) : #downto(types(c) as List<TUVar>, type(value(c)), TupleF())
         (c:TDefVar) : #super(type(c), type(value(c)))
         (c:TDefn) : #equal(a2(c), type(body(c)))
         (c:TDefmethod) : #equal(a2(c), type(body(c)))
         (c) : false

   ;Constraint generation for expressions
   defn gen-exp (e:TExp) :
      ;Specialized constraints
      match(e, ctxt(e)) :
         (e:TFn, c:CExp) :
            ;Expected return type
            #upto(exp-a2, type(c), SubF(a2(e), type(e))) where :
               val exp-a2 = type(ctxt(body(e)) as CExp)
            ;Infer argument types
            for t in a1(e) do :
               #upto(t, type(c), SubF(t, type(e)))
            ;Inferred return type
            #equal(a2(e), type(body(e)))
         (e:TFn, c:CDo) :
            ;Expected return type
            #equal(exp-a2, ret(c)) where :
               val exp-a2 = type(ctxt(body(e)) as CExp)
            ;Infer argument types
            if length(args(e)) == length(args(c)) :
               #equal(a1(e), args(c))
            ;Inferred return type
            #equal(a2(e), type(body(e)))
            ;Expected argument types
            set-expected(args(e), exp-a1) where :
               val exp-a1 = #or(a1(e), #fresh)
               #super(exp(c), TArrow(TTuple(exp-a1), a2(e)))
         (e:TMulti, c:CExp) :
            for f in funcs(e) do :
               ;Expected return type
               #upto(exp-a2, type(c), SubF(a2(f), type(e))) where :
                  val exp-a2 = type(ctxt(body(f)) as CExp)
               ;Infer argument types
               for t in a1(f) do :
                  #upto(t, type(c), SubF(t, type(e)))
         (e:TMulti, c:CDo) :
            for f in funcs(e) do :
               if length(args(f)) == length(args(c)) : gen-exp(f)
               else : #equal(a1(f), TBot())
         (e:TSet, c) :
            #super(type(ref(e)), type(value(e)))
            #expect(c, type(e))
         (e:TDo, c) :
            val fc = ctxt(func(e)) as CDo
            ;Expected argument types
            #downto(exp-a1, exp(fc), ArgsF()) where :
               val cs = #ctxts(args(e)) as List<CExp>
               val exp-a1 = #types(cs) as List<TUVar>
            ;Infer return type
            #downto(type(e) as TUVar, exp(fc), RetF(length(args(e))))
            ;Set expected type
            #expect(c, type(e))
         (e:TPrim, c) :
            #expect(c, type(e))
         (e:TCallLS, c) :
            #expect(c, type(e))
         (e:TTupleExp, c:CExp) :
            ;Expected expression types
            #upto(exp-a1, type(c), TupleF()) where :
               val cs = #ctxts(exps(e)) as List<CExp>
               val exp-a1 = #types(cs) as List<TUVar>
            #expect(c, type(e))
         (e:TRef, c:CExp) :
            ;Expected type
            #super(expected(n(e)), type(c))
         (e:TRef, c:CDo) :
            ;Expected type
            #super(expected(n(e)), TArrow(TTuple(args(c)), ret(c)))
            #super(exp(c), type(e))            
         (e:TArgExp, c:CExp) :
            val cvs = cargs(e) as List<TUVar>
            ;Apply parametric types
            val t* = #targ-apply(type(ref(e)), targs(e), cvs)
            #equal(type(e), uncap(t*))
            ;Capture cargs 
            #flow(cvs, t*, type(c))
         (e:TArgExp, c:CDo) :
            val cvs = cargs(e) as List<TUVar>
            ;Apply parametric types
            val t* = #targ-apply(type(ref(e)), targs(e), cvs)
            #equal(type(e), uncap(t*))
            ;Capture cargs
            #flow(cvs, t*, TArrow(TTuple(args(c)), #?))
            ;Expected type
            #super(exp(c), type(e))
         (e:TArgExp, c:CMulti) :
            val cvs = cargs(e) as List<TUVar>
            ;Apply parametric types
            val t* = #targ-apply(type(ref(e)), targs(e), cvs)
            #equal(type(e), uncap(t*))
            ;Capture cargs
            #flow(cvs, t*, type(c))
         (e:TMix, c:CExp) :
            val sv = sel(e) as SVar
            ;Compute Selection
            #select(sv, #types(exps(e)), type(c))
            ;Compute Mixture
            #mix(type(e) as TUVar, #thresh(sv), #types(exps(e)))
         (e:TMix, c:CDo) :
            val sv = sel(e) as SVar
            ;Compute Selection
            #select(sv, #types(exps(e)), TArrow(TTuple(args(c)), #?))
            ;Compute Mixture
            #mix(type(e) as TUVar, sv, #types(exps(e)))
            ;Expected type
            #super(exp(c), type(e))
         (e:TMix, c:CMulti) :
            val sv = sel(e) as SVar
            ;Compute selection
            #select-method(sv, type(c), #types(exps(e)))
            ;Compute pick
            #pick(type(e) as TUVar, sv, #types(exps(e)))
         (e:TMatch, c) :
            #equal(type(e), reduce(TOr, #types(branches(e))))
         (e:TLetVar, c) :
            #super(ntype(e), type(value(e)))
         (e:TLetTuple, c) :
            #downto(ts, vt, TupleF()) where :
               val ts = ntypes(e) as List<TUVar>
               val vt = type(value(e))
         (e, c) :
            false

      ;Constraints for subexpressions and commands
      match(e) :
         (e:TLetRec) :
            gen-comm(defns(e))
            gen-exp(body(e))
         (e:TMulti) :
            for f in funcs(e) do :
               gen-exp(body(f))
         (e:TNew) :
            gen-comm(methods(e))
         (e:TArgExp) :
            false
         (e) :
            do(gen-exp, e)

   ;======== Fill Results =========
   defn fill (prog:TProg, results:SolveResults) :
      defn fill (s:Selection) -> Selection :
         match(s) :
            (s:SVar) : sels(results)[n(s)]
            (s) : s
      defn fill (t:Type) -> Type :
         match(t) :
            (t:TUVar) : get?(types(results), n(t), t)
            (t) : map(fill, t)
      defn fill (e:TExp) -> TExp :
         val e2 = map(fill{_:Type}, e)
         val e3 = map(fill{_:TExp}, e2)
         val e4 = map(fill{_:TComm}, e3)
         map(fill{_:Selection}, e4)
      defn fill (c:TComm) -> TComm :
         val c2 = map(fill{_:TExp}, c)
         map(fill{_:Type}, c2)
      TProg(hier(prog),
            map(fill, comms(prog)))
   
   ;======== Driver ========
   println("generate type vars")
   val prog* = generate-type-vars(prog)
   println(prog*)
   println("generate constraints")
   gen-comm(comms(prog*))
   println("solve constraints")
   val results = solve(constraints)
   println("fill results")
   fill(prog*, results)

;============================================================
;=================== Print a Type ===========================
;============================================================

defn printable (nm:NameMap, t:Type) :
   defn #name (n:Int) : name(nm[n] as VarEntry)
   defn #carg (n:Int) : "?~" << [#name(n)]
   defn cms (xs:Streamable) : join(xs, ", ")
   defn lvl (t:Type) :
      match(t) :
         (t:TArrow|TPoly) : 0
         (t:TOr) : 1
         (t) : 2         
   defn p0 (t:Type) : pt(t, 0)
   defn p0 (ts:List<Type>) : map(p0, ts)
   defn p1 (t:Type) : pt(t, 1)
   defn p2 (t:Type) : pt(t, 2)
   defn pt (t:Type, l:Int) :
      val r = match(t) :
         (t:TArrow) :
            match(a(t)) :
               (a:TTuple) :
                  if length(types(a)) == 1 : "~ -> ~" << [p1(types(a)[0]), p0(b(t))]
                  else : "(~@) -> ~" << [cms(p0(types(a))), p0(b(t))]
               (a:TGradual) : "(? ...) -> ~" << [p0(b(t))]
         (t:TPoly) :
            val ts = concat(stream(#name, targs(t)), stream(#carg, cargs(t)))
            val f = p0(func(t))
            "<~@> . ~" << [cms(ts), f]
         (t:TOr) :
            "~|~" << [p1(a(t)), p1(b(t))]
         (t:TAnd) :
            "~&~" << [p2(a(t)), p2(b(t))]
         (t:TOf) :
            match(type(t)) :
               (ta:TTuple) :
                  if empty?(types(ta)) : #name(n(t))
                  else : "~<~@>" << [#name(n(t)), cms(p0(types(ta)))]
               (ta:TGradual) :
                  #name(n(t))
         (t:TGradual) :
            "?"
         (t:TBot) :
            "Void"
         (t:TVar) :
            #name(n(t))
         (t:TUVar) :
            "?"
         (t:TCap) :
            "?~" << [#name(n(t))]
         (t:TNone) :
            "none"
         (t:TTuple) :
            "[~@]" << [cms(p0(types(t)))]
      if lvl(t) < l : "(~)" << [r]
      else : r
   p0(t)
   
;============================================================
;=================== Report Errors ==========================
;============================================================

defn report-errors (nm:NameMap, prog:TProg) :
   ;======== State ========
   val errors = Vector<Vector<?>>()   

   ;======== Utilities ========
   defn unknown? (x:True|False|Maybe|Unknown) : x typeof Unknown
   defn not-st? (x:Type, y:Type) : subtype?(x, y) == false
   defn not-st? (x:TExp, y:Type) : not-st?(type(x), y)
   defn not-st? (x:Type, y:TExp) : not-st?(x, type(y))
   defn not-st? (x:TExp, y:TExp) : not-st?(type(x), type(y))
   defn cms (xs:Streamable) : join(xs, ", ")
   defn #pr (t:Type) : printable(nm, t)
   defn #pr (ts:Streamable<Type>) : stream(#pr, ts)
   
   defn add-error (p:Int, msg) :
      while length(errors) < p + 1 :
         add(errors, Vector<?>())
      add(errors[p], msg)

   defn unresolved? (t:Type) :
      match(t) :
         (t:TUVar) : true
         (t) : any?(unresolved?, children(t))

   defn resolved-pr (x) :
      match(x) :
         (xs:Streamable) : reduce(max{_, resolved-pr(_)}, 0, x)
         (x:Type) : 1 when unresolved?(x) else 0
         (x:TExp) : resolved-pr(type(x))

   defn all-pairs<?T> (xs:Streamable<?T>) :
      val xs* = to-array(xs)
      generate<[T, T]> :
         for i in 0 to length(xs*) do :
            for j in (i + 1) to length(xs*) do :
               yield([xs*[i], xs*[j]])

   defn has-name? (n:Int) :
      key?(nm,n) and (nm[n] typeof VarEntry)

   defn #name (n:Int) :
      name(nm[n] as VarEntry)
      
   ;======== Phase 1: Error Detection ========
   defn check (c:TComm) :
      match(c) :
         (c:TDef) :
            if unresolved?(type(c)) :
               no-inferred-type(c)
            else if not-st?(value(c), type(c)) :
               bad-assign(c)
         (c:TDefTuple) :
            val t? = TTuple(map(TGradual{}, types(c)))
            val t = TTuple(types(c))
            if not-st?(value(c), t?) :
               not-tuple(c)
            else if any?(unresolved?, types(c)) :
               no-inferred-type(c)
            else if not-st?(value(c), t) :
               bad-assign(c)
         (c:TDefVar) :
            if unresolved?(type(c)) :
               no-inferred-type(c)
            else if (value(c) not-typeof TElide) and
                    not-st?(value(c), type(c)) :
               bad-assign(c)
         (c:TDefn) :
            if unresolved?(a2(c)) :
               no-inferred-ret(c)
            else if not-st?(body(c), a2(c)) :
               bad-ret(c)
         (c:TDefmethod) :
            if unresolved?(a2(c)) :
               no-inferred-ret(c)
            else if not-st?(body(c), a2(c)) :
               bad-ret(c)
            check-multi(multi(c))               
            val t = type(ctxt(multi(c)) as CMulti)
            match(t, type(multi(c))) :
               (t:TArrow, tm:TArrow) :
                  if submethod?(t, tm) == false :
                     bad-method(c)
               (t, tm) : false      
         (c) :
            false
      do(check{_:TExp}, c)   

   defn check-multi (e:TExp) :
      match(e) :
         (e:TRef) :
            false
         (e:TMix) :
            val ss = sels(sel(e) as SSel)
            if all?({_ typeof False}, ss) :
               no-method(e)
            else :
               ;Check subexpressions
               val es = to-list $
                  for (e in exps(e), s in ss) filter :
                     s != false
               do(check-multi, es)
               ;Check Ambiguity
               amb-method(e) when length(es) > 1
         (e:TArgExp) :
            defn tarity (t:Type) :
               match(t) :
                  (t:TArrow) : 0
                  (t:TPoly) : length(targs(t))
            if tarity(type(ref(e))) != length(targs(e)) :
               bad-tarity(e)
            else if any?(unresolved?, cargs(e)) :
               no-capture(e)
               
   defn check (e:TExp) :
      match(e) :
         (e:TLet) :
            if unresolved?(ntype(e)) :
               no-inferred-type(e)
            else if not-st?(value(e), ntype(e)) :
               bad-assign(e)
         (e:TLetVar) :
            if unresolved?(ntype(e)) :
               no-inferred-type(e)
            else if value(e) not-typeof TElide and
                    not-st?(value(e), ntype(e)) :
               bad-assign(e)
         (e:TLetTuple) :
            val t? = TTuple(map(TGradual{}, ntypes(e)))
            val t = TTuple(ntypes(e))
            if not-st?(value(e), t?) :
               not-tuple(e)
            else if any?(unresolved?, ntypes(e)) :
               no-inferred-type(e)
            else if not-st?(value(e), t) :
               bad-assign(e)
         (e:TFn) :
            if any?(unresolved?, a1(e)) :
               no-inferred-args(e)
            if unresolved?(a2(e)) :
               no-inferred-ret(e)
            else if not-st?(body(e), a2(e)) :
               bad-ret(e)
         (e:TMatch) :
            if none?(unresolved?{type(_)}, args(e)) :
               for b in branches(e) do :               
                  if any?({not overlap?(type(_), _)}, args(e), atypes(b)) :
                     bad-branch(e, b)
         (e:TCast) :
            if not unresolved?(type(exp(e))) :
               if not overlap?(type(exp(e)), type(e)) :
                  bad-cast(e)
         (e:TSet) :
            if not-st?(value(e), ref(e)) :
               bad-set(e)
         (e:TDo) :
            val t? = TArrow(TGradual(), TGradual())
            val t = TArrow(TTuple(map(type, args(e))), TGradual())
            if type(func(e)) not-typeof TNone :
               if not-st?(func(e), t?) :
                  not-function(e)
               else if not-st?(func(e), t) :
                  bad-call(e)
         (e:TMix) :
            val ss = sels(sel(e) as SSel)
            if all?({_ typeof False}, ss) :
               no-mix(e)
            else if any?(unknown?, ss) :
               amb-mix(e)
            else :
               ;Check subexpressions
               val es = to-list $
                  for (e in exps(e), s in ss) filter :
                     s != false
               do(check{_:TExp}, es)
               ;Check disjoint
               (amb-mix(e) when not disjoint) where :
                  val u = match(ctxt(e)) :
                     (c:CExp) : type(c)
                     (c:CDo) : TArrow(TTuple(args(c)), TGradual())
                  val ts = map(type, es)
                  val disjoint = for [x, y] in all-pairs(ts) all? :
                     disjoint?(x as TArrow, y as TArrow, u)
         (e:TArgExp) :
            defn tarity (t:Type) :
               match(t) :
                  (t:TArrow) : 0
                  (t:TPoly) : length(targs(t))
            if tarity(type(ref(e))) != length(targs(e)) :
               bad-tarity(e)
            else if any?(unresolved?, cargs(e)) :
               no-capture(e)
         (e) :
            false
      if e not-typeof TMix :      
         do(check{_:TExp}, e)
      do(check{_:TComm}, e)

   ;======== Phase 2: Report Creation ========
   defn name? (x:Int|List<Int>, str:String, default:String) :
      match(x) :
         (xs:List<Int>) :
            if all?(has-name?, xs) : str << [cms(stream(#name, xs))]
            else : default
         (x:Int) :
            if has-name?(x) : str << [#name(x)]
            else : default
   defn def-name (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      match(c) :
         (c:TDef) : name?(n(c), "global value ~", "global value")
         (c:TDefVar) : name?(n(c), "global variable ~", "global variable")
         (c:TDefTuple) : name?(ns(c), "global values ~@", "tuple elements")
         (c:TLet) : name?(n(c), "value ~", "value")
         (c:TLetVar) : name?(n(c), "variable ~", "variable")
         (c:TLetTuple) : name?(ns(c), "values ~@", "tuple elements")         
   defn no-inferred-type (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      val p = resolved-pr(type(value(c)))
      add-error(p, "Could not infer type of ~." << [def-name(c)])

   defn def-type (c:TLetTuple|TLetVar|TLet|TDefTuple|TDefVar|TDef) :
      match(c) :
         (c:TLetVar|TLet) : ntype(c)
         (c:TDef|TDefVar) : type(c)
         (c:TLetTuple) : TTuple(ntypes(c))
         (c:TDefTuple) : TTuple(types(c))
   defn bad-assign (c:TDef|TDefVar|TLet|TLetVar|TLetTuple|TDefTuple) :
      val name = def-name(c)
      val t = def-type(c)
      val p = resolved-pr([t, value(c)])
      add-error(p, "Cannot assign expression of type ~ to ~ with declared type ~." <<
         [#pr(type(value(c))), name, #pr(t)])

   defn not-tuple (c:TDefTuple|TLetTuple) :
      val n = length(ns(c))
      val p = resolved-pr(value(c))
      add-error(p, "Cannot deconstruct expression of type ~ into tuple of length ~." <<
         [#pr(type(value(c))), n])

   defn no-inferred-args (e:TFn) :
      val w = match(ctxt(e)) :
         (c:CExp) : type(c)
         (c:CDo) : TArrow(TTuple(args(c)), TGradual())
      val p = resolved-pr(w)
      for (a in args(e), t in a1(e), i in 0 to false) do :
         if unresolved?(t) :
            val name = name?(a, "argument ~", "argument ~" % [i])
            add-error{p, _} $
               if unresolved?(w) : "Could not infer type for ~." << [name]
               else : "Could not infer type for ~. Function used in context expecting type ~." << [name, #pr(w)]

   defn no-inferred-ret (e:TFn|TDefn|TDefmethod) :
      val p = resolved-pr([a1(e), body(e)])
      val name = match(e) :
         (e:TFn) : "anonymous function"
         (e:TDefn) : name?(n(e), "function ~", "function")
         (e:TDefmethod) : "method"
      add-error(p, "Could not infer return type for ~." << [name])

   defn bad-ret (f:TDefn|TDefmethod|TFn) :
      val name = match(f) :
         (f:TDefn) : name?(n(f), "function ~", "function")
         (f:TDefmethod) : "method"
         (f:TFn) : "anonymous function"
      defn loop (e:TExp) :
         match(e) :
            (e:TLet|TLetVar|TLetTuple|TLetRec|TBranch) : loop(body(e))
            (e:TMatch) : do(loop, branches(e))
            (e) : 
               val p = resolved-pr([a2(f), e])
               if not-st?(e, a2(f)) :
                  add-error(p, "Cannot return an expression of type ~ for ~ with declared return type ~." << [
                     #pr(type(e)), name, #pr(a2(f))])
      loop(body(f))

   defn bad-branch (e:TMatch, b:TBranch) :
      val bargs = stream({[_, _]}, args(b), atypes(b))
      for (a in args(e), [ba, bt] in bargs, i in 0 to false) do :
         if not overlap?(type(a), bt) :
            val name = name?(ba, "argument ~", "argument ~" % [i])
            add-error(0, "Branch type ~ for ~ cannot match against match argument of type ~." << [
               #pr(bt), name, #pr(type(a))])

   defn bad-cast (e:TCast) :
      add-error(0, "Cannot cast expression of type ~ to type ~." << [
         #pr(type(exp(e))), #pr(type(e))])

   defn bad-set (e:TSet) :
      val p = resolved-pr([ref(e), value(e)])
      val name = name?(n(ref(e)), "variable ~", "variable")
      add-error(p, "Cannot assign expression of type ~ to ~ with declared type ~." << [
         #pr(type(value(e))), name, #pr(type(ref(e)))])

   defn not-function (e:TDo) :
      val p = resolved-pr(func(e))
      val name = match(func(e)) :
         (f:TRef) : name?(n(f), "Value ~", "Value")
         (f) : "Expression"
      add-error(p, "~ of type ~ cannot be called as a function." << [
         name, #pr(type(func(e)))])

   defn bad-call (e:TDo) :
      val p = resolved-pr([func(e), args(e)])
      val name = get-name(func(e)) where :
         defn get-name (e:TExp) :
            match(e) :
               (e:TRef) : name?(n(e), "function ~", "function")
               (e:TMix) : get-name(head(exps(e)))
               (e) : "expression"
      add-error(p, "Cannot call ~ of type ~ with arguments of type ~@" << [
         name, #pr(type(func(e))), cms(map(#pr{type(_)}, args(e)))])

   defn get-ref (e:TExp) :
      match(e) :
         (e:TRef) : e
         (e:TArgExp) : ref(e)
   defn possibilities (exps:Streamable<TExp>) :      
      join{_, "\n"} $ for e in exps stream :
         val r = get-ref(e)
         "   ~: ~" << [#name(n(r)), #pr(type(r))]                        
   defn no-mix (e:TMix) :
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      match(ctxt(e)) :
         (c:CDo) :
            val p = resolved-pr([exps(e), args(c)])
            add-error(p, "No appropriate function ~ for arguments of type ~@. Possibilities are:\n~@" << [
               name, cms(#pr(args(c))), possibilities(exps(e))])
         (c:CExp) :
            val p = resolved-pr([exps(e), type(c)])
            add-error(p, "No appropriate function ~ for expected type ~. Possibilities are:\n~@" << [
               name, #pr(type(c)), possibilities(exps(e))])
   defn amb-mix (e:TMix) :
      val exps* = to-list $
         for (e in exps(e), s in sels(sel(e) as SSel)) filter :
            s != false
      val name = #name(n(r)) where :
         val r = get-ref(head(exps*))
      match(ctxt(e)) :
         (c:CDo) :
            val p = resolved-pr([map(get-ref, exps*), args(c)])
            add-error(p, "Ambiguous call to overloaded function ~ with arguments of type ~@. Possibilities are:\n~@" << [
               name, cms(#pr(args(c))), possibilities(exps*)])
         (c:CExp) :      
            val p = resolved-pr([map(get-ref, exps*), type(c)])
            add-error(p, "Ambiguous reference to overloaded function ~ for expected type ~. Possibilities are:\n~@" << [
               name, #pr(type(c)), possibilities(exps*)])

   defn no-method (e:TMix) :
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      val t = type(ctxt(e) as CMulti)
      val p = resolved-pr([exps(e), t])
      add-error(p, "No appropriate multi ~ for method of type ~@. Possibilities are:\n~@" << [
         name, #pr(t), possibilities(exps(e))])

   defn amb-method (e:TMix) :
      val exps* = to-list $
         for (e in exps(e), s in sels(sel(e) as SSel)) filter :
            s != false
      val name = #name(n(r)) where :
         val r = get-ref(head(exps(e)))
      val t = type(ctxt(e) as CMulti)
      val p = resolved-pr([exps(e), t])
      add-error(p, "Multiple appropriate multis ~ for method of type ~@. Possibilities are:\n~@" << [
         name, #pr(t), possibilities(exps*)])         

   defn bad-method (c:TDefmethod) :
      val name = get-name(multi(c)) where :
         defn get-name (e:TExp) :
            match(e) :
               (e:TMix) : get-name(head(exps(e)))
               (e:TArgExp) : get-name(ref(e))
               (e:TRef) : #name(n(e))
      val t = type(ctxt(multi(c)) as CMulti)
      val p = resolved-pr([t, type(multi(c))])
      add-error(p, "Cannot attach method of type ~ to multi ~ of type ~." << [
         #pr(t), name, #pr(type(multi(c)))])

   defn bad-tarity (e:TArgExp) :
      match(type(ref(e))) :
         (t:TPoly) :
            add-error(0, "Function ~ of type ~ requires ~ type arguments, but received ~@." << [
               #name(n(ref(e))), #pr(type(ref(e))), cms(#pr(targs(e)))])
         (t:TArrow) :
            add-error(0, "Function ~ of type ~ does not accept type arguments, but received ~@." << [
               #name(n(ref(e))), #pr(type(ref(e))), cms(#pr(targs(e)))])

   defn no-capture (e:TArgExp) :
      val rt = type(ref(e)) as TPoly
      for (a in cargs(rt), t in cargs(e)) do :
         if unresolved?(t) :
            val a-name = name?(a, "argument ~", "argument")
            val r-name = #name(n(ref(e)))
            match(ctxt(e)) :
               (c:CExp|CMulti) :
                  val p = resolved-pr(type(c))
                  add-error(p, "Could not capture ~ for function ~ of type ~. Expected type is ~." << [
                     a-name, r-name, #pr(rt), #pr(type(c))])
               (c:CDo) :
                  val p = resolved-pr(args(c))
                  add-error(p, "Could not capture ~ for function ~ of type ~. Called with arguments of type ~@." << [
                     a-name, r-name, #pr(rt), cms(#pr(args(c)))])                  

   ;======== Driver ========
   do(check, comms(prog))
   for i in 0 to length(errors) do :
      if not empty?(errors[i]) :
         do(println, errors[i])
         throw(TypeError())   

;============================================================
;=============== General Utilities ==========================
;============================================================
defstruct Fresh

defstruct ConstraintError <: Exception
defstruct TypeError <: Exception

defn uncap (t:Type) :
   match(t) :
      (t:TCap) : TUVar(n(t))
      (t) : map(uncap, t)

defn map<?T> (f:Int -> ?T, r:Range) :
   to-list(stream(f, r))
