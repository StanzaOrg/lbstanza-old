defpackage stz/verbose :
  import core
  import collections

;Represents the program verbosity level.
var VERBOSE-LEVEL:Int = 0

;Return true if verbose is on.
public defn verbose-setting? () -> True|False :
  VERBOSE-LEVEL > 0

;Run the given body with the given verbosity setting.
public defn run-with-verbose-level<?T> (body:() -> ?T, level:Int) -> T :
  let-var VERBOSE-LEVEL = level :
    body()

;Main functions for testing the verbosity level
;and then printing.

#for (vprintf in [vprint vprintln vlnprint]
      printf in [print println lnprint]) :
      
  public defn vprintf (o:OutputStream, level:Int, x) :
    if VERBOSE-LEVEL >= level :
      printf(o, x)

  public defn vprintf (o:OutputStream, x) :
    vprintf(o, 1, x)

  public defn vprintf (level:Int, x) :
    vprintf(current-output-stream(), level, x)

  public defn vprintf (x) :
    vprintf(current-output-stream(), 1, x)