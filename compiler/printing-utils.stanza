defpackage stz/printing-utils :
  import core
  import line-wrap

;============================================================
;========================= Type =============================
;============================================================

public deftype FormattedField <: Printable

;Represents a field that is always printed out.
public deftype SingleFormattedField <: FormattedField

;Represents a field that can choose whether or not to be
;printed out.
public deftype OptionalFormattedField <: FormattedField
defmulti empty? (f:OptionalFormattedField) -> True|False

;Represents a field that expands into multiple items.
public deftype MultiFormattedField <: FormattedField
defmulti items (f:MultiFormattedField) -> Seqable

;------------------------------------------------------------
;-------------------- Printing Algorithm --------------------
;------------------------------------------------------------

;Example:
;  beginning-header :
;    item1
;    item2
;    item3
public defn colon-field-list (fs:Seqable) :
  colon-list $ field-items(fs)

;Example:
;  beginning-header(
;    item1
;    item2
;    item3)
public defn indented-field-list (fs:Seqable) :
  indented-list $ field-items(fs)

;Example:
;  beginning-header(item1, item2, item3)
public defn comma-field-list (fs:Seqable) :
  "%," % [field-items(fs)]

;Expand a bunch of fields into individual items.
defn field-items (fs:Seqable) -> Seq :
  for f in fs seq-cat :
    match(f) :
      (f:SingleFormattedField) : [f]
      (f:OptionalFormattedField) : [] when empty?(f) else [f]
      (f:MultiFormattedField) : field-items(items(f))
      (f) : [f]

;============================================================
;===================== List of Fields =======================
;============================================================

;Example:
;  myfield: 23
public defn simple-field (name:String, x) :
  new SingleFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, "%_: %~" % [name, x])

;Example:
;  myfield:
;    core, collections, hello
;    you, are, something
public defn line-wrapped-field (name:String, input-x) :
  val [x-empty?, x-seqable?, x] = match(input-x) :
    (input-x:Seqable) :
      val x = to-seq(input-x)
      [empty?(x), true, x]
    (input-x) :
      [false, false, input-x]  

  new SingleFormattedField :
    defmethod print (o:OutputStream, this) :
      if x-empty? :
        print(o, "%_: ()" % [name])
      else if x-seqable? :
        val block = LineWrapped("%," % [x])
        print(o, "%_:\n%_" % [name, Indented(block)])
      else :
        print(o, "%_:\n%_" % [name, Indented(x)])

;Example:
;  bold
public defn bool-flag-field (name:String, x:True|False) :
  new OptionalFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, name)
    defmethod empty? (this) :
      x is False      

;Example:
;  face? = "Times New Roman"
public defn falseable-field (name:String, x) :
  new OptionalFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, "%_: %~" % [name, x])
    defmethod empty? (this) :
      x is False

;Example:
;  nets :
;    net 13 (x, y, z)
;But where the category is hidden if x is false.
public defn falseable-list-fields (name:String, xs:Seqable|False) :
  new OptionalFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, "%_ %_" % [name, colon-field-list(xs as Seqable)])
    defmethod empty? (this) :
      xs is False

;Example:
;  "Times New Roman"
public defn falseable-field (x) :
  new OptionalFormattedField :
    defmethod print (o:OutputStream, this) :
      write(o, x)
    defmethod empty? (this) :
      x is False

;Example:
;  x
;  y
;  z
public defn inline-fields (xs:Seqable) :
  new MultiFormattedField :
    defmethod items (this) : xs

;Example:
;  nets :
;    net 13 (x, y, z)
;    net 14 (w, z)
public defn named-list-fields (name:String, xs:Seqable) :
  new SingleFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, "%_ %_" % [name, colon-field-list(xs)])

;Example:
;  nets :
;    net 13 (x, y, z)
;    net 14 (w, z)
;But whole field is discarded if list is empty.
public defn named-emptyable-list-fields (name:String, input-xs:Seqable) :
  val xs = to-seq(input-xs)
  new OptionalFormattedField :
    defmethod print (o:OutputStream, this) :
      print(o, "%_ %_" % [name, colon-field-list(xs)])
    defmethod empty? (this) :
      empty?(xs)

;============================================================
;==================== Print using Colons ====================
;============================================================

public defn indented-list (xs:Seqable) :
  val xs-seq = to-seq(xs)
  if empty?(xs-seq) :
    new Printable :
      defmethod print (o:OutputStream, this) :
        false
  else :
    Indented("\n%n" % [xs-seq])

public defn colon-list (xs:Seqable) :
  val xs-seq = to-seq(xs)
  if empty?(xs-seq) :
    new Printable :
      defmethod print (o:OutputStream, this) :
        print(o, ": ()")
  else :
    val items = Indented("%n" % [xs-seq])
    ":\n%_" % [items]

;============================================================
;===================== Abbreviation =========================
;============================================================

public defn abbreviate (xs:Tuple, n:Int) -> Tuple :
  val len = length(xs)
  if len <= n :
    xs
  else :
    val head-items = for i in 0 to (n - 1) seq : xs[i]
    val ellipsis = "%_" % ["..."]
    val last-item = xs[len - 1]
    to-tuple $ cat(head-items, [ellipsis, last-item])