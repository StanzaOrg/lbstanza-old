defpackage stz/dl :
  import core
  import collections
  import stz/dl-ir
  import stz/utils

;<doc>=======================================================
;====================== Documentation =======================
;============================================================

# Fundamental State #

  packages:Vector<PackageIO>

# Retrieving a PackageIO #

  packageio (e:DEnv, name:Symbol) -> PackageIO|False

Used to represent the environment for the REPL during resolving and
typechecking.

# Retrieving all PackageIOs #

  packageios (e:DEnv) -> Collection<PackageIO>

Useful function to be able to calculate the package dependency graph.
The packages are also ordered according to when they are first added
to the environment.

# Loading new packages #

  load (e:DEnv, ps:Collection<PackageIO>, live-recs:Tuple<Rec>) -> LoadResult

Loads the given packages into the environment. The operation succeeds
and returns LoadSuccess only if the new packages are consistent with
the definitions of the existing environment. Otherwise, the operation
returns LoadErrors.

The live-recs argument holds all the records that the currently live
objects depend upon. This prevents the user from changing the type
signature of a function that is still in active use.

# Updating existing packages #

  update (e:DEnv, ps:Collection<PackageIO>, live-recs:Tuple<Rec>) -> LoadResult
  
Updates the given packages in the environment. This is meant to be
used to update code definitions in the environment without
re-evaluating the top-level expressions. The operation behaves very
similarly to load except that it additionally enforces two checks. The
given packages must already exist in the environment, and the packages
cannot change the type of any global variable.

# Unloading existing packages #

  unload (e:DEnv, packages:Collection<Symbol>, live-recs:Tuple<Rec>) -> LoadResult

Unload the given packages from the environment. 

# Convenience: Compute the package dependency graph #

  package-dependency-graph (env:DEnv) -> HashTable<Symbol, List<Symbol>>

Computes the package dependency graph of all loaded packages.


;============================================================
;=======================================================<doc>

;============================================================
;======================= Interface ==========================
;============================================================

public :
  deftype DEnv
  defmulti packageios (e:DEnv) -> Collection<PackageIO>
  defmulti packageio (e:DEnv, name:Symbol) -> PackageIO|False
  defmulti load (e:DEnv, ps:Collection<PackageIO>, live-recs:Tuple<Rec>) -> LoadResult
  defmulti update (e:DEnv, ps:Collection<PackageIO>, live-recs:Tuple<Rec>) -> LoadResult
  defmulti unload (e:DEnv, ps:Collection<Symbol>, live-recs:Tuple<Rec>) -> LoadResult
  defmulti check-imports-defined (e:DEnv, packageio:PackageIO) -> LoadResult

  deftype LoadResult
  defstruct LoadErrors <: LoadResult&Exception : (errors: Tuple<LoadError>)
  defstruct LoadSuccess <: LoadResult

  deftype LoadError <: Exception
  defmulti source (e:LoadError) -> DependencySource
  defstruct MissingDependency <: LoadError :
    source:DependencySource with: (as-method => true)
    expected:Rec
  defstruct HangingDependency <: LoadError :
    source:DependencySource with: (as-method => true)
    expected:Rec
  defstruct MismatchedDependency <: LoadError :
    source:DependencySource with: (as-method => true)
    expected:Rec
    actual:Rec
  defstruct ChangedGlobal <: LoadError :
    source:DependencySource with: (as-method => true)
    expected:Rec
    actual:Rec

  deftype DependencySource
  defstruct LoadedPackage <: DependencySource : (package:Symbol)
  defstruct LiveObject <: DependencySource

;============================================================
;=============== Descriptor Environment =====================
;============================================================

public defn DEnv () :
  ;Initialize package table and live set
  val packages = Vector<PackageIO>()
  val package-indices = HashTable<Symbol,Int>()

  ;Packages
  defn add-package (p:PackageIO) :
    match(get?(package-indices,package(p))) :
      (i:Int) :
        packages[i] = p
      (_:False) :
        val i = length(packages)
        add(packages, p)
        package-indices[package(p)] = i

  ;============================
  ;==== Ensure Consistency ====
  ;============================
  ;Sanity consistency check
  defn ensure-consistency () :
    #if-not-defined(OPTIMIZE) :
      ;Ensure that all imported records match the exported records exactly.
      val rec-table = to-hashtable<RecId,Rec> $
        seq-cat(exported-entries, packages)
      for import in seq-cat(imports,packages) do :
        if not transient?(import) :
          val r = rec(import)
          match(get?(rec-table, id(r))) :
            (r2:Rec) : fatal("Mismatched dependency: %_ != %_" % [r, r2]) when not match?(r, r2)
            (r2:False) : fatal("Missing dependency: %_" % [id(r)])
    false

  ;<doc>===========================
  ;==== Error Checking Utility ====
  ;================================
  For anything that may create errors, call it like this:

    within check-for-errors() :
      my-body()
      if there-is-error? :
        error!()

  And the block above will return a LoadResult.
  ;================================
  ;===========================<doc>

  ;Accumulate any detected errors in buffer
  val error-accum = Vector<LoadError>()
  defn error! (e:LoadError) : add(error-accum, e)
  defn check-for-errors (f:() -> False) -> LoadResult :
    f()
    if empty?(error-accum) :
      LoadSuccess()
    else :
      val e = LoadErrors(to-tuple(error-accum))
      clear(error-accum)
      e

  ;<doc>==========================
  ;==== Check Imports Defined ====
  ;===============================
  Check whether all the imports required by the given packageio are
  defined in the environment.

  Input:
    packageio: PackageIO
    denv: DEnv
  ;===============================
  ;==========================<doc>
  defn check-imports-defined (packageio:PackageIO) :
    val src = LoadedPackage(package(packageio))
    for group in group-by(package{id(rec(_))}, imports(packageio)) do :
      val package = key(group)
      val imports = value(group)

      match(get?(package-indices, package))  :
        (i:Int) :
          val io = packages[i]
          val export-table = to-hashtable(id{rec(_)}, exports(io))
          for i in imports do :
            match(get?(export-table, id(rec(i)))) :
              (e:Export) :
                if not match?(rec(i), rec(e)) :
                  error! $ MismatchedDependency(src, rec(i), rec(e))
              (_:False) :
                error! $ MissingDependency(src, rec(i))
        (_:False) :
          for i in imports do :          
            error! $ MissingDependency(src, rec(i))

  ;<doc>=============================
  ;=== Check For Changed Globals ====
  ;==================================
  When updating packages, we preserve the state of the existing global
  variables, and thus we cannot change their type signature. This pass
  ensures that we do not change the type signatures of any global in
  the given packages.
  
  Since this is used during updating, it is assumed that these
  packages all have been loaded before.
  ;==================================
  ;=============================<doc>

  ;Check whether globals have been redefined.
  ;During updating, globals are not allowed to change their types.
  defn check-for-changed-globals (packageios:Collection<PackageIO>) :
    ;Retrieve all exported global records in packageio
    defn global-recs (io:PackageIO) :
      for e in exports(io) seq? :
        match(id(rec(e))) :
          (id:ValId) : One(rec(e))
          (id) : None()

    ;Check against previous definitions
    for new-io in packageios do :
      ;Get old IO
      val index = package-indices[package(new-io)]
      val old-io = packages[index]
      
      ;Create a table to quickly lookup the new definition by id.
      val new-rec-table = to-hashtable(id, global-recs(new-io))

      ;Compare the old globals to the new globals
      val src = LoadedPackage(package(new-io))
      for old-r in global-recs(old-io) do :
        match(get?(new-rec-table, id(old-r))) :
          ;Case: The global is no longer defined. That is okay.
          (_:False) : false
          ;Case: The global is defined. Ensure that it matches.
          (new-r:Rec) :
            if not match?(new-r, old-r) :
              error! $ ChangedGlobal(src, old-r, new-r)

  ;<doc>=========================================
  ;=== Check New Exports Against Old Imports ====
  ;==============================================
  Check whether environment will remaining consistent if given
  packages are loaded into the environment.
  ;==============================================
  ;=========================================<doc>

  defn check-load-consistency (ps:Collection<PackageIO>, live-recs:Tuple<Rec>) :
    ;Construct export table
    val export-table = HashTable<RecId,Rec>()
    val in-export-table = HashSet<Symbol>()
    defn add-to-export-table (packageio:PackageIO) :
      if add(in-export-table, package(packageio)) :
        for e in exports(packageio) do :
          val r = rec(e)
          export-table[id(r)] = r
    do(add-to-export-table, ps)
    do(add-to-export-table, packages)

    ;Check whether the imported record, imported, from 'src'
    ;is satisfied by the reloaded-packages.
    defn check-dependency (src:DependencySource, imported:Rec) :
      val rid = id(imported)
      match(get?(export-table, rid)) :
        (exported:Rec) :
          if not match?(imported, exported) :
            error! $ MismatchedDependency(src, imported, exported)
        (_:False) :
          error! $ MissingDependency(src, imported)

    ;Return true if the given package p is an existing package that
    ;has a possibility of being inconsistent against the proposed
    ;reload set.
    val reloaded-packages = to-hashset<Symbol>(seq(package,ps))
    defn affected? (p:PackageIO) :
      defn reloaded? (name:Symbol) : reloaded-packages[name]
      if not reloaded?(package(p)) :
        any?(reloaded?, imported-packages(p))

    ;Check imports in package
    defn check-imports (p:PackageIO) :
      val src = LoadedPackage(package(p))
      for import in imports(p) do :
        check-dependency(src, rec(import)) when not transient?(import)

    ;Check imports of existing packages and of reloaded packages
    do(check-imports, filter(affected?, packages))
    do(check-imports, ps)
        
    ;Check all the dependencies from the live objects
    do(check-dependency{LiveObject(), _}, live-recs)

  ;<doc>================================================
  ;==== Check Deleting Exports against Kept Imports ====
  ;=====================================================
  Consider removing the given set of packages, and determine whether
  this can be done without leaving hanging dependencies. Look at all
  the imports from the packages that will be remaining after the
  prune, and see whether they import anything from the packages that
  will be removed.
  ;=====================================================
  ;================================================<doc>
  
  defn check-removing-exports-against-old-imports (remove-set:HashSet<Symbol>, live-recs:Tuple<Rec>) :
    ;Return true if the given package p may possibly be affected by the remove set,
    ;ie. if it is not itself one of the packages to be removed, but it imports from a package
    ;to be removed.
    defn affected? (p:PackageIO) :
      not remove-set[package(p)] and any?({remove-set[_]}, imported-packages(p))
    ;Check whether the given record creates a dependency
    defn check-dependency (src:DependencySource, imported:Rec) :
      if remove-set[package(id(imported))] :
        error! $ HangingDependency(src, imported)
    ;Check all (non-transient) dependencies from affected packages
    for p in filter(affected?, packages) do :
      val src = LoadedPackage(package(p))
      for import in imports(p) do :
        check-dependency(src, rec(import)) when not transient?(import)
    ;Check dependencies from live objects
    do(check-dependency{LiveObject(), _}, live-recs)

  ;================================
  ;==== Return new Environment ====
  ;================================
  new DEnv :
    defmethod packageios (this) :
      packages
    defmethod packageio (this, name:Symbol) :
      val i = get?(package-indices, name)
      match(i:Int) : packages[i]
    defmethod load (this, pkgs:Collection<PackageIO>, live-recs:Tuple<Rec>) :
      val load-result = within check-for-errors() :
        check-load-consistency(pkgs, live-recs)
      match(load-result:LoadSuccess) :
        do(add-package, pkgs)
        ensure-consistency()
      load-result
    defmethod update (this, pkgs:Collection<PackageIO>, live-recs:Tuple<Rec>) :
      val load-result = within check-for-errors() :
        check-for-changed-globals(pkgs)
        check-load-consistency(pkgs, live-recs)
      match(load-result:LoadSuccess) :
        do(add-package, pkgs)
        ensure-consistency()
      load-result
    defmethod unload (this, pkgnames:Collection<Symbol>, live-recs:Tuple<Rec>) :
      val unload-set = to-hashset<Symbol>(pkgnames)
      val load-result = within check-for-errors() :
        check-removing-exports-against-old-imports(unload-set, live-recs)
      match(load-result:LoadSuccess) :
        ;Remove from package vector
        remove-when({unload-set[package(_)]}, packages)
        ;Recompute package indices
        clear(package-indices)
        for (io in packages, i in 0 to false) do :
          package-indices[package(io)] = i
        ;Ensure consistency
        ensure-consistency()
      load-result
    defmethod check-imports-defined (this, packageio:PackageIO) :
      within check-for-errors() :
        check-imports-defined(packageio)

;============================================================
;======================= Utilities ==========================
;============================================================

public defn package-dependency-graph (env:DEnv) :
  to-hashtable<Symbol,List<Symbol>> $
    for io in packageios(env) seq :
      package(io) => to-list(imported-packages(io))

;Retrieve all exported entries from packageio
defn exported-entries (p:PackageIO) :
  for e in exports(p) seq :
    val r = rec(e)
    id(r) => r

;============================================================
;===================== Printers =============================
;============================================================

defn source-str (s:DependencySource) :
  match(s) :
    (s:LoadedPackage) : "The package %~ requires" % [package(s)]
    (s:LiveObject) : "There is a live object that requires"

defmethod print (o:OutputStream, e:MissingDependency) :
  print(o, "Missing definition: %_ the following definition:" % [source-str(source(e))])
  val o2 = IndentedStream(o)
  lnprint(o2, format(expected(e)))

defmethod print (o:OutputStream, e:HangingDependency) :
  print(o, "Hanging dependency: %_ the following definition:" % [source-str(source(e))])
  val o2 = IndentedStream(o)
  lnprint(o2, format(expected(e)))
  
defmethod print (o:OutputStream, e:MismatchedDependency) :
  val o2 = IndentedStream(o)
  print(o, "Inconsistent definition: %_ the following definition:" % [source-str(source(e))])
  lnprint(o2, format(expected(e)))
  lnprint(o, "but it has been replaced with:")
  lnprint(o2, format(actual(e)))

defmethod print (o:OutputStream, e:ChangedGlobal) :
  val o2 = IndentedStream(o)
  print(o, "Globals are not allowed to be redefined during code update. %_ the following definition:" % [
    source-str(source(e))])
  lnprint(o2, format(expected(e)))
  lnprint(o, "but it has been replaced with:")
  lnprint(o2, format(actual(e)))

defmethod print (o:OutputStream, e:LoadErrors) :
  print(o, "%n" % [errors(e)])
    