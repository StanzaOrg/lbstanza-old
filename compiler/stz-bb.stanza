defpackage stz/bb :
   import core
   import verse
   import stz/algorithms
   import stz/tgt-ir with :
      prefix => tgt-
   import stz/bb-ir

;                            Main Driver
;                            ===========

public defn compile (d:tgt-DefFn) :
   var u:Unit = to-bb-ir(d)
   println("Basic block IR:\n~" << [u])
   u = remove-critical-edges(u)
   u = analyze-liveness(u)
   u = map(emit-early-saves, u)
   println("Allocate classes for:\n~" << [u])
   u = map(allocate-classes, u)
   println("Propagate assumptions for:\n~" << [u])
   u = map(propagate-assumptions, u)
   println(u)
   println("Register assignment for:\n~" << [u])
   u = register-assignment(u)
   println("Finished:\n~" << [u])

;                          Various Utilities
;                          =================

defn map (f: WBlock -> Block, u:Unit) :
   Unit(defs(u),
        map(f, blocks(u) as Vector<WBlock>))

defn in-reverse<?T> (xs:Vector<?T>) :
   for i in (length(xs) - 1) through 0 by -1 stream :
      xs[i]

defn reverse!<?T> (xs:Vector<?T>) :
   val n = length(xs)
   for i in 0 to n / 2 do :
      val j = n - 1 - i
      val xi = xs[i]
      val xj = xs[j]
      xs[i] = xj
      xs[j] = xi

defn reverse-post-order (bs:Vector<Block>) -> Vector<Block> :
   ;Block Table
   val block = HashTable<Symbol,Block>(symbol-hash)
   for b in bs do : block[name(b)] = b
   
   ;Mark Table
   val mark-table = HashTable<Symbol,True>(symbol-hash)
   defn marked? (b:Block) : key?(mark-table, name(b))
   defn mark (b:Block) : mark-table[name(b)] = true

   ;Add blocks to ys
   val ys = Vector<Block>()
   defn* loop (b:Block) :
      if not marked?(b) :
         do(loop{block[_]}, next(b))
         add(ys, b)            
         mark(b)
   loop(bs[0])

   ;Return reversed
   reverse!(ys)
   ys

defn find!<?T> (f: T -> True|False, xs:Streamable<?T>) -> T :
   find(f, xs) as T


;                           Translation to IR
;                           =================

defn to-bb-ir (d:tgt-DefFn) -> Unit :
   println("TODO: Not everything is a ComOp")
   println("TODO: Defs need to be properly populated.")
   println("TODO: Some operators need to have immediates pulled out to values.")
   
   ;     State
   ;     -----   
   val defs = Vector<tgt-DefVar>()
   val blocks = Vector<Block>()

   ;     Update State
   ;     ------------
   defn close-block (b:Block, ns:List<Symbol>) :
      add(blocks, Block(name(b), ins(b), ns))
   defn fresh-block (name:Symbol) :
      Block(name, Vector<Ins>(), List())


   ;     Find all blocks
   ;     ---------------   
   defn* scan-block (b:Block, str-i:Stream<tgt-Ins>) :
      defn to-imm (x:tgt-Imm) :
         match(x) :
            (x:tgt-Var) : Var(tgt-name(x))
            (x) : Val(x)
      defn to-var (x:tgt-Var) :
         to-imm(x) as Var
      
      if more?(str-i) :
         match(next(str-i)) :
            (i:tgt-SetIns) :
               val x = to-var(tgt-x(i))
               val y = to-imm(tgt-y(i))
               add(ins(b), Set(x, y))
               scan-block(b, str-i)
            (i:tgt-BinOp) :
               val x = to-var(tgt-x(i))
               val ys = list(to-imm(tgt-y(i)), to-imm(tgt-z(i)))
               val top = tgt-op(i)
               val op = switch contains?{_, top} :
                  [tgt-ADD-OP, tgt-MUL-OP] :
                     Op(ComOp(top), list(x), ys)
                  [tgt-SUB-OP] :
                     Op(NComOp(top), list(x), ys)
                  [tgt-DIV-OP] :
                     Op(Div2Op(top), list(x), ys)
               add(ins(b), op)
               scan-block(b, str-i)
            (i:tgt-Call) :
               val xs = map(to-var, tgt-xs(i))
               val f = to-imm(tgt-f(i))
               val ys = map(to-imm, tgt-ys(i))
               add(ins(b), Call(tgt-arity(i), xs, f, ys))
               scan-block(b, str-i)
            (i:tgt-Return) :
               val xs = map(to-imm, tgt-xs(i))
               add(ins(b), Return(xs))
               close-block(b, List())
               scan-block(fresh-block(gensym()), str-i)            
            (i:tgt-Label) :
               close-block(b, list(tgt-name(i)))
               scan-block(fresh-block(tgt-name(i)), str-i)
            (i:tgt-Goto) :
               val n = gensym()
               close-block(b, list(tgt-name(i)))
               scan-block(fresh-block(n), str-i)
            (i:tgt-Branch) :
               val alt = gensym()
               val xs = list(to-imm(tgt-x(i)), to-imm(tgt-y(i)))
               val i* = Branch(ComOp(tgt-op(i)), xs)
               add(ins(b), i*)
               close-block(b, list(tgt-name(i), alt))
               scan-block(fresh-block(alt), str-i)

   ;     Entry Block
   ;     -----------
   val entry = fresh-block(gensym())
   add(ins(entry), Args(vs)) where :
      val vs = for def in tgt-args(d) map :
         Var(tgt-name(def))
   scan-block(entry, to-stream(tgt-body(d)))

   ;     Return result
   ;     -------------
   Unit(defs, blocks)



;                       Critical Edge Removal
;                       =====================

defn remove-critical-edges (u:Unit) :

   ;     Forward past empty blocks
   ;     -------------------------   
   val forwards = HashTable<Symbol,Symbol>(symbol-hash)
   
   defn resolve (n:Symbol) :
      match(get?(forwards, n, false)) :
         (f:False) :
            n
         (f:Symbol) :
            val n* = resolve(f)
            forwards[n] = n*
            n*

   defn next* (b:Block) :
      map(resolve, next(b))
            
   for b in blocks(u) do :
      if empty?(ins(b)) :
         if length(next(b)) == 1 :
            forwards[name(b)] = head(next(b))


   ;     Count predecessors for blocks
   ;     -----------------------------
   val num-preds = HashTable<Symbol,Int>(symbol-hash)
   defn inc-pred (b:Symbol) :
      num-preds[b] = 1 + get?(num-preds, b, 0)

   for b in blocks(u) do :
      if not empty?(ins(b)) :
         do(inc-pred, next*(b))


   ;     Insert safe blocks
   ;     ------------------
   val safe-blocks = Vector<Block>()
   val blocks* = Vector<Block>()

   defn safe-block (n:Symbol) :
      val name = gensym(`safe)
      add(safe-blocks, Block(name, Vector<Ins>(), list(n)))
      name

   defn multi-in? (b:Symbol) :
      get?(num-preds, b, 0) > 1
      
   defn multi-out? (b:Block) :
      length(next(b)) > 1

   
   for b in blocks(u) map :
      add{blocks*, Block(name(b), ins(b), _)} $
         if multi-out?(b) :
            for n in next*(b) map :
               if multi-in?(n) : safe-block(n)
               else : n
         else :
            next*(b)
   do(add{blocks*, _}, safe-blocks)         

   ;     Return result
   ;     -------------
   Unit(defs(u), blocks*)



;                       Working IR
;                       ==========

;     Working Instructions
;     --------------------
definterface Working
defstruct WArgs <: Ins&Working :
   xs: List<Port>
defstruct WCall <: Ins&Working :
   arity: Int
   xs: List<Port>
   f: Imm
   ys: List<Imm>
defstruct Save <: Ins&Working :
   x: Var   
defstruct Load <: Ins&Working :
   x: Var
defstruct Unload <: Ins&Working :
   x: Var
defstruct Xchg <: Ins&Working :
   x: Var
   y: Var
   swap: Reg|False

defn cms (xs) : join(xs, ", ")
defmethod print (o:OutputStream, i:Ins&Working) :
   print{o, _} $ match(i) :
      (i:WArgs) : "(~@) = Args" << [cms(xs(i))]
      (i:WCall) : "(~@) = ~(~@)" << [cms(xs(i)), f(i), cms(ys(i))]
      (i:Save) : "save ~" << [x(i)]
      (i:Load) : "load ~" << [x(i)]
      (i:Unload) : "unload ~" << [x(i)]
      (i:Xchg) : "xchg ~ ~ through ~" << [x(i), y(i), swap(i)]

;     Any Value
;     ---------
defstruct Any
defmethod print (o:OutputStream, x:Any) :
   print(o, "any")

;     Port Definition
;     ---------------
definterface Port
defstruct LivePort <: Port :
   name: Symbol
   saved?: True|False|Any
   loaded?: True|False|Any
   loc: Loc
defstruct DeadPort <: Port

defmethod print (o:OutputStream, p:Port) :
   print{o, _} $ match(p) :
      (p:LivePort) : "~ (s:~, l:~, at:~)" << [name(p), saved?(p), loaded?(p), loc(p)]
      (p:DeadPort) : "dead"

;     Working Block
;     -------------
defstruct WBlock <: Block :
   name: Symbol with : (as-method => true)
   ins: Vector<Ins> with : (as-method => true)
   next: List<Symbol> with : (as-method => true)
   in: List<LivePort>
   out: List<LivePort>
   dist: List<Int>

defmethod print (o:OutputStream, b:WBlock) :
   val io = IndentedStream(o, 3)
   print(o, "block ~ :" << [name(b)])
   print(io, "\nin: ~@" << [cms(in(b))])
   for e in ins(b) do :
      print(io, "\n~" << [e])
   print(io, "\nnext ~@" << [cms(next(b))])
   print(io, "\nout: ~@" << [cms(out(b))])
   print(io, "\ndist: ~@" << [cms(dist(b))])



;                       Block Liveness
;                       ==============

defn analyze-liveness (u:Unit) :
   ;     Propagation Tables
   ;     ------------------
   defn bhash ([b,x]:[Symbol,Symbol]) : 7 * symbol-hash(b) + symbol-hash(x)
   val defs-tab = HashTable<[Symbol,Symbol],True>(bhash)
   val ins-tab = HashTable<[Symbol,Symbol],False|Int>(bhash)

   ;     Find immediate defs and live-ins
   ;     --------------------------------
   defn find-defs-uses (b:Block) :   
      ;Update def/ins tables      
      defn key (x:Var) : [name(b), name(x)]
      defn def (x:Var) :
         defs-tab[key(x)] = true
         ins-tab[key(x)] = false
      defn use (x:Imm, i:Int) :
         match(x) :
            (x:Var) : ins-tab[key(x)] = i
            (x) : false
      defn def (xs:List<Var>) : do(def, xs)
      defn use (xs:List<Imm>, i:Int) : do(use{_, i}, xs)

      ;Sweep through instructions      
      val n = length(ins(b))
      for (e in in-reverse(ins(b)), i in (n - 1) through 0 by -1) do :
         match(e) :
            (e:Set) : (def(x(e)), use(y(e), i))
            (e:Op) : (def(xs(e)), use(ys(e), i))
            (e:Call) : (def(xs(e)), use(f(e), i), use(ys(e), i))
            (e:Args) : def(xs(e))
            (e:Return) : use(xs(e), i)
            (e:Branch) : use(xs(e), i)
            
   do(find-defs-uses, blocks(u))

   ;     Find block predecessors and block lengths
   ;     -----------------------------------------
   val preds = HashTable<Symbol,List<Symbol>>(symbol-hash)
   val lengths = HashTable<Symbol,Int>(symbol-hash)
   for b in blocks(u) do :
      preds[name(b)] = List()
      lengths[name(b)] = length(ins(b))
   for b in blocks(u) do :
      for n in next(b) do :
         preds[n] = List(name(b), preds[n])

   ;     Propagation of live-ins and live-outs
   ;     -------------------------------------
   ;Iterate through a table of live vars
   defn do-for-live (f: [Symbol,Symbol,Int] -> False,
                     es: HashTable<[Symbol,Symbol],False|Int>) :
      for entry in es do :
         if value(entry) != false :
            val [b,x] = key(entry)
            f([b, x, value(entry) as Int])

   ;Propagate block liveness results
   val outs-tab = HashTable<[Symbol,Symbol],Int>(bhash)
   fixpoint $ fn (progress) :
      defn add-live (live:HashTable<[Symbol,Symbol],False|Int>,
                     b:Symbol, x:Symbol, d:Int) :
         match(get?(live, [b,x], false)) :
            (d2:Int) :
               if d < d2 :
                  live[[b,x]] = d
                  progress()
            (d2:False) :
               live[[b,x]] = d
               progress()

      for [b,x,d] in ins-tab do-for-live :
         for p in preds[b] do : add-live(outs-tab, p, x, d)
      for [b,x,d] in outs-tab do-for-live :
         if not key?(defs-tab, [b,x]) :
            add-live(ins-tab, b, x, d + lengths[b])

   ;     Create results table
   ;     --------------------
   defn to-port (x:Symbol) : LivePort(x, Any(), Any(), NoLoc())
   val in-lists = HashTable<Symbol,List<Symbol>>(symbol-hash)
   val out-lists = HashTable<Symbol,List<Symbol>>(symbol-hash)
   val dist-lists = HashTable<Symbol,List<Int>>(symbol-hash)
   for [b,x,d] in ins-tab do-for-live :
      in-lists[b] = List(x, get?(in-lists, b, List()))
   for [b,x,d] in outs-tab do-for-live :
      out-lists[b] = List(x, get?(out-lists, b, List()))
      dist-lists[b] = List(d, get?(dist-lists, b, List()))

   let :
      val blocks* = for b in blocks(u) map :
         WBlock(name(b),
                ins(b),
                next(b),
                map(to-port, get?(in-lists, name(b), List()))
                map(to-port, get?(out-lists, name(b), List()))
                get?(dist-lists, name(b), List()))

      Unit(defs(u), blocks*)



;                             Usage Table
;                             ===========

;     Interface
;     ---------
defclass UsageTable
defmulti get (t:UsageTable, x:Symbol, p:Pos) -> Int

;     Type of Usage
;     -------------
definterface Usage
defmulti n (u:Usage) -> Int
defstruct UsedUsage <: Usage : (n:Int with: (as-method => true))
defstruct KillUsage <: Usage : (n:Int with: (as-method => true))

;     Position Tracker
;     ----------------
defstruct Pos :
   index: Int
   mid?: True|False

defn hash (p:Pos) :
   match(mid?(p)) :
      (m:True) : index(p) * 2 + 1
      (m:False) : index(p) * 2

defn less-eq? (a:Pos, b:Pos) :
   hash(a) <= hash(b)

defmethod print (o:OutputStream, p:Pos) :
   print(o, "~~" << [
      index(p),
      ".5" when mid?(p) else ""])

;     Analysis
;     --------
defn usage-table (b:WBlock) :
   ;     State
   ;     -----
   val usages = HashTable<Symbol, Vector<Usage>>(symbol-hash)

   ;     Update usages state
   ;     -------------------
   defn ensure-usage-vector (x:Symbol) :
      if not key?(usages, x) : usages[x] = Vector<Usage>(4)
      
   defn add-use-usage (x:Symbol, p:Pos) :
      ensure-usage-vector(x)
      add(usages[x], UsedUsage(hash(p)))
      
   defn add-kill-usage (x:Symbol, p:Pos) :
      ensure-usage-vector(x)
      add(usages[x], KillUsage(hash(p)))

   ;     Search through expressions for usages
   ;     -------------------------------------
   defn used (x:Imm, p:Pos) :
      match(x) : 
         (x:Var) : add-use-usage(name(x), p)
         (x:Val) : false
   defn used (x:LivePort, p:Pos) :
      add-use-usage(name(x), p)
   defn used (xs:List<Imm>, p:Pos) :
      for x in xs do : used(x, p)
   defn defd (x:Var|Port, p:Pos) :
      match(x) :
         (x:Var|LivePort) : add-kill-usage(name(x), p)
         (x) : false
   defn defd (xs:List<Var|Port>, p:Pos) :
      for x in xs do : defd(x, p)
      
   
   for (e in ins(b), i in 0 to false) do :
      match(e) :
         (e:Set) :
            used(y(e), Pos(i, false))
            defd(x(e), Pos(i, true))
         (e:WArgs) :
            defd(xs(e), Pos(i, true))
         (e:WCall) :
            used(f(e), Pos(i, false))
            used(ys(e), Pos(i, false))
            defd(xs(e), Pos(i, true))
         (e:Return) :
            used(xs(e), Pos(i, true))
         (e:Op) :
            used(ys(e), Pos(i, false))
            defd(xs(e), Pos(i, true))
         (e:Branch) :
            used(xs(e), Pos(i, false))
         (e:Save) :
            false

   for (x in out(b), d in dist(b)) do :
      val i = length(ins(b)) + d
      used(x, Pos(i, false))


   ;     Compute distance until use
   ;     --------------------------

   new UsageTable :
      defmethod get (this, x:Symbol, p:Pos) :
         val i = hash(p)
         val use = find({n(_) >= i}, usages[x])
         match(use) :
            (use:UsedUsage) : n(use) - i
            (use:KillUsage|False) : error("No usage found for ~." % [x])



;                      Release Table
;                      =============

;     Interface
;     ---------
defclass ReleaseTable
defmulti releases-up-to (r:ReleaseTable, p:Pos) -> Stream<Symbol>

;     Analysis
;     --------
defn release-table (b:WBlock) :
   ;     Algorithm State
   ;     ---------------
   val load-table = HashTable<Symbol,True|False>(symbol-hash)

   defn marked-loaded? (x:Symbol) :
      get?(load-table, x, false)

   defn clear-loaded () :
      clear(load-table)

   defn mark-loaded (x:Symbol|Imm|Port, v:True|False) :
      match(x) :
         (x:Symbol) : load-table[x] = v
         (x:Var) : load-table[name(x)] = v
         (x:LivePort) : (load-table[name(x)] = v) when loaded?(x) != false
         (x) : false
         
   defn mark-loaded (xs:List<Imm|Port>, v:True|False) :
      for x in xs do : mark-loaded(x, v)

   ;     Output State
   ;     ------------
   val release-list = Vector<KeyValue<Pos,Symbol>>()

   ;     Analysis
   ;     --------
   defn release-if-not-loaded (p:Pos, x:Symbol|Imm|Port) :
      match(x) :
         (x:Symbol) :
            add(release-list, p => x) when not marked-loaded?(x)
         (x:Var) : release-if-not-loaded(p, name(x))
         (x:LivePort) : release-if-not-loaded(p, name(x))
         (x) : false

   ;Initialize live-out as live
   mark-loaded(out(b), true)
      
   ;Analyze expression list
   val n = length(ins(b))
   for (e in in-reverse(ins(b)), i in (n - 1) through 0 by -1) do :
      defn defd (x:Imm|Port) :
         release-if-not-loaded(Pos(i + 1, false), x)
         mark-loaded(x, false)
      defn defd (xs:List<Imm|Port>) :
         do(defd, xs)
         
      defn used (x:Imm|Port) :
         release-if-not-loaded(Pos(i, true), x)
         mark-loaded(x, true)
      defn used (xs:List<Imm|Port>) :
         do(used, xs)
      
      match(e) :
         (e:Set) : (defd(x(e)), used(y(e)))
         (e:WArgs) : defd(xs(e))
         (e:WCall) :
            defd(xs(e))
            clear-loaded()
            used(f(e))
            used(ys(e))
         (e:Return) : used(xs(e))
         (e:Op) : (defd(xs(e)), used(ys(e)))
         (e:Branch) : used(xs(e))
         (e:Save) : used(x(e)) 
         (e:Load) : defd(x(e))

   ;     Compute releases to a given position
   ;     ------------------------------------
   new ReleaseTable :
      defmethod releases-up-to (this, p:Pos) :
         generate<Symbol> :
            defn* loop () :
               if not empty?(release-list) :
                  val r = peek(release-list)
                  if key(r) <= p :
                     yield(value(r))
                     pop(release-list)
                     loop()
            loop()
      defmethod print (o:OutputStream, this) :
         val io = IndentedStream(o, 3)
         print(o, "Release Table:")
         for e in in-reverse(release-list) do :
            print(io, "\n~" << [e])



;                       Save Emission
;                       =============

defn emit-early-saves (b:WBlock) :
   ;     Algorithm State
   ;     ---------------
   ;requires-save = true implies a variable needs to be saved. (default = false)
   ;prefers-load = false implies no benefit if a variable is loaded. (default = true)
   ;is-live = true implies a variable is still going to be used. (default = false)
   
   val requires-save = HashTable<Symbol,True|False>(symbol-hash)
   val prefers-load = HashTable<Symbol,True|False>(symbol-hash)
   val is-live = HashTable<Symbol,True|False>(symbol-hash)

   defn currently-live () :
      stream(key, filter(value, is-live))

   ;     Output State
   ;     ------------
   val accum = Vector<Ins>()
   
   defn emit (i:Ins) :
      add(accum, i)

   ;     Analysis Helpers
   ;     ----------------
   ;Updates state for a defined variable
   defn defd (x:Var) :
      is-live[name(x)] = false
      requires-save[name(x)] = false
      prefers-load[name(x)] = false

   ;Updates state for a used variable
   defn used (x:Imm) :
      match(x) :
         (x:Var) :
            is-live[name(x)] = true
            prefers-load[name(x)] = true
         (x) : false

   ;Updates state for a variable that was live through a call
   defn lived-through-call (x:Symbol) :
      requires-save[x] = true
      prefers-load[x] = false

   ;Convenience
   defn defd (xs:List<Var>) : do(defd, xs)
   defn used (xs:List<Imm>) : do(used, xs)
   defn lived-through-call (xs:Stream<Symbol>) : do(lived-through-call, xs)

   ;Retrieve the variable as a port according to the current algorithm state.
   defn get-port (x:Symbol) :
      val sv = get?(requires-save, x, false)
      val ld = get?(prefers-load, x, true)
      val lv = get?(is-live, x, false)
      match(lv, sv, ld) :
         (lv:False, sv, ld) : DeadPort()
         (lv:True, sv:False, ld:False) : LivePort(x, Any(), false, NoLoc())
         (lv:True, sv:False, ld:True) : LivePort(x, Any(), Any(), NoLoc())
         (lv:True, sv:True, ld:False) : LivePort(x, true, false, NoLoc())
         (lv:True, sv:True, ld:True) : LivePort(x, true, Any(), NoLoc())    

   defn get-port (x:Var) : get-port(name(x))
   defn get-port (xs:List<Var>) : map(get-port, xs)

   ;Insert a Save for the variable if it needs to be saved.
   defn save-if-required (x:Var) :
      if get?(requires-save, name(x), false) :
         emit(Save(x))

   defn save-if-required (xs:List<Var>) :
      do(save-if-required, xs)

   ;     Analysis
   ;     --------
   ;Exit variables
   for p in out(b) do :
      is-live[name(p)] = true
      
   ;Sweep through instructions
   ;For each instruction:
   ;1. Save any required values
   ;2. Emit the updated instruction
   ;3. Update the algorithm state
   for e in in-reverse(ins(b)) do :
      match(e) :
         (e:Set) :
            save-if-required(x(e))
            emit(e)
            defd(x(e))
            used(y(e))
         (e:Args) :
            emit(WArgs(get-port(xs(e))))
            defd(xs(e))
         (e:Call) :
            emit(WCall(arity(e), get-port(xs(e)), f(e), ys(e)))
            defd(xs(e))
            lived-through-call(currently-live())
            used(f(e))
            used(ys(e))
         (e:Return) :
            emit(e)
            used(xs(e))
         (e:Op) :
            save-if-required(xs(e))
            emit(e)
            defd(xs(e))            
            used(ys(e))
         (e:Branch) :
            emit(e)
            used(xs(e))

   ;Entry variables
   val in* = for x in in(b) map :
      get-port(name(x)) as LivePort

   ;     Return results
   ;     --------------
   reverse!(accum)
   WBlock(name(b),
          accum,
          next(b),
          in*,
          out(b),
          dist(b))


;                       Class Allocation
;                       ================

;     Return all unique Vars
;     ----------------------
val UNIQUE-TABLE = HashTable<Symbol,True>(symbol-hash)
defn unique-vars (vs:List<Imm>) :
   val r = for v in vs filter :
      match(v) :
         (v:Var) :
            match(get?(UNIQUE-TABLE, name(v), false)) :
               (e:True) :
                  false
               (e:False) :
                  UNIQUE-TABLE[name(v)] = true
                  true
         (v:Val) :
            false
   to-list(r) as List<Var>
   

;     Allocate classes
;     ----------------
defn allocate-classes (b:WBlock) -> WBlock :
   println("TODO: Handle different types of registers.")
   println("TODO: Have the correct number of free registers.")

   ;     Algorithm State
   ;     ---------------
   ;saved = true : The variable has been saved.
   ;      | false : The variable is not saved.
   ;      | any : It is unknown whether the variable is saved or not.
   ;      | none : The variable is not live.
   ;loaded = true : The variable is loaded.
   ;       | false : The variable is not loaded.
   ;       | any : It is unknown whether the variable is loaded.
   ;       | none : The variable is not live.

   val saved = HashTable<Symbol, True|False|Any>(symbol-hash)
   val loaded = HashTable<Symbol, True|False|Any>(symbol-hash)
   val rt = release-table(b)
   val ut = usage-table(b)
   var num-free-reg = 8

   ;     Output State
   ;     ------------
   val accum = Vector<Ins>()
   
   defn emit (i:Ins) :
      add(accum, i)

   ;     Analysis Helpers
   ;     ----------------

   ;Saving / Loading / Defining / Killing
   defn kill (x:Symbol) :
      match(loaded[x]) :
         (l:True|Any) : num-free-reg = num-free-reg + 1
         (l:False) : false
      loaded[x] = false
      
   defn save (x:Symbol) :
      match(saved[x]) :
         (s:True) : false
         (s:False|Any) : emit(Save(Var(x)))
      saved[x] = true

   defn load (x:Symbol) :
      match(loaded[x]) :
         (l:True) : false
         (l:False) :
            emit(Load(Var(x)))
            num-free-reg = num-free-reg - 1
         (l:Any) :
            emit(Load(Var(x)))
      loaded[x] = true

   defn unload (x:Symbol) :
      match(loaded[x]) :
         (l:True|Any) :
            emit(Unload(Var(x)))
            num-free-reg = num-free-reg + 1
         (l:False) : false
      loaded[x] = false   

   defn define (x:Symbol) :
      loaded[x] = true
      saved[x] = false
      num-free-reg = num-free-reg - 1

   defn define (x:Port) :
      match(x) :
         (x:LivePort) :
            match(loaded?(x)) :
               (l:False) :
                  loaded[name(x)] = false
               (l:Any) :
                  loaded[name(x)] = Any()
                  num-free-reg = num-free-reg - 1
            match(saved?(x)) :
               (s:True|Any) : saved[name(x)] = s
         (x:DeadPort) :
            false

   defn currently-loaded () :
      stream{key, _} $
      for e in loaded filter :
         value(e) typeof True|Any

   ;Convenience
   defn save (x:Var) : save(name(x))
   defn load (x:Imm) : load(name(x as Var)) when x typeof Var
   defn load (xs:List<Imm>) : do(load, xs)
   defn kill (xs:Streamable<Symbol>) : do(kill, xs)
   defn define (x:Var) : define(name(x))
   defn define (xs:List<Var>) : do(define, xs)
   defn define (xs:List<Port>) : do(define, xs)

   ;Computes the number of spills required given the variables used,
   ;the releases following, and then the number of variables defined.
   defn num-spills-required (used:List<Imm>, rs:List<Symbol>, defs:List<Var>) :
      val num-loads = for u in unique-vars(used) count :
         loaded[name(u)] == false
      val num-released = for r in rs count :
         loaded[r] != false
      val num-results = length(defs)
      val additional = num-loads + max(0, num-results - num-released)
      max(0, additional - num-free-reg)

   defn spill (p:Pos, n:Int) :
      defn save-and-unload (x:Symbol) : (save(x), unload(x))
      defn dist (x:Symbol) : ut[x, p]
      if n == 1 :
         save-and-unload(maximum-by-key(currently-loaded(), dist))
      else if n > 1 :
         val vs = to-vector(currently-loaded())
         sort-by-key!(vs, dist)
         for i in 0 to n do :
            save-and-unload(pop(vs))

   ;     Analysis
   ;     --------   
   ;Live in variables
   let :
      define(in(b))
      val ns = max(0, neg(num-free-reg))
      spill(Pos(0,false), ns)

   ;Scan through instructions
   for (e in ins(b), i in 0 to false) do :      
      kill(releases-up-to(rt, Pos(i, false)))
      match(e) :
         (e:Set) :
            val rs = to-list(releases-up-to(rt, Pos(i, true)))
            val ns = num-spills-required(List(), rs, list(x(e)))
            spill(Pos(i,false), ns)
            kill(rs)
            define(x(e))
            emit(e)
         (e:Op) :
            val rs = to-list(releases-up-to(rt, Pos(i, true)))
            val ns = num-spills-required(ys(e), rs, xs(e))
            spill(Pos(i,false), ns)
            load(ys(e))
            kill(rs)
            define(xs(e))
            emit(e)
         (e:Branch) :
            val ns = num-spills-required(xs(e), List(), List())
            spill(Pos(i,false), ns)
            load(xs(e))
            emit(e)
         (e:Return) :
            emit(e)
         (e:Save) :
            save(x(e))
         (e:WArgs) :
            emit(e)
            define(xs(e))
            val ns = max(0, neg(num-free-reg))
            spill(Pos(i + 1,false), ns)
         (e:WCall) :
            emit(e)
            val rs = to-list(releases-up-to(rt, Pos(i, true)))
            kill(rs)
            define(xs(e))
            val ns = max(0, neg(num-free-reg))
            spill(Pos(i + 1, false), ns)

   ;Live out variables
   val out* = for x in out(b) map :
      LivePort(name(x),
               saved[name(x)],
               loaded[name(x)],
               loc(x))

   ;     Return Result
   ;     -------------
   WBlock(name(b), accum, next(b),
          in(b), out*, dist(b))



;                    Assumption Propagation
;                    ======================

definterface Assumption
defstruct Accept <: Assumption 
defstruct Assume <: Assumption : (value: True|False|Any)

defn propagate-assumptions (b:WBlock) :
   ;     Algorithm State
   ;     ---------------
   val save-assumptions = HashTable<Symbol, Assumption>(symbol-hash)
   val load-assumptions = HashTable<Symbol, Assumption>(symbol-hash)

   ;     Output State
   ;     ------------
   val accum = Vector<Ins>()
   defn emit (i:Ins) :
      add(accum, i)

   ;     Analysis Helpers
   ;     ----------------
   defn mark-port (x:Port) :
      match(x) :
         (x:LivePort) :
            load-assumptions[name(x)] = match(loaded?(x)) :
               (l:Any) : Accept()
               (l:True|False) : Assume(l)
            save-assumptions[name(x)] = match(saved?(x)) :
               (s:Any) : Accept()
               (s:True|False) : Assume(s)
         (x:DeadPort) :
            false

   defn get-port (x:Port) :
      defn get-value (x:Assumption) :
         match(x) :
            (x:Assume) : value(x)
            (x:Accept) : Any()            
      match(x) :
         (x:LivePort) :
            LivePort(name(x),
                     get-value(save-assumptions[name(x)]),
                     get-value(load-assumptions[name(x)]),
                     loc(x))
         (x:DeadPort) :
            x

   defn clear-assumptions () :
      clear(save-assumptions)
      clear(load-assumptions)

   defn save (x:Symbol) :
      match(get?(save-assumptions, x, false)) :
         (a:Accept) : save-assumptions[x] = Assume(true)
         (a:Assume|False) : emit(Save(Var(x)))
         
   defn load (x:Symbol) :
      match(get?(load-assumptions, x, false)) :
         (a:Accept) : load-assumptions[x] = Assume(true)
         (a:Assume|False) : emit(Load(Var(x)))

   defn unload (x:Symbol) :
      match(get?(load-assumptions, x, false)) :
         (a:Accept) : load-assumptions[x] = Assume(false)
         (a:Assume|False) : false

   defn define (x:Symbol) :
      if get?(load-assumptions, x, false) typeof Accept :
         load-assumptions[x] = Assume(Any())
      if get?(save-assumptions, x, false) typeof Accept :
         save-assumptions[x] = Assume(Any())

   ;Convenience
   defn save (x:Var) : save(name(x))
   defn load (x:Var) : load(name(x))
   defn unload (x:Var) : unload(name(x))
   defn define (x:Var) : define(name(x))
   defn define (xs:List<Var>) : do(define, xs)


   ;     Analysis
   ;     --------         
   defn* scan (ins:Stream<Ins>, process-assumptions: () -> False) :
      defn* continue () :
         scan(ins, process-assumptions)
         
      defn* continue-with-ports (ports:List<Port>, return: (List<Port>) -> Ins) :
         val i = length(accum)
         emit(Args(List())) ;Emit placeholder
         process-assumptions()
         clear-assumptions()
         do(mark-port, ports)
         scan{ins, _} $ fn* () :
            val ports* = map(get-port, ports)
            accum[i] = return(ports*)

      if more?(ins) :
         match(next(ins)) :
            (e:Set) : (define(x(e)), emit(e), continue())
            (e:Op) : (define(xs(e)), emit(e), continue())
            (e:Branch) : (emit(e), continue())
            (e:Return) : (emit(e), continue())
            (e:Save) : (save(x(e)), continue())
            (e:Load) : (load(x(e)), continue())
            (e:Unload) : (unload(x(e)), continue())
            (e:WArgs) : continue-with-ports(xs(e), WArgs{_})
            (e:WCall) : continue-with-ports(xs(e),
                           WCall{arity(e), _, f(e), ys(e)})
      else :
         process-assumptions()

   ;Scan and process input ports
   do(mark-port, in(b))
   var in*
   scan{to-stream(ins(b)), _} $ fn* () :
      in* = map(get-port, in(b)) as List<LivePort>

   ;     Return result
   ;     -------------
   WBlock(name(b),
          accum,
          next(b),
          in*,
          out(b),
          dist(b))



;                      Register Assignment
;                      ===================

;     Assignment Mini-Language
;     ------------------------
defstruct FreeReg <: Loc :
   prefer: List<Int>
   prefer-not: List<Int>
   
defn FreeReg (p:List<Int>) : FreeReg(p, List())
defn FreeReg () : FreeReg(List(), List())

defmethod print (o:OutputStream, f:FreeReg) :
   print(o, "FreeReg(~@ not ~@)" << [cms(prefer(f)) cms(prefer-not(f))])

;     Algorithm
;     ---------
defn register-assignment (b:WBlock, ports-in:List<LivePort>) :
   ;     Utilities
   ;     ---------
   ;Convert list of liveports to a table of locations
   defn ports-to-table (ports:List<LivePort>) :
      val table = HashTable<Symbol,Loc>(symbol-hash)
      for p in ports do :
         table[name(p)] = loc(p)
      table

   ;Retrieve all the liveports in the given list
   defn liveports (ports:List<Port>) :
      val ports* = to-list $
         for p in ports filter :
            p typeof LivePort
      ports* as List<LivePort>

   ;Retrieve all Vars in given immediates
   defn vars-in (xs:Streamable<Imm>) :
      {_ as Stream<Var>} $
         for x in xs filter :
            x typeof Var

   ;Retrieve all Registers in given immediates
   defn regs-in (xs:List<Imm>) -> List<Int> :
      val locs = stream(loc, vars-in(xs))
      val regs = filter({_ typeof Reg}, locs)
      to-list(stream(n, regs as Stream<Reg>))

   ;     Algorithm State
   ;     ---------------
   val locs = HashTable<Symbol,Loc>(symbol-hash)
   val reg-slots = Array<False|Symbol>(8, false)
   val rt = release-table(b)

   ;     Output State
   ;     ------------
   val accum = Vector<Ins>()
   defn emit (i:Ins) :
      add(accum, i)      

   ;     State Updaters
   ;     --------------
   defn assign (x:Symbol, l:Loc) :
      defn next-free-reg (pref:List<Int>, npref:List<Int>) :
         label<Reg> return :
            ;First try preferred registers
            for r in pref do :
               if not contains?(npref, r) :
                  return(Reg(r)) when reg-slots[r] == false
            ;Otherwise search for free register   
            val i = for i in 0 to length(reg-slots) find! :
               reg-slots[i] == false
            Reg(i)

      match(l) :
         (l:FreeReg) : assign(x, next-free-reg(prefer(l), prefer-not(l)))
         (l:Stack) : locs[x] = l
         (l:Reg) :
            locs[x] = l
            reg-slots[n(l)] = x
      
   defn assign (xs:List<Symbol>, ls:List<Loc>) :         
      ;First assign stacks and regs
      for (x in xs, l in ls) do :
         assign(x, l) when l typeof Stack|Reg            
      ;Next assign free registers
      for (x in xs, l in ls) do :
         assign(x, l) when l typeof FreeReg

   ;Convenience
   defn assign (x:Var, l:Loc) :
      assign(name(x), l)
   defn assign (xs:List<Var>, ls:List<Loc>) :
      assign(map(name, xs), ls)      
   defn assign (xs:List<LivePort>, ls:List<Loc>) :
      assign(map(name, xs), ls)

   ;Update state by freeing the given variable
   defn free (x:Symbol) :
      match(locs[x]) :
         (loc:Reg) :
            reg-slots[n(loc)] = false
            locs[x] = Stack(false)
         (loc) : false

   defn free (xs:Streamable<Symbol>) :
      do(free, xs)

   ;     State Reader
   ;     ------------
   ;Fill the given immediate with its location
   defn fill<?T> (x:?T&Imm) :
      {_ as T&Imm} $ match(x) :
         (x:Var) : Var(name(x), locs[name(x)])
         (x:Val) : x
   defn fill<?T> (xs:List<?T&Imm>) -> List<T> :
      map(fill, xs)

   ;Fill the given port with its location
   defn fill<?T> (x:?T&Port) :
      {_ as T&Port} $ match(x) :
         (x:LivePort) : LivePort(name(x), saved?(x), loaded?(x), locs[name(x)])
         (x:DeadPort) : x
   defn fill<?T> (xs:List<?T&Port>) -> List<T> :
      map(fill, xs)

   ;     Swap Helpers
   ;     ------------
   ;Find a register that is either free, or one of the given registers
   ;and is not vetoed.
   defn swap-reg (free:List<Symbol>, veto:List<Int>) :
      label<Int> return :
         ;Try to find a free register
         val i = for i in 0 to length(reg-slots) find :
            (not contains?(veto, i)) and
            reg-slots[i] == false
         return(i as Int) when i typeof Int
         ;Find one of the given registers
         for i in 0 to length(reg-slots) find! :
            (not contains?(veto, i)) and
            contains?(free, reg-slots[i])

   ;Swap the two register contents
   defn swap (src:Int, dst:Int) :
      if reg-slots[dst] == false :
         val x = reg-slots[src] as Symbol
         emit(Set(Var(x, Reg(dst)), Var(x, Reg(src))))
         free(x)
         assign(x, Reg(dst))
      else :
         val x = reg-slots[src] as Symbol
         val y = reg-slots[dst] as Symbol
         val swap = for i in 0 to length(reg-slots) search :
            if (i != src) and (i != dst) and (reg-slots[i] == false) :
               Reg(i)
         emit(Xchg(Var(x, Reg(src)), Var(y, Reg(dst)), swap))
         free(x)
         free(y)
         assign(x, Reg(dst))
         assign(y, Reg(src))
            
   ;Ensure that the following registers are free
   defn ensure-free (reg:List<Int>, rs:List<Symbol>) :
      defn* loop (reg:List<Int>, veto:List<Int>) :
         if not empty?(reg) :
            val r = head(reg)
            if (reg-slots[r] != false) and not contains?(rs, reg-slots[r]) :
               swap(r, swap-reg(rs, veto))
            loop(tail(reg), List(r, veto))
      loop(reg, List())

   ;Ensure that the following value is in a certain register
   defn ensure-reg (x:Symbol, r:Int) :
      val src = n(locs[x] as Reg)
      swap(src, r) when src != r

   ;     Analysis
   ;     --------
   
   ;Assign initial registers   
   val in* = let :
      val pt = ports-to-table(ports-in)
      val locs = for x in in(b) map :
         match(loaded?(x), pt[name(x)]) :
            (ld:True, loc:Reg) : loc
            (ld:True, loc:Stack) : FreeReg()
            (ld:False, loc) : Stack(false)
            (ld:Any, loc) : loc
      assign(in(b), locs)
      fill(in(b))
   
   ;Assign concrete locations
   for (e in ins(b), i in 0 to false) do :
      free(releases-up-to(rt, Pos(i, false)))
      match(e) :
         (e:Set) :
            val y* = fill(y(e))
            free(releases-up-to(rt, Pos(i, true)))
            assign(x(e), FreeReg(regs-in(list(y*))))
            emit(Set(fill(x(e)), y*))
         (e:Op) :
            ;Implement different rules depending on operation type
            val ys* = match(op(e)) :
               (op:ComOp) :
                  val ys* = fill(ys(e))
                  free(releases-up-to(rt, Pos(i, true)))
                  assign(head(xs(e)), FreeReg(regs-in(ys*)))
                  ys*
               (op:NComOp) :
                  val ys* = fill(ys(e))
                  free(releases-up-to(rt, Pos(i, true)))
                  assign(head(xs(e)), FreeReg(regs-in(list(ys*[0])),
                                              regs-in(list(ys*[1]))))
                  ys*
               (op:Div2Op) :
                  val rs = to-list(releases-up-to(rt, Pos(i, true)))
                  ensure-free(list(0), rs)
                  val ys* = fill(ys(e))
                  free(rs)
                  assign(head(xs(e)), Reg(0))
                  ys*
            emit(Op(op(e), fill(xs(e)), ys*))
         (e:Branch) :
            emit(Branch(op(e), fill(xs(e))))
         (e:Return) :
            emit(Return(fill(xs(e))))
         (e:Save) :
            emit(Save(fill(x(e))))            
         (e:Load) :
            assign(list(x(e)), list(FreeReg()))
            emit(Load(fill(x(e))))
         (e:WArgs) :
            val lxs = liveports(xs(e))
            val locs = for x in lxs map :
               match(loaded?(x)) :
                  (l:True|Any) : FreeReg()
                  (l:False) : Stack(false)
            assign(lxs, locs)
            emit(WArgs(fill(xs(e))))
         (e:WCall) :
            val f* = fill(f(e))
            val ys* = fill(ys(e))
            free(releases-up-to(rt, Pos(i, true)))
            val lxs = liveports(xs(e))
            val locs = for x in lxs map :
               match(loaded?(x)) :
                  (l:True|Any) : FreeReg()
                  (l:False) : Stack(false)
            assign(lxs, locs)
            emit(WCall(arity(e), fill(xs(e)), f*, ys*))
   
   ;Output locations
   val out* = fill(out(b))

   ;     Return result
   ;     -------------
   WBlock(name(b), accum, next(b),
          in*, out*, dist(b))


defn register-assignment (p:Unit) :
   ;     Algorithm State
   ;     ---------------
   val block-table = HashTable<Symbol,False|WBlock>(symbol-hash)
   val blocks* = Vector<Block>()

   ;     Algorithm
   ;     ------------------   
   ;Initially fill table
   for b in blocks(p) do :
      block-table[name(b)] = b as WBlock

   ;Assign registers to the given block   
   defn assign-regs (b:WBlock, ports-in:List<LivePort>) :
      val b* = register-assignment(b, ports-in)
      add(blocks*, b*)
      block-table[name(b*)] = false
      b*

   ;Assign registers to successors
   defn* assign-successors (b:WBlock) :
      for n in next(b) do :
         match(block-table[n]) :
            (s:WBlock) : assign-successors(assign-regs(s, out(b)))
            (s:False) : false

   ;Driver
   val b0 = blocks(p)[0] as WBlock
   val b0* = assign-regs(b0, List())
   assign-successors(b0*)
   Unit(defs(p), blocks*)
   



;                   Register Assignment Verification
;                   ================================

defn verify-register-assignment (b:WBlock) :
   ;     Algorithm State
   ;     ---------------
   val regs = Array<False|Symbol>(8, false)
   val saved = HashTable<Symbol,True|False>(symbol-hash)

   ;     State Updaters
   ;     --------------
   defn define (x:Port) :
      match(x) :
         (x:LivePort) :
            match(saved?(x), loaded?(x), loc(x)) :
               (sv:True, ld:True, loc:Reg) :
                  regs[n(loc)] = name(x)
                  saved[name(x)] = true
               (sv:False, ld:True, loc:Reg) :
                  regs[n(loc)] = name(x)
                  saved[name(x)] = false
               (sv:True, ld:False, loc:Stack) :
                  saved[name(x)] = true
         (x:DeadPort) :
            false

   defn define (x:Var) :
      val r = n(loc(x) as Reg)
      regs[r] = name(x)
      saved[name(x)] = false

   defn mov (x:Var, y:Imm) :
      val r = n(loc(x) as Reg)
      regs[r] = name(x)
      match(y) :
         (y:Val) :
            saved[name(x)] = false
         (y:Var) : 
            if name(x) != name(y) :
               saved[name(x)] = false

   defn load (x:Var) :
      val r = n(loc(x) as Reg)
      regs[r] = name(x)

   defn save (x:Var) :
      saved[name(x)] = true

   defn clear-registers () :
      for i in 0 to length(regs) do :
         regs[i] = false

   defn swap-register-contents (x:Reg, y:Reg) :
      val vx = regs[n(x)]
      val vy = regs[n(y)]
      regs[n(x)] = vy
      regs[n(y)] = vx   

   ;     State Checkers
   ;     --------------
   defn reg! (x:Imm) :
      match(x) :
         (x:Var) : loc(x) as Reg
         (x:Val) : false
   defn saved! (x:Symbol) : saved[x] as True
   defn saved! (x:Var) : saved!(name(x))
   defn not-saved! (x:Symbol) : saved[x] as False
   defn holds! (r:Reg, x:Symbol) : (regs[n(r)] == x) as True
   defn holds! (r:Reg, x:Var) : holds!(r, name(x))
   
   defn check! (p:Port) :
      match(p) :
         (p:LivePort) :
            match(saved?(p), loaded?(p), loc(p)) :
               (sv:True, ld:True, lc:Reg) :
                  holds!(lc, name(p))
                  saved!(name(p))
               (sv:False, ld:True, lc:Reg) :
                  holds!(lc, name(p))
                  not-saved!(name(p))
               (sv:True, ld:False, lc:Stack) :
                  saved!(name(p))
         (p:DeadPort) :
            false

   defn check! (x:Imm) :
      match(x) :
         (x:Var) :
            match(loc(x)) :
               (l:Reg) : holds!(l, name(x))
               (l:Stack) : saved!(name(x))
         (x:Val) : false

   ;     Algorithm
   ;     ---------
   
   ;Input ports
   do(define, in(b))

   ;Check each instruction
   for e in ins(b) do :
      match(e) :
         (e:Set) :
            check!(y(e))
            reg!(x(e))
            mov(x(e), y(e))
         (e:Xchg) :
            check!(x(e))
            check!(y(e))
            val rx = loc(x(e)) as Reg
            val ry = loc(y(e)) as Reg
            swap-register-contents(rx, ry)
         (e:Op) :
            ;General operations
            for y in ys(e) do : (check!(y), reg!(y))
            for x in xs(e) do : (reg!(x), define(x))

            ;OpClass post conditions
            match(op(e)) :
               (op:Div2Op) : holds!(Reg(0), head(xs(e)))
               (op) : false
         (e:Branch) :
            for x in xs(e) do : (check!(x), reg!(x))
         (e:Return) :
            do(check!, xs(e))
         (e:Save) :
            check!(x(e))
            save(x(e))
         (e:Load) :
            saved!(x(e))
            load(x(e))
         (e:WArgs) :
            do(define, xs(e))
         (e:WCall) :
            do(check!, ys(e))
            check!(f(e))
            clear-registers()
            do(define, xs(e))

   ;Output ports
   do(check!, out(b))
