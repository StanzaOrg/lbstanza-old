defpackage stz/type :
  import core
  import collections
  import stz/il-ir
  import stz/il-to-tl
  import stz/tl-ir
  import stz/dl-ir
  import stz/tl-to-dl
  import stz/type-errors
  import stz/tl-formatter
  import stz/type-gen-equations
  import stz/type-equation-solver
  import stz/type-fill
  import stz/tl-guards
  import stz/type-error-formatter
  import stz/type-hierarchy
  import stz/namemap

;============================================================
;====================== Typing Environment ==================
;============================================================

;Represents the type definitions from non-source-defined packages.
public deftype Env

;Return the publicly visible exports from a package.
;Used to retrieve the definitions from packages that are not
;defined directly in source as an IPackage.
public defmulti exports (e:Env, package:Symbol) -> PackageExports

;============================================================
;====================== Main Entrypoint =====================
;============================================================

public defn type-program (ipackages:Tuple<IPackage>,
                          environment:Env) -> TProg|TypeErrors :
  ;Return TypeErrors if necessary.
  label<TProg|TypeErrors> return :

    ;Gather all the exports from the environment.
    val exports = gather-exports(ipackages, environment)

    ;Convert the input packages to a typed program.
    val tprog = match(to-tl(ipackages, exports)) :
      (prog:TProg) : prog
      (err:TypeErrors) : return(err)

    ;Generating type equations
    val [tprog-with-vars, type-eqns] = generate-type-equations(tprog)

    ;Sanity check: Ensure TProg satisfies post-type-gen conditions.
    guard!(tprog-with-vars, TypeGenTL)

    ;Solving type equations.
    set-debug-formatter(TLFormatter(tprog-with-vars))
    val solver = SolverState(type-eqns, hierarchy(tprog-with-vars))
    solve(solver)

    ;Now that they are solved, fill in the
    ;program with the solutions.
    val tprog-solved = match(fill-solution(tprog-with-vars, solver)) :
      (es:TypeErrors) : return(FormattedTypeErrors(namemap(tprog-with-vars), hierarchy(tprog-with-vars), es))
      (prog:TProg) : prog

    ;Sanity check: Ensure TProg satisfies Solved invariants.
    guard!(tprog-solved, SolvedTL)

    ;Return solved tprog with the PackageIO filled.
    compute-packageios(tprog-solved)

;============================================================
;============= Gather Exports from Environment ==============
;============================================================

;Gather the exports from non-source-defined packages.
defn gather-exports (ipackages:Tuple<IPackage>, env:Env) -> Tuple<Export> :
  ;Initialize set with all source-defined packages.
  val defined-packages = to-hashset<Symbol>(seq(name, ipackages))

  ;Include the exports for all the imported packages
  val export-accum = Vector<Export>()

  ;Add exports from 'p' to 'export-accum' if it hasn't
  ;already been added.
  defn include-package (p:Symbol) -> False :
    if add(defined-packages, p) :
      include-package(exports(env,p))

  ;Add the given exports, and all its transitive
  ;imports to 'export-accum'.
  defn include-package (pex:PackageExports) -> False :
    add-all(export-accum, exports(pex))
    do(include-package, imported-packages(pex))

  ;Include all imports from packages.
  for p in ipackages do :
    do(include-package{package(_)}, imports(p))

  ;Always include the 'core' package.
  include-package(`core)

  ;Return the accumulated exports.
  to-tuple(export-accum)

;============================================================
;=============== Format Type Errors =========================
;============================================================

;Represents a type error with formatting tables for printing.
public deftype FormattedTypeError <: TypeError
public defmulti error (e:FormattedTypeError) -> TypeError

;Create a formatted type error.
public defn FormattedTypeError (f:TypeErrorFormatter, e:TypeError) -> FormattedTypeError :
  new FormattedTypeError :
    defmethod error (this) :
      e
    defmethod print (o:OutputStream, this) :
      print(o, format(f, e))

;Format all errors.
public defn FormattedTypeErrors (namemap:NameMap,
                                 hier:TypeHierarchy,
                                 input-es:TypeErrors) -> TypeErrors :
  val formatter = TypeErrorFormatter(namemap, hier)
  val es = prune-errors(input-es)
  val ferrors = map(FormattedTypeError{formatter, _}, errors(es))
  TypeErrors(ferrors)