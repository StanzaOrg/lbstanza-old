;See License.txt for details about licensing.

#use-added-syntax(stz-fast-print-lang)
defpackage stz/asm-emitter :
  import core
  import collections
  import stz/asm-ir
  import stz/utils
  ;import stz/ids
  import stz/backend

;============================================================
;================== Restrictions ============================
;============================================================

defn check-restriction (ins:Ins, backend:Backend) :
   ;     Error
   ;     -----
   defn RE () : fatal("Instruction %_ does not satisfy restrictions." % [ins])

   ;     Types of Types
   ;     --------------
   defn #L (t:ASMType) : t is LongT
   defn #I (t:ASMType) : t is IntT
   defn #B (t:ASMType) : t is ByteT
   defn #F (t:ASMType) : t is FloatT
   defn #D (t:ASMType) : t is DoubleT
   defn #BI (t:ASMType) : t is ByteT|IntT
   defn #IL (t:ASMType) : t is IntT|LongT
   defn #i (t:ASMType) : t is ByteT|IntT|LongT
   defn #f (t:ASMType) : t is FloatT|DoubleT

   ;     Assumptions about Immediates
   ;     ----------------------------
   defn #SR! (x:Imm) : RE() when x is-not RegSP|Reg
   defn #SRI! (x:Imm) : RE() when x is-not RegSP|Reg|IntImm
   defn #SRIM! (x:Imm) : RE() when x is-not RegSP|Reg|IntImm|Mem|ExMem
   defn #R! (x:Imm) : RE() when x is-not Reg
   defn #RI! (x:Imm) : RE() when x is-not Reg|IntImm
   defn #F! (x:Imm) : RE() when x is-not FReg
   defn #SRM! (x:Imm) : RE() when x is-not RegSP|Reg|Mem|ExMem
   defn #RM! (x:Imm) : RE() when x is-not Reg|Mem|ExMem
   defn #M! (x:Imm) : RE() when x is-not Mem|ExMem
   defn #R! (x:Imm, num:Int) :
      #R!(x)
      RE() when n(x as Reg) != num
   defn #R0! (x:Imm) : #R!(x, 0)
   defn #R3! (x:Imm) : #R!(x, 3)
   defn #IR2! (x:Imm) :
      match(x) :
         (x:Reg) : RE() when n(x) != 2
         (x:IntImm) : false
         (x) : RE()

   ;     Assumptions about Operators
   ;     ---------------------------
   defn #DIVMOD! (op:Op) : RE() when op is-not DivModOp

   ;     Compound Assumptions
   ;     --------------------
   defn #nII! (x:Imm, y:Imm) :
      match(x, y) :
         (x:IntImm, y:IntImm) : RE()
         (x, y) : false
   defn #neq! (x:Imm, y:Imm) :
      RE() when x == y
   defn #neq! (x:ASMType, y:ASMType) :
      RE() when x == y

   ;     Position Independence
   ;     ---------------------
   ;Ensure that x is not an external memory location
   ;if backend requires loads/stores to go through the global offset table.
   defn #nExM! (x:Imm) :
     if use-global-offset-table?(backend) :
       RE() when x is ExMem
   ;Ensure that x is not an external memory location with
   ;non-zero offset if backend requires calls to go through the procedure linkage table.
   defn #nExM0! (x:Imm) :
     if use-procedure-linkage-table?(backend) :
       match(x:ExMem) :
         RE() when offset(x) != 0

   ;     Dispatch on type of instruction
   ;     -------------------------------
   match(ins) :
      (ins:SetIns) :
         val [t, x, y] = [type(ins), x(ins), y(ins)]
         switch _{t} :
            #L : (#SR!(x), #SRIM!(y))
            #i : (#R!(x), #RI!(y))
            #f : (#F!(x), #F!(y))
      (ins:XchgIns) :
         val [x, y] = [x(ins), y(ins)]
         #R!(x), #R!(y)
      (ins:ConvertIns) :
         val [xt, yt, x, y] = [xtype(ins), ytype(ins), x(ins), y(ins)]
         if #i(xt) and #i(yt) : (#R!(x), #R!(y))
         else if #f(xt) and #f(yt) : (#F!(x), #F!(y))
         else if #IL(xt) and #f(yt) : (#R!(x), #F!(y))
         else if #f(xt) and #IL(yt) : (#F!(x), #R!(y))
         else : RE()
      (ins:InterpretIns) :
         val [xt, yt, x, y] = [xtype(ins), ytype(ins), x(ins), y(ins)]
         #SR!(x) when #i(xt)
         #F!(x) when #f(xt)
         #SR!(y) when #i(yt)
         #F!(y) when #f(yt)
         if #I(xt) : (RE() when not (#I(yt) or #F(yt)))
         else if #L(xt) : (RE() when not (#L(yt) or #D(yt)))
         else if #F(xt) : (RE() when not (#I(yt) or #F(yt)))
         else if #D(xt) : (RE() when not (#L(yt) or #D(yt)))
         else : RE()
      (ins:NoOp) :
        false
      (ins:UnaOp) :
         val [t, x, y, op] = [type(ins), x(ins), y(ins), op(ins)]
         if #L(t) : (#SR!(x), #SR!(y))
         else if #I(t) : (#R!(x), #R!(y))
         else : RE()
      (ins:VoidBinOp) :
         val [t, y, z, op] = [type(ins), y(ins), z(ins), op(ins)]
         if #L(t) : (#SR!(y), #SR!(z))
         else : RE()
      (ins:BinOp) :
         val [t, x, y, z, op] = [type(ins), x(ins), y(ins), z(ins), op(ins)]
         if #L(t) :
            defn com-op () : (#SR!(x), #SRI!(y), #SRI!(z))
            defn ncom-op () : (#SR!(x), #SRI!(y), #SRI!(z), #neq!(x, z))
            defn shf-op () : (#SR!(x), #SRI!(y), #IR2!(z), #neq!(x, z))
            defn cmp-op () : (#R!(x), #SRI!(y), #SRI!(z), #nII!(y,z))
            defn test-op () : (#R!(x), #RI!(y), #R!(z))
            match(op) :
               (op:AddOp) : com-op()
               (op:SubOp) : ncom-op()
               (op:MulOp) : com-op()
               (op:AndOp) : com-op()
               (op:OrOp) : com-op()
               (op:XorOp) : com-op()
               (op:ShlOp) : shf-op()
               (op:ShrOp) : shf-op()
               (op:AshrOp) : shf-op()
               (op:EqOp) : cmp-op()
               (op:NeOp) : cmp-op()
               (op:LtOp) : cmp-op()
               (op:GtOp) : cmp-op()
               (op:LeOp) : cmp-op()
               (op:GeOp) : cmp-op()
               (op:UleOp) : cmp-op()
               (op:UltOp) : cmp-op()
               (op:UgtOp) : cmp-op()
               (op:UgeOp) : cmp-op()
               (op:BitSetOp) : cmp-op()
               (op:BitNotSetOp) : cmp-op()
               (op:TestBitOp) : test-op()
               (op:TestAndSetBitOp) : test-op()
               (op:TestAndClearBitOp) : test-op()
               (op) : RE()
         else if #I(t) :
            defn com-op () : (#R!(x), #RI!(y), #RI!(z))
            defn ncom-op () : (#R!(x), #RI!(y), #RI!(z), #neq!(x, z))
            defn shf-op () : (#R!(x), #RI!(y), #IR2!(z), #neq!(x, z))
            defn cmp-op () : (#R!(x), #RI!(y), #RI!(z), #nII!(y,z))
            match(op) :
               (op:AddOp) : com-op()
               (op:SubOp) : ncom-op()
               (op:MulOp) : com-op()
               (op:AndOp) : com-op()
               (op:OrOp) : com-op()
               (op:XorOp) : com-op()
               (op:ShlOp) : shf-op()
               (op:ShrOp) : shf-op()
               (op:AshrOp) : shf-op()
               (op:EqOp) : cmp-op()
               (op:NeOp) : cmp-op()
               (op:LtOp) : cmp-op()
               (op:GtOp) : cmp-op()
               (op:LeOp) : cmp-op()
               (op:GeOp) : cmp-op()
               (op:UleOp) : cmp-op()
               (op:UltOp) : cmp-op()
               (op:UgtOp) : cmp-op()
               (op:UgeOp) : cmp-op()
               (op:BitSetOp) : cmp-op()
               (op:BitNotSetOp) : cmp-op()
               (op) : RE()
         else if #B(t) :
            defn com-op () : (#R!(x), #RI!(y), #RI!(z))
            defn ncom-op () : (#R!(x), #RI!(y), #RI!(z), #neq!(x, z))
            defn shf-op () : (#R!(x), #RI!(y), #IR2!(z), #neq!(x, z))
            defn cmp-op () : (#R!(x), #RI!(y), #RI!(z), #nII!(y,z))
            match(op) :
               (op:AddOp) : com-op()
               (op:SubOp) : ncom-op()
               (op:AndOp) : com-op()
               (op:OrOp) : com-op()
               (op:XorOp) : com-op()
               (op:ShlOp) : shf-op()
               (op:ShrOp) : shf-op()
               (op:AshrOp) : shf-op()
               (op:EqOp) : cmp-op()
               (op:NeOp) : cmp-op()
               (op:LtOp) : cmp-op()
               (op:GtOp) : cmp-op()
               (op:LeOp) : cmp-op()
               (op:GeOp) : cmp-op()
               (op:UleOp) : cmp-op()
               (op:UltOp) : cmp-op()
               (op:UgtOp) : cmp-op()
               (op:UgeOp) : cmp-op()
               (op) : RE()
         else if #f(t) :
            defn reg-op () : (#F!(x), #F!(y), #F!(z))
            defn ncom-op () : (#F!(x), #F!(y), #F!(z), #neq!(x, z))
            defn cmp-op () : (#R!(x), #F!(y), #F!(z))
            match(op) :
               (op:AddOp) : reg-op()
               (op:SubOp) : ncom-op()
               (op:MulOp) : reg-op()
               (op:DivOp) : ncom-op()
               (op:EqOp) : cmp-op()
               (op:NeOp) : cmp-op()
               (op:LtOp) : cmp-op()
               (op:GtOp) : cmp-op()
               (op:LeOp) : cmp-op()
               (op:GeOp) : cmp-op()
               (op:UleOp) : cmp-op()
               (op:UltOp) : cmp-op()
               (op:UgtOp) : cmp-op()
               (op:UgeOp) : cmp-op()
               (op) : RE()
         else : RE()
      (ins:DualOp) :
         val [t, x1, x2, op, y, z] = [type(ins), x1(ins), x2(ins), op(ins), y(ins), z(ins)]
         if #IL(t) : (#R0!(x1), #R3!(x2), #R!(y), #R!(z), #DIVMOD!(op), #neq!(x1, z), #neq!(x2, z))
         else : RE()
      (ins:Load) :
         val [t, x, y] = [type(ins), x(ins), y(ins)]
         #nExM!(y)
         if #L(t) : (#SR!(x), #SRIM!(y))
         else if #i(t) : (#R!(x), #SRIM!(y))
         else if #f(t) : (#F!(x), #SRIM!(y))
         else : RE()
      (ins:Store) :
         val [t, x, y] = [type(ins), x(ins), y(ins)]
         #nExM!(x)
         if #L(t) : (#SRIM!(x), #SRI!(y))
         else if #i(t) : (#SRIM!(x), #RI!(y))
         else if #f(t) : (#SRIM!(x), #F!(y))
         else : RE()
      (ins:Call) :
         #nExM0!(x(ins))
         #RM!(x(ins))
      (ins:Return) :
         false
      (ins:Goto) :
         #nExM!(x(ins))
         #RM!(x(ins))
      (ins:LoadGoto) :
         #nExM!(x(ins))
         #M!(x(ins))         
      (ins:Break) :
         val [t, x, op, y, z] = [type(ins), x(ins), op(ins), y(ins), z(ins)]
         #nExM!(x)
         if #L(t) : (#M!(x), #SRI!(y), #SRI!(z), #nII!(y,z))
         else if #i(t) : (#M!(x), #RI!(y), #RI!(z), #nII!(y,z))
         else if #f(t) : (#M!(x), #F!(y), #F!(z))
         else : RE()
      (ins:Label|ExLabel) :
         false
      (ins:DefData|DefText|DefByte|DefInt|DefLong|DefDirectives|DefExportLabel|
           DefFloat|DefDouble|DefString|DefBytes|DefSpace|DefLabel|Comment) :
         false


;============================================================
;====================== Emitter =============================
;============================================================

;         Names of Registers for Different Types
;         ======================================

val REG-LONG-NAMES = [
   "%rax", "%rbx", "%rcx", "%rdx", "%rsi", "%rdi", "%rbp", "%r8",
   "%r9", "%r10", "%r11", "%r12", "%r13", "%r14", "%r15"]

val REG-INT-NAMES = [
   "%eax", "%ebx", "%ecx", "%edx", "%esi", "%edi", "%ebp", "%r8d",
   "%r9d", "%r10d", "%r11d", "%r12d", "%r13d", "%r14d", "%r15d"]

val REG-BYTE-NAMES = [
   "%al", "%bl", "%cl", "%dl", "%sil", "%dil", "%bpl", "%r8b",
   "%r9b", "%r10b", "%r11b", "%r12b", "%r13b", "%r14b", "%r15b"]

val REG-FLOAT-NAMES = [
   "%xmm0", "%xmm1", "%xmm2", "%xmm3",
   "%xmm4", "%xmm5", "%xmm6", "%xmm7",
   "%xmm8", "%xmm9", "%xmm10", "%xmm11",
   "%xmm12", "%xmm13", "%xmm14", "%xmm15"]


;               Generation of Instructions
;               ==========================

defn gen (os:OutputStream, ins:Ins, backend:Backend) :
   ;     Utilities
   ;     ---------
   defn chars (s:String) :
      val n = length(s) + 1
      val rem = ((n + 3) & -4) - n
      cat(seq(to-int, s), repeat(0, 1 + rem))
   defn #lbl (n:Int) : "__L%_" % [n]
   defn #lbl (i:Mem) : #lbl(n(i))
   defn #lbl (i:ExMem) :
     if prepend-underscore?(backend) : "_%_" % [lbl(i)]
     else : lbl(i)

   ;     Predicates for Types
   ;     --------------------
   defn #B (t:ASMType) : t is ByteT
   defn #I (t:ASMType) : t is IntT
   defn #L (t:ASMType) : t is LongT
   defn #IL (t:ASMType) : t is IntT|LongT
   defn #i (t:ASMType) : t is ByteT|IntT|LongT
   defn #f (t:ASMType) : t is FloatT|DoubleT

   ;     Position Independence
   ;     ---------------------
   ;Check whether x is an external memory location when
   ;backend requires global offset table.
   defn #ExM (i:Imm) :
      use-global-offset-table?(backend) and i is ExMem
   ;Check whether i is an external memory location when
   ;backend requires procedure linkage table.
   defn #ExMPLT (i:Imm) :
      use-procedure-linkage-table?(backend) and i is ExMem

   ;     Size Suffix
   ;     -----------
   defn size-suffix (t:ASMType) :
      match(t) :
         (t:ByteT) : "b"
         (t:IntT) : "l"
         (t:LongT) : "q"
         (t:FloatT) : "s"
         (t:DoubleT) : "d"

   ;     Immediate Names
   ;     ---------------
   defn #imm (t:ASMType, x:Imm) :
      match(x) :
         (x:IntImm) :
            "$%_" % [value(x)]
         (x:Reg) :
            match(t) :
               (t:ByteT) : REG-BYTE-NAMES[n(x)]
               (t:IntT) : REG-INT-NAMES[n(x)]
               (t:LongT) : REG-LONG-NAMES[n(x)]
         (x:FReg) :
            REG-FLOAT-NAMES[n(x)]
         (x:RegSP) :
            "%rsp"

   ;     Set Instruction
   ;     ---------------
   defn #set (t:ASMType, x:Loc, y:Imm) :
      match(y) :
         (y:Mem|ExMem) :
            #println(os, "   leaq (%_ + %_)(%%rip), %_" % [#lbl(y), offset(y), #imm(t,x)])
         (y) :
            if #i(t) : #println(os, "   mov%_ %_, %_" % [size-suffix(t), #imm(t,y), #imm(t,x)])
            else if #f(t) : #println(os, "   movs%_ %_, %_" % [size-suffix(t), #imm(t,y), #imm(t,x)])
            else : fatal("Unreachable")

   defn #pic-set (x:Loc, y:ExMem) :
      #println(os, "  movq %_@GOTPCREL(%%rip), %_" % [#lbl(y), #imm(LongT(),x)])
      #println(os, "  addq $_, %_" % [offset(y), #imm(LongT(), x)]) when offset(y) != 0

   ;     Convert Instruction
   ;     -------------------
   defn #conv (x:Loc, y:Imm, xtype:ASMType, ytype:ASMType) :
      val xsz = size-suffix(xtype)
      val ysz = size-suffix(ytype)

      ;Integer to Integer Conversions
      if #i(xtype) and #i(ytype) :
         defn int-less-eq? (a:ASMType, b:ASMType) :
            if #L(a) : #L(b)
            else if #I(a) : #IL(b)
            else : true
         ;Truncation Conversion
         if int-less-eq?(xtype, ytype) :
            #set(xtype, x, y)
         ;Extension Conversion
         else :
            val mov = "movz" when #B(ytype) else "movs"
            #println(os, "   %_%_%_ %_, %_" % [mov, ysz, xsz, #imm(ytype, y), #imm(xtype, x)])

      ;Float to Float Conversions
      else if #f(xtype) and #f(ytype) :
         ;If Equal
         if xtype == ytype :
            #set(xtype, x, y)
         else :
            #println(os, "   cvts%_2s%_ %_, %_" % [ysz, xsz, #imm(ytype, y), #imm(xtype, x)])

      ;Float to Integer Conversions
      else if #i(xtype) and #f(ytype) :
         #println(os, "   cvtts%_2si%_ %_, %_" % [ysz, xsz, #imm(ytype, y), #imm(xtype, x)])

      ;Integer to Float Conversions
      else if #f(xtype) and #i(ytype) :
         #println(os, "   cvtsi2s%_%_ %_, %_" % [xsz, ysz, #imm(ytype, y), #imm(xtype, x)])

      ;Error
      else :
         fatal("Unreachable")

   ;     Reinterpret Instruction
   ;     -----------------------
   defn #inter (x:Loc, y:Imm, xtype:ASMType, ytype:ASMType) :
      val xsz = size-suffix(xtype)
      val ysz = size-suffix(ytype)

      ;Noop Conversions
      if xtype == ytype :
         #set(xtype, x, y)
      ;Float to Integer Conversions
      else if #i(xtype) and #f(ytype) :
         #println(os, "   movq %_, %_" % [#imm(DoubleT(),y), #imm(LongT(),x)])
      ;Integer to Float Conversions
      else if #f(xtype) and #i(ytype) :
         #println(os, "   movq %_, %_" % [#imm(LongT(),y), #imm(DoubleT(),x)])
      else :
         fatal("Unreachable")

   ;     Save/Load Instructions
   ;     ----------------------
   defn #load (t:ASMType, x:Loc, y:Imm, o:Int) :
      val sz = size-suffix(t)
      val mov = if #i(t) : "mov"
                else if #f(t) : "movs"
                else : fatal("Unreachable")
      match(y) :
         (y:Reg|RegSP) : #println(os, "   %_%_ %_(%_), %_" % [mov, sz, o, #imm(LongT(), y), #imm(t, x)])
         (y:Mem|ExMem) : #println(os, "   %_%_ (%_ + %_)(%%rip), %_" % [mov, sz, #lbl(y), o + offset(y), #imm(t, x)])
         (y:IntImm) : #println(os, "   %_%_ (%_ + %_), %_" % [mov, sz, value(y), o, #imm(t, x)])

   defn #store (t:ASMType, x:Imm, y:Imm, o:Int) :
      val sz = size-suffix(t)
      val mov = if #i(t) : "mov"
                else if #f(t) : "movs"
                else : fatal("Unreachable")
      match(x) :
         (x:Reg|RegSP) : #println(os, "   %_%_ %_, %_(%_)" % [mov, sz, #imm(t, y), o, #imm(LongT(), x)])
         (x:Mem|ExMem) : #println(os, "   %_%_ %_, (%_ + %_)(%%rip)" % [mov, sz, #imm(t, y), #lbl(x), o + offset(x)])
         (x:IntImm) : #println(os, "   %_%_ %_, (%_ + %_)" % [mov, sz, #imm(t, y), value(x), o])

   defn #una (t:ASMType, x:Loc, op:Op, y:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:NotOp) : "not"
         (op:NegOp) : "neg"
      if x == y :
         #println(os, "   %_%_ %_" % [comm, sz, #imm(t,x)])
      else :
         #set(t, x, y)
         #una(t, x, op, x)

   defn #com (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:AddOp) : "add"
         (op:MulOp) : "imul"
         (op:AndOp) : "and"
         (op:OrOp) : "or"
         (op:XorOp) : "xor"
      if x == y :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,z), #imm(t,x)])
      else if x == z :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,y), #imm(t,x)])
      else :
         #set(t, x, y)
         #com(t, x, x, op, z)

   defn #ncom (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:SubOp) : "sub"
      if x == y :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,z), #imm(t,x)])
      else :
         #set(t, x, y)
         #ncom(t, x, x, op, z)

   defn #test-bit (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val comm = match(op) :
         (op:TestBitOp) : "bt"
         (op:TestAndSetBitOp) : "bts"
         (op:TestAndClearBitOp) : "btr"
      #println(os, "   %_q %_, (%_)" % [comm, #imm(t,y), #imm(t,z)])
      ;x = CF ? -1 : 0
      #println(os, "   sbbq %_, %_" % [#imm(t,x), #imm(t,x)])
      #println(os, "   andq $1, %_" % [#imm(t,x)])

   defn #set-bit (t:ASMType, y:Imm, op:Op, z:Imm) :
      val comm = match(op) :
         (op:SetBitOp) : "bts"
         (op:ClearBitOp) : "btr"
      #println(os, "   %_q %_, (%_)" % [comm, #imm(t,y), #imm(t,z)])

   defn #shf (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:ShlOp) : "shl"
         (op:ShrOp) : "shr"
         (op:AshrOp) : "sar"
      if x == y :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(ByteT(), z), #imm(t,x)])
      else :
         #set(t, x, y)
         #shf(t, x, x, op, z)

   defn #div (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      #println(os, "   movzbw %_, %%ax" % [#imm(t, y)])
      #println(os, "   idivb %_" % [#imm(t, z)])
      println(os, "   shrw $8, %%ax") when op is ModOp

   defn #cmp (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val comm = cmp-suffix(op)
      val sz = size-suffix(t)
      match(y, z) :
         (y:Reg, z) :
            #println(os, "   %_%_ %_, %_" % [cmp-op(op), sz, #imm(t,z), #imm(t,y)])
            #println(os, "   movq $0, %_" % [#imm(LongT(), x)])
            #println(os, "   set%_ %_" % [comm, #imm(ByteT(), x)])
         (y, z:Reg) :
            #cmp(t, x, z, swap(op), y)

   defn cmp-op (op:Op) :
      match(op) :
         (op:EqOp) : "cmp"
         (op:NeOp) : "cmp"
         (op:LtOp) : "cmp"
         (op:GtOp) : "cmp"
         (op:LeOp) : "cmp"
         (op:GeOp) : "cmp"
         (op:UleOp) : "cmp"
         (op:UltOp) : "cmp"
         (op:UgtOp) : "cmp"
         (op:UgeOp) : "cmp"
         (op:BitSetOp) : "bt"
         (op:BitNotSetOp) : "bt"

   defn cmp-suffix (op:Op) :
      match(op) :
         (op:EqOp) : "e"
         (op:NeOp) : "ne"
         (op:LtOp) : "l"
         (op:GtOp) : "g"
         (op:LeOp) : "le"
         (op:GeOp) : "ge"
         (op:UleOp) : "be"
         (op:UltOp) : "b"
         (op:UgtOp) : "a"
         (op:UgeOp) : "ae"
         (op:BitSetOp) : "c"
         (op:BitNotSetOp) : "nc"

   defn #fcom (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:AddOp) : "adds"
         (op:MulOp) : "muls"
      if x == y :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,z), #imm(t,x)])
      else if x == z :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,y), #imm(t,x)])
      else :
         #set(t, x, y)
         #fcom(t, x, x, op, z)

   defn #fncom (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = match(op) :
         (op:SubOp) : "subs"
         (op:DivOp) : "divs"
      if x == y :
         #println(os, "   %_%_ %_, %_" % [comm, sz, #imm(t,z), #imm(t,x)])
      else :
         #set(t, x, y)
         #fncom(t, x, x, op, z)

   defn fcmp-suffix (op:Op) :
      match(op) :
         (op:EqOp) : "e"
         (op:NeOp) : "ne"
         (op:LeOp) : "be"
         (op:LtOp) : "b"
         (op:GtOp) : "a"
         (op:GeOp) : "ae"

   defn #fcmp (t:ASMType, x:Loc, y:Imm, op:Op, z:Imm) :
      val comm = fcmp-suffix(op)
      val sz = size-suffix(t)
      match(y, z) :
         (y:FReg, z:FReg) :
            #println(os, "   ucomis%_ %_, %_" % [sz, #imm(t,z), #imm(t,y)])
            #println(os, "   movq $0, %_" % [#imm(LongT(), x)])
            #println(os, "   set%_ %_" % [comm, #imm(ByteT(), x)])

   defn #br (t:ASMType, x:Imm, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = cmp-suffix(op)
      match(y, z) :
         (y:Reg, z) :
            val x = x as Mem|ExMem
            #println(os, "   %_%_ %_, %_" % [cmp-op(op), sz, #imm(t,z), #imm(t,y)])
            #println(os, "   j%_ %_ + %_" % [comm, #lbl(x), offset(x)])
         (y, z:Reg) :
            #br(t, x, z, swap(op), y)

   defn #fbr (t:ASMType, x:Imm, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      val comm = fcmp-suffix(op)
      match(y, z) :
         (y:FReg, z:FReg) :
            val x = x as Mem|ExMem
            #println(os, "   ucomis%_ %_, %_" % [sz, #imm(t,z), #imm(t,y)])
            #println(os, "   j%_ %_ + %_" % [comm, #lbl(x), offset(x)])

   ;     Dual Divide Operator
   ;     --------------------
   defn #divmod (t:ASMType, x1:Loc, x2:Loc, y:Imm, op:Op, z:Imm) :
      val sz = size-suffix(t)
      if x1 == y :
         match(t) :
            (t:LongT) : println(os, "   cqo")
            (t:IntT) : println(os, "   cdq")
         #println(os, "   idiv%_ %_" % [sz, #imm(t,z)])
      else :
         #set(t, x1, y)
         #divmod(t, x1, x2, x1, op z)

   ;====== Driver ======
   match(ins) :
      (ins:XchgIns) :
         val [t, x, y] = [LongT(), x(ins), y(ins)]
         #println(os, "   xchgq %_, %_" % [#imm(t,x), #imm(t,y)])
      (ins:SetIns) :
         val [t, x, y] = [type(ins), x(ins), y(ins)]
         if #ExM(y) : #pic-set(x, y as ExMem)
         else : #set(t, x, y)
      (ins:NoOp) :
         println(os, "   nop")
      (ins:UnaOp) :
         val [t, x, op, y] = [type(ins), x(ins), op(ins), y(ins)]
         match(op) :
            (op:LowestZeroBitCountOp) : #println(os, "   tzcntq %_, %_" % [#imm(t,y), #imm(t,x)])
            (op) : #una(t, x, op, y)
      (ins:VoidBinOp) :
         #set-bit(type(ins), y(ins), op(ins), z(ins))
      (ins:BinOp) :
         val [t, x, op, y, z] = [type(ins), x(ins), op(ins), y(ins), z(ins)]
         if #i(t) :
            match(op) :
               (op:AddOp) : #com(t, x, y, op, z)
               (op:SubOp) : #ncom(t, x, y, op, z)
               (op:MulOp) : #com(t, x, y, op, z)
               (op:AndOp) : #com(t, x, y, op, z)
               (op:OrOp) : #com(t, x, y, op, z)
               (op:XorOp) : #com(t, x, y, op, z)
               (op:ShlOp) : #shf(t, x, y, op, z)
               (op:ShrOp) : #shf(t, x, y, op, z)
               (op:AshrOp) : #shf(t, x, y, op, z)
               (op:EqOp) : #cmp(t, x, y, op, z)
               (op:NeOp) : #cmp(t, x, y, op, z)
               (op:LtOp) : #cmp(t, x, y, op, z)
               (op:GtOp) : #cmp(t, x, y, op, z)
               (op:LeOp) : #cmp(t, x, y, op, z)
               (op:GeOp) : #cmp(t, x, y, op, z)
               (op:UleOp) : #cmp(t, x, y, op, z)
               (op:UltOp) : #cmp(t, x, y, op, z)
               (op:UgtOp) : #cmp(t, x, y, op, z)
               (op:UgeOp) : #cmp(t, x, y, op, z)
               (op:BitSetOp) : #cmp(t, x, y, op, z)
               (op:BitNotSetOp) : #cmp(t, x, y, op, z)
               (op:TestBitOp|TestAndSetBitOp|TestAndClearBitOp) : #test-bit(t, x, y, op, z)
         else if #f(t) :
            match(op) :
               (op:AddOp) : #fcom(t, x, y, op, z)
               (op:SubOp) : #fncom(t, x, y, op, z)
               (op:MulOp) : #fcom(t, x, y, op, z)
               (op:DivOp) : #fncom(t, x, y, op, z)
               (op:EqOp) : #fcmp(t, x, y, op, z)
               (op:NeOp) : #fcmp(t, x, y, op, z)
               (op:LtOp) : #fcmp(t, x, y, op, z)
               (op:GtOp) : #fcmp(t, x, y, op, z)
               (op:LeOp) : #fcmp(t, x, y, op, z)
               (op:GeOp) : #fcmp(t, x, y, op, z)
               (op:UleOp) : #fcmp(t, x, y, op, z)
               (op:UltOp) : #fcmp(t, x, y, op, z)
               (op:UgtOp) : #fcmp(t, x, y, op, z)
               (op:UgeOp) : #fcmp(t, x, y, op, z)
      (ins:DualOp) :
         val [t, x1, x2, op, y, z] = [type(ins), x1(ins), x2(ins), op(ins), y(ins), z(ins)]
         #divmod(t, x1, x2, y, op, z)
      (ins:Load) :
         #load(type(ins), x(ins), y(ins), offset(ins))
      (ins:Store) :
         #store(type(ins), x(ins), y(ins), offset(ins))
      (ins:Label) :
         #println(os, "%_:" % [#lbl(n(ins))])
      (ins:ExLabel) :
         if prepend-underscore?(backend) :
            #println(os, "   .globl _%_" % [name(ins)])
            #println(os, "_%_:" % [name(ins)])
         else :
            #println(os, "   .globl %_" % [name(ins)])
            #println(os, "%_:" % [name(ins)])
      (ins:Goto) :
         match(x(ins)) :
            (x:Reg) : #println(os, "   jmp *%_" % [#imm(LongT(), x)])
            (x:Mem|ExMem) : #println(os, "   jmp %_ + %_" % [#lbl(x), offset(x)])
      (ins:LoadGoto) :
         val x = x(ins) as Mem
         #println(os, "   jmp *(%_ + %_)(%%rip)" % [#lbl(x), offset(x)])
      (ins:Break) :
         val [t, x, op, y, z] = [type(ins), x(ins), op(ins), y(ins), z(ins)]
         if #i(t) : #br(t, x, y, op, z)
         else if #f(t) : #fbr(t, x, y, op, z)
         else : fatal("Unreachable")
      (ins:Call) :
         val x = x(ins)
         if #ExMPLT(x) :
            #println(os, "   callq %_@plt" % [#lbl(x as ExMem)])
         else :
            match(x) :
               (x:Reg) : #println(os, "   call *%_" % [#imm(LongT(), x)])
               (x:Mem|ExMem) : #println(os, "   call %_ + %_" % [#lbl(x), offset(x)])
      (ins:ConvertIns) :
         #conv(x(ins), y(ins), xtype(ins), ytype(ins))
      (ins:InterpretIns) :
         #inter(x(ins), y(ins), xtype(ins), ytype(ins))
      (ins:Return) : println(os, "   ret")
      (ins:DefByte) : #println(os, "   .byte %_" % [value(ins)])
      (ins:DefInt) : #println(os, "   .long %_" % [value(ins)])
      (ins:DefLong) : #println(os, "   .quad %_" % [value(ins)])
      (ins:DefFloat) : #println(os, "   .long %_" % [bits(value(ins))])
      (ins:DefDouble) : #println(os, "   .quad %_" % [bits(value(ins))])
      (ins:DefString) : println(os, "   .byte %," % [chars(value(ins))])
      (ins:DefBytes) : println(os, "   .byte %," % [value(ins)])
      (ins:DefSpace) : #println(os, "   .space %_" % [size(ins)]) when size(ins) > 0
      (ins:DefLabel) : #println(os, "   .quad %_" % [#lbl(n(ins))])
      (ins:DefData) :
        println(os, "   .data")
        println(os, "   .align 8")
      (ins:DefText) : println(os, "   .text")
      (ins:DefDirectives) : println(os, "   .section .drectve")
      (ins:DefExportLabel) : #println(os, \<S>   .asciz "  -export:\"%_\""<S> % [value(ins)])
      (ins:Comment) : #println(os, "//%_" % [msg(ins)])

;============================================================
;===================== Driver ===============================
;============================================================

;Emit the x86 assembly instruction string to the given output stream
;corresponding to the given instruction for the given backend.
public defn emit-asm (o:OutputStream, e:Ins, backend:Backend) -> False :
  #if-not-defined(OPTIMIZE) :
    check-restriction(e, backend)
  gen(o,e,backend)
