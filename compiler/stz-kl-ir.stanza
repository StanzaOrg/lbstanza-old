defpackage stz/kl-ir :
   import core
   import collections
   import stz/ids

;Use utilities for defining ASTs quickly.
#use-overlay(stz/ast-lang, stz/printer-lang)

;All expressions have a type field for internal use.
public defmulti type (e:KExp) -> False|KType

public defast :
   ;     KType Nodes
   ;     -----------
   deftype KType <: Equalable
   defnodes KType :
      KOf: (n:Int)
      KTVar: (n:Int)
      KAnd: (a:KType, b:KType)
      KOr: (a:KType, b:KType)
      KTop
      KBot

      ;     Working IR
      ;     ----------
      KUVar: (n:Int)
      KTags: (tags:List<Int>)

   ;     KLevel Top Level Commands
   ;     -------------------------
   deftype KComm
   defnodes KComm :
      KDef :
         n: Int
         value: KExp
      KDefVar :
         n: Int
         type: KType
         value: False|KExp
      KDefn :
         n: Int
         value: Func
      KDefmulti :
         n: Int
         targs: List<Int>
         a1: List<KType>
         a2: KType
         info: False|FileInfo
      KDefmethod :
         n: Int
         multi: Int
         targs: List<KType>
         thisn: False|Int
         func: KFn
      KDefType :
         n: Int
         parent: False|KType
      KInit :
         exp: KExp

      KLSDef :
         n: Int
         type: KLSType
         value: KLSExp
      KLSDefVar :
         n: Int
         type: KLSType
         value: False|KLSExp
      KLSDefn :
         tail?: True|False
         n: Int
         targs: List<Int>
         a1: List<KLSType>
         a2: KLSType
         args: List<Int>
         body: KLSComm
         info: False|FileInfo
      KLSExternFn :
         n: Int
         lbl: Symbol
         a1: List<KLSType>
         a2: KLSType
         args: List<Int>
         body: KLSComm
         info: False|FileInfo
      KLSDefmethod :
         tail?: True|False
         n: Int
         multi: Int
         multi-targs: List<KType>
         targs: List<Int>
         a1: List<KLSType>
         a2: KLSType
         args: List<Int>
         body: KLSComm
         info: False|FileInfo
      KLSDefType :
         n: Int
         parent: False|KType
         fields: List<StructField>
         rfield: False|StructField
      KExtern :
         n: Int
         type: KLSType
         lbl: Symbol
      KLSInit :
         comm: KLSComm

      ;     Working IR
      ;     ----------
      KDefConst :
         n: Int
         value: ?
      KDefTypeObject :
         n: Int
         args: List<Int>
         type: KType
      KLSDefCode :
         n: Int
         arity: Int
         args: List<Int>
         types: List<KLSType>
         body: KLSComm
         info: False|FileInfo
      

   ;     Kform Expressions
   ;     -----------------
   deftype KExp :
      type: False|KType with: (as-method => true)
   defnodes KExp :
      KSet :
         n: Int
         value: KExp
      KCast :
         exp: KExp
         targ: KType
         info: False|FileInfo
      KCheckLength :
         exp: KExp
         length: Int
         info: False|FileInfo
      KLet :
         def: KDef|KDefVar|KDefn
         body: KExp
      KLetRec :
         defs: List<KDefn|KDef>
         body: KExp
         info: False|FileInfo
      KSeq :
         a: KExp
         b: KExp
      KDispatch :
         args: List<KExp>
         branches: List<KBranch>
         info: False|FileInfo
      KMatch :
         args: List<KExp>
         branches: List<KBranch>
         info: False|FileInfo
      KNew :
         class: KType
         methods: List<KDefmethod>
         info: False|FileInfo
      KDo :
         func: KExp
         args: List<KExp>
         a1: List<KType>
         a2: KType
         info: False|FileInfo
      KVar :
         n: Int
         info: False|FileInfo
      KPrim :
         op: KOp
         args: List<KExp>
      KLiteral :
         value: ?
      KTuple :
         exps: List<KExp>
         info: False|FileInfo
      KTupleGet :
         exp: KExp
         index: Int
         info: False|FileInfo
      KMix :
         exps: List<KCurry>
         info: False|FileInfo
      KCurry :
         n: Int
         targs: List<KType>
         a1: List<KType>
         a2: KType
         info: False|FileInfo

      ;     Working IR
      ;     ----------
      KLetLoop :
         n: Int
         targs: List<Int>
         args: List<Int>
         targ-values: List<KType>
         arg-values: List<KExp>
         body: KExp
      KGoto :
         loop: KExp
         args: List<KExp>  
      KCheckVoid :
         exp: KExp
         info: False|FileInfo
      KObject :
         n: Int
         args: List<KExp>
         info: False|FileInfo
      KObjectGet :
         object: KExp
         index: Int
      KTypeObject :
         n: Int
         args: List<KExp>
         info: False|FileInfo
      KClosure :
         n: Int
         args: List<KExp>
         info: False|FileInfo
      KClosureGet :
         exp: KExp
         index: Int
      KCall :
         tail?: True|False
         arity: Int
         func: Int
         args: List<KExp>
         info: False|FileInfo
      KCallDeref :
         tail?: True|False
         arity: Int
         func: KExp
         args: List<KExp>
         info: False|FileInfo
      KBox :
         value: False|KExp
      KBoxGet :
         box: KExp
      KBoxSet :
         box: KExp
         value: KExp
      KBranchError :
         amb?: True|False
         info: False|FileInfo


   ;     KForm LoStanza Types
   ;     --------------------
   deftype KLSType
   defnodes KLSType :
      KByteT
      KIntT
      KLongT
      KFloatT
      KDoubleT
      KUnknownT
      KPtrT: (type:KLSType)
      KRefT: (type:KType)
      KFnT: (a:List<KLSType>, r:False|KLSType, b:KLSType)
      KStructT: (n:Int)


   ;     KForm Location
   ;     --------------
   deftype KLoc
   defnodes KLoc :
      Value: (exp:KLSExp)
      Deref: (exp:KLSExp, type:KLSType)
      Deptr: (exp:KLSExp, type:KLSType)
      Slot: (exp:KLSExp, type:KLSType, index:KLSExp)
      Field: (base:KLoc, n:Int) ;TODO: Should include type to make things easier


   ;     KForm LoStanza Commands
   ;     -----------------------
   deftype KLSComm
   defnodes KLSComm :
      KLSIgnoreComm :
         exp: KLSExp
      KLSSet :
         exp: KLoc
         value: KLSExp
      KLSLabels :
         blocks: List<KLSLabeledBlock>
      KLSLabel :
         n: Int
      KLSLabeledBlock :
         n: Int
         args: List<Int>
         types: List<KLSType>
         body: KLSComm
      KLSGoto :
         n: Int
         args: List<KLSExp>
      KLSReturn :
         exp: KLSExp
      KLSDefLocal :
         n: Int
         value: KLSExp
      KLSDefLocalVar :
         n: Int
         type: KLSType
         value: False|KLSExp
      KLSSeq :
         a: KLSComm
         b: KLSComm
      KLSIf :
         pred: KLSExp
         conseq: KLSComm
         alt: KLSComm
      KLSMatch :
         args: List<KLSExp>
         branches: List<KLSBranch>
         info: False|FileInfo
      KLSDispatch :
         args: List<KLSExp>
         branches: List<KLSBranch>
         info: False|FileInfo
      KLSSkip
      KLSBranchError :
         amb?: True|False
         info: False|FileInfo

   ;     KForm LoStanza Expressions
   ;     --------------------------
   deftype KLSExp
   defnodes KLSExp :
      KLSLet :
         comm: KLSComm
         exp: KLSExp
      KLSVar :
         n: Int
      KLSRef :
         n: Int
         args: List<KLSExp>
         info: False|FileInfo
      KLSRefArray :
         n: Int
         args: List<KLSExp>
         info: False|FileInfo
      KLSStruct :
         n: Int
         args: List<KLSExp>
      KLSPtr :
         exp: KLoc
      KLSRead :
         exp: KLoc
      KLSPrim :
         op: KLSOp
         args: List<KLSExp>
         type: KLSType
      KLSCall :
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
         new-stack?: True|False
         info: False|FileInfo
      KLSSwitchStack :
         func: KLSExp
         arg: KLSExp
         type: KLSType
         info: False|FileInfo
      KLSCallC :
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
         info: False|FileInfo
      KLSSizeof :
         type: KLSType
      KLSTagof :
         n: Int
      KLSConv :
         exp: KLSExp
         type: KLSType
         info: False|FileInfo
      KLSInterpret :
         exp: KLSExp
         type: KLSType      
      KLSLitByte :
         value: Byte
      KLSLitInt :
         value: Int
      KLSLitLong :
         value: Long
      KLSLitFloat :
         value: Float
      KLSLitDouble :
         value: Double
      KLSLitString :
         value: String
      KLSMix :
         exps: List<KLSCurry>
      KLSCurry :
         n: Int
         targs: List<KType>
         a1: List<KLSType>
         a2: KLSType
      KLSFn :
         ref: KExp

      ;     Working IR
      ;     ----------
      KLSCast :
         exp: KLSExp
         type: KType
         info: False|FileInfo
      KLSTypeObject :
         n: Int
         args: List<KLSExp>
         info: False|FileInfo
      FuncExp :
         func: Func
      KLSClosure :
         n: Int
         args: List<KLSExp>
         info: False|FileInfo
      KLSWCall :
         tail?: True|False
         arity: Int
         func: Int
         args: List<KLSExp>
         type: KLSType
         new-stack?: True|False
         info: False|FileInfo
      KLSWCallDeptr :
         tail?: True|False
         arity: Int
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
         new-stack?: True|False
         info: False|FileInfo
      KLSWCallDeref :
         tail?: True|False
         arity: Int
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
         new-stack?: True|False
         info: False|FileInfo
      KLSWCallC :
         func: Int
         args: List<KLSExp>
         type: KLSType
      KLSWCallCDeptr :
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
      KLSWSwitchStack :
         tail?: True|False
         func: KLSExp
         args: List<KLSExp>
         type: KLSType
         info: False|FileInfo
      

   ;     KForm Mappers/Doers
   ;     -------------------
   make-mapper(f:KExp -> KExp, e:KLSExp)
   make-mapper(f:KLSExp -> KLSExp, e:KLSExp)
   make-mapper(f:KLSComm -> KLSComm, e:KLSExp)
   make-mapper(f:KLoc -> KLoc, e:KLSExp)
   make-mapper(f:KType -> KType, e:KLSExp)
   make-mapper(f:KLSType -> KLSType, e:KLSExp)
   make-doer(f:KLSExp -> ?, e:KLSExp)
   make-doer(f:KLSComm -> ?, e:KLSExp)   

   make-mapper(f:KLSExp -> KLSExp, c:KLSComm)
   make-mapper(f:KLSComm -> KLSComm, c:KLSComm)
   make-mapper(f:KLoc -> KLoc, c:KLSComm)
   make-mapper(f:KLSType -> KLSType, c:KLSComm)
   make-doer(f:KLSExp -> ?, c:KLSComm)
   make-doer(f:KLSComm -> ?, c:KLSComm)
   
   make-mapper(f:KLSExp -> KLSExp, l:KLoc)
   make-mapper(f:KLoc -> KLoc, l:KLoc)
   make-mapper(f:KLSType -> KLSType, l:KLoc)
   make-doer(f:KLSExp -> ?, l:KLoc)
   make-doer(f:KLoc -> ?, l:KLoc)
   make-doer(f:KLSType -> ?, l:KLoc)

   make-mapper(f:KExp -> KExp, e:KExp)
   make-mapper(f:KComm -> KComm, e:KExp)
   make-mapper(f:KType -> KType, e:KExp)
   make-doer(f:KExp -> ?, e:KExp)
   make-doer(f:KComm -> ?, e:KExp)

   make-mapper(f:KExp -> KExp, c:KComm)
   make-mapper(f:KLSComm -> KLSComm, c:KComm)
   make-mapper(f:KLSExp -> KLSExp, c:KComm)
   make-mapper(f:KType -> KType, c:KComm)
   make-mapper(f:KLSType -> KLSType, c:KComm)
   make-doer(f:KExp -> ?, c:KComm)
   make-doer(f:KLSComm -> ?, c:KComm)
   make-doer(f:KLSExp -> ?, c:KComm)
   make-doer(f:KType -> KType, c:KComm)
   make-doer(f:KLSType -> KLSType, c:KComm)

   make-mapper(f:KType -> KType, t:KType)
   make-doer(f:KType -> ?, t:KType)

   make-mapper(f:KType -> KType, t:KLSType)
   make-mapper(f:KLSType -> KLSType, t:KLSType)
   
   defmapper (f:KLSExp -> KLSExp) :
      (x:List<KLSExp>) : map(f, x)
      (x:KLSExp) : f(x)
      (x:False|KLSExp) : f(x as KLSExp) when x is-not False
      (x:KLoc) : mapr(f, x)
      (x:Int|KLSExp) : f(x as KLSExp) when x is KLSExp else x
      (x:List<KLSCurry>) : map({f(_) as KLSCurry}, x)
   defdoer (f:KLSExp -> ?) :
      (x:List<KLSExp>) : do(f, x)
      (x:KLSExp) : f(x)
      (x:False|KLSExp) : f(x as KLSExp) when x is-not False
      (x:KLoc) : dor(f, x)
      (x:Int|KLSExp) : f(x as KLSExp) when x is KLSExp
      (x:List<KLSCurry>) : do(f, x)
      
   defmapper (f:KLSComm -> KLSComm) :
      (x:List<KLSBranch>) : for b in x map : map(f, b)
      (x:List<KLSLabeledBlock>) : for b in x map : f(b) as KLSLabeledBlock
      (x:KLSComm) : f(x)      
   defdoer (f:KLSComm -> ?) :
      (x:List<KLSBranch>) : for b in x do : do(f, b)
      (x:List<KLSLabeledBlock>) : for b in x do : f(b)
      (x:KLSComm) : f(x)
      
   defmapper (f:KLoc -> KLoc) :
      (x:KLoc) : f(x)
   defdoer (f:KLoc -> ?) :
      (x:KLoc) : f(x)
      
   defmapper (f:KExp -> KExp) :
      (x:KExp) : f(x)
      (x:False|KExp) : f(x as KExp) when x is KExp
      (x:Func) : map(f, x)
      (x:KFn) : map(f, x) as KFn
      (x:List<KExp>) : map(f, x)
      (x:List<KBranch>) : for b in x map : map(f, b)
      (x:Int|KExp) : f(x as KExp) when x is KExp else x
      (x:List<KCurry>) : map({f(_) as KCurry}, x)
   defdoer (f:KExp -> ?) :
      (x:KExp) : f(x)
      (x:False|KExp) : f(x as KExp) when x is KExp
      (x:Func) : do(f, x)
      (x:KFn) : do(f, x)
      (x:List<KExp>) : do(f, x)
      (x:List<KBranch>) : for b in x do : do(f, b)
      (x:Int|KExp) : f(x as KExp) when x is KExp
      (x:List<KCurry>) : do(f, x)     

   defmapper (f:KComm -> KComm) :
      (x:KDef|KDefVar|KDefn) : f(x) as KDef|KDefVar|KDefn
      (x:List<KDefn|KDef>) : map({f(_) as KDefn|KDef}, x)
      (x:List<KDefmethod>) : map({f(_) as KDefmethod}, x)            
   defdoer (f:KComm -> ?) :
      (x:KDef|KDefVar|KDefn) : f(x)
      (x:List<KDefn|KDef>) : do(f, x)
      (x:List<KDefmethod>) : do(f, x)
      
   defmapper (f:KType -> KType) :
      (x:Func) : map(f, x)
      (x:KFn) : map(f, x)
      (x:KType) : f(x)
      (xs:List<KType>): map(f, xs)
      (x:False|KType) : f(x as KType) when x is-not False
      (xs:List<KBranch>) : for b in xs map : map(f, b)
   defdoer (f:KType -> ?) :
      (x:Func) : do(f, x)
      (x:KFn) : do(f, x)
      (x:KType) : f(x)
      (xs:List<KType>): do(f, xs)
      (x:False|KType) : f(x as KType) when x is-not False
      (xs:List<KBranch>) : for b in xs do : do(f, b)

   defmapper (f:KLSType -> KLSType) :
      (x:KLSType) : f(x)
      (x:False|KLSType) : f(x as KLSType) when x is-not False
      (x:List<KLSType>) : map(f, x)
      (x:StructField) : StructField(n(x), f(type(x)))
      (xs:List<StructField>) : for x in xs map : map(f, x)
      (x:False|StructField) : map(f, x as StructField) when x is-not False
   defdoer (f:KLSType -> ?) :
      (x:KLSType) : f(x)
      (x:False|KLSType) : f(x as KLSType) when x is-not False
      (x:List<KLSType>) : do(f, x)
      (x:StructField) : f(type(x))
      (xs:List<StructField>) : for x in xs do : f(type(x))
      (x:False|StructField) : f(type(x as StructField)) when x is-not False

   ignore :
      Byte
      Int
      Long
      Float
      Double
      String
      Symbol
      True|False
      ?
      List<Int>
      False|Int
      KOp
      KLSOp
      False|FileInfo



;                      AST Values
;                      ==========

public defstruct KProg :
   comms: List<KComm>

public deftype Func
public defmulti n (f:Func) -> Int
public defmulti info (f:Func) -> False|FileInfo

public defstruct KFn <: Func :
   n: Int with: (as-method => true)
   tail?: True|False
   targs: List<Int>
   a1: List<KType>
   a2: KType
   closure: False|Int
   args: List<Int>
   body: KExp
   info: False|FileInfo with: (as-method => true)

public defstruct KMultifn <: Func :
   n: Int with: (as-method => true)
   funcs: List<KFn|KCode>
   info: False|FileInfo with: (as-method => true)

public defstruct StructField :
   n: Int
   type: KLSType

public defstruct KBranch :
   args: List<Int>
   types: List<KType>
   body: KExp
   info: False|FileInfo

public defstruct KLSBranch :
   args: List<Int>
   types: List<KType>
   body: KLSComm

public defstruct KOp <: Equalable :
   name: Symbol

public val INT-ADD-OP = KOp(`int-add)
public val INT-SUB-OP = KOp(`int-sub)
public val INT-MUL-OP = KOp(`int-mul)
public val INT-DIV-OP = KOp(`int-div)
public val INT-MOD-OP = KOp(`int-mod)
public val INT-AND-OP = KOp(`int-and)
public val INT-OR-OP = KOp(`int-or)
public val INT-XOR-OP = KOp(`int-xor)
public val INT-SHL-OP = KOp(`int-shl)
public val INT-SHR-OP = KOp(`int-shr)
public val INT-ASHR-OP = KOp(`int-ashr)
public val INT-EQ-OP = KOp(`int-eq)
public val INT-NE-OP = KOp(`int-ne)
public val INT-LE-OP = KOp(`int-le)
public val INT-LT-OP = KOp(`int-lt)
public val INT-GT-OP = KOp(`int-gt)
public val INT-GE-OP = KOp(`int-ge)
public val INT-NOT-OP = KOp(`int-not)
public val INT-NEG-OP = KOp(`int-neg)
public val IDENTICAL-OP = KOp(`identical?)
public val AGET-OP = KOp(`aget)
public val ASET-OP = KOp(`aset)
public val ALEN-OP = KOp(`alen)
public val CHAR-AGET-OP = KOp(`char-aget)
public val CHAR-ASET-OP = KOp(`char-aset)
public val STRING-GET-OP = KOp(`string-get)
public val STRING-LEN-OP = KOp(`string-len)

public deftype KLSOp <: Equalable
public defmulti name (op:KLSOp) -> Symbol

defstruct #KLSOp <: KLSOp :
   name: Symbol with: (as-method => true)
public val ADD-OP = #KLSOp(`add)
public val SUB-OP = #KLSOp(`sub)
public val MUL-OP = #KLSOp(`mul)
public val DIV-OP = #KLSOp(`div)
public val MOD-OP = #KLSOp(`mod)
public val AND-OP = #KLSOp(`and)
public val OR-OP = #KLSOp(`or)
public val XOR-OP = #KLSOp(`xor)
public val SHL-OP = #KLSOp(`shl)
public val SHR-OP = #KLSOp(`shr)
public val ASHR-OP = #KLSOp(`ashr)
public val EQ-OP = #KLSOp(`eq)
public val NE-OP = #KLSOp(`ne)
public val LT-OP = #KLSOp(`lt)
public val GT-OP = #KLSOp(`gt)
public val LE-OP = #KLSOp(`le)
public val GE-OP = #KLSOp(`ge)
public val ULT-OP = #KLSOp(`ult)
public val UGT-OP = #KLSOp(`ugt)
public val ULE-OP = #KLSOp(`ule)
public val UGE-OP = #KLSOp(`uge)
public val NOT-OP = #KLSOp(`not)
public val NEG-OP = #KLSOp(`neg)

public val SP-OP = #KLSOp(`sp)
public defstruct GetAddrOp <: KLSOp :
   name: Symbol with: (as-method => true)
   lbl: Int
public defstruct GetterOp <: KLSOp :
   name: Symbol with: (as-method => true)
   lbl: Int
public defstruct SetterOp <: KLSOp :
   name: Symbol with: (as-method => true)
   lbl: Int

;                     Constructors
;                     ============

public defn KVar (x:Int) :
   KVar(x, false, false)

public defn KLSSeq (xs:Seqable<KLSComm>) :
   reduce-right(KLSSeq, xs)

;                   Working IR Values
;                   =================

public defstruct KCode <: Func :
   n: Int with: (as-method => true)
   arity: Int
   args: List<Int>
   body: KExp
   info: False|FileInfo with: (as-method => true)


;                       Equality
;                       ========

defmethod equal? (a:KLSOp, b:KLSOp) :
   name(a) == name(b)
   
defmethod equal? (a:KOp, b:KOp) :
   name(a) == name(b)

defmethod equal? (x:KType, y:KType) :
   match(x, y) :
      (x:KOf, y:KOf) : n(x) == n(y)
      (x:KTVar, y:KTVar) : n(x) == n(y)
      (x:KAnd, y:KAnd) : (a(x) == a(y)) and (b(x) == b(y))
      (x:KOr, y:KOr) : (a(x) == a(y)) and (b(x) == b(y))
      (x:KTop, y:KTop) : true
      (x:KBot, y:KBot) : true
      (x, y) : false

;                       Mappers
;                       =======

public defn map (f:KLSComm -> KLSComm, b:KLSBranch) :
   KLSBranch(args(b), types(b), f(body(b)))
public defn do (f:KLSComm -> ?, b:KLSBranch) :
   f(body(b))

public defn map (f:KType -> KType, b:KLSBranch) :
   KLSBranch(args(b), map(f, types(b)), body(b))
public defn do (f:KType -> ?, b:KLSBranch) :
   do(f, types(b))

public defn map (f:KExp -> KExp, b:KBranch) :
   KBranch(args(b), types(b), f(body(b)), info(b))
public defn do (f:KExp -> ?, b:KBranch) :
   f(body(b))

public defn map<?T> (f:KExp -> KExp, x:?T&Func) -> T :
   {_ as T&Func} $ match(x) :
      (x:KFn) :
         KFn(n(x), tail?(x), targs(x), a1(x), a2(x), closure(x), args(x), f(body(x)), info(x))
      (x:KMultifn) :
         KMultifn{n(x), _, info(x)} $
         for func in funcs(x) map :
            map(f, func) as KFn|KCode
      (x:KCode) :
         KCode(n(x), arity(x), args(x), f(body(x)), info(x))
public defn do (f:KExp -> ?, x:Func) :
   match(x) :
      (x:KFn) : f(body(x))
      (x:KMultifn) : for func in funcs(x) do : do(f, func)
      (x:KCode) : f(body(x))

public defn map (f:KType -> KType, b:KBranch) :
   KBranch(args(b), map(f, types(b)), body(b), info(b))

public defn do (f:KType -> KType, b:KBranch) :
   do(f, types(b))

public defn map<?T> (f:KType -> KType, x:?T&Func) -> T :
   {_ as T&Func} $ match(x) :
      (x:KFn) :
         val a1* = map(f, a1(x))
         val a2* = f(a2(x))
         KFn(n(x), tail?(x), targs(x), a1*, a2*, closure(x), args(x), body(x), info(x))
      (x:KMultifn) :
         KMultifn{n(x), _, info(x)} $
         for func in funcs(x) map :
            map(f, func) as KFn|KCode
      (x:KCode) : x

public defn do (f:KType -> KType, x:Func) :
   match(x) :
      (x:KFn) :
         do(f, a1(x))
         f(a2(x))
      (x:KMultifn) :
         for func in funcs(x) do :
            do(f, func)
      (x:KCode) :
         false

public defn map (f:KLSType -> KLSType, s:StructField) :
   StructField(n(s), f(type(s)))


;                   Recursive Mappers
;                   =================

public defn mapr (f:KLSExp -> KLSExp, l:KLoc) :
   defn fl (l:KLoc) : mapr(f, l)
   map(fl, map(f, l))

public defn dor (f:KLSExp -> ?, l:KLoc) :
   defn fl (l:KLoc) : dor(f, l)
   do(f, l)
   do(fl, l)

public defn mapr (f:KExp -> KExp, e:KExp) :
   defn fc (c:KComm) : map(f, c)
   map(fc, map(f, e))

public defn dor (f:KExp -> ?, e:KExp) :
   defn fc (c:KComm) : do(f, c)
   do(f, e)
   do(fc, e)

public defn dor (f:KComm -> ?, c:KComm) :
   defn fe (e:KExp) : dor(f, e)
   do(fe, c)

public defn dor (f:KComm -> ?, e:KExp) :
   defn fe (c:KExp) : dor(f, c)
   do(f, e)
   do(fe, e)

public defn mapr (f:KType -> KType, e:KExp) :
   defn fc (c:KComm) : mapr(f, c)
   defn fe (e:KExp) : mapr(f, e)
   map(fc, map(fe, map(f, e)))

public defn mapr (f:KType -> KType, c:KComm) :
   defn fe (e:KExp) : mapr(f, e)
   map(fe, map(f, c))

public defn mapr (f:KLSExp -> KLSExp, c:KLSComm) :
   defn fc (c:KLSComm) : mapr(f, c)
   map(fc, map(f, c))

public defn mapr (f:KLSExp -> KLSExp, e:KLSExp) :
   defn fc (c:KLSComm) : mapr(f, c)
   map(fc, map(f, e))

public defn mapr (f:KLSComm -> KLSComm, c:KLSComm) :
   defn fe (e:KLSExp) : mapr(f, e)
   map(fe, map(f, c))

public defn mapr (f:KLSComm -> KLSComm, e:KLSExp) :
   defn fe (e:KLSExp) : mapr(f, e)
   map(fe, map(f, e))

public defn mapr (f:KType -> KType, c:KLSComm) -> KLSComm :
   defn fc (c:KLSComm) : mapr(f, c)
   defn fe (e:KLSExp) : mapr(f, e)
   defn ft (t:KLSType) : mapr(f, t)
   map(ft, map(fe, map(fc, c)))
   
public defn mapr (f:KType -> KType, e:KLSExp) -> KLSExp :
   defn fc (c:KLSComm) : mapr(f, c)
   defn fe (e:KLSExp) : mapr(f, e)
   defn ft (t:KLSType) : mapr(f, t)
   map(ft, map(fc, map(fe, map(f, e))))

public defn mapr (f:KType -> KType, t:KLSType) -> KLSType :
   defn ft (t:KLSType) : mapr(f, t)
   map(ft, map(f, t))

public defn dor (f:KLSExp -> ?, c:KLSComm) -> False :
   defn fc (c:KLSComm) : dor(f, c)
   do(f, c)
   do(fc, c)

public defn dor (f:KLSExp -> ?, e:KLSExp) -> False :
   defn fc (c:KLSComm) : dor(f, c)
   do(f, e)
   do(fc, e)

;                       Printers
;                       ========

defprinter (p:KProg) :
   KProg :
      ($prog comms ...)

defprinter (f:Func) :
   KFn :
      if tail?(f) : ($fn* (targs ...) (a1 ...) a2 closure (args ...) body)
      else : ($fn (targs ...) (a1 ...) a2 closure (args ...) body)
   KMultifn :
      ($multi funcs ...)

   ;Working IR
   ;----------
   KCode :
      ($code arity (args ...) body)


defprinter (b:KBranch) :
   KBranch :
      ($branch (args ...) (types ...) body)


defprinter (b:KLSBranch) :
   KLSBranch :
      ($branch (args ...) (types ...) body)


defprinter (t:KType) :
   KOf :
      custom{"$T%_" % [n(t)]}
   KTVar :
      custom{"$V%_" % [n(t)]}
   KUVar :
      custom{"$UV%_" % [n(t)]}
   KAnd :
      ($and a b)
   KOr :
      ($or a b)
   KTop :
      custom{"$top"}
   KBot :
      custom{"$bot"}

   ;     Working IR
   ;     ----------
   KTags :
      ($tags tags ...)


defprinter (c:KComm) :
   KDef :
      ($def n value)
   KDefVar :
      if value(c) is False : ($defvar n)
      else : ($defvar n value)
   KDefn :
      ($def n value)
   KDefmulti :
      ($defmulti n (targs ...) (a1 ...) a2)
   KDefmethod :
      ($defmethod n multi (targs ...) thisn func)
   KDefType :
      ($deftype n parent)
   KInit :
      custom{exp(c)}

   KLSDef :
      ($lsdef n type value)
   KLSDefVar :
      ($lsdefvar n type value)
   KLSDefn :
      ($lsdefn n (targs ...) (a1 ...) a2 (args ...) body)
   KLSExternFn :
      ($ls-extern-fn n lbl (a1 ...) a2 (args ...) body)
   KLSDefmethod :
      ($lsdefmethod n multi (multi-targs ...) (targs ...) (a1 ...) a2 (args ...) body)
   KLSDefType :
      ($lsdeftype n parent (fields ...) rfield)
   KExtern :
      ($extern n type lbl)
   KLSInit :
      ($lsinit comm)
   
   ;     Working IR
   ;     ----------
   KDefConst :
      ($defconst n value)
   KDefTypeObject :
      ($deftypeobject n (args ...) type)
   KLSDefCode :
      ($lsdefcode n arity (args ...) (types ...) body)

defprinter (e:KExp) :
   KSet :
      ($set n value)
   KCast :
      ($cast exp targ)
   KCheckVoid :
      ($check-void exp)
   KCheckLength :
      ($check-length exp length)      
   KLet :
      ($let def body)
   KLetRec :
      ($letrec (defs ...) body)
   KSeq :
      ($seq a b)
   KDispatch :
      ($dispatch (args ...) branches ...)
   KMatch :
      ($match (args ...) branches ...)
   KNew :
      ($new class methods ...)
   KDo :
      ($do (a1 ...) a2 func args ...)
   KVar :
      custom{"$v%_" % [n(e)]}
   KPrim :
      ($prim op args ...)
   KLiteral :
      ($value value)
   KTuple :
      ($tuple exps ...)
   KTupleGet :
      ($tuple-get exp index)
   KMix :
      ($mix exps ...)
   KCurry :
      ($curry n (targs ...) (a1 ...) a2)

   ;Working IR
   ;----------
   KObject :
      ($object n args ...)
   KObjectGet :
      ($object-get object index)
   KTypeObject :
      ($type-object n args ...)
   KClosure :
      ($closure n args ...)
   KClosureGet :
      ($closure-get exp index)
   KCall :
      ($call tail? arity func args ...)
   KCallDeref :
      ($call-deref tail? arity func args ...)
   KBox :
      ($box value)
   KBoxGet :
      ($box-get box)
   KBoxSet :
      ($box-set box value)
   KBranchError :
      ($branch-error amb?)
   KLetLoop :
      ($loop n (targs ...) (args ...) (targ-values ...) (arg-values ...) body)
   KGoto :
      ($goto loop args ...)

defprinter (t:KLSType) :
   KByteT :
      custom{"$byte"}
   KIntT :
      custom{"$int"}
   KLongT :
      custom{"$long"}
   KFloatT :
      custom{"$float"}
   KDoubleT :
      custom{"$double"}
   KUnknownT :
      custom{"$?"}
   KPtrT :
      ($ptr type)
   KRefT :
      ($ref type)
   KFnT :
      ($fn a r b)
   KStructT :
      custom{"$T%_" % [n(t)]}


defprinter (l:KLoc) :
   Value :
      custom{exp(l)}
   Deref :
      ($deref exp)
   Deptr :
      ($deptr exp)
   Slot :
      ($slot exp type index)
   Field :
      ($field base n)
      

defprinter (c:KLSComm) :
   KLSIgnoreComm :
      custom{exp(c)}
   KLSSet :
      ($set exp value)
   KLSLabels :
      ($labels blocks ...)
   KLSLabel :
      ($label n)
   KLSLabeledBlock :
      ($block n (args ...) (types ...) body)
   KLSGoto :
      ($goto n args ...)
   KLSReturn :
      ($return exp)
   KLSDefLocal :
      ($deflocal n value)
   KLSDefLocalVar :
      ($deflocalvar n type value)
   KLSSeq :
      ($seq a b)
   KLSIf :
      ($if pred conseq alt)
   KLSMatch :
      ($match (args ...) branches ...)
   KLSDispatch :
      ($dispatch (args ...) branches ...)
   KLSSkip :
      custom{"$skip"}
   KLSBranchError :
      ($ls-branch-error)


defprinter (e:KLSExp) :
   KLSLet :
      ($let comm exp)
   KLSVar :
      custom{"$v%_" % [n(e)]}
   KLSRef :
      ($ref n args ...)
   KLSRefArray :
      ($refarray n args ...)
   KLSStruct :
      ($struct n args ...)
   KLSPtr :
      ($ptr exp)
   KLSRead :
      ($read exp)
   KLSCall :
      if new-stack?(e) : ($call* type func args ...)
      else : ($call type func args ...)
   KLSSwitchStack :
      ($switch-stack type func arg)
   KLSCallC :
      ($call-c type func args ...)
   KLSSizeof :
      ($sizeof type)
   KLSTagof :
      ($tagof n)
   KLSConv :
      ($conv exp type)
   KLSInterpret :
      ($interpret exp type)
   KLSCast :
      ($cast exp type)
   KLSTypeObject :
      ($typeobj n args ...)
   KLSPrim :
      ($prim type op args ...)
   KLSLitByte :
      ($lit-byte value)
   KLSLitInt :
      ($lit-int value)
   KLSLitLong :
      ($lit-long value)
   KLSLitFloat :
      ($lit-float value)
   KLSLitDouble :
      ($lit-double value)
   KLSLitString :
      ($lit-string value)
   KLSMix :
      ($mix exps ...)
   KLSCurry :
      ($curry n (targs ...) (a1 ...) a2)
   KLSFn :
      ($fn ref)

   ;     Working IR
   ;     ----------
   FuncExp :
      ($func func)
   KLSClosure :
      ($closure n args ...)
   KLSWCall :
      if tail?(e) : ($wcall* arity func (args ...) type)
      else : ($wcall arity func (args ...) type)
   KLSWCallDeptr :
      if tail?(e) : ($wcall-deptr* arity func (args ...) type)
      else : ($wcall-deptr arity func (args ...) type)      
   KLSWCallDeref :
      if tail?(e) : ($wcall-deref* arity func (args ...) type)
      else : ($wcall-deref arity func (args ...) type)
   KLSWCallC :
      ($wcall-c func (args ...) type)
   KLSWCallCDeptr :
      ($wcall-c-deptr func (args ...) type)      
   KLSWSwitchStack :
      if tail?(e) : ($wswitch-stack* type func args ...)
      else : ($wswitch-stack type func args ...)

defmethod print (o:OutputStream, op:KLSOp) :
   print(o, name(op))

defmethod print (o:OutputStream, f:StructField) :
   print(o, "(%_, %~)" % [n(f), type(f)])


public defn children (t:KType) -> Collection<KType> :
   val ts = Vector<KType>(4)
   do(add{ts, _}, t)
   ts

public defn subexps (e:KExp) -> Collection<KExp> :
   val es = Vector<KExp>(4)
   do(add{es, _}, e)
   es