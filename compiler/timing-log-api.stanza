defpackage stz/timing-log-api :
  import core
  import collections
  import stz/timing-log

;============================================================
;============== Datastructure Representation ================
;============================================================

public defstruct TimerLabel :
  name:String
  parent:TimerLabel|False
  id:Int|False with:
    init => false
    setter => set-id
with:
  constructor => #TimerLabel

;Create a timer label whose name is computed by appending the
;given suffix to its parent timer.
public defn TimerLabel (parent:TimerLabel, suffix:Suffix) -> TimerLabel :
  val name = string-join([name(parent) ": " string(suffix)])
  TimerLabel(name, parent)

;============================================================
;=================== Label Registry =========================
;============================================================

;Store all created timer labels here.
val TIMER-LABELS = Vector<TimerLabel>()

;Create a new TimerLabel.
;The label is stored in the registry so that we can initialize
;them with ids later.
public defn TimerLabel (name:String, parent:TimerLabel|False) -> TimerLabel :
  val lbl = #TimerLabel(name, parent)
  match(TIMING-LOG:TimingLog) :
    val parent-id = match(parent:TimerLabel) : id(parent) as Int
    set-id(lbl, id(TIMING-LOG, name, parent-id))
  add(TIMER-LABELS, lbl)
  lbl

;Create a new TimerLabel with no parent.
public defn TimerLabel (name:String) -> TimerLabel :
  TimerLabel(name, false)

;============================================================
;===================== Timer IDs ============================
;============================================================

;Create an ID using the currently active timing log and assign
;it to all the timing labels.
defn initialize-all-labels () -> False :
  val log = TIMING-LOG as TimingLog
  for lbl in TIMER-LABELS do :
    val parent-id = match(parent(lbl)) :
      (p:TimerLabel) : id(p) as Int
      (f:False) : false
    set-id(lbl, id(log, name(lbl), parent-id))

;Uninitialize all the labels.
defn uninitialize-all-labels () -> False :
  for lbl in TIMER-LABELS do :
    set-id(lbl, false)

;============================================================
;======================= Global Log =========================
;============================================================

;The global timing log.
var TIMING-LOG:TimingLog|False = false

;Create a new timing log with the given filename and execute
;the body.
public defn create-timing-log<?T> (body:() -> ?T, filename:String) -> T :
  val log = TimingLog(filename)
  try :
    TIMING-LOG = log
    initialize-all-labels()
    body()
  finally :
    close(log)
    TIMING-LOG = false
    uninitialize-all-labels()

;Log time taken to execute the current body.
public defn log-time<?T> (body:() -> ?T, timer:TimerLabel) -> T :
  match(TIMING-LOG:TimingLog) :
    dynamic-wind(
      fn () : log(TIMING-LOG, id(timer) as Int, StartEvent)
      body
      fn () : log(TIMING-LOG, id(timer) as Int, StopEvent))
  else :
    body()

;Log time taken to execute a new task with the given name.
public defn log-time<?T> (body:() -> ?T, name:String) -> T :
  log-time(body, false, name)

;Log time taken to execute some child task. The child task is part
;of the given parent.
public defn log-time<?T> (body:() -> ?T, parent:TimerLabel, suffix:Suffix) -> T :
  log-time(body, parent, string-join([name(parent) " -- " string(suffix)]))

;Marker object for making API look nice.
public defstruct Suffix :
  string
with:
  constructor => suffix

;Log time taken to execute some child task. The child task is part
;of the given parent.
public defn log-time<?T> (body:() -> ?T, parent:TimerLabel|False, child:String) -> T :
  match(TIMING-LOG:TimingLog) :
    val parent-id = match(parent:TimerLabel) :
                      id(parent) as Int
    val child-id = id(TIMING-LOG, child, parent-id)
    dynamic-wind(
      fn () : log(TIMING-LOG, child-id, StartEvent)
      body
      fn () : log(TIMING-LOG, child-id, StopEvent))
  else :
    body()

;Exclude the body from the time it takes to execute the given task.
public defn exclude-log-time<?T> (body:() -> ?T, timer:TimerLabel) -> T :
  match(TIMING-LOG:TimingLog) :
    dynamic-wind(
      fn () : log(TIMING-LOG, id(timer) as Int, StopEvent)
      body
      fn () : log(TIMING-LOG, id(timer) as Int, StartEvent))
  else :
    body()