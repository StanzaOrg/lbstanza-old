defpackage stz/packed-class-table :
  import core
  import collections
  import stz/stable-arrays

;============================================================
;============== Representation of a Record ==================
;============================================================

public deftype ClassTableRecord
public defmulti index (r:ClassTableRecord) -> Int
public defmulti name (r:ClassTableRecord) -> String

public defstruct LeafClassRecord <: ClassTableRecord :
  index:Int with: (as-method => true)
  name:String with: (as-method => true)
  size: Int
  roots: Tuple<Int>
  root-names: Tuple<Symbol>
with:
  printer => true

public defstruct ArrayClassRecord <: ClassTableRecord :
  index:Int with: (as-method => true)
  name:String with: (as-method => true)
  base-size: Int
  item-size: Int
  base-roots: Tuple<Int>
  item-roots: Tuple<Int>
  base-root-names: Tuple<Symbol>
  item-root-names: Tuple<Symbol>
with:
  printer => true

;============================================================
;====================== Static Packer =======================
;============================================================

public deftype StaticPacker <: Packer
public defmulti make-label (p:StaticPacker) -> Int
public defmulti emit-comment (p:StaticPacker, str:String) -> False
public defmulti emit-label (p:StaticPacker, id:Int) -> False
public defmulti emit-pointer-to-label (p:StaticPacker, id:Int) -> False
public defmulti emit (p:StaticPacker, x:Int) -> False
public defmulti emit (p:StaticPacker, x:String) -> False

;============================================================
;================== Overall Static Packer ===================
;============================================================

public defn pack (records:Collection<ClassTableRecord>, packer:StaticPacker) -> False :
  ;Sanity check.
  for (r in records, i in 0 to false) do :
    if index(r) != i :
      fatal("Class %_ is supposed to be at index %_, but is actually at index %_." % [
            name(r), index(r), i])

  ;Generate labels
  val labels = to-tuple(seq(make-label{packer}, records))

  ;Emit descriptors
  for (r in records, i in 0 to false) do :
    val descriptor = fast-layout-descriptor(r)
    emit(packer, case(descriptor))
    emit(packer, num-base-bytes(descriptor))
    emit-pointer-to-label(packer, labels[i])

  ;Emit full layout records
  for (r in records, i in 0 to false) do :
    ;Emit comment and pack!
    emit-comment(packer, to-string("Full Layout Record: Class %_ (index %_)" % [name(r), index(r)]))
    emit-label(packer, labels[i])
    pack(r, packer)

defmethod pack (p:StaticPacker, x:Int) : emit(p, x)
defmethod pack (p:StaticPacker, s:String) : emit(p, s)

;============================================================
;================= Overall Dynamic Packer ===================
;============================================================

;Representation of class descriptor in class table.
lostanza deftype ClassDescriptor :
  case:int
  num-base-bytes:int
  full-layout:ptr<?>

;Returns the number of class entries in the allocated byte array.
lostanza defn num-class-entries (table:ref<StableByteArray>) -> ref<Int> :
  return new Int{(table.length / sizeof(ClassDescriptor)) as int}

;Returns the number of bytes required to hold the given maximum class index.
lostanza defn num-bytes-required (max-class-index:ref<Int>) -> ref<Int> :
  return new Int{((max-class-index.value + 1) * sizeof(ClassDescriptor)) as int}

;Pack the given records into the given class table.
public defn pack (records:Collection<ClassTableRecord>, class-table:StableByteArray) -> StableByteArray :
  ;Delete all the existing records.
  val current-num-class-entries = num-class-entries(class-table)
  for r in records do :
    val i = index(r)
    if i < current-num-class-entries :
      ensure-class-entry-deleted(class-table, i)

  ;Enlarge the table if necessary.
  val size-required = num-bytes-required(maximum(-1, seq(index, records)))
  val new-class-table =
    if length(class-table) < size-required :
      enlarge(class-table, size-required, 0Y)
    else :
      class-table

  ;Pack and store the records
  for r in records do :
    val i = index(r)
    pack-and-store(new-class-table, i, fast-layout-descriptor(r), pack(r))

  ;Return the potentially enlarged table.
  new-class-table

;Call free on the entry at index i in the given table.
;Set the entry to null.
lostanza defn ensure-class-entry-deleted (table:ref<StableByteArray>, index:ref<Int>) -> ref<False> :
  val i = index.value
  val descriptors = table.data as ptr<ClassDescriptor>
  val p = descriptors[i].full-layout
  if p != null : call-c clib/free(p)
  descriptors[i] = ClassDescriptor{0, 0, null}
  return false

;Pack the given record into bytes, and then store the entry
;at the given index in the table. Assumes that the slot currently contains null.
lostanza defn pack-and-store (table:ref<StableByteArray>,
                              index:ref<Int>,
                              descriptor:ref<FastLayoutDescriptor>,
                              record:ref<PackedClassRecord>) -> ref<False> :
  val i = index.value
  val data = table.data as ptr<ClassDescriptor>
  val packed-rec = pack(record)
  data[i] = ClassDescriptor{
              case(descriptor).value,
              num-base-bytes(descriptor).value,
              packed-rec}
  return false

;============================================================
;==================== Internal Packer =======================
;============================================================

deftype Packer
defmulti pack (p:Packer, x:Int) -> False
defmulti pack (p:Packer, s:String) -> False

;============================================================
;================== Fast Layout Descriptor ==================
;============================================================

;Cases
val CASE-BASE-WITH-NO-REFS = 0
val CASE-BASE-WITH-REFS = 1
val CASE-ARRAY-1-BYTE-TAIL = 2
val CASE-ARRAY-4-BYTE-TAIL = 3
val CASE-ARRAY-8-BYTE-TAIL = 4
val CASE-ARRAY-REF-TAIL = 5
val CASE-GENERAL = 6

defstruct FastLayoutDescriptor :
  case:Int
  num-base-bytes:Int

defn fast-layout-descriptor (r:ClassTableRecord) -> FastLayoutDescriptor :
  match(r) :
    (r:LeafClassRecord) :
      val num-fields = (size(r) / 8) when size(r) % 8 == 0
      val case =
        if empty?(roots(r)) : CASE-BASE-WITH-NO-REFS
        else if length(roots(r)) == num-fields : CASE-BASE-WITH-REFS
        else : CASE-GENERAL
      FastLayoutDescriptor(case, size(r))
    (r:ArrayClassRecord) :
      val case =
        if empty?(base-roots(r)) :
          switch(item-size(r)) :
            1 :
              CASE-ARRAY-1-BYTE-TAIL
            4 :
              CASE-ARRAY-4-BYTE-TAIL
            8 :
              if item-roots(r) == [0] : CASE-ARRAY-REF-TAIL
              else : CASE-ARRAY-8-BYTE-TAIL
            else :
              CASE-GENERAL
        else :
          CASE-GENERAL
      FastLayoutDescriptor(case, base-size(r))

;============================================================
;============== Packing Full Layout Descriptor  =============
;============================================================

;Leaf Class Definition:
;  num-bytes:Int, size:Int, item-size:Int (0), num-roots:Int,
;  roots:Int ..., name:String
;Array Class Definition:
;  num-bytes:Int, base-size:Int, item-size:Int, num-base-roots:Int, num-item-roots:Int,
;  base-roots:Int ..., item-roots:Int ..., name:String
defn pack (r:ClassTableRecord, packer:Packer) -> False :
  defn pack (x:Int) : /pack(packer, x)
  defn pack (x:String) : /pack(packer, x)
  match(r) :
    (r:LeafClassRecord) :
      val num-ints = 1                      ;num-bytes
                   + 1                      ;size
                   + 1                      ;0
                   + 1                      ;length(roots)
                   + length(roots(r))       ;roots
      pack(num-ints * 4)
      pack(size(r))
      pack(0)
      pack(length(roots(r)))
      do(pack, roots(r))
      pack(name(r))
    (r:ArrayClassRecord) :
      val num-ints = 1                            ;num-bytes
                   + 1                            ;base-size
                   + 1                            ;item-size
                   + 1                            ;length(base-roots)
                   + 1                            ;length(item-roots)
                   + length(base-roots(r))        ;base-roots
                   + length(item-roots(r))        ;item-roots
      pack(num-ints * 4)
      pack(base-size(r))
      pack(item-size(r))
      pack(length(base-roots(r)))
      pack(length(item-roots(r)))
      do(pack, base-roots(r))
      do(pack, item-roots(r))
      pack(name(r))

;============================================================
;=================== Pack into Record =======================
;============================================================

defstruct PackedClassRecord :
  ints:Tuple<Int>
  name:String

defn pack (r:ClassTableRecord) -> PackedClassRecord :
  val ints = Vector<Int>()
  var name:String|False = false
  val packer = new Packer :
    defmethod pack (this, x:Int) :
      add(ints, x)
    defmethod pack (this, s:String) :
      fatal("Expected only one string.") when name is String
      name = s
  pack(r, packer)
  PackedClassRecord(to-tuple(ints), name as String)

;============================================================
;=================== Pack into Bits =========================
;============================================================

defn num-bytes (r:PackedClassRecord) -> Int :
  length(ints(r)) * 4 + length(name(r)) + 1

lostanza defn pack (r:ref<PackedClassRecord>) -> ptr<?> :
  ;Malloc the correct number of bytes
  val num-bytes = num-bytes(r).value
  val record:ptr<?> = call-c clib/malloc(num-bytes)
  var p:ptr<?> = record

  ;Store ints
  val ints = ints(r)
  for (var i:long = 0, i < ints.length, i = i + 1) :
    [p as ptr<int>] = ints.items[i].value
    p = p + sizeof(int)

  ;Store bytes
  val name = name(r)
  for (var i:long = 0, i < name.length, i = i + 1) :
    [p as ptr<byte>] = name.chars[i]
    p = p + sizeof(byte)

  ;Return the allocated memory.
  return record
