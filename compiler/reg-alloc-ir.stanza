defpackage stz/reg-alloc-ir :
  import core
  import collections
  import stz/printing-utils

;============================================================
;==================== IR Definition =========================
;============================================================

;------------------------------------------------------------
;-------------------- Program Model -------------------------
;------------------------------------------------------------

public defstruct Program :
  num-vars:Int  
  stmts:Tuple<ProgramStmt>
  
public deftype ProgramStmt

public defstruct LabelStmt <: ProgramStmt :
  index:Int

public defstruct OperationStmt <: ProgramStmt :
  op-id:Int
  op-data
  args:Tuple<Int>
  results:Tuple<Int>

public defstruct BranchStmt <: ProgramStmt :
  targets:Tuple<Int>

public defstruct GotoStmt <: ProgramStmt :
  target:Int

;------------------------------------------------------------
;-------------------- Operation Model -----------------------
;------------------------------------------------------------

public defstruct OperationModel :
  id:Int
  results:Tuple<Int>
  args:Tuple<Int>
  temp:Tuple<Int>
  stmts:Tuple<ModelStmt>

public deftype ModelStmt

public defstruct Require <: ModelStmt :
  arg:Int
  location:Location

public defstruct Release <: ModelStmt :
  arg:Int

public defstruct RequireSaveLive <: ModelStmt

;------------------------------------------------------------
;-------------------- Location Specifiers -------------------
;------------------------------------------------------------

public deftype Location

public defstruct AnyReg <: Location

public defstruct AnyFReg <: Location

public defstruct Reg <: Location :
  index:Int

public defstruct FReg <: Location :
  index:Int

public defstruct Mem <: Location

public defstruct StackMem <: Location 

;============================================================
;====================== Output IR ===========================
;============================================================

;defstruct Save :
;  arg:Int
;  loc:Register
;
;defstruct Load :
;  arg:Int
;  loc:Register
;
;defstruct Relocate :
;  arg:Int
;  src:Register
;  dst:Register

;============================================================
;====================== Printers ============================
;============================================================

defmethod print (o:OutputStream, p:Program) :
  val items = [
    simple-field("num-vars", num-vars(p))
    inline-fields(stmts(p))]
  print(o, named-list-fields("program", items))

defmethod print (o:OutputStream, m:OperationModel) :
  val result-str = "" when empty?(results(m))
              else "(%,) = " % [seq(varid, results(m))]
  val temp-str = "" when empty?(temp(m))
            else " (temp %,)" % [seq(varid, temp(m))]
  print(o, "model %_op%_(%,)%_ %_" % [
    result-str
    id(m)
    seq(varid, args(m))
    temp-str,
    colon-list(stmts(m))])

defmethod print (o:OutputStream, s:ProgramStmt) :
  match(s) :
    (s:LabelStmt) :
      print(o, "label %_" % [labelid(index(s))])
    (s:OperationStmt) :
      val result-str = "" when empty?(results(s))
                  else "(%,) = " % [seq(varid,results(s))]
      print(o, "%_op%_(%,)" % [result-str, op-id(s), seq(varid,args(s))])
    (s:BranchStmt) :
      print(o, "branch (%,)" % [seq(labelid, targets(s))])
    (s:GotoStmt) :
      print(o, "goto %_" % [labelid(target(s))])

defmethod print (o:OutputStream, s:ModelStmt) :
  match(s) :
    (s:Require) :
      print(o, "require %_ for %_" % [location(s), varid(arg(s))])
    (s:Release) :
      print(o, "release %_" % [varid(arg(s))])
    (s:RequireSaveLive) :
      print(o, "require-save-live")

defmethod print (o:OutputStream, l:Location) :
  print{o, _} $ match(l) :
    (l:AnyReg) : "Rx"
    (l:AnyFReg) : "Fx"
    (l:Reg) : "R%_" % [index(l)]
    (l:FReg) : "F%_" % [index(l)]
    (l:Mem) : "mem"
    (l:StackMem) : "stack"

defn varid (x:Int) :
  "V%_" % [x]

defn labelid (x:Int) :
  "L%_" % [x]