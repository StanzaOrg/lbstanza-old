defpackage stz/reg-alloc-engine :
  import core
  import collections
  import stz/reg-alloc-ir
  import stz/printing-utils
  import stz/reg-alloc-ports
  import stz/reg-alloc-model-analysis
  import stz/reg-alloc-model-properties

;============================================================
;=================== State Definition =======================
;============================================================

;Holds the complete state of the register allocation engine.
public defstruct RegAllocEngine :
  program:Program
  blocks:Vector<Block> with:
    init => Vector<Block>()
  models:Tuple<OperationModel>
  model-properties:Vector<ModelProperties>
  model-analyzer:ModelAnalyzer
  var-types:IntTable<RegisterType>

public defstruct Block :
  index:Int
  start:Int
  length:Int
  succs:Tuple<Int>
  preds:Tuple<Int>
  ends-with-goto?:True|False
  inputs:Vector<Port> with:
    init => Vector<Port>()
  outputs:Vector<Port> with:
    init => Vector<Port>()
  defs:Vector<Int> with:
    init => Vector<Int>()

;============================================================
;==================== Constructor ===========================
;============================================================

;Create a new Register Allocator engine.
public defn RegAllocEngine (prog:Program, models:Tuple<OperationModel>) -> RegAllocEngine :
  val mprops = analyze-models(models)
  val manalyzer = ModelAnalyzer(models, mprops)
  val var-types = to-inttable<RegisterType> $
    for v in vars(prog) seq : id(v) => type(v)
  RegAllocEngine(prog, models, mprops, manalyzer, var-types)

;============================================================
;======================== Printers ==========================
;============================================================

defmethod print (o:OutputStream, e:RegAllocEngine) :
  val items = [
    program(e)
    named-list-fields("blocks", blocks(e))]
  print(o, "RegAllocEngine%_" % [colon-field-list(items)])

defmethod print (o:OutputStream, block:Block) :
  val items = [
    simple-field("start", start(block))
    simple-field("length", length(block))
    simple-field("succs", succs(block))
    simple-field("preds", preds(block))
    simple-field("ends-with-goto?", ends-with-goto?(block))
    named-list-fields("inputs", inputs(block))
    named-list-fields("outputs", outputs(block))
    simple-list-field("defs", defs(block))]
  println(o, "block B%_ %_" % [index(block), colon-field-list(items)])

defmethod print (o:OutputStream, m:ModelProperties) :
  val items = [
    simple-field("returns?", returns?(m))
    simple-field("clears-registers?", clears-registers?(m))
    simple-field("arg-types", arg-types(m))
    simple-field("result-types", result-types(m))]
  print(o, "model-properties %_%_" % [
    id(m), colon-field-list(items)])

