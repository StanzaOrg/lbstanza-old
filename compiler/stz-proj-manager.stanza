defpackage stz/proj-manager :
  import core
  import collections
  import stz/pkg
  import stz/proj
  import stz/utils
  import stz/algorithms
  import stz/aux-file

;<doc>=======================================================
;=================== Project Manager ========================
;============================================================

### Purpose ###

The ProjManager handles all things related to Stanza packages:
  - Where packages are located.
  - Which packages are conditionally imported.
  - Whether package .pkg files are up-to-date.
  - Track whether package .pkg files are up-to-date.
  - What external dependencies are pulled in by packages.

### Looking for the source file of a package ###

Input: The name of the package

Output: The PkgLocation structure if the package is found. 

### Updating the Auxiliary File ###

Input:

The statistics corresponding to all the saved packages.

### Collect Conditional Imports From Current Load List ###

Input:

The names of the packages that have currently been loaded. 

Output:

The names of all the new packages that need to be loaded, based upon
the conditional dependencies.

;============================================================
;=======================================================<doc>

public deftype ProjManager
public defmulti find-package (l:ProjManager, name:Symbol) -> PkgLocation|False
public defmulti update-aux-file (l:ProjManager, pkgs:Seqable<SavedPkg>) -> False
public defmulti conditional-imports (l:ProjManager, packages:Seqable<Symbol>) -> Tuple<Symbol>

public defn ProjManager (proj:ProjFile, params:ProjParams, auxfile:AuxFile) :
  ;Build source file table
  val source-file-table = to-hashtable<Symbol,String> $
    for s in filter-by<DefinedInStmt>(stmts(proj)) seq :
      package(s) => filename(s)

  ;Find a package
  defn find-package (name:Symbol) -> PkgLocation|False :
    val pkg-path = find-pkg(name, optimize?(params))
    val src-path = get?(source-file-table, name)
    val read-pkg? = match(pkg-path, src-path) :
      (pkg-path:String, src-path:String) :
        if file-exists?(src-path) :
          val rec = PkgRecord(name,
                              filestamp(pkg-path), filestamp(src-path)
                              flags(params), optimize?(params))
          key?(auxfile, rec)
      (pkg-path:String, src-path:False) : true
      (pkg-path:False, src-path) : false
    if pkg-path is-not False or src-path is-not False :
      PkgLocation(name, src-path, pkg-path, read-pkg?)

  ;Updating the auxiliary file
  defn update-aux-file (pkgs:Seqable<SavedPkg>) :
    for pkg in pkgs do :
      val r = PkgRecord(package(pkg), filestamp(pkg), source(pkg),
                        flags(params), optimize?(params))
      add(auxfile, r)    

  ;Retrieve conditional imports
  defn conditional-imports (packages:Seqable<Symbol>) :
    val load-set = to-hashset<Symbol>(packages)
    val next-load-set = HashSet<Symbol>()
    val import-stmts = to-tuple(filter-by<ImportWhenStmt>(stmts(proj)))
    within progress = fixpoint() :
      for s in import-stmts do :
        if all?({load-set[_]}, dependencies(s)) :
          if add(load-set, package(s)) :
            add(next-load-set, package(s))
            progress()
    to-tuple(next-load-set)

  ;Return new project manager
  new ProjManager :
    defmethod find-package (this, name:Symbol) : find-package(name)
    defmethod update-aux-file (this, pkgs:Seqable<SavedPkg>) : update-aux-file(pkgs)
    defmethod conditional-imports (this, packages:Seqable<Symbol>) : conditional-imports(packages)

;============================================================
;==================== Utilities =============================
;============================================================

public defn delete-aux-file () :
  val name = "stanza.aux2"
  delete-file(name) when file-exists?(name)

;============================================================
;===================== Structures ===========================
;============================================================

public defstruct ProjParams :
  flags: Tuple<Symbol>
  optimize?: True|False

public defstruct PkgLocation :
  package: Symbol
  source-file: String|False
  pkg-file: String|False
  read-pkg?: True|False
with:
  constructor => #PkgLocation
  printer => true

public defn PkgLocation (package:Symbol, source-file:String|False, pkg-file:String|False, read-pkg?:True|False) :
  if read-pkg? :
    fatal("Cannot read from pkg-file when pkg-file is false.") when pkg-file is False
    #PkgLocation(package, source-file, pkg-file as String, true)
  else :
    fatal("Did not read from pkg, but source-file is false.") when source-file is False
    #PkgLocation(package, source-file as String, pkg-file, false)

public defstruct SavedPkg :
  package: Symbol
  filestamp: FileStamp
  source: FileStamp

public defn filename (l:PkgLocation) -> String :
  if read-pkg?(l) : pkg-file(l) as String
  else : source-file(l) as String