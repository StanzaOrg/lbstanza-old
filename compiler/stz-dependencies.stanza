defpackage stz/dependencies :
  import core
  import collections
  import line-wrap
  import stz/compiler
  import stz/front-end
  import stz/il-ir
  import stz/pkg
  import stz/dl-ir
  import stz/algorithms
  import stz/proj-manager
  import stz/graphviz
  
public defn analyze-dependencies (settings:BuildSettings,
                                  ignore-cache?:True|False,
                                  output:String|False,
                                  graphviz:String|False) :
  defn driver () :
    val dep-result = dependencies(settings, ignore-cache?)
    val dependencies = scan-dependencies(packages(dep-result))
    val source-files = SourceFiles $
      for stamp in pkgstamps(dep-result) seq :
        val l = location(stamp)
        package(l) => filename(l)
    val text = new Printable :
      defmethod print (o:OutputStream, this) :
        println(o, source-files)
        println(o, dependencies)
        if not empty?(errors(dep-result)) :
          println(o, "Errors:")
          for e in errors(dep-result) do :
            println(o, Indented(e))
          
    ;Output text
    val wrapped = new Printable :
      defmethod print (o:OutputStream, this) :
        print(LineWrapStream(o, 80), text)
    match(output:String) : spit(output, wrapped)
    else : println(wrapped)
    
    ;Output to graphviz
    match(graphviz:String) :
      spit(graphviz, to-graphviz(dependencies))      
    
  defn scan-dependencies (ps:Collection<IPackage|Pkg>) :
    ;Process only successfully resolved packages
    val resolve-set = to-hashset<Symbol> $ seq(name, ps)
    defn resolved? (ps:Seqable<Symbol>) : filter({resolve-set[_]}, ps)

    ;Compute the package dependency graph
    val dependency-graph = to-hashtable<Symbol,List<Symbol>> $
      for p in ps seq :
        val imported-packages = match(p) :
          (p:IPackage) : unique(seq(package, imports(p)))
          (p:Pkg) : imported-packages(packageio(p))
        name(p) => to-list $ resolved?(imported-packages)
    ;Component graph
    defn list? (x:List<Symbol>|Symbol) :
      match(x:Symbol) : List(x)
      else : x as List<Symbol>
    val components = to-tuple(seq(list?,strong-components(dependency-graph)))
    ;Component table    
    val component-table = HashTable<Symbol,Int>()
    for (component in components, i in 0 to false) do :
      for p in component do : component-table[p] = i
    ;Component imports
    defn component-dependencies (c:Int) -> List<Symbol> :
      val cs = unique $ for p in components[c] seq-cat :
        for d in dependency-graph[p] seq :
          component-table[d]
      for d in cs seq-append :
        List() when d == c else components[d]
    val component-dependencies-list = to-tuple $
      for i in 0 to length(components) seq :
        components[i] => component-dependencies(i)
    ;Return Dependencies structure
    Dependencies(to-tuple(dependency-graph), component-dependencies-list)

  ;Launch
  driver()

;============================================================
;======================= Datastructures =====================
;============================================================

defstruct Dependencies :
  package-dependencies: Tuple<KeyValue<Symbol,List<Symbol>>>
  component-dependencies: Tuple<KeyValue<List<Symbol>,List<Symbol>>>

defmethod print (o:OutputStream, d:Dependencies) :
  defn empty-paren? (xs:Tuple) : " ()" when empty?(xs) else ""
  
  print(o, "Package Dependencies:%_" % [empty-paren?(package-dependencies(d))])  
  val import-list = for entry in package-dependencies(d) seq :
    "\n%~ imports %~" % [key(entry), value(entry)]
  print(o, Indented("%*" % [import-list]))

  lnprint(o, "Package Group Dependencies:%_" % [empty-paren?(component-dependencies(d))])
  val component-list = for entry in component-dependencies(d) seq :
    "\n%~ imports %~" % [key(entry), value(entry)]
  print(o, Indented("%*" % [component-list]))

defstruct SourceFiles :
  entries: Tuple<KeyValue<Symbol,String>>
with:
  constructor => #SourceFiles

defn SourceFiles (entries:Seqable<KeyValue<Symbol,String>>) :
  #SourceFiles(to-tuple(entries))

defmethod print (o:OutputStream, sf:SourceFiles) :
  defn empty-paren? (xs:Tuple) : " ()" when empty?(xs) else ""
  print(o, "Source Files:%_" % [empty-paren?(entries(sf))])
  val source-list = for e in entries(sf) seq :
    "\n%~ loaded-from %~" % [key(e), value(e)]
  print(o, Indented("%*" % [source-list]))

;============================================================
;=================== Graphviz Conversion ====================
;============================================================

defn to-graphviz (d:Dependencies) -> DigraphViz :
  val entries = to-tuple $
    for entry in package-dependencies(d) seq-cat :
      val pkg = to-string(key(entry))
      for import in value(entry) seq :
        pkg => to-string(import)
  DigraphViz("Dependencies", entries)