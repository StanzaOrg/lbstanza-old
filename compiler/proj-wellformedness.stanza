defpackage stz/proj-wellformedness :
  import core
  import collections
  import stz/params
  import stz/proj-ir
  import stz/proj-env
  import stz/proj-stage0
  import stz/proj-errors
  import stz/utils
  import stz/check-set-equal
  import stz/proj-value-types

;============================================================
;========================= API ==============================
;============================================================

public deftype ProjWellformedness

;Check the given file, and make any required preprocessing/modifications.
public defmulti check (wf:ProjWellformedness, f:ProjFileS0) -> False

;============================================================
;=========== General Wellformedness Algorithm ===============
;============================================================

public defn ProjWellformedness (platform:Symbol,
                                env:ProjEnv) -> ProjWellformedness :
                                
  ;Create table of package manager info.
  val package-manager-table = to-hashtable(name, foreign-package-managers(env))

  new ProjWellformedness :
    defmethod check (this, f:ProjFileS0) :
      ;Create a vector to hold any detected errors.
      val errors = Vector<ProjFileError>()

      ;Check for wellformedness and duplicates.
      check-wellformed(f, platform, package-manager-table, errors)
      check-duplicates(f, errors)

      ;Throw errors if there is any.
      throw-proj-file-errors?(errors)

;============================================================
;============== Wellformedness of each Statement ============
;============================================================

;Checks a single item for wellformedness errors.
;Recursing into subitems are handled by the caller.
defmulti check-item-wellformed (x:ProjItem,
                                platform:Symbol,
                                pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                                errors:Vector<ProjFileError>) -> False :
  false

;Included files must end with .proj.
defmethod check-item-wellformed (x:IncludeStmtS0,
                                 platform:Symbol,
                                 pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                                 errors:Vector<ProjFileError>) :
  if not suffix?(path(x), ".proj") :
    val msg = "Expected a .proj file here, but found %_." % [path(x)]
    add(errors, ProjFileError(info(x), to-string(msg)))

;Check whether the platforms are properly handled.
defmethod check-item-wellformed (x:CondPlatform,
                                 platform:Symbol,
                                 pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                                 errors:Vector<ProjFileError>) :
  ;Check whether all the platforms are either supported platforms, or
  ;the 'else' keyword.
  val n = length(values(x))
  for (entry in values(x), i in 0 to false) do :
    if key(entry) == `else :
      if i != n - 1 :
        val msg = "The 'else' keyword can only be used as last entry in list."
        add(errors, ProjFileError(info(x), msg))
    else :
      if not supported-platform?(key(entry)) :
        val msg = "'%~' does not refer to a supported platform." % [key(entry)]
        add(errors, ProjFileError(info(x), to-string(msg)))

  ;Check whether the current platform is one of the supported options.
  val current-platform-supported? =
    for entry in values(x) any? :
      key(entry) == `else or
      key(entry) == platform

  ;Issue error if current platform is not supported.
  if not current-platform-supported? :
    val msg = "There is no available option for the current platform '%~'." % [platform]
    add(errors, ProjFileError(info(x), to-string(msg)))

defmethod check-item-wellformed (x:ForeignPackageParamsStmtS0,
                                 platform:Symbol,
                                 pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                                 errors:Vector<ProjFileError>) :      
  ;Look for the corresponding info from the package manager.
  match(get?(pmtable, package-manager(x))) :
    (pminfo:ForeignPackageManagerInfo) :
      ;Check all expected fields are there.
      val given-names = map(key, entries(x))
      val expected-items = for param in params(pminfo) map :
        Item(name(param), optional?(param))
      val diffs = check-set-equal(given-names, expected-items)
      for diff in diffs do :
        match(diff) :
          (diff:UnsupportedItem) :
            val msg = "The '%~' parameter is not a recognized parameter for the %_ \
                       package manager." % [name(diff), package-manager(x)]
            add(errors, ProjFileError(info(x), to-string(msg)))
          (diff:DuplicateItem) :
            val msg = "There is a duplicate entry for the '%~' parameter." % [name(diff)]
            add(errors, ProjFileError(info(x), to-string(msg)))
          (diff:MissingItem) :
            val msg = "The '%~' parameter is a required parameter for the %_ \
                       package manager, but it is not provided." % [name(diff), package-manager(x)]
            add(errors, ProjFileError(info(x), to-string(msg)))
    (f:False) :
      val msg = "The package manager '%~' is not one of the supported \
                 external package managers." % [package-manager(x)]
      add(errors, ProjFileError(info(x), to-string(msg)))

defmethod check-item-wellformed (s:RequiresStmtS0,
                                 platform:Symbol,
                                 pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                                 errors:Vector<ProjFileError>) :
  ;Track which package managers already have a listing.
  val fp-set = HashSet<Symbol>()
  
  for fp in foreign-packages(s) do :
    ;First check for duplicate entries.
    if fp-set[package-manager(fp)] :
      val msg = "Duplicate entry listing foreign packages for %_ package manager." % [package-manager(fp)]
      add(errors, ProjFileError(info(s), to-string(msg)))
    ;Then check whether this package manager is supported.
    else if not key?(pmtable, package-manager(fp)) :
      val msg = "The package manager '%~' is not one of the supported \
                 external package managers." % [package-manager(fp)]
      add(errors, ProjFileError(info(s), to-string(msg)))
    ;Then, everything is good, and put it in the set.
    else :
      add(fp-set, package-manager(fp))

;============================================================
;============= Welformedness of Whole File ==================
;============================================================

;Check the given item for wellformedness.
defn check-wellformed (f:ProjFileS0,
                       platform:Symbol,
                       pmtable:HashTable<Symbol,ForeignPackageManagerInfo>,
                       errors:Vector<ProjFileError>) -> False :
  defn check-item (x:ProjItem) :
    do(check-item, x)
    check-item-wellformed(x, platform, pmtable, errors)
  do(check-item, stmts(f))

;============================================================
;================= Check for Duplicates =====================
;============================================================

;Check for duplicate statements in the same file.
defn check-duplicates (f:ProjFileS0, errors:Vector<ProjFileError>) -> False :
  val groups = group-by(duplication-key, stmts(f))
  for group in groups do :
    if key(group) is-not False :
      if length(value(group)) > 1 :
        val stmts = reverse(value(group))
        val stmt0 = head(stmts)
        for stmt in tail(stmts) do :
          add(errors, DuplicateStmt(info(stmt), stmt-description(stmt), info(stmt0)))