defpackage stz/linking-errors :
  import core
  import collections

public deftype LinkingError <: Exception

;Used to bundle a collection of errors together.
public defstruct LinkingErrors <: LinkingError :
  errors:Tuple<LinkingError>
defmethod print (o:OutputStream, e:LinkingErrors) :
  val error-str = "%n" % [errors(e)]
  print(o, "Errors occurred during linking:\n%_" % [
    Indented(error-str)])

;Throw LinkingErrors if not empty.
public defn throw-linker-errors? (es:Vector<LinkingError>) -> False :
  if not empty?(es) :
    throw(LinkingErrors(to-tuple(es)))

;Occurs when the program depends upon a C file or flag, but the
;compilation statement indicates a different type for this dependency.
public defstruct MismatchedCompilationStmt <: LinkingError :
  file?: True|False
  name: String|Tuple<String>
defmethod print (o:OutputStream, e:MismatchedCompilationStmt) :
  defn type-str (file?:True|False) :
    "file" when file? else "flag"
  print(o, "The program has a stated dependency upon the C %_ %~, but the compilation \
            statement in the .proj file indicates that %~ is a %_." % [
    type-str(file?(e)), name(e), name(e), type-str(not file?(e))])

;Occurs when one of the build commands for compiling an external
;dependency throws an exception.
public defstruct ErrorCompilingExternalDependency <: LinkingError :
  file?:True|False
  name:String|Tuple<String>
  error:Exception
defmethod print (o:OutputStream, e:ErrorCompilingExternalDependency) :
  defn type-str (file?:True|False) :
    "file" when file? else "flag"
  print(o, "Error occurred when compiling external %_ %~: %_" % [
    type-str, name(e), error(e)])

;Occurs when one of the build commands for compiling an external
;dependency throws an exception.
public defstruct ErrorLinkingAsmfile <: LinkingError :
  asmfile:String
  exefile:String
  error:Exception
defmethod print (o:OutputStream, e:ErrorLinkingAsmfile) :
  print(o, "Error occurred when linking %~ to form executable %~: %_" % [
    asmfile(e), exefile(e), error(e)])

;Occurs when one of the build commands for compiling an external
;dependency throws an exception.
public defstruct NoConfigurationForPackageManager <: LinkingError :
  package-manager:Symbol
  packages: Tuple<String>
defmethod print (o:OutputStream, e:NoConfigurationForPackageManager) :
  val package-str = "%," % [seq(written, packages(e))]
  print(o, "The program depends upon the %_ package(s) from the %_ package manager, however \
            there is no 'foreign-package-params(%_)' statement in the .proj file to configure \
            the manager." % [
            package-str, package-manager(e), package-manager(e)])