defpackage stz/comments-main :
  import core
  import stz/comments
  import stz/arg-parser

defn check-comments (parsed:ParseResult) :
  ;Retrieve name of doc structure file
  val structure-file = let :
    val args = args(parsed)
    if length(args) != 1 :
      println("No doc structure file provided.")
      exit(-1)
    head(args)

  ;Retrieve blocks to accept
  val accepts = to-tuple(multiple?(parsed, "accept", List()))

  ;Launch!
  try :
    check-project-doc-consistency(structure-file, accepts)
  catch (e:IOException|CommentDocErrors) :
    println(e)
    exit(-1)

defn main () :
  val command = 
    Command("check-docs",
      [MultipleFlag("accept", true)]
      check-comments)
  val args = command-line-arguments()[1 to false]
  try :
    parse-args([command], command, args)
  catch (e:ArgParserException) :
    println(e)
    exit(-1)

main()
  