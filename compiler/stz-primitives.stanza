;See License.txt for details about licensing.

defpackage stz/primitives :
   import core
   import collections
   import stz/tl-ir
   import stz/ids

;============================================================
;================= HiStanza Primitives ======================
;============================================================

public deftype Primitive <: Equalable
public defmulti name (p:Primitive) -> Symbol
public defmulti prim-code (p:Primitive) -> Byte

public val PRIMITIVES = Vector<Primitive>()

public defn primitive? (primname:Symbol) :
   primitive(primname) is-not False

public defn primitive (primname:Symbol) :
   for p in PRIMITIVES find :
      primname == name(p)

public defn primitive! (primname:Symbol) :
   primitive(primname) as Primitive

public defn primitive! (code:Byte) :
  PRIMITIVES[to-int(code)]

defmethod equal? (a:Primitive, b:Primitive) :
   name(a) == name(b)

defmethod print (o:OutputStream, p:Primitive) :
   print(o, name(p))

defn make-primitive (name:Symbol) :
   val code = to-byte(length(PRIMITIVES))
   val p = new Primitive :
      defmethod name (this) : name
      defmethod prim-code (this) : code
   add(PRIMITIVES, p)
   p
   
public val ADD-OP = make-primitive(`add)
public val SUB-OP = make-primitive(`sub)
public val MUL-OP = make-primitive(`mul)
public val DIV-OP = make-primitive(`div)
public val MOD-OP = make-primitive(`mod)
public val AND-OP = make-primitive(`and)
public val OR-OP = make-primitive(`or)
public val XOR-OP = make-primitive(`xor)
public val SHL-OP = make-primitive(`shl)
public val SHR-OP = make-primitive(`shr)
public val ASHR-OP = make-primitive(`ashr)
public val EQ-OP = make-primitive(`eq)
public val NE-OP = make-primitive(`ne)
public val LT-OP = make-primitive(`lt)
public val GT-OP = make-primitive(`gt)
public val LE-OP = make-primitive(`le)
public val GE-OP = make-primitive(`ge)
public val NOT-OP = make-primitive(`not)
public val NEG-OP = make-primitive(`neg)
public val IDENTICAL-OP = make-primitive(`identical?)
public val AGET-OP = make-primitive(`aget)
public val ASET-OP = make-primitive(`aset)
public val ALEN-OP = make-primitive(`alen)
public val CHAR-AGET-OP = make-primitive(`char-aget)
public val CHAR-ASET-OP = make-primitive(`char-aset)
public val STRING-GET-OP = make-primitive(`string-get)
public val STRING-LEN-OP = make-primitive(`string-len)

;============================================================
;================= LoStanza Primitives ======================
;============================================================

public deftype LSPrimitive <: Equalable
public defmulti name (p:LSPrimitive) -> Symbol
public defmulti prim-code (p:LSPrimitive) -> Byte
   
public val LS-PRIMITIVES = Vector<LSPrimitive>()

defn register (p:LSPrimitive) :
   add(LS-PRIMITIVES, p)
   p

defmethod equal? (a:LSPrimitive, b:LSPrimitive) :
   name(a) == name(b)

defmethod print (o:OutputStream, p:LSPrimitive) :
   print(o, name(p))

public defn ls-primitive? (primname:Symbol) :
   ls-primitive(primname) is-not False

public defn ls-primitive (primname:Symbol) :
   for p in LS-PRIMITIVES find :
      primname == name(p)

public defn ls-primitive! (primname:Symbol) :
   ls-primitive(primname) as LSPrimitive

public defn ls-primitive! (code:Byte) :
   LS-PRIMITIVES[to-int(code)]

;Getter Primitives
public deftype GetterPrimitive <: LSPrimitive
public defmulti type (p:GetterPrimitive) -> LSType
public defmulti lbl (p:GetterPrimitive) -> Int

;Addr Primitives
public deftype AddrPrimitive <: LSPrimitive
public defmulti type (p:AddrPrimitive) -> LSType
public defmulti lbl (p:AddrPrimitive) -> Int

;Setter Primitives
public deftype SetterPrimitive <: LSPrimitive
public defmulti type (p:SetterPrimitive) -> LSType
public defmulti lbl (p:SetterPrimitive) -> Int

;Creation Convenience
defn make-ls-primitive (name:Symbol) :
  val code = to-byte(length(LS-PRIMITIVES))
  register $ new LSPrimitive :
    defmethod name (this) : name
    defmethod prim-code (this) : code
defn make-getter-primitive (name:Symbol, type:LSType, lbl:Int) :
  val code = to-byte(length(LS-PRIMITIVES))
  register $ new GetterPrimitive :
    defmethod name (this) : name
    defmethod type (this) : type
    defmethod lbl (this) : lbl
    defmethod prim-code (this) : code
defn make-getter-primitive (name:Symbol, type:LSType) :
  val code = to-byte(length(LS-PRIMITIVES))
  register $ new GetterPrimitive :
    defmethod name (this) : name
    defmethod type (this) : type
    defmethod prim-code (this) : code
defn make-setter-primitive (name:Symbol, type:LSType, lbl:Int) :
  val code = to-byte(length(LS-PRIMITIVES))
  register $ new SetterPrimitive :
    defmethod name (this) : name
    defmethod type (this) : type
    defmethod lbl (this) : lbl
    defmethod prim-code (this) : code
defn make-addr-primitive (name:Symbol, type:LSType, lbl:Int) :
  val code = to-byte(length(LS-PRIMITIVES))
  register $ new AddrPrimitive :
    defmethod name (this) : name
    defmethod type (this) : type
    defmethod lbl (this) : lbl
    defmethod prim-code (this) : code

val STACK-TYPE = RefT(TOf(-1, TTuple(List())))
public val GET-HEAP-OP = make-getter-primitive(`heap, PtrT(UnknownT()), HEAP-PTR-LBL)
public val SET-HEAP-OP = make-setter-primitive(`set-heap, PtrT(UnknownT()), HEAP-PTR-LBL)
public val GET-HEAP-LIM-OP = make-getter-primitive(`heap-limit, PtrT(UnknownT()), HEAP-LIM-LBL)
public val SET-HEAP-LIM-OP = make-setter-primitive(`set-heap-limit, PtrT(UnknownT()), HEAP-LIM-LBL)
public val GET-NUM-FREE-STACKS-OP = make-getter-primitive(`num-free-stacks, LongT(), NUM-FREE-STACKS-LBL)
public val SET-NUM-FREE-STACKS-OP = make-setter-primitive(`set-num-free-stacks, LongT(), NUM-FREE-STACKS-LBL)
public val GET-FREE-STACKS-OP = make-getter-primitive(`free-stacks, PtrT(STACK-TYPE), FREE-STACKS-LBL)
public val SET-FREE-STACKS-OP = make-setter-primitive(`set-free-stacks, PtrT(STACK-TYPE), FREE-STACKS-LBL)
public val GET-STACK-POOL-OP = make-getter-primitive(`stack-pool, PtrT(STACK-TYPE), STACK-POOL-LBL)
public val SET-STACK-POOL-OP = make-setter-primitive(`set-stack-pool, PtrT(STACK-TYPE), STACK-POOL-LBL)
public val GET-STACK-OP = make-ls-primitive(`stack)
public val GET-STACKFRAME-OP = make-ls-primitive(`stackframe)

public val SET-STACK-OP = make-ls-primitive(`set-stack)
public val GET-GLOBAL-TABLE-OP = make-addr-primitive(`globals, PtrT(UnknownT()), GLOBAL-TABLE-LBL)
public val GET-GLOBAL-MAP-OP = make-addr-primitive(`global-map, PtrT(UnknownT()), GLOBAL-MAP-LBL)
public val GET-OBJECT-TABLE-OP = make-addr-primitive(`object-table, PtrT(UnknownT()), OBJECT-TABLE-LBL)
public val GET-CONSTS-OP = make-getter-primitive(`consts, PtrT(UnknownT()), -1)
public val GET-CONSTS-DATA-OP = make-getter-primitive(`consts-data, PtrT(UnknownT()), -1)
public val GET-INFO-TABLE-OP = make-addr-primitive(`info-table, PtrT(UnknownT()), INFO-TABLE-LBL)
public val GET-C-RSP-OP = make-getter-primitive(`c-rsp, PtrT(UnknownT()), C-RSP-LBL)
public val SET-C-RSP-OP = make-setter-primitive(`set-c-rsp, PtrT(UnknownT()), C-RSP-LBL)

public val LS-ADD-OP = make-ls-primitive(`add)
public val LS-SUB-OP = make-ls-primitive(`sub)
public val LS-MUL-OP = make-ls-primitive(`mul)
public val LS-DIV-OP = make-ls-primitive(`div)
public val LS-MOD-OP = make-ls-primitive(`mod)
public val LS-AND-OP = make-ls-primitive(`and)
public val LS-OR-OP = make-ls-primitive(`or)
public val LS-XOR-OP = make-ls-primitive(`xor)
public val LS-SHL-OP = make-ls-primitive(`shl)
public val LS-SHR-OP = make-ls-primitive(`shr)
public val LS-ASHR-OP = make-ls-primitive(`ashr)
public val LS-EQ-OP = make-ls-primitive(`eq)
public val LS-NE-OP = make-ls-primitive(`ne)
public val LS-LT-OP = make-ls-primitive(`lt)
public val LS-GT-OP = make-ls-primitive(`gt)
public val LS-LE-OP = make-ls-primitive(`le)
public val LS-GE-OP = make-ls-primitive(`ge)
public val LS-ULT-OP = make-ls-primitive(`ult)
public val LS-UGT-OP = make-ls-primitive(`ugt)
public val LS-ULE-OP = make-ls-primitive(`ule)
public val LS-UGE-OP = make-ls-primitive(`uge)
public val LS-NOT-OP = make-ls-primitive(`not)
public val LS-NEG-OP = make-ls-primitive(`neg)
public val LS-BITS-OP = make-ls-primitive(`bits)
public val LS-FNUM-OP = make-ls-primitive(`fnum)

public val LS-YIELD-OP = make-ls-primitive(`yield)
public val LS-ENTER-OP = make-ls-primitive(`enter-stack)
public val LS-NEW-STACK-OP = make-ls-primitive(`new-stack)
public val LS-LAST-STACK-OP = make-ls-primitive(`last-stack)