defpackage stz/trie-table :
  import core
  import collections
  import stz/hash
  import dispatch-dag

;<doc>=======================================================
;=================== Trie Table Representation ==============
;============================================================

Here is how a trie is stored:

  I | N | D0 | DTable ... | Key, Value ... | Default

Where:

  I is the index of the register to perform dispatch on.
  N is the length of the table.
  D0 is the d-parameter of the first-level hash table.
  DTable are the entries of the first-level hash table.
  Key are the keys for each branch.
  Value are the values for each branch.

If N is less than or equal to 4, then the DTable is omitted, and we
perform a linear lookup instead.

If a key is not in the table, then we interpret the action given by Default.

Two cases are encoded into the value and Default :

  (- (Target + 1)) when Value < 0
  (Table offset)   when Value >= 0

When the Value is negative, it encodes the target to return.
Otherwise, it encodes the address of the next table.

;============================================================
;=======================================================<doc>

;Encode Dag into Int Vector
public defn encode-dag (dag:Dag, start-depth:Int, soln-id:Soln -> Int) -> Vector<Int> :
  ;Accumulator for storing encoded trie
  val accum = Vector<Int|TrieId>()
  defn emit (x:Int|TrieId) : add(accum,x)

  ;Overall algorithm
  defn driver () :
    val addresses = to-tuple $
      for e in entries(dag) seq :
        val address = length(accum)
        encode(e)
        address
    fill-addresses(addresses)
    accum as Vector<Int>

  ;Convert end of dag into trie-id
  defn to-trie-id (x:Int|Soln) :
    match(x) :
      (x:Int) : TrieId(x)
      (x:Soln) : (- (soln-id(x) + 1))

  ;Encode a trie for dispatching on given arg-index.
  defn encode (dag:DagEntry) :
    ;Spread out entries
    val entries = Vector<KeyValue<Int,Int|TrieId>>()
    for e in /entries(dag) do :
      val tgt = to-trie-id(value(e))
      for v in values(key(e)) do :
        add(entries, v => tgt)

    ;Compute the table
    val n = length(entries)
    emit(start-depth + depth(dag))
    emit(n)
    if n <= 4 :
      for e in entries do :
        emit(key(e))
        emit(value(e))
      emit(to-trie-id(default(dag)))
    else :
      val table = PerfectHashTable(entries)
      fatal("Unexpected size difference") when n != length(table)
      emit(d0(table))
      for i in 0 to n do :
        emit(dentry(table,i))
      for i in 0 to n do :
        val e = entry(table,i)
        emit(key(e))
        emit(value(e))
      emit(to-trie-id(default(dag)))

  defn fill-addresses (addresses:Tuple<Int>) :
    ;Fill in delayed values
    for v in accum map! :
      match(v:TrieId) : addresses[id(v)]
      else : v as Int
          
  ;Launch
  driver()

defstruct TrieId :
  id: Int