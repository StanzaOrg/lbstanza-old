#use-added-syntax(stz-serializer-lang)
defpackage stz/aux-file :
  import core
  import core/sha256
  import collections
  import stz/serializer
  import stz/utils
  import stz/front-end
  import stz/proj-manager
  import stz/params
  import stz/proj
  import stz/proj-utils

;============================================================
;==================== Aux File Definition ===================
;============================================================

public deftype AuxFile
public defmulti key? (f:AuxFile, r:PkgRecord|ExternalFileRecord) -> True|False
public defmulti target-up-to-date? (f:AuxFile, target:Symbol, settings:BuildRecordSettings, proj:ProjFile) -> True|False
public defmulti add (f:AuxFile, r:AuxRecord) -> False
public defmulti save (f:AuxFile) -> False

public defn AuxFile (path:String) -> AuxFile :
  val records = AuxRecords([]) when not file-exists?(path)
           else read-aux-records(path)
  val new-records = Vector<AuxRecord>()
  new AuxFile :
    defmethod key? (this, r:PkgRecord|ExternalFileRecord) :
      contains?(filter-by<PkgRecord|ExternalFileRecord>(/records(records)), r)
    defmethod target-up-to-date? (this, target:Symbol, settings:BuildRecordSettings, proj:ProjFile) :
      defn main () :
        val r = matching-record()
        match(r:BuildRecord) :
          matches-settings?(r) and
          record-up-to-date?(r) and
          matching-isolate?(r)
      defn matching-record () :
        for r in filter-by<BuildRecord>(/records(records)) find :
          /target(r) == target
      defn matches-settings? (r:BuildRecord) :
        /settings(r) == settings
      defn record-up-to-date? (r:BuildRecord) :
        all?(up-to-date?, packages(r)) and
        all?(up-to-date?, files(r))
      defn matching-isolate? (r:BuildRecord) :
        val isolate* = isolate-stmts(proj, packages(proj-isolate(r)))
        isomorphic?(proj-isolate(r), isolate*)
      main()
    defmethod add (this, r:AuxRecord) :
      add(new-records, r)
    defmethod save (this) :
      val records* = combine(records, new-records)
      write-aux-records(path, records*)

public defn AuxFile () :
  AuxFile(aux-file-path())

defn aux-file-path () :
  norm-path $ string-join $ [STANZA-INSTALL-DIR "/stanza.aux"]

;============================================================
;==================== Utilities =============================
;============================================================

public defn delete-aux-file () :
  val name = aux-file-path()
  delete-file(name) when file-exists?(name)

;============================================================
;=================== Check Up-to-Date =======================
;============================================================

defn hashstamp? (file:String|False) :
  match(file:String) :
    sha256-hash-file(file) when file-exists?(file)

defn up-to-date? (s:FileStamp) :
  hashstamp?(filename(s)) == hashstamp(s)

defn up-to-date? (s:PackageStamp) :
  val l = location(s)
  if read-pkg?(l) :
    hash-equal?(hashstamp?(pkg-file(l)), pkg-hashstamp(s)) and
    hash-equal?(hashstamp?(source-file(l)), source-hashstamp(s))
  else :
    hash-equal?(hashstamp?(source-file(l)), source-hashstamp(s))

;============================================================
;=================== Aux Serializer =========================
;============================================================

defn write-aux-records (name:String, auxfile:AuxRecords) :
  val f = FileOutputStream(name)
  try : serialize(f, auxfile)
  finally : close(f)

defn read-aux-records (name:String) -> AuxRecords :
  val f = FileInputStream(name)
  try : deserialize-auxrecords(f)
  catch (e:DeserializeException) : throw(CorruptedAuxFile(name))
  finally : close(f)

;============================================================
;===================== Aux File Structure ===================
;============================================================

defstruct AuxRecords :
  records: Tuple<AuxRecord>
with:
  printer => true

public deftype AuxRecord

public defstruct PkgRecord <: AuxRecord & Hashable & Equalable :
  package: Symbol
  filestamp: FileStamp
  source-stamp: FileStamp
  flags: Tuple<Symbol>
  optimize?: True|False
with:
  constructor => #PkgRecord
  printer => true

public defstruct BuildRecord <: AuxRecord :
  target: Symbol
  packages: Tuple<PackageStamp>
  files: Tuple<FileStamp>
  settings: BuildRecordSettings
  proj-isolate: ProjIsolate
with:
  printer => true

public defstruct ExternalFileRecord <: AuxRecord & Hashable & Equalable :
  filetype: ExtFileType
  dependencies: Tuple<FileStamp>
  commands: Tuple<String>
with:
  printer => true

public deftype ExtFileType <: Equalable & Hashable
public defstruct ExternalFile <: ExtFileType: (filestamp:FileStamp)
with: (printer => true)
public defstruct ExternalFlag <: ExtFileType: (flag:String)
with: (printer => true)

public defstruct BuildRecordSettings <: AuxRecord & Hashable & Equalable :
  inputs: Tuple<String|Symbol>
  vm-packages: Tuple<String|Symbol>
  platform: Symbol|False
  assembly: String|False
  output: String|False
  pkg-dir: String|False
  optimize?: True|False
  ccfiles: Tuple<String>
  ccflags: Tuple<String>
  flags: Tuple<Symbol>
with:
  printer => true

;------------------------------------------------------------
;---------------- Canonical Constructor ---------------------
;------------------------------------------------------------

public defn PkgRecord (package:Symbol,
                       filestamp:FileStamp,
                       source-stamp:FileStamp,
                       flags:Tuple<Symbol>,
                       optimize?:True|False) :
  #PkgRecord(package, filestamp, source-stamp,
             qsort(flags), optimize?)

;------------------------------------------------------------
;------------------ Hashing and Equality --------------------
;------------------------------------------------------------

defn key (r:PkgRecord) :
  [package(r), filestamp(r), source-stamp(r), flags(r), optimize?(r)]
defmethod equal? (a:PkgRecord, b:PkgRecord) : key(a) == key(b)
defmethod hash (r:PkgRecord) : hash $ key(r)

defn key (r:ExternalFileRecord) : [filetype(r), dependencies(r), commands(r)]
defmethod equal? (a:ExternalFileRecord, b:ExternalFileRecord) : key(a) == key(b)
defmethod hash (r:ExternalFileRecord) : hash $ key(r)

defmethod equal? (a:ExternalFile, b:ExternalFile) : filestamp(a) == filestamp(b)
defmethod hash (f:ExternalFile) : hash(filestamp(f))
defmethod equal? (a:ExternalFlag, b:ExternalFlag) : flag(a) == flag(b)
defmethod hash (f:ExternalFlag) : hash(flag(f))

defn key (r:BuildRecordSettings) :
  [inputs(r), vm-packages(r), platform(r), assembly(r), output(r),
   pkg-dir(r), optimize?(r), ccfiles(r), ccflags(r), flags(r)]
defmethod equal? (a:BuildRecordSettings, b:BuildRecordSettings) : key(a) == key(b)
defmethod hash (r:BuildRecordSettings) : hash $ key(r)

;============================================================
;======================== Printer ===========================
;============================================================

defmethod print (o:OutputStream, f:AuxRecords) :
  print(o, "auxrecords :")
  val o2 = IndentedStream(o)
  do(lnprint{o2, _}, records(f))

;============================================================
;================== Combining Records =======================
;============================================================

defn combine (file:AuxRecords, records:Seqable<AuxRecord>) :
  ;Strip category
  defn strip-key (r:AuxRecord) :
    match(r) :
      (r:PkgRecord) : [`pkg, filename(filestamp(r))]
      (r:BuildRecord) : [`build, target(r)]
      (r:ExternalFileRecord) :
        match(filetype(r)) :
          (f:ExternalFile) : [`external-file, filename(filestamp(f))]
          (f:ExternalFlag) : [`external-flag, flag(f)]

  ;Build table, and override earlier records with later records with same key
  val table = HashTable<?,AuxRecord>()
  for r in cat(/records(file), records) do :
    table[strip-key(r)] = r

  ;Build new AuxRecords
  AuxRecords(to-tuple(values(table)))

;============================================================
;================= Serializer Definition ====================
;============================================================

defserializer (out:FileOutputStream, in:FileInputStream) :

  ;----------------------------------------------------------
  ;--------------------- Records ----------------------------
  ;----------------------------------------------------------
  defunion auxrecords (AuxRecords) :
    AuxRecords: (records:tuple(auxrecord))

  defunion auxrecord (AuxRecord) :
    PkgRecord: (package:symbol, filestamp:filestamp, source-stamp:filestamp,
                flags:tuple(symbol), optimize?:bool)
    ExternalFileRecord: (filetype:filetype, dependencies:tuple(filestamp), commands:tuple(string))
    BuildRecord: (target:symbol, packages:tuple(pkgstamp), files:tuple(filestamp), settings:build-record-settings, proj-isolate:isolate)

  defunion filetype (ExtFileType) :
    ExternalFile: (filestamp:filestamp)
    ExternalFlag: (flag:string)

  defunion build-record-settings (BuildRecordSettings) :
    BuildRecordSettings: (inputs:tuple(string-or-symbol), vm-packages:tuple(string-or-symbol),
                          platform:opt<Symbol>(symbol), assembly:opt<String>(string), output:opt<String>(string),
                          pkg-dir:opt<String>(string), optimize?:bool, ccfiles:tuple(string), ccflags:tuple(string), flags:tuple(symbol))

  defunion pkgstamp (PackageStamp) :
    PackageStamp: (location:pkglocation, source-hashstamp:opt<ByteArray>(shahash), pkg-hashstamp:opt<ByteArray>(shahash))

  defunion pkglocation (PkgLocation) :
    PkgLocation: (package:symbol, source-file:opt<String>(string), pkg-file:opt<String>(string), read-pkg?:bool)

  defunion filestamp (FileStamp) :
    FileStamp: (filename:string, hashstamp:shahash)

  defunion isolate (ProjIsolate) :
    ProjIsolate: (packages:tuple(symbol), stmts:tuple(projstmt))

  defunion projstmt (ProjStmt) :
    DefinedInStmt: (package:symbol, filename:string)
    RequiresStmt: (package:symbol, ccfiles:tuple(string), ccflags:tuple(string))
    ImportWhenStmt: (package:symbol, dependencies:tuple(symbol))
    CompileStmt: (file?:bool, name:string, dependencies:tuple(string), commands:tuple(string))

  ;----------------------------------------------------------
  ;-------------------- Combinators -------------------------
  ;----------------------------------------------------------

  reader defn read-tuple<?T> (f: () -> ?T) :
    val n = length!(read-int())
    to-tuple(repeatedly(f, n))

  writer defn write-tuple<?T> (f: T -> False, xs:Tuple<?T>) :
    write-int(length(xs))
    do(f, xs)

  reader defn read-opt<T> (f: () -> T) :
    val n = read-byte()
    f() when n == 1Y

  writer defn write-opt<T> (f: T -> False, x:T|False) :
    match(x) :
      (x:False) :
        write-byte(0Y)
      (x:T) :
        write-byte(1Y)
        f(x)

  ;----------------------------------------------------------
  ;----------------------- Atoms ----------------------------
  ;----------------------------------------------------------
  defatom bool (x:True|False) :
    writer :
      match(x) :
        (x:True) : put(out, 1Y)
        (x:False) : put(out, 0Y)
    reader :
      switch(get-byte(in)) :
        1Y : true
        0Y : false
        else : throw(DeserializeException())

  defatom byte (x:Byte) :
    writer :
      put(out, x)
    reader :
      match(get-byte(in)) :
        (x:Byte) : x
        (x:False) : throw(DeserializeException())

  defatom int (x:Int) :
    writer :
      put(out, x)
    reader :
      match(get-int(in)) :
        (x:Int) : x
        (x:False) : throw(DeserializeException())

  defatom long (x:Long) :
    writer :
      put(out, x)
    reader :
      match(get-long(in)) :
        (x:Long) : x
        (x:False) : throw(DeserializeException())

  defatom string (x:String) :
    writer :
      write-int(length(x))
      print(out, x)
    reader :
      val n = length!(read-int())
      String(repeatedly(read-char, n))

  defatom symbol (x:Symbol) :
    writer :
      write-string(to-string(x))
    reader :
      to-symbol(read-string())

  defatom char (x:Char) :
    writer :
      print(out, x)
    reader :
      match(get-char(in)) :
        (x:Char) : x
        (x:False) : throw(DeserializeException())

  defatom string-or-symbol (x:String|Symbol) :
    writer :
      match(x) :
        (x:String) :
          put(out, 0Y)
          write-string(x)
        (x:Symbol) :
          put(out, 1Y)
          write-symbol(x)
    reader :
      switch(get-byte(in)) :
        0Y : read-string()
        1Y : read-symbol()
        else : throw(DeserializeException())

  defatom shahash (x:ByteArray) :
    writer :
      for i in 0 to 32 do :
        put(out, x[i])
    reader :
      val bytes = ByteArray(32)
      for i in 0 to 32 do :
        bytes[i] = read-byte()
      bytes

defn non-neg! (x:Int) -> Int :
  if x < 0 : throw(DeserializeException())
  else : x

defn length! (x:Int) -> Int :
  if x < 0 : throw(DeserializeException())
  else if x > 1048576 : throw(DeserializeException())
  else : x

;============================================================
;====================== Exceptions ==========================
;============================================================

public defstruct CorruptedAuxFile <: Exception :
  filename: String

defmethod print (o:OutputStream, f:CorruptedAuxFile) :
  print(o, "Auxiliary file %~ is corrupted." % [filename(f)])