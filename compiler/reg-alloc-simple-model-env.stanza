defpackage stz/reg-alloc-simple-model-env :
  import core
  import collections
  import stz/reg-alloc-ir
  import stz/reg-alloc-model-env
  import stz/reg-alloc-model-properties

public defn SimpleModelEnv (models:Tuple<OperationModel>,
                            props:Tuple<ModelProperties>) -> ModelEnv :
  SimpleModelEnv(models, One(props))

public defn SimpleModelEnv (models:Tuple<OperationModel>) -> ModelEnv :
  SimpleModelEnv(models, None())

defn SimpleModelEnv (models:Tuple<OperationModel>,
                     props:Maybe<Tuple<ModelProperties>>) -> ModelEnv :
  ;Verify model indexing.
  for (m in models, i in 0 to false) do :
    if id(m) != i :
      fatal("Models are not densely indexed.")

  ;Helper: Retrieve the model properties for model m.
  defn get-props (m:Int) -> ModelProperties :
    if empty?(props) : fatal("No model properties.")
    else : value!(props)[m]

  new ModelEnv :
    defmethod num-models (this) :
      length(models)
    defmethod model (this, id:Int) :
      models[id]
    defmethod arg-types (this, id:Int) :
      map(type{location(_)}, args(models[id]))
    defmethod result-types (this, id:Int) :
      map(type{location(_)}, results(models[id]))
    defmethod returns? (this, id:Int) :
      returns?(get-props(id))
    defmethod clears-registers? (this, id:Int) :
      clears-registers?(get-props(id))
    defmethod forced-release? (this, op-id:Int, arg-index:Int) :
      forced-release?(get-props(op-id))[arg-index]
    defmethod forced-release? (this, op-id:Int) :
      any-forced-release?(get-props(op-id))