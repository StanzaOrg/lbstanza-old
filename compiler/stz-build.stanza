;See License.txt for details about licensing

defpackage stz/build-lang :
  import core
  import collections
  import reader

;============================================================
;=================== Build File Parsing =====================
;============================================================

public defstruct ProjectBuild :
  name: Symbol
  filenames: List<String>
  link: List<Symbol>
  assembly: Maybe<String>
  output: Maybe<String>
  platform: Maybe<Symbol>
  ccfiles: List<String>
  ccflags: Maybe<String>
  flags: List<Symbol>
  pkg: Maybe<String>
  optimize?: True|False
  verbose?: True|False

defsyntax stanza-build :
  defn ut (x) : unwrap-token(x)
  val no-value = gensym()

  ;================
  ;==== Errors ====
  ;================
  defn BE (info:False|FileInfo, msg) :
    val msg* = match(info) :
      (info:FileInfo) : "%_: %_" % [info, msg]
      (info:False) : msg
    throw(Exception(msg*))

  ;=======================
  ;==== Type Checking ====
  ;=======================
  defn as-option (key:Symbol, info:False|FileInfo, x) :
    defn E! (description:String) :
      BE(info, "Option %_ should be a %_." % [key, description])
    defn as-strings () :
      match(x:List) : x when all?({_ is String}, x) else E!("list of strings")
      else : E!("list of strings")
    defn as-symbols () :
      match(x:List) : x when all?({_ is Symbol}, x) else E!("list of symbols")
      else : E!("list of symbols")      
    defn as-string () :
      match(x:String) : x
      else : E!("string")
    defn as-symbol () :
      match(x:Symbol) : x
      else : E!("symbol")
    defn as-no-value () :
      if x != no-value :
        BE(info, "Option %_ does not accept a value." % [key])
      no-value
    
    key => switch(key) :
      `filenames : as-strings()
      `link : as-symbols()
      `s : as-string()
      `o : as-string()
      `platform : as-symbol()
      `ccfiles : as-strings()
      `ccflags : as-string()
      `flags : as-symbols()
      `pkg : as-string()
      `optimize : as-no-value()
      `verbose : as-no-value()
      else : BE(info, "Unrecognized project build option: %_." % [key])  

  ;========================
  ;==== Option Parsing ====
  ;========================
  defn make-project (info:False|FileInfo, name:Symbol, es:List<KeyValue<Symbol,?>>) :
    defn field? (name:Symbol) :
      for e in es first :
        if key(e) == name : One(value(e))
        else : None()
    defn field (name:Symbol) :
      val x = field?(name)
      if empty?(x) : BE(info, "Required build option %_ is missing." % [name])
      else : value!(x)
    defn list (name:Symbol) :
      val x = field?(name)
      if empty?(x) : List()
      else : value!(x)
    defn bool (name:Symbol) :
      any?({key(_) == name}, es)
    ProjectBuild(
      name,
      field(`filenames),
      list(`link),
      field?(`s),
      field?(`o),
      field?(`platform),
      list(`ccfiles)
      field?(`ccflags),
      list(`flags),
      field?(`pkg),
      bool(`optimize),
      bool(`verbose))

  ;===============
  ;==== Rules ====
  ;===============
  ;Symbols
  defproduction sym: Symbol
  defrule sym = (?x) when ut(x) is Symbol :
    ut(x)

  ;Projects
  public defproduction proj!: ProjectBuild
  defrule proj! = (?name:#sym : (?es:#entry! ...)) :
    make-project(closest-info(), name, es)
  fail-if proj! = () :
    BE(closest-info(), "Invalid project entry.")

  ;Entries
  defproduction entry!: KeyValue<Symbol,?>
  defrule entry! = (?key:#sym : ?value) :
    as-option(key, closest-info(key), unwrap-all(value))
  defrule entry! = (?key:#sym) :
    as-option(key, closest-info(key), no-value)
  fail-if entry! = () :
    BE(closest-info(), "Invalid key-value entry.")

public defn read-build-file () :
  try :
    val forms = read-file("stanza.build")
    parse-syntax[stanza-build / #proj! ...](forms)
  catch (e:FileOpenException) :
    throw(NoBuildFileError())

defstruct NoBuildFileError <: Exception
defmethod print (o:OutputStream, e:NoBuildFileError) :
  print(o, "Could not open Stanza build file stanza.build. Please run stanza init to create an example build file.")


defpackage stz/build :
  import core
  import collections
  import stz/serializer
  import stz/main
  import stz/build-lang

#use-added-syntax(stz-serializer-lang)

;============================================================
;==================== Timestamp Table =======================
;============================================================

defstruct TimestampTable :
  entries: List<KeyValue<String,Long>>

defn changed? (tt:TimestampTable, filename:String) :
  val e = find({key(_) == filename}, entries(tt))
  match(e:KeyValue<String,Long>) :
    value(e) != time-modified(filename)
  else : true

;============================================================
;================ File Timestamp Parsing ====================
;============================================================

defserializer (out:FileOutputStream, in:FileInputStream) :
  ;=================
  ;==== Overall ====
  ;=================
  defunion timestamp-table (TimestampTable) :
    TimestampTable: (entries:list(kv))

  defunion kv (KeyValue<String,Long>) :
    KeyValue: (key:string, value:long)

  ;=====================
  ;==== Combinators ====
  ;=====================
  reader defn read-list<?T> (f: () -> ?T) :
    val n = read-int()
    to-list(repeatedly(f, n))

  writer defn write-list<?T> (f: T -> False, xs:List<?T>) :
    write-int(length(xs))
    do(f, xs)

  ;===============
  ;==== Atoms ====
  ;===============
  defatom int (x:Int) :
    writer :
      put(out, x)
    reader :
      match(get-int(in)) :
        (x:Int) : x
        (x:False) : throw(DeserializeException())

  defatom char (x:Char) :
    writer :
      print(out, x)
    reader :
      match(get-char(in)) :
        (x:Char) : x
        (x:False) : throw(DeserializeException())

  defatom long (x:Long) :
    writer :
      put(out, x)
    reader :
      match(get-long(in)) :
        (x:Long) : x
        (x:False) : throw(DeserializeException())
      
  defatom string (x:String) :
    writer :
      write-int(length(x))
      print(out, x)
    reader :
      val n = read-int()
      String(repeatedly(read-char, n))

defn read-time-stamp-table () :
  try :
    val f = FileInputStream("stanza.aux")
    deserialize-timestamp-table(f)
  catch (e:DeserializeException) :
    throw(CorruptedAuxFileError())
  catch (e:FileOpenException) :
    TimestampTable(List())

defn update-time-stamp-table (table:TimestampTable, build:ProjectBuild) :
  defn existing? (e:KeyValue<String,Long>) :
    not contains?(filenames(build), key(e))
    
  TimestampTable{to-list(_)} $
    cat(
      for filename in filenames(build) seq :
        filename => time-modified(filename)
      filter(existing?, entries(table)))

defn write-time-stamp-table (table:TimestampTable) :
  try :
    val f = FileOutputStream("stanza.aux")
    serialize(f, table)
  catch (e:SerializeException|IOException) :
    throw(BadWriteAuxError())    

defstruct CorruptedAuxFileError <: Exception
defmethod print (o:OutputStream, e:CorruptedAuxFileError) :
  print(o, "Stanza auxiliary file stanza.aux is corrupted. Clean the project and try again.")

defstruct BadWriteAuxError <: Exception
defmethod print (o:OutputStream, e:BadWriteAuxError) :
  print(o, "Could not write Stanza auxiliary file stanza.aux.")

;============================================================
;======================= Build ==============================
;============================================================

defn changed-filenames (table:TimestampTable, build:ProjectBuild) :
  to-list $
    filter(changed?{table, _}, filenames(build))

public defn build (compile:BuildJob -> ?, proj:Symbol) :
  val builds = read-build-file()
  val ts-table = read-time-stamp-table()
  val build = let :
    val build = find({name(_) == proj}, builds)
    if build is False :
      throw(Exception("No build project with name %_." % [proj]))
    build as ProjectBuild
  ;Build pkg files
  val pkg-dir = if empty?(pkg(build)) : One(".")
                else : pkg(build)
  val filenames = changed-filenames(ts-table, build)
  if not empty?(filenames) :
    compile $ BuildJob(
      filenames,
      None(),
      None(),
      platform(build),
      List(),
      None(),
      flags(build),
      pkg-dir,
      optimize?(build),
      verbose?(build))
  ;Save time stamp
  write-time-stamp-table(
    update-time-stamp-table(ts-table, build))
  if not empty?(assembly(build)) or not empty?(output(build)) :
    ;Build linked file
    compile $ BuildJob(
      map(to-string,link(build)),
      assembly(build),
      output(build),
      platform(build),
      ccfiles(build),
      ccflags(build),
      flags(build),
      pkg-dir,
      optimize?(build),
      verbose?(build))

public defn build (proj:Symbol) :
  build(compile, proj)
    
;============================================================
;==================== Initialization ========================
;============================================================

val EXAMPLE-BUILD-FILE =
\<MSG>
;This is an entry for the default project: "main".
;You can add more entries like this for other projects.
main :

  ;List all the files in your project here.
  filenames :
    "src/myfile.stanza"
    "src/myfile2.stanza"

  ;List the stanza package to use as your main program
  ;entry point
  link :
    mypkg2

  ;This option tells the compiler the name of the
  ;output binary.
  o: "prog"

  ;Uncomment this option to specify for which platform to
  ;compile to. The default is the same platform that Stanza
  ;was installed on. Options are linux, windows, or os-x.
  ;platform: linux

  ;This is currently commented out, which means that Stanza
  ;generates a temporary .s file during compilation,
  ;and then deletes it afterwards.
  ;Uncomment this to provide a name for the generated .s file.  
  ;s: "prog.s"

  ;Uncomment this option to generate the .pkg files in the
  ;"mypkgs" folder. Currently they are generated into the
  ;current working directory.
  ;pkg: "mypkgs"

  ;Uncomment this option to include additional source files written in
  ;C to be compiled together with the Stanza program.
  ;ccfiles:
  ;  "src/my_c_lib.c"

  ;Uncomment this option to include additional compiler options to
  ;pass to the gcc compiler during the final linking phase.
  ;ccflags: "-D USE_FAST_MATH"

  ;Uncomment this option to provide additional compile-time flags
  ;to the compiler.
  ;flags:
  ;  PRINT-INTERMEDIATE-RESULTS
  ;  WITH-SANITY-CHECKS

  ;Uncomment this option to compile in optimized mode.
  ;optimize
<MSG>

public defn init () :
  spit("stanza.build", EXAMPLE-BUILD-FILE)
    
;============================================================
;======================== Cleaning ==========================
;============================================================

public defn clean () :
  try : delete-file("stanza.aux")
  catch (e:FileDeletionError) : false