defpackage stz/compiler :
   import core
   import reader
   import macro-utils
   import stz/params
   import stz/utils
   import stz/algorithms
   import stz/parser
   import stz/input
   import stz/renamer
   import stz/resolver
   import stz/type
   import stz/kform
   import stz/tgt

defn macroexpand (forms:List) -> List :
   with-syntax(core) :
      match-syntax(forms) :
         (?es:#exp ...) :
            es

defn vprintln (x) :
   if flag-defined?(`VERBOSE) :
      println(x)

public defn compile (filenames:List<String>, output:False|String, save-pkgs:True|False) -> True|False :
   var timer 
   defn start-time (name:String) :
      timer = MillisecondTimer(name)
      start(timer)
   defn stop-time () :
      println(timer)
      
   try :
      if flag-defined?(`VERBOSE) :
         stz/ids/print-all-ids()
         
      start-time("Read Forms")
      vheader("Form")
      val forms = map(read-file, filenames)
      vprintln(forms)
      stop-time()

      start-time("Expand Forms")
      vheader("Expanded")
      val expanded = cons(`$begin, seq-append(macroexpand, forms))
      vprintln(expanded)
      stop-time()

      start-time("Convert to Input")
      vheader("Input")
      val input = read-prog(expanded)
      vprintln(input)
      stop-time()

      start-time("Check Input")
      vheader("Check")
      check(input)
      stop-time()

      start-time("Rename Input")
      vheader("Renamer")
      val [namemap, renamed] = rename(input)
      vprintln(renamed)
      stop-time()

      start-time("Resolve Input")
      vheader("Resolver")
      val resolved = resolve(namemap, renamed)
      vprintln(resolved)
      stop-time()

      start-time("Convert to Typed")
      vheader("Type System")
      val typed = type-program(namemap, resolved)
      vprintln(typed)
      stop-time()

      if output is String :
         start-time("Compile KForm")
         vheader("Compiled")
         val compiled = compile(typed, List(), namemap, save-pkgs)
         vprintln(compiled)
         stop-time()

         start-time("Assemble and Emit")
         vheader("Assemble and Emit")      
         compile(compiled, output as String)
         stop-time()
         
         ;Compilation successful
         true
      else if save-pkgs :
         fatal("Not Implemented.")
         
         ;Compilation successful
         true
      else :
         true
   catch (e:Exception) :
      println(e)
      false