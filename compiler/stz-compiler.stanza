;See License.txt for details about licensing.

defpackage stz/compiler :
   import core
   import reader
   import macro-utils
   import stz/params
   import stz/utils
   import stz/algorithms
   import stz/parser
   import stz/input
   import stz/renamer
   import stz/resolver
   import stz/type
   import stz/kform
   import stz/tgt

defn macroexpand (forms:List) -> List :
   with-syntax(core) :
      match-syntax(forms) :
         (?es:#exp ...) :
            es

defn vprintln (x) :
   if flag-defined?(`VERBOSE) :
      println(x)

public defn compile (filenames:List<String>, output:False|String, save-pkgs:True|False) -> True|False :      
   try :
      if flag-defined?(`VERBOSE) :
         stz/ids/print-all-ids()
         
      vheader("Form")
      val forms = map(read-file, filenames)
      vprintln(forms)

      vheader("Expanded")
      val expanded = cons(`$begin, seq-append(macroexpand, forms))
      vprintln(expanded)

      vheader("Input")
      val input = read-prog(expanded)
      vprintln(input)

      vheader("Check")
      check(input)

      vheader("Renamer")
      val [namemap, renamed] = rename(input)
      vprintln(renamed)

      vheader("Resolver")
      val resolved = resolve(namemap, renamed)
      vprintln(resolved)

      vheader("Type System")
      val typed = type-program(namemap, resolved)
      vprintln(typed)

      if output is String :
         vheader("Compiled")
         val compiled = compile(typed, List(), namemap, save-pkgs)
         vprintln(compiled)

         vheader("Assemble and Emit")      
         compile(compiled, output as String)
         
         ;Compilation successful
         true
      else if save-pkgs :
         fatal("Not Implemented.")
         
         ;Compilation successful
         true
      else :
         true
   catch (e:Exception) :
      println(e)
      false