;See License.txt for details about licensing.

defpackage stz/namemap :
  import core
  import collections
  import stz/dl-ir
  import stz/visibility

public deftype NameMap <: Collection<VEntry>
public defmulti add (n:NameMap, e:VEntry) -> False
public defmulti get (nm:NameMap, n:Int) -> VEntry
public defmulti key? (n:NameMap, n:Int) -> True|False
public defmulti toplevel (n:NameMap) -> Seqable<VEntry>

public defn NameMap () :
  val table = IntTable<VEntry>()
  val toplevel = Vector<VEntry>()

  new NameMap :
    defmethod add (this, e:VEntry) -> False :
      table[n(e)] = e
      add(toplevel,e) when visibility(e) is-not Local
    defmethod toplevel (this) :
      toplevel
    defmethod get (this, n:Int) -> VEntry :
      table[n]
    defmethod key? (this, n:Int) :
      key?(table,n)
    defmethod to-seq (this) :
      to-seq(values(table))
    defmethod print (o:OutputStream, this) :
      print(o, "NameMap:")
      for io in o do-indented :
        for entry in values(table) do :
          print(io, "\n%_" % [entry])

public defn NameMap (ns:Seqable<NameMap|PackageExports>) :
  val namemap = NameMap()
  for e in ns do :
    match(e) :
      (nm:NameMap) : do(add{namemap, _}, nm)
      (p:PackageExports) : do(add{namemap, VEntry(_)}, exports(p))
  namemap

public defstruct VEntry :
  n: Int
  package: Symbol
  name: Symbol
  visibility: Visibility
  type: EType
  info: FileInfo|False
with:
  printer => true

public defstruct Local <: Visibility
defmethod print (o:OutputStream, l:Local) :
  print(o, "local")

public deftype EType <: Equalable
public defstruct ValE <: EType
public defstruct VarE <: EType
public defstruct TVarE <: EType
public defstruct CapVarE <: EType
public defstruct TypeE <: EType
public defstruct MultiE <: EType
public defstruct FnE <: EType
public defstruct LValE <: EType
public defstruct LVarE <: EType
public defstruct LFnE <: EType
public defstruct LTypeE <: EType
public defstruct FieldE <: EType
public defstruct LabelE <: EType

defmethod equal? (a:EType, b:EType) :
  ($prim identical? a b)

defmethod print (o:OutputStream, t:EType) :
  print{o, _} $ match(t) :
    (t:ValE) : "ValE"
    (t:VarE) : "VarE"
    (t:TVarE) : "TVarE"
    (t:CapVarE) : "CapVarE"
    (t:TypeE) : "TypeE"
    (t:MultiE) : "MultiE"
    (t:FnE) : "FnE"
    (t:LValE) : "LValE"
    (t:LVarE) : "LVarE"
    (t:LFnE) : "LFnE"
    (t:LTypeE) : "LTypeE"
    (t:FieldE) : "FieldE"
    (t:LabelE) : "LabelE"

public defn VEntry (e:Export) :
  defn etype (r:Rec) :
    match(r) :
      (r:ValRec) :
        match(mutable?(r), lostanza?(r)) :
          (m:False, ls:False) : ValE()
          (m:True, ls:False) : VarE()
          (m:False, ls:True) : LValE()
          (m:True, ls:True) : LVarE()
      (r:FnRec) :
        if lostanza?(r) : LFnE()
        else : FnE()
      (r:MultiRec) : MultiE()
      (r:ExternFnRec) : LFnE()
      (r:ExternRec) :
        match(type(r)) :
          (t:DFnT) : LFnE()
          (t) : LVarE()
      (r:StructRec) : LTypeE()
      (r:TypeRec) : TypeE()
  val rid = id(rec(e))
  VEntry(n(e), package(rid), name(rid), visibility(e), etype(rec(e)), false)