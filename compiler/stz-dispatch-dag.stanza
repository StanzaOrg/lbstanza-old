defpackage stz/dispatch-dag :
  import core
  import collections
  import stz/set-utils

;============================================================
;==================== Input Datastructure ===================
;============================================================

;Represents the main input to the DAG creation algorithm, the
;set of branches for which we need to create the dispatch dag.
;- branches: The set of branches to create the DAG for.
;- abstract-nums: The identifiers of the abstract types referred to in each Arg.
;  An "abstract type" is a type for which we will never directly create a concrete object.
;- instance-branches: The indices of all the branches that represent instance methods. These branches
;  contain at least one Arg that corresponds to a single concrete type.
public defstruct BranchTable :
  branches: Tuple<Branch>
  abstract-nums: IntSet with: (default => IntSet())
  instance-branches: IntSet with: (init => compute-instance-branches(branches, abstract-nums))

;Represents the type of a match/method argument.
public deftype Arg <: Hashable & Equalable

;Represents all types.
public defstruct Top <: Arg

;Represents a set of types.
;- values: The set of types represented by this argument.
public defstruct Nums <: Arg :
  values: Tuple<Int> with: (ensure => sorted!)

;Represents a branch. 
public defstruct Branch :
  args: Tuple<Arg>

;============================================================
;=================== Solution Datastructure =================
;============================================================

;Represents a "solution": The final branch that a match/dispatch statement
;should dispatch to when given some input arguments.
public deftype Soln <: Hashable & Equalable

;Represents no solution. There is no branch that matches the
;given input arguments.
public defstruct NoSoln <: Soln

;Represents a unique solution. There is exactly one branch that matches
;the given input arguments.
;- index: The index of the branch in the branch table.
public defstruct UniqueSoln <: Soln :
  index:Int
  
;Represents an ambiguous solution. There are at least two branches that
;matches the given input arguments, and neither is more specific than
;the other.
;- index1: The index of the first branch in the branch table.
;- index2: The index of the second branch in the branch table.
public defstruct AmbSoln <: Soln :
  index1:Int,
  index2:Int with: (ensure => different!(index1))
with:
  constructor => #AmbSoln

;Normalized constructor.
public defn AmbSoln (a:Int, b:Int) :
  #AmbSoln(min(a,b), max(a,b))

;============================================================
;==================== DAG Datastructure =====================
;============================================================

;Represents a dispatch dag.
;- depth: The total number of arguments in the branch table.
;  This places a maximum limit on the number of arguments whose types
;  must be checked to compute the final branch to dispatch to.
;- entries: The tuple of entries in the dag. Dispatch begins from
;  the first entry.
public defstruct Dag :
  depth: Int
  entries: Tuple<DagEntry>

;Represents a single entry in the dag. This tells us which
;argument we need to check, and what types it can take on, and
;the next dag entry we need to go to depending on the type.
;- depth: The index of the argument we need to check.
;- entries: Each entry, nums => s, tells us what should happen
;  if the argument has type matching 'nums'. If s is an Int, then we
;  should go to the DagEntry with that index. If s is a Soln, then
;  that tells us the final branch to dispatch to.
;- default: What happens if none of the above entries is a match.
;  If it is an Int, then go to the DagEntry at that index. If it
;  is a Soln, then that tells us the final branch to dispatch to.
public defstruct DagEntry :
  depth: Int
  entries: Tuple<KeyValue<Nums,Int|Soln>>
  default: Int|Soln

;An empty dag is one that deterministically goes to a specific branch,
;and does not need to check any argument.
defn EmptyDag (s:Soln) -> Dag :
  val e = DagEntry(0, [], s)
  Dag(0, [e])

;============================================================
;===================== Error Checking =======================
;============================================================

defn different! (a:Int) :
  fn (description:String, b:Int) :
    if a == b :
      fatal("The value given to %_ (%_) should not be equal to %_." % [
        description, b, a])

defn sorted! (description:String, vs:Tuple<Int>) :
  for i in 0 to length(vs) - 1 do :
    if vs[i] >= vs[i + 1] :
      fatal("The value given to %_ (%_) is not sorted." % [description, vs])

;============================================================
;=================== Hash and Equality ======================
;============================================================

defmethod hash (a:Arg) :
  match(a) :
    (a:Top) : 0
    (a:Nums) : hash(values(a))

defmethod equal? (a:Arg, b:Arg) :
  match(a, b) :
    (a:Top, b:Top) : true
    (a:Nums, b:Nums) : values(a) == values(b)
    (a,b) : false

defmethod hash (a:Soln) -> Int:
  match(a):
    (a:NoSoln) : 0
    (a:UniqueSoln) : (- index(a))
    (a:AmbSoln) : index1(a) * index2(a)

defmethod equal? (a:Soln, b:Soln):
  match(a, b):
    (a:NoSoln, b:NoSoln): true
    (a:UniqueSoln, b:UniqueSoln): index(a) == index(b)
    (a:AmbSoln, b:AmbSoln): index1(a) == index1(b) and index2(a) == index2(b)
    (a, b): false

;============================================================
;====================== Printing ============================
;============================================================

defmethod print (o:OutputStream, n:Nums) :
  print(o, "(%,)" % [values(n)])
defmethod print (o:OutputStream, t:Top) :
  print(o, "(TOP)")
defmethod print (o:OutputStream, b:Branch) :
  print(o, args(b))
defmethod print (o:OutputStream, t:BranchTable) :
  print(o, "abstract: %," % [abstract-nums(t)])
  lnprint(o, "%n" % [branches(t)])

defmethod print (o:OutputStream, s:NoSoln) :
  print(o, "NoSoln")
defmethod print (o:OutputStream, s:UniqueSoln) :
  print(o, "UniqueSoln(%_)" % [index(s)])
defmethod print (o:OutputStream, s:AmbSoln) :
  print(o, "AmbSoln(%_, %_)" % [index1(s), index2(s)])

;============================================================
;====================== Relations ===========================
;============================================================

;Return -1 if x is more specific than y (i.e. encompasses fewer concrete types.)
;Return 0 if x is equal to y.
;Return 1 if x is less specific than y.
;Return false if x and y are incomparable.
defn compare-arg (x:Arg, y:Arg) -> Int|False :
  match(x, y) :
    (x:Top, y:Top) : 0
    (x:Nums, y:Top) : -1
    (x:Top, y:Nums) : 1
    (x:Nums, y:Nums) :
      match(subset?(values(x), values(y)), subset?(values(y), values(x))) :
        (a:True, b:False) : -1
        (a:False, b:True) : 1
        (a:True, b:True) : 0
        (a:False, b:False) : false

;Return false if any arg is incomparable.
;Otherwise return -1/0/1 according to lexicographic ordering.
defn compare-branch (a:Branch, b:Branch) -> Int|False :
  val cs = to-tuple $ seq(compare-arg, args(a), args(b))
  if none?({_ is False}, cs) :
    let loop (i:Int = 0) :
      if i < length(cs) :
        if cs[i] == 0 : loop(i + 1)
        else : cs[i]
      else : 0

;Return true if the given arg has no types.
defn bottom? (x:Arg) -> True|False :
  match(x:Nums) :
    empty?(values(x))

;Return true if there exists a type for which
;the arguments overlap.
defn overlap? (x:Arg, y:Arg) -> True|False :
  if bottom?(x) or bottom?(y) :
    false
  else :
    match(x, y) :
      (x:Top, y) : true
      (x, y:Top) : true
      (x:Nums, y:Nums) : overlap?(values(x), values(y))

;Return true if y is one of the types that is covered
;by the argument x.
defn covered? (y:Int, x:Arg) -> True|False :
  match(x) :
    (x:Top) : true
    (x:Nums) : contains?(values(x), y)

;Return true if a is as specific or more specific than b.
defn subbranch? (a:Branch, b:Branch) -> True|False :
  fatal("Mismatched lengths") when length(args(a)) != length(args(b))
  val c = compare-branch(a, b)
  c == 0 or c == -1

;Returns true if a is strictly more specific than b.
;Returns false otherwise.
;Used in topological ordering to discover ambiguous branches.
defn strict-subbranch? (a:Branch, b:Branch) -> True|False :
  compare-branch(a,b) == -1

;Given the concrete ids of the types of a set of arguments,
;return true if they are allowed to enter the given branch.
defn case? (case:Tuple<Int>, b:Branch) -> True|False :
  fatal("Mismatched lengths") when length(case) != length(args(b))
  all?(covered?, case, args(b))

;Fast implementation to check whether:
;  compare-arg(x, union(ys)) == -1/0
defn subarg-of-union? (x:Arg, ys:Seqable<Arg>) -> True|False :
  label<True|False> return :
    val nums = for y in ys seq :
      match(y:Top) : return(true)
      else : values(y as Nums)
    match(x:Nums) :
      subset-of-union?(values(x), nums)

;============================================================
;=============== Union and Intersection =====================
;============================================================

public defn union (args:Seqable<Arg>) -> Arg :  
  label<Arg> return :
    val nums = for arg in args seq :
      match(arg:Top) : return(arg)
      else : values(arg as Nums)
    if empty?(nums) : Nums([])
    else : Nums $ to-tuple $ union(nums)

public defn intersect (args:Seqable<Arg>) -> Arg :
  val nums = seq(values, filter-by<Nums>(args))
  if empty?(nums) : Top()
  else : Nums $ to-tuple $ intersect(nums)

;============================================================
;============= Compute Instance Branches ====================
;============================================================

;Compute the set of instance branches
defn compute-instance-branches (branches:Tuple<Branch>, abstract-nums:IntSet) -> IntSet :
  ;Helper: Return true if the given argument corresponds
  ;to a single concrete type.
  defn single-concrete-arg? (a:Arg) -> True|False :
    match(a:Nums) :
      if length(values(a)) == 1 :
        val v = values(a)[0]
        not abstract-nums[v]
  ;Helper: Return true if the given branch is an
  ;instance method.
  defn instance-branch? (b:Branch) -> True|False :
    any?(single-concrete-arg?, args(b))
  ;Compute the set of instance methods
  to-intset $ for (i in 0 to false, b in branches) filter :
    instance-branch?(b)
    
;============================================================
;======================= Soln Checker =======================
;============================================================

public defn check (s:Soln, case:Tuple<Int>, table:BranchTable) -> True|False :
  val bs = branches(table)
  match(s) :
    (s:NoSoln) :
      for i in 0 to length(bs) none? :
        case?(case, bs[i])
    (s:UniqueSoln) :
      for i in 0 to length(bs) all? :
        if i == index(s) :  (case?(case, bs[i]) )
        else : subbranch?(bs[index(s)], bs[i]) or not case?(case, bs[i])
    (s:AmbSoln) :
      defn no-other-satisfying-subbranch? (i:Int) :
        for j in 0 to length(bs) none? :
          i != j and case?(case, bs[j]) and subbranch?(bs[j], bs[i])
      case?(case, bs[index1(s)]) and
      case?(case, bs[index2(s)]) and
      no-other-satisfying-subbranch?(index1(s)) and
      no-other-satisfying-subbranch?(index2(s))

public defn check-ordered (s:Soln, case:Tuple<Int>, table:BranchTable) -> True|False :
  val bs = branches(table)
  match(s) :
    (s:NoSoln) :
      for i in 0 to length(bs) none? :
        case?(case, bs[i])
    (s:UniqueSoln) :
      case?(case, bs[index(s)]) and
      for i in 0 to index(s) none? :
        case?(case, bs[i])

;============================================================
;====================== Ancestor Table ======================
;============================================================

;Returns a strict ancestor table.
defn ancestor-table (branches:Tuple<Branch>) :
  val ancestors = IntListTable<Int>()
  within (ba, bb) = overlaps(branches) :
    switch(compare-branch(branches[ba], branches[bb])) :
      -1 : add(ancestors, ba, bb)
      1 : add(ancestors, bb, ba)
      0 : false
      false : false
  ancestors

defn overlaps (return:(Int, Int) -> ?, branches:Tuple<Branch>) :
  val overlap-count = HashTable<[Int,Int],Int>(0)
  val num-coords = length(args(branches[0]))
  for coord in 0 to num-coords do :
    within (branch-a, branch-b) = sweep-line(coordinate-intervals(branches, coord)) :
      val p = [branch-a, branch-b]
      val count = update(overlap-count, {_ + 1}, p)
      return(branch-a, branch-b) when count == num-coords

defn coordinate-intervals (branches:Collection<Branch>, coord:Int) :
  val ints = Vector<IntervalEvent>()
  for (b in branches, branch-id in 0 to false) do :
    val [start,end] = match(args(b)[coord]):
      (a:Top): [INT-MIN, INT-MAX]
      (a:Nums): [xs[0], xs[n - 1]] where :
                  val xs = values(a)
                  val n = length(xs)
    add(ints, IntervalEvent(branch-id, true, start))
    add(ints, IntervalEvent(branch-id, true, end))
  qsort!(ints)
  ints

defn sweep-line (return:(Int, Int) -> ?, intervals:Collection<IntervalEvent>) :
  val id-set = HashSet<Int>()
  for e in intervals do :
    if start?(e) :
      for existing-id in id-set do :
        return(min(id(e), existing-id),
               max(id(e), existing-id))
      add(id-set, id(e))
    else :
      remove(id-set, id(e))

defstruct IntervalEvent <: Comparable<IntervalEvent> :
  id:Int
  start?:True|False
  value:Int

defmethod compare (a:IntervalEvent, b:IntervalEvent) :
  val c = compare(value(a), value(b))
  if c == 0 :
    match(start?(a), start?(b)) :
      (sa:True, sb:False) : -1
      (sa:False, sb:True) : 1
      (sa, sb) : compare(id(a), id(b))
  else : c

;============================================================
;================= Topological Solution =====================
;============================================================

defn topo-order (n:Int, ancestors:Table<Int,List<Int>>) -> Tuple<Int> :
  val list = Vector<Int>()
  val visited = Array<True|False>(n,false)
  let loop (xs:Seqable<Int> = 0 to n):
    for x in xs do :
      if not visited[x] :
        visited[x] = true
        loop(ancestors[x])
        add(list, x)
  reverse!(list)
  to-tuple(list)

defn topo-soln (input-solns:Tuple<Soln>, btable:BranchTable) -> Soln :
  ;If any solution is an instance method, then consider
  ;branches only amongst instance methods.
  val instance-solns = for s in input-solns filter :
    instance-branches(btable)[index(s as UniqueSoln)]
  val solns = input-solns when empty?(instance-solns)
         else to-tuple(instance-solns)

  ;Get the ids from the soln ids
  val soln-ids = map(index{_ as UniqueSoln}, solns)

  ;Get the first id, and the rest id
  val id0 = soln-ids[0]
  val rest-ids = soln-ids[1 to false]

  ;Discover whether any of the branches is not a strict
  ;subbranch of branch0.
  val amb-id = for b in rest-ids find :
    not strict-subbranch?(branches(btable)[id0], branches(btable)[b])
  match(amb-id:Int) : 
    AmbSoln(id0, amb-id)
  else :
    UniqueSoln(id0)

;============================================================
;================== Argument Collapsing =====================
;============================================================

;Helper algorithm used during creation of the trie.
;Suppose we are forming a trie out of N branches, each taking M arguments.
;This algorithm bundles groups of types together such that if two types
;always appear together, they can be represented simply using a single group.
;TOP is regarded as a distinct type. 
;
;Input:
;- args: The i'th argument we are currently inspecting for each branch.
;  Will have length N.
;
;Returns [groups, types-in-groups]:
;- groups: For each branch, the groups that it accepts as arguments.
;  Will have length N. Note in groups, the marker 'Top' now means "the
;  rest of the types".
;- types-in-groups: For each group, the types that are within each group.

defn collapse-args (args:Tuple<Arg>, abstract:IntSet) -> [Tuple<Tuple<Int|Top>>, Vector<List<Int>>] :
  ;Compute all the branches that each Arg is included in.
  val included-in-branches = IntListTable<Int>()
  for (arg in args, branch-id in 0 to false) do :
    match(arg:Nums) :
      for x in values(arg) do:
        if not abstract[x] :
          add(included-in-branches, x, branch-id)

  ;Compute the values that should be in each group.
  val grouped-table = ListTable<List<Int>,Int>()
  for [x,branches] in pairs(included-in-branches) do :
    add(grouped-table, branches, x)

  ;Compute group ids (densely indexed), and mapping from group id to values.
  val group-ids = HashTable<List<Int>,Int>()
  val group-values = Vector<List<Int>>()
  for ([bs, xs] in pairs(grouped-table), i in 0 to false) do :
    group-ids[bs] = i
    group-values[i] = xs

  ;Compute the branches that are in each group
  val branch-groups = IntListTable<Int>()
  for [bs,g] in pairs(group-ids) do :
    for b in bs do :
      add(branch-groups, b, g)

  ;Compute the new mapped arguments
  val args* = to-tuple $
    for (arg in args, b in 0 to false) seq :
      match(arg) :
        (arg:Nums) : to-tuple(branch-groups[b])
        (arg:Top) : to-tuple(cat([Top()], 0 to length(group-values)))

  ;Return new mapped arguments and values for each group
  [args*, group-values]

;============================================================
;====================== Compute Trie ========================
;============================================================

defn form-trie<T> (branches:Tuple<Branch>,
                   targets:Tuple<Soln>,
                   abstract-nums:IntSet,
                   Node: (Int, Tuple<KeyValue<Int|Top,T>>, Vector<List<Int>>) -> T,
                   Leaf: Tuple<Soln> -> T) :
  defn form (depth:Int, branches:Tuple<List<Arg>>, targets:Tuple<Soln>) -> T :
    if empty?(branches[0]) :
      Leaf(targets)
    else :
      ;Construct the dispatch direction for each group.
      ;Each entry, g => (args => soln ...), means that for group g, we need to
      ;dispatch to the trie formed from the corresponding entries.
      ;The entries are sorted according to the incoming branch order.
      val table = ListTable<Int|Top, KeyValue<List<Arg>,Soln>>()
      val [groups, group-values] = collapse-args(map(head,branches), abstract-nums)      
      for (group in in-reverse(groups), branch in in-reverse(branches), target in in-reverse(targets)) do :
        for v in group do :
          add(table, v, tail(branch) => target)
          
      val trie-entries = to-tuple $
        for [v, entries-list] in pairs(table) seq :
          val entries = to-tuple(entries-list)
          v => form(depth + 1, map(key,entries), map(value,entries))
      Node(depth, trie-entries, group-values)
  form(0, branch-lists, targets) where :
    val branch-lists = map(to-list{args(_)}, branches)

;============================================================
;============== Compute Dag from Branch Table ===============
;============================================================

defn compute-dag (b-table:BranchTable, branch-order:Collection<Int>, collapse-solns:Tuple<Soln> -> Soln) -> Dag :
  ;Form DAG entries
  val dag-table = HashTable<DagEntry,Int>(dag-hash, dag-equal?)
  val dag-entries = Vector<DagEntry|False>()
  defn dag-index (e:DagEntry) :
    if not key?(dag-table,e) :
      add(dag-entries, e)
      dag-table[e] = length(dag-entries) - 1
    dag-table[e]

  ;Form a dag entry
  defn make-dag-entry (depth:Int, entries:Tuple<KeyValue<Int|Top, Int|Soln>>, group-values:Vector<List<Int>>) -> Int|Soln :
    val dag-entries = Vector<KeyValue<Nums,Int|Soln>>()
    var default-entry:Int|Soln = NoSoln()
    within (keys, v) = merge-keys(entries) :
      if any?({_ is Top}, keys) : default-entry = v
      else : add(dag-entries, Nums(qsort(seq-cat({group-values[_ as Int]}, keys))) => v)
    if empty?(dag-entries) : default-entry
    else : dag-index(DagEntry(depth, to-tuple(dag-entries), default-entry))

  ;Create all dag entries
  val branches* = to-tuple(seq({branches(b-table)[_]}, branch-order))
  val targets* = to-tuple(seq(UniqueSoln, branch-order))
  add(dag-entries, false)
  match(form-trie<Int|Soln>(branches*, targets*, abstract-nums(b-table), make-dag-entry, collapse-solns)) :
    (s:Soln) :
      EmptyDag(s)
    (s:Int) :
      ;Return Dag datastructure
      dag-entries[0] = pop(dag-entries)
      val depth = length(args(branches(b-table)[0]))
      Dag(depth, to-tuple(dag-entries) as Tuple<DagEntry>)

;============================================================
;======================= DAG Utilities ======================
;============================================================

;------------------------------------------------------------
;------------------ Hashing and Equality --------------------
;------------------------------------------------------------

defn dag-equal? (a:DagEntry, b:DagEntry) :
  if depth(a) == depth(b) and default(a) == default(b) and length(entries(a)) == length(entries(b)) :
    val b-set = to-hashset<KeyValue<Nums,Int|Soln>>(entries(b))
    all?({b-set[_]}, entries(a))

defn dag-hash (e:DagEntry) :
  hash $ [depth(e), sum(seq(hash, entries(e))), default(e)]

;------------------------------------------------------------
;------------------------ Printer ---------------------------
;------------------------------------------------------------

defmethod print (o:OutputStream, dag:Dag) :
  val o2 = IndentedStream(o)
  print(o, "Dag:")
  lnprint(o2, "depth: %_" % [depth(dag)])
  for (e in entries(dag), i in 0 to false) do :
    lnprint(o2, "%_) %_" % [i, e])

defmethod print (o:OutputStream, e:DagEntry) :
  val o2 = IndentedStream(o)
  print(o, "DagEntry:")
  lnprint(o2, "depth: %_" % [depth(e)])
  for [n, s] in pairs(entries(e)) do :
    lnprint(o2, "case %_ : %_" % [n, s])
  if default(e) is-not NoSoln :
    lnprint(o2, "else : %_" % [default(e)])

;============================================================
;====================== DAG Checker =========================
;============================================================

defn all-cases (dag:Dag) :
  val case = Array<Int>(depth(dag), 0)
  generate<KeyValue<Tuple<Int>,Soln>> :
    let visit (s:Int|Soln = 0) :
      match(s) :
        (s:Soln) :
          yield(to-tuple(case) => s)
        (s:Int) :
          val dag-e = entries(dag)[s]
          for e in entries(dag-e) do :
            for v in values(key(e)) do :
              case[depth(dag-e)] = v
              visit(value(e))
          case[depth(dag-e)] = -1
          visit(default(dag-e))

;============================================================
;================== Computing the Dispatch Dag ==============
;============================================================

public defn compute-dispatch-dag (btable:BranchTable, topological?:True|False) -> Dag :
  ;Canonicalize table
  val num-branches = length(branches(btable))
  ;Empty branch table
  if empty?(branches(btable)) :
    EmptyDag(NoSoln())
  ;Topological branch table
  else if topological? :
    val ancestors = ancestor-table(branches(btable))
    val topo-order = topo-order(num-branches,ancestors)
    compute-dag(btable, topo-order, topo-soln{_, btable})
  ;Ordered branch table
  else :
    defn first-soln (solns:Tuple<Soln>) : solns[0]
    compute-dag(btable, 0 to num-branches, first-soln)

;============================================================
;=================== Resolving a Dag ========================
;============================================================

public defn all-solns (dag:Dag, case:Tuple<Arg>, include-amb?:True|False) -> Tuple<Soln> :
  ;Track whether a dagentry has already been processed
  val visited = Array<True|False>(length(entries(dag)), false)

  ;Accumulate all the reachable solutions
  val solns = HashSet<Soln>()

  ;Test whether the default case is reachable
  defn default-reachable? (e:DagEntry, arg:Arg) :
    not subarg-of-union?(arg, seq(key, entries(e)))

  ;Algorithm
  defn* loop (e:DagEntry) :
    val arg = case[depth(e)]    
    for e in entries(e) do :
      loop(value(e)) when overlap?(key(e), arg)
    if default-reachable?(e, arg) :
      loop(default(e))

  defn* loop (e:Int|Soln) :
    match(e) :
      (soln:NoSoln) :
        false
      (soln:AmbSoln) :
        add(solns, soln) when include-amb?
      (soln:Soln) :
        add(solns, soln)
      (dag-index:Int) :
        if not visited[dag-index] :
          visited[dag-index] = true
          loop(entries(dag)[dag-index])

  ;Start from beginning
  loop(0)
  to-tuple(solns)

;============================================================
;======================= Utilities ==========================
;============================================================

defn ListTable<K,V> () :
  HashTable<K,List<V>>(List())
defn IntListTable<V> () :
  IntTable<List<V>>(List())
defn add<?K,?V> (table:Table<?K,List<?V>>, k:K, v:V) :
  update(table, cons{v, _}, k)

defn pairs<?K,?V> (xs:Seqable<KeyValue<?K,?V>>) -> Seq<[K,V]> :
  for x in xs seq :
    [key(x), value(x)]

defn merge-keys<?K,?V> (return:(Collection<K>, V) -> ?, kvs:Seqable<KeyValue<?K,?V>>) :
  val table = ListTable<V,K>()
  for [k, v] in pairs(kvs) do:
    add(table, v, k)
  for [v, ks] in pairs(table) do:
    return(ks,v)
