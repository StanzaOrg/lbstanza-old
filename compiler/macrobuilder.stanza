defpackage stz/macrobuilder :
  import core
  import collections
  import stz/macroexpander
  import stz/params

;This is a small utility for calling the Stanza compiler to build a macro target.
;
;The StanzaMacroexpander uses this utility when it encounters a macro plugin that may not
;have been built yet.

public defn MacroBuilder () -> MacroSystem :
  new MacroSystem :
    defmethod compile-build-target (this, target:Symbol) :
      val exe = system-filepath(StanzaCompiler)
      val code = call-system $ [exe, "build", to-string(target)]
      if code != 0 :
        throw(MacroBuildError(target))

;Occurs when the compilation fails.
public defstruct MacroBuildError <: Exception :
  target:Symbol

defmethod print (o:OutputStream, e:MacroBuildError) :
  print(o, "Errors occurred during compilation of build target '%~'." % [target(e)])