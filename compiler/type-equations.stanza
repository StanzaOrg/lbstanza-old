defpackage stz/type-equations :
  import core
  import collections
  import stz/types
  import stz/type-variance
  import stz/type-locs
  import stz/type-formatter
  import stz/printing-utils
  import stz/type-instfns
  import stz/type-capside
  import stz/primitives
  import stz/type-fargs

;<doc>=======================================================
;============================================================
;============================================================

Operations:

- Calling a function. Check whether a function can be
  called in the given scenario.

- Calling a function or expression. What are the injected
  expected types into each argument expression?

- Passing an argument to a function. Check whether one type
  is a subtype of another.

- Casting an expression to some type. Is the cast allowed?

- Matching an expression. Is the match type allowed?

- Defining a method. Is this an appropriate method for
  a multi?

- Injecting an expected type to an anonymous function. Retrieve
  the type of the function argument(s) from the injected type.

- Injecting an expected type to a tuple. What is the resulting
  injected types to each of the tuple elements?

- Destructuring a tuple. Given the type of an expression,
  what are the types of the destructured tuple elements.

- Determine whether a mixture of overloaded functions are
  disjoint and hence allowed to be mixed.

- Referencing an overloaded function. Which ones are
  appropriate. 

- Analyzing an anonymous function: The anonymous function
  is expected to be of type T. Then, what is the expected
  type of the body of the function?  

;============================================================
;=======================================================<doc>

;Represents a type equation.
public deftype TypeEqn

;Capture all the given variables to satisfy a <: b.
public defstruct CaptureAllEqn <: TypeEqn :
  ns:Tuple<Int>
  side:CapSide
  a:Tuple<Type>
  b:Tuple<Type>

;Capture all the LoStanza variables to satisfy a <: b.
public defstruct LSCaptureAllEqn <: TypeEqn :
  ns:Tuple<Int>
  side:CapSide
  a:Tuple<LSType>
  b:Tuple<LSType>  

;Capture a specific variable n, to satisfy a <: b.
public defstruct CaptureEqn <: TypeEqn :
  n:Int
  side:CapSide
  variance:Variance
  a:Tuple<Type>
  b:Tuple<Type>

;Capture a specific variable n, to satisfy a <: b.
public defstruct LSCaptureEqn <: TypeEqn :
  n:Int
  side:CapSide
  variance:Variance
  a:Tuple<LSType>
  b:Tuple<LSType>

;Infer a variables n.
public defstruct InferEqn <: TypeEqn :
  n:Int
  a:Type|InferLoc
  b:Type|InferLoc

;Infer a variable 'n' from LoStanza. 
public defstruct LSInferEqn <: TypeEqn :
  n:Int
  a:LSType
  b:InferLoc

;Infer the type of an overloaded function in expression
;position.
;- functions: These are the known types of the functions
;  that are overloaded.
;- y: This is the expected type for this expression.
;- instantiations: Starts as None. When y is solved, functions
;  are instantiated and the instantiation information is
;  saved here.
;- arrows: The instantiated arrow types are saved here. 
public defstruct OverloadExpEqn <: TypeEqn :
  n:Int
  sel:Int
  inst:Int
  functions:Tuple<KeyValue<Int,TFunction>>,
  instantiations:Maybe<Tuple<InstFn>> with: (updater => sub-instantiations)
  y:Type

;Infer the type of an overloaded function in addr(...) expression.
;- xs: These are the known types of the functions
;  that are overloaded.
;- y: This is the expected type for this expression.
public defstruct LSOverloadAddrEqn <: TypeEqn :
  n:Int
  sel:Int
  xs:Tuple<FnT>
  y:LSType

;Infer the type of an overloaded function in call
;position.
public defstruct OverloadCallEqn <: TypeEqn :
  n:Int
  sel:Int
  xs:Tuple<TArrow>
  args:Tuple<Type>

;Infer the type of an overloaded function in LoStanza call
;position.
public defstruct LSOverloadCallEqn <: TypeEqn :
  n:Int
  sel:Int
  xs:Tuple<FnT>
  args:Tuple<LSType>

;Select an appropriate multi for the given method.
public defstruct SelectMultiEqn <: TypeEqn :
  n:Int
  sel:Int
  xs:Tuple<TArrow>
  a1:Tuple<FArg<Type>>
  a2:Type

;Perform the given substitutions in the given
;type. 
public defstruct SubEqn <: TypeEqn :
  n:Int
  x:Type|LSType
  ys:Tuple<KeyValue<Int,Type>>

;Indicate that n :> x.
public defstruct SuperEqn <: TypeEqn :
  n:Int
  x:Type

;Indicate that n <: x.
public defstruct SubBoundsEqn <: TypeEqn :
  n:Int
  x:Type

;Directly indicate the type of a variable.
public defstruct EqualEqn <: TypeEqn :
  n:Int
  type:Type

;Directly indicate the type of a LoStanza variable.
public defstruct LSEqualEqn <: TypeEqn :
  n:Int
  type:LSType

;Directly indicate a variable is unsatisfiable.
public defstruct UnsatisfiableEqn <: TypeEqn :
  n:Int

;Compute the result type of a primitive operation.
public defstruct LSPrimEqn <: TypeEqn :
  n:Int
  op:LSPrimitive
  args:Tuple<LSType>

;Compute the result of a HiStanza primitive operation.
public defstruct PrimEqn <: TypeEqn :
  n:Int
  op:Primitive
  args:Tuple<Type>

;Perform some type operation on a LoStanza type.
public defstruct TypeOperationEqn <: TypeEqn :
  n:Int
  operation:TypeOperation
  type:LSType

;Represent the various type operations we can
;perform on a type.
public deftype TypeOperation

;Given T, return type of T.name.
;Used to compute result of a Field expression.
public defstruct GetField <: TypeOperation :
  name:Symbol

;Given ptr<T>, return T.
;Used to compute expected type of an addr(...) expression.
public defstruct Deptr <: TypeOperation

;Given ptr<T>, or rest<T>, return T.
;Used to compute the result of a slot access expression.
public defstruct Deslot <: TypeOperation

;Given ref<T> or ptr<T>, return T.
;Used to compute result of a Deref expression.
public defstruct DerefDeptr <: TypeOperation

;Given ref<T>, return T (HiStanza).
;Used to compute expected type of fn(...) expression.
public defstruct ToHistanza <: TypeOperation

;Given T, return ptr<T>.
;Used to compute result of addr(...) expression.
public defstruct PtrTo <: TypeOperation  

;============================================================
;======================= Accessors ==========================
;============================================================

;Retrieve the instantiated Tuple<TArrow> types.
public defn xs (e:OverloadExpEqn) -> Tuple<TArrow> :
  val insts = value!(instantiations(e))
  map(type, insts)

;============================================================
;=================== Printers ===============================
;============================================================

defmethod print (o:OutputStream, e:CaptureAllEqn) :
  print(o, "capture(%,) %_ <: %_" % [ns(e), a(e), b(e)])

defmethod print (o:OutputStream, e:CaptureEqn) :
  print(o, "capture(%_) (%_) %_ <: %_" % [n(e), variance(e), a(e), b(e)])

defmethod print (o:OutputStream, e:InferEqn) :
  print(o, "infer(%_) %_ <: %_" % [n(e), a(e), b(e)])

defmethod print (o:OutputStream, e:OverloadExpEqn) :
  print(o, "overload(%_, sel: %_, inst: %_) (%,) <: %_" % [n(e), sel(e), inst(e), functions(e), y(e)])

defmethod print (o:OutputStream, e:OverloadCallEqn) :
  print(o, "overload(%_, sel: %_) call (%,) with %_" % [n(e), sel(e), xs(e), args(e)])

defmethod print (o:OutputStream, e:SubEqn) :
  print(o, "sub %_ = %_ [%,]" % [n(e), x(e), ys(e)])

defmethod print (o:OutputStream, e:SuperEqn) :
  print(o, "var %_ :> %_" % [n(e), x(e)])

defmethod print (o:OutputStream, e:EqualEqn) :
  print(o, "var %_ = %_" % [n(e), type(e)])

defmethod print (o:OutputStream, op:TypeOperation) :
  print{o, _} $ match(op) :
    (op:GetField) : "GetField(%_)" % [name(op)]
    (op:Deptr) : "Deptr"
    (op:DerefDeptr) : "DerefDeptr"
    (op:ToHistanza) : "ToHistanza"
    (op:PtrTo) : "PtrTo"
    (op:Deslot) : "Deslot"

;============================================================
;================== Formatting ==============================
;============================================================

public defn format (f:TypeFormatter, e:TypeEqn) :
  defn fmt (t:Type) : format(f,t)
  defn fmt (t:LSType) : format(f,t)
  defn fmt (n:Int) : format(f,TVar(n,false))
  defn fmt (ns:Tuple<Int>) : seq(fmt,ns)
  defn fmt (l:InferLoc) : l
  defn fmt (t:Tuple<Type>) : seq(fmt,t)
  defn fmt (t:Tuple<LSType>) : seq(fmt,t)
  defn fmt (e:KeyValue<Int,Type>) : fmt(key(e)) => fmt(value(e))
  defn fmt (e:Tuple<KeyValue<Int,Type>>) : seq(fmt,e)
  defn fmt (e:Tuple<FArg<Type>>) : e
  match(e) :
    (e:CaptureAllEqn) : "capture(%,) %_ (%,) <: (%,)" % [fmt(ns(e)), side(e), fmt(a(e)), fmt(b(e))]
    (e:LSCaptureAllEqn) : "ls-capture(%,) %_ (%,) <: (%,)" % [fmt(ns(e)), side(e), fmt(a(e)), fmt(b(e))]
    (e:CaptureEqn) : "capture(%_) (%_) (%,) <: (%,)" % [fmt(n(e)), variance(e), fmt(a(e)), fmt(b(e))]
    (e:LSCaptureEqn) : "ls-capture(%_) (%_) (%,) <: (%,)" % [fmt(n(e)), variance(e), fmt(a(e)), fmt(b(e))]
    (e:InferEqn) : "infer(%_) %_ <: %_" % [fmt(n(e)), fmt(a(e)), fmt(b(e))]
    (e:LSInferEqn) : "ls-infer(%_) %_ <: %_" % [fmt(n(e)), fmt(a(e)), fmt(b(e))]
    (e:OverloadExpEqn) :
      val fs = fmt(functions(e))
      "overload(%_, sel: %_, inst: %_) (%_) <: %_" % [fmt(n(e)), sel(e), inst(e), indented-field-list(fs), fmt(y(e))]
    (e:LSOverloadAddrEqn) :
      val fs = seq(fmt, xs(e))
      "overload(%_, sel: %_) (%_) <: %_" % [fmt(n(e)), sel(e), indented-field-list(fs), fmt(y(e))]
    (e:OverloadCallEqn) :
      "overload(%_, sel: %_) call (%,) with (%,)" % [fmt(n(e)), sel(e), fmt(xs(e)), fmt(args(e))]
    (e:LSOverloadCallEqn) :
      val fs = seq(fmt, xs(e))
      "ls-overload(%_, sel: %_) call (%_) with (%,)" % [fmt(n(e)), sel(e), indented-field-list(fs), seq(fmt,args(e))]
    (e:SelectMultiEqn) :
      val fs = seq(fmt, xs(e))
      "select-multi(%_, sel:%_) %_ -> %_ <: (%_)" % [fmt(n(e)), sel(e), fmt(a1(e)), fmt(a2(e)), indented-field-list(fs)]
    (e:SubEqn) : "sub %_ = %_ [%,]" % [fmt(n(e)), fmt(x(e)), fmt(ys(e))]
    (e:SuperEqn) : "var %_ :> %_" % [fmt(n(e)), fmt(x(e))]
    (e:SubBoundsEqn) : "var %_ <: %_" % [fmt(n(e)), fmt(x(e))]
    (e:EqualEqn) : "var %_ = %_" % [fmt(n(e)), fmt(type(e))]
    (e:LSEqualEqn) : "var %_ = %_" % [fmt(n(e)), fmt(type(e))]
    (e:UnsatisfiableEqn) : "var %_ is unsatisfiable" % [fmt(n(e))]
    (e:LSPrimEqn) : "var %_ = %_(%,)" % [fmt(n(e)), op(e), seq(fmt,args(e))]
    (e:PrimEqn) : "var %_ = %_(%,)" % [fmt(n(e)), op(e), seq(fmt,args(e))]
    (e:TypeOperationEqn) : "var %_ = %_(%_)" % [fmt(n(e)), operation(e), fmt(type(e))]
