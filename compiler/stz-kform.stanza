defpackage stz/kform :
   import core
   import verse
   import stz/kl-ir
   import stz/utils
   import stz/lexer
   import stz/padder
   import stz/tgt-ir with: (prefix => tgt-)
   import stz/tgt with: (prefix => tgt-)


;                           Driver
;                           ======

public defn compile (prog:KProg, filename:String) :
   val gt = global-table(prog)
   val tt = type-table(prog)
   
   headline("Transform")
   val p2 = transform(prog, gt)
   println(p2)

   headline("Compile to Tgt IR")
   val p3 = compile(p2, tt, gt)
   println(p3)
   
   tgt-compile(p3, filename)
   


;                       Stanza Machine State
;                       ====================

val HEAP-PTR-LBL = fresh-id()
val HEAP-LIM-LBL = fresh-id()
var EXTEND-HEAP-LBL : False|Int = false

val REF-TAG = 1
val MARKER-TAG = 2
val VOID-TAG = (-1 << 3) + MARKER-TAG


;                     Type Analysis
;                     =============

;     Type Table Interface
;     --------------------
defclass TypeTable
defmulti expand (tt:TypeTable, t:KLSType) -> List<tgt-TgtType> 
defmulti pad+expand (tt:TypeTable, t:KLSType) -> List<tgt-TgtType> 
defmulti offsets (tt:TypeTable, t:KLSType) -> List<Int>      
defmulti size (tt:TypeTable, t:KLSType) -> Int               
defmulti alignment (tt:TypeTable, t:KLSType) -> Int
defmulti field-offset (tt:TypeTable, f:Int) -> Int           
defmulti field-index (tt:TypeTable, f:Int) -> Int            
defmulti field-type (tt:TypeTable, f:Int) -> KLSType         
defmulti rfield (tt:TypeTable, n:Int) -> Int                 
defmulti tag (tt:TypeTable, n:Int) -> Int                    

;     Analysis
;     --------
defn type-table (prog:KProg) :
   ;     Search through all types
   ;     ------------------------
   val lstypes = HashTable<Int, KLSDefType>({_})
   val types = HashTable<Int, KDefType>({_})
   for c in comms(prog) do :
      match(c) :
         (c:KDefType) :
            error("Untested.")
            types[n(c)] = c
         (c:KLSDefType) :
            error("Untested.")
            lstypes[n(c)] = c
         (c) : false

   ;     Compute Expansions
   ;     ------------------
   val struct-expansions = HashTable<Int, List<tgt-TgtType>>({_})
   defn expand (t:KLSType) :
      defn expand-struct (n:Int) :
         error("Untested.")
         for f in fields(lstypes[n]) map-append :
            expand(type(f))
      match(t) :
         (t:KByteT) : list(tgt-BYTE-TYPE)
         (t:KIntT) : list(tgt-INT-TYPE)
         (t:KLongT) : list(tgt-LONG-TYPE)
         (t:KFloatT) : list(tgt-FLOAT-TYPE)
         (t:KDoubleT) : list(tgt-DOUBLE-TYPE)
         (t:KPtrT) : list(tgt-LONG-TYPE)
         (t:KRefT) : list(tgt-REF-TYPE)
         (t:KStructT) :
            if not key?(struct-expansions, n(t)) :
               struct-expansions[n(t)] = expand-struct(n(t))
            struct-expansions[n(t)]
         (t) : error("No expansion for ~." % [t])

   ;     Compute Field Types and Indices
   ;     -------------------------------
   val field-structs = HashTable<Int,Int>({_})
   val field-indices = HashTable<Int,Int>({_})
   val field-types = HashTable<Int,KLSType>({_})
   for def in values(lstypes) do :
      error("Untested.")
      for (f in fields(def), i in 0 to false) do :
         field-structs[n(f)] = n(def)
         field-indices[n(f)] = i
         field-types[n(f)] = type(f)
      if rfield(def) != false :
         val f = rfield(def) as StructField
         field-structs[n(f)] = n(def)
         field-indices[n(f)] = length(fields(def))
         field-types[n(f)] = type(f)

   ;     Compute Alignments, Padding, and Size
   ;     -------------------------------------
   val pad+expansions = HashTable<Int, List<tgt-TgtType>>({_})
   val struct-alignments = HashTable<Int, Int>({_})
   val type-sizes = HashTable<Int,Int>({_})
   val field-offsets = HashTable<Int,Int>({_})

   defn type-size (t:KLSType) :
      error("Untested.")
      match(t) :
         (t:KByteT) : 1
         (t:KIntT) : 4
         (t:KLongT) : 8
         (t:KFloatT) : 4
         (t:KDoubleT) : 8
         (t:KPtrT) : 8
         (t:KRefT) : 8
         (t:KUnknownT) : error("No size.")
         (t:KFnT) : error("No size.")
         (t:KStructT) :
            compute-struct(n(t)) when not key?(type-sizes, n(t))
            type-sizes[n(t)]

   defn type-alignment (t:KLSType) :
      error("Untested.")
      match(t) :
         (t:KStructT) :
            compute-struct(n(t)) when not key?(struct-alignments, n(t))
            struct-alignments[n(t)]
         (t) : type-size(t)

   defn pad+expand (t:KLSType) :
      error("Untested.")
      match(t) :
         (t:KStructT) :
            pad+expansions[n(t)] when not key?(pad+expansions, n(t))
            pad+expansions[n(t)]
         (t) : expand(t)

   defn compute-struct (sn:Int) :
      error("Untested.")
      ;Pad struct fields
      val def = lstypes[sn]
      val items = for f in fields(def) stream :
         Item(type-alignment(type(f)), n(f))         
      val [items*, aln] = match(rfield(def)) :
         (rf:False) : pad(items, 1)
         (rf:StructField) : pad(items, type-alignment(type(rf)))

      ;Compute struct alignment, field offsets, type sizes, and padded expansions
      var accum = 0
      val expansion = Vector<tgt-TgtType>()
      for x in items* do :
         match(x) :
            (x:Item) :
               val f = data(x) as Int
               val ft = field-types[f]
               field-offsets[f] = accum
               do(add{expansion, _}, pad+expand(ft))
               accum = accum + type-size(ft)
            (x:Padding) :
               switch {size(x) == _} :
                  1 : add(expansion, tgt-BYTE-TYPE)
                  4 : add(expansion, tgt-INT-TYPE)
               accum = accum + size(x)
      type-sizes[sn] = accum         
      pad+expansions[sn] = to-list(expansion)
      struct-alignments[sn] = aln
      
   do(compute-struct, keys(lstypes))         

   ;     Compute Struct Tags
   ;     -------------------
   val tag-table = HashTable<Int,Int>({_})
   val tag-types = Vector<Int>()
   for def in values(lstypes) do :
      error("Untested.")
      tag-table[n(def)] = length(tag-types)
      add(tag-types, n(def))
   for def in values(types) do :
      error("Untested.")
      tag-table[n(def)] = length(tag-types)
      add(tag-types, n(def))

   
   ;     Return Table
   ;     ------------
   new TypeTable :
      defmethod expand (this, t:KLSType) :
         expand(t)
      defmethod pad+expand (this, t:KLSType) :
         pad+expand(t)
      defmethod offsets (this, t:KLSType) :
         match(t) :
            (t:KStructT) :
               for f in fields(lstypes[n(t)]) map :
                  field-offset(this, n(f))
            (t) : list(0)
      defmethod size (this, t:KLSType) :
         type-size(t)
      defmethod alignment (this, t:KLSType) :
         type-alignment(t)
      defmethod field-offset (this, f:Int) :
         field-offsets[f]
      defmethod field-type (this, f:Int) :
         field-types[f]
      defmethod field-index (this, f:Int) :
         field-indices[f]
      defmethod rfield (this, s:Int) :
         n(rfield(lstypes[s]) as StructField)
      defmethod tag (this, n:Int) :
         tag-table[n]


;                   Command Buffer
;                   ==============

;     Emission of DefIns Structures
;     -----------------------------
var DEF-INS-VECTOR : False|Vector<tgt-DefIns> = false
defn emit (i:tgt-DefIns) : add(DEF-INS-VECTOR as Vector<tgt-DefIns>, i)


;     Emission of Ins Structures
;     --------------------------
var INSTRUCTION-VECTOR : False|Vector<tgt-Ins> = false
defn emit (i:tgt-Ins) : add(INSTRUCTION-VECTOR as Vector<tgt-Ins>, i)
defn args (xs:List<tgt-Var>) : emit(tgt-ArgsIns(xs))
defn set (x:tgt-Var, y:tgt-Imm) : emit(tgt-SetIns(x, y))
defn conv (x:tgt-Var, y:tgt-Imm, s?:True|False) : emit(tgt-ConvertIns(x, y, s?))
defn add (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-ADD-OP, y, z))
defn mul (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-MUL-OP, y, z))
defn shl (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-SHL-OP, y, z))
defn bit-and (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-AND-OP, y, z))
defn load (x:tgt-Var, y:tgt-Imm, o:Int) : emit(tgt-Load(x, y, o))
defn store (x:tgt-Imm, y:tgt-Imm, o:Int) : emit(tgt-Store(x, y, o))
defn addr (x:tgt-Var, y:tgt-Imm, o:Int) : emit(tgt-Addr(x, y, o))
defn call (a:Int, xs:List<tgt-Var>, f:tgt-Imm, ys:List<tgt-Imm>) : emit(tgt-Call(a, xs, f, ys))
defn ffi-call (xs:List<tgt-Var>, f:tgt-Imm, ys:List<tgt-Imm>) : emit(tgt-CallC(xs, f, ys))
defn tcall (a:Int, f:tgt-Imm, ys:List<tgt-Imm>) : emit(tgt-TCall(a, f, ys))
defn return (xs:List<tgt-Imm>) : emit(tgt-Return(xs))
defn tlabel (n:Int) : emit(tgt-Label(n))
defn goto (n:Int) : emit(tgt-Goto(n))
defn jne (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-NE-OP, x, y))
defn jeq (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-EQ-OP, x, y))
defn jlt (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-LT-OP, x, y))
defn jle (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-LE-OP, x, y))
defn jult (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-ULT-OP, x, y))
defn jule (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-ULE-OP, x, y))
defn byte (v:Int) : tgt-ByteImm(v)
defn int (v:Int) : tgt-IntImm(v)
defn long (v:Int) : tgt-LongImm(v)
defn mem (n:Int) : tgt-Mem(n)
defn xmem (name:Symbol) : tgt-ExMem(name)

;     Heap Instructions
;     -----------------
defn heap-ptr () :
   error("Untested.")
   val p = make-var(tgt-LONG-TYPE)
   load(p, mem(HEAP-PTR-LBL), 0)
   p

defn heap-lim () :
   error("Untested.")
   val p = make-var(tgt-LONG-TYPE)
   load(p, mem(HEAP-LIM-LBL), 0)
   p
   
defn extend-heap (sz:tgt-Imm) :
   error("Untested.")
   val endL = fresh-id()
   val p = heap-ptr()
   add(p, p, sz)
   jule(endL, p, heap-lim())
   call(1, List(), mem(EXTEND-HEAP-LBL as Int), list(sz))
   tlabel(endL)

defn extend-heap (sz:Int) :
   error("Untested.")
   extend-heap(long(sz))

defn inc-heap-ptr (sz:tgt-Imm) :
   error("Untested.")
   val p = heap-ptr()
   add(p, p, sz)
   store(mem(HEAP-PTR-LBL), p, 0)

defn inc-heap-ptr (sz:Int) :
   error("Untested.")
   inc-heap-ptr(long(sz))

;     Compound Value Instructions
;     ---------------------------
defn value-lbl (ins:tgt-DefIns) :
   val n = fresh-id()
   emit(tgt-TLabel(n))
   emit(ins)
   n   
defn float-lbl (f:Float) : value-lbl(tgt-DefFloat(f))
defn double-lbl (f:Float) : value-lbl(tgt-DefDouble(f))
defn string-lbl (s:String) : value-lbl(tgt-DefString(s))

;                  Variable Management
;                  ===================

;     Definition of VarTable
;     ----------------------
defclass VarTable <: Gettable<Int,List<tgt-Var>> &
                     Settable<Int,List<tgt-Var>>
defmulti make-var (v:VarTable, t:tgt-TgtType) -> tgt-Var
defmulti types (v:VarTable, n:Int) -> List<tgt-TgtType>
defmulti defs (v:VarTable) -> Vector<tgt-DefVar>

defn VarTable () :
   val defs = Vector<tgt-DefVar>()
   val table = HashTable<Int,List<tgt-Var>>({_})
   val type-table = HashTable<Int,tgt-TgtType>({_})
   
   new VarTable :
      defmethod make-var (this, t:tgt-TgtType) :
         val n = fresh-id()
         add(defs, tgt-DefVar(n, t))
         type-table[n] = t
         tgt-Var(n)
      defmethod set (this, n:Int, vs:List<tgt-Var>) :
         table[n] = vs
      defmethod get (this, n:Int) :
         table[n]
      defmethod types (this, x:Int) :
         for v in this[x] map :
            type-table[tgt-n(v)]
      defmethod defs (this) :
         defs

defn get (v:VarTable, ns:List<Int>) :
   map-append({v[_]}, ns)
defn get (t:VarTable, v:KLSVar) :
   t[n(v)]
defn get (t:VarTable, vs:List<KLSVar>) :
   t[map(n,vs)]

defn make-vars (v:VarTable, ts:List<tgt-TgtType>) :
   for t in ts map :
      make-var(v, t)

;     Implicit VarTable
;     -----------------
var VAR-TABLE : False|VarTable = false
defn VAR-TABLE! () : VAR-TABLE as VarTable
defn make-var (t:tgt-TgtType) : make-var(VAR-TABLE!(), t)
defn make-vars (ts:List<tgt-TgtType>) : map(make-var, ts)
defn tgt-vars (n:Int) : VAR-TABLE!()[n]
defn tgt-vars (ns:List<Int>) : VAR-TABLE!()[ns]
defn tgt-vars (v:KLSVar) : VAR-TABLE!()[v]
defn tgt-vars (vs:List<KLSVar>) : VAR-TABLE!()[vs]
defn tgt-types (v:KLSVar) : types(VAR-TABLE!(), n(v))


;                    Global Table
;                    ============

defclass GlobalTable
defmulti global? (t:GlobalTable, n:Int) -> True|False
defmulti function? (t:GlobalTable, n:Int) -> True|False
defmulti function-lbl (t:GlobalTable, n:Int) -> Int|Symbol

defn global-table (prog:KProg) :
   val globals = HashTable<Int,True>({_})
   val functions = HashTable<Int,Int|Symbol>({_})
   
   for c in comms(prog) do :
      defn mark-global (n:Int) :
         globals[n] = true
      defn mark-function (n:Int, lbl:Int|Symbol) :
         globals[n] = true
         functions[n] = lbl
      
      match(c) :
         (c:KDef|KDefVar) :
            error("Untested.")
            mark-global(n(c))
         (c:KDefn) :
            error("Untested.")
            mark-function(n(c), n(c))
         (c:KDefmulti) :
            error("Untested.")
            mark-function(n(c), n(c))
         (c:KLSDef) :
            error("Untested.")
            mark-global(n(c))
         (c:KLSDefn) : mark-function(n(c), n(c))
         (c:KExtern) : mark-global(n(c))
         (c:KExternFn) : mark-function(n(c), name(c))
         (c) : false

   new GlobalTable :
      defmethod global? (this, n:Int) : key?(globals, n)
      defmethod function? (this, n:Int) : key?(functions, n)
      defmethod function-lbl (this, n:Int) : functions[n]



;               LoStanza Function Normalization
;               ===============================

defn in-reverse<?T> (xs:Vector<?T>) :
   for i in (length(xs) - 1) through 0 by -1 stream :
      xs[i]

defn normalize (comm:KLSComm) :
   defn norm-scope (c:KLSComm) :
      val def-vector = Vector<KeyValue<Int,KLSExp|KLSType>>()
      defn fresh-var (value:KLSExp) :
         val n = fresh-id()
         add(def-vector, n => value)
         KLSVar(n)

      defn norm-exp (e:KLSExp) :
         match(e) :
            (e:KLSVar) : e
            (e:KLSExp) : fresh-var(map(norm-exp, e))

      defn norm-comm (c:KLSComm) :
         match(c) :
            (c:KLSSet) :
               error("Untested.")
               KLSSet(map(norm-exp, exp(c)),
                      norm-exp(value(c)))
            (c:KLSLet) :
               error("Untested.")
               add(def-vector, n(c) => value(c))
               norm-comm(body(c))
            (c:KLSLetVar) :
               error("Untested.")
               add(def-vector, n(c) => type(c))
               norm-comm(body(c))
            (c:KLSSeq) :
               KLSSeq(norm-comm(a(c))
                      norm-scope(b(c)))
            (c:KLSIf) :
               error("Untested.")
               KLSIf(norm-exp(pred(c))
                     norm-scope(conseq(c))
                     norm-scope(alt(c)))
            (c:KLSMatch) :
               error("Untested.")
               KLSMatch(map(norm-exp, args(c)),
                        map(map{norm-scope, _}, branches(c)))
            (c) :
               map(norm-exp, c)

      var ret = norm-comm(c)
      for entry in in-reverse(def-vector) do :
         ret = match(value(entry)) :
            (v:KLSExp) : KLSLet(key(entry), v, ret)
            (t:KLSType) : KLSLetVar(key(entry), t, ret)
      ret

   ;Return result
   norm-scope(comm)


;                   LoStanza Global Access
;                   ======================

defn make-global-readers (c:KLSComm, gt:GlobalTable) :
   defn add-loc-readers (l:KLoc) -> KLoc :
      error("Untested.")
      match(l) :
         (l:Value) :
            if exp(l) not-typeof KLSVar :
               error("Not in normalized form.")
            l
         (l:Field) : Field(add-loc-readers(base(l)), n(l))
         (l) : map(add-exp-readers, l)
   defn add-exp-readers (e:KLSExp) :
      match(e) :
         (e:KLSVar) :
            if global?(gt, n(e)) : KLSRead(Value(e))
            else : e
         (e:KLSPtr) :
            error("Untested.")
            KLSPtr(add-loc-readers(exp(e)))
         (e:KLSRead) :
            error("Untested.")
            KLSRead(add-loc-readers(exp(e)))
         (e) : map(add-exp-readers, e)
   defn add-comm-readers (c:KLSComm) :
      match(c) :
         (c:KLSSet) :
            error("Untested.")
            KLSSet(add-loc-readers(exp(c))
                   add-exp-readers(value(c)))
         (c) :
            map(add-comm-readers, map(add-exp-readers, c))

   normalize(add-comm-readers(c))


;                 LoStanza Call Conversion
;                 ========================

defn convert-calls (c:KLSComm, gt:GlobalTable) :
   defn direct (f:KLSVar) :
      if function?(gt, n(f)) : n(f)
      else : f
   defn conv-comm (c:KLSComm) :
      map(conv-comm, map(conv-exp, c))
   defn conv-exp (e:KLSExp) :
      match(map(conv-exp, e)) :
         (e:KLSDo) :
            KLSWDo(length(args(e)), direct(func-var(e)), args(e), type(e))
         (e:KLSCallC) :
            KLSWCallC(direct(func-var(e)), args(e), type(e))
         (e:KLSCallS) :
            error("Convert Stanza call.")               
         (e) : e

   conv-comm(c)

;                   Transformations
;                   ===============

defn transform (prog:KProg, gt:GlobalTable) :
   val cs = for c in comms(prog) map :
      match(c) :
         (c:KLSDefn) :
            var body* = convert-calls(body(c), gt)
            body* = make-global-readers(body*, gt)
            KLSDefn(n(c), targs(c), a1(c), a2(c), args(c), body*)
         (c:KLSInit) :
            var comm* = convert-calls(comm(c), gt)
            comm* = make-global-readers(comm*, gt)
            KLSInit(comm*)
         (c) : c
   KProg(cs)      
      

;                      Compilation
;                      ===========

defn compile (prog:KProg, tt:TypeTable, gt:GlobalTable) -> tgt-Program :
   ;     Overall Compiler
   ;     ----------------
   val globals = Vector<KeyValue<Int,KLSType>>()
   
   defn compile (c:KComm) :
      match(c) :
         (c:KDef) : error("Compile KDef command.")
         (c:KDefVar) : error("Compile KDefVar command.")
         (c:KDefn) : error("Compile KDefn command.")
         (c:KDefmulti) : error("Compile KDefmulti command.")
         (c:KDefmethod) : error("Compile KDefmethod command.")
         (c:KDefType) : error("Compile KDefType command.")
         (c:KExpComm) : error("Compile KExpComm command.")
         (c:KLSDef) : add(globals, n(c) => type(c))
         (c:KLSDefn) : compile-lsdefn(c)
         (c:KLSDefType) : false
         (c:KExtern) : false
         (c:KExternFn) : false
         (c:KLSInit) : false

   ;     Global Labels Compiler
   ;     ----------------------
   defn compile-globals () :
      ;Sort all globals by alignment for packing
      sort-by-key!(globals, alignment{tt, value(_)})
      ;Pad all the items to ensure structures are aligned
      val items = for e in globals stream :
         Item(alignment(tt, value(e)), e)
      val [items*, aln] = pad(items, 1)
      ;Start emission of table
      emit(tgt-DefData())
      for x in items* do :
         match(x) :
            (x:Item) :
               val entry = data(x) as KeyValue<Int,KLSType>
               val n = key(entry)
               val type = value(entry)
               emit(tgt-TLabel(n))
               for t in pad+expand(tt, type) do :
                  emit $ switch {t == _} :
                     tgt-BYTE-TYPE : tgt-DefByte(0)
                     tgt-INT-TYPE : tgt-DefInt(0)
                     tgt-LONG-TYPE : tgt-DefLong(0)
                     tgt-FLOAT-TYPE : tgt-DefFloat(0.0)
                     tgt-DOUBLE-TYPE : tgt-DefDouble(0.0)
                     tgt-REF-TYPE : tgt-DefLong(VOID-TAG)
            (x:Padding) :
               emit(tgt-DefSpace(size(x)))
      emit(tgt-DefText())
   

   ;     Entry Function Compiler
   ;     -----------------------
   defn compile-entry () :
      val var-table = VarTable()
      val ins-vector = Vector<tgt-Ins>()
      
      let-var INSTRUCTION-VECTOR = ins-vector :
         let-var VAR-TABLE = var-table :
            ;Compile initializing expressions
            for c in comms(prog) do :
               match(c) :
                  (c:KLSInit) : compile-comm(comm(c))
                  (c) : false
            ;Return zero
            return(list(long(0)))

            ;Create entry function, and return label
            val n = fresh-id()
            emit(tgt-DefFn(n, defs(var-table), ins-vector))
            n

   ;     Compiling a LoStanza Function
   ;     -----------------------------
   defn compile-lsdefn (c:KLSDefn) :
      val var-table = VarTable()
      val ins-vector = Vector<tgt-Ins>()

      ;Driver
      let-var INSTRUCTION-VECTOR = ins-vector :
         let-var VAR-TABLE = var-table :

            ;Compile function prelude
            for (v in args(c), t in a1(c)) do :
               var-table[v] = make-vars(expand(tt, t))
            args(var-table[args(c)])
         
            ;Compile function body
            compile-comm(body(c))

      ;Emit function definition
      emit(tgt-DefFn(n(c), defs(var-table), ins-vector))


   ;     Compiling LoStanza Constructs
   ;     -----------------------------
   ;Compile function body
   defn compile-comm (c:KLSComm) :      
      match(c) :
         (c:KLSCall) :
            compile-exp(exp(c), List{})
         (c:KLSTCall) :
            error("Untested.")
            compile-tail-exp(exp(c))
         (c:KLSSet) :
            error("Untested.")
            defn value-offsets () :
               val t = match(exp(c)) :
                  (l:Deref|Deptr|Slot) : type(l)
                  (l:Field) : field-type(tt, n(l))
               offsets(tt, t)
                  
            defn* loop (l:KLoc, off:Int, base-n:Int) :
               match(l) :
                  (l:Value) :
                     val xs = tgt-vars(exp-var(l))
                     compile-exp(value(c), tailn{xs, base-n})
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o + 8 - REF-TAG)
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o)
                  (l:Slot) :
                     val i = head(tgt-vars(index-var(l)))
                     val b = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)                        
                     val p = make-var(tgt-LONG-TYPE)
                     mul(p, long(size(tt, type(l))), i)
                     add(p, p, b)                        
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o)
                  (l:Field) :
                     loop(base(l),
                          off + field-offset(tt, n(l)),
                          base-n + field-index(tt, n(l)))
            loop(exp(c), 0, 0)
         (c:KLSLabel) :
            error("Untested.")
            tlabel(n(c))
         (c:KLSGoto) :
            error("Untested.")
            goto(n(c))
         (c:KLSReturn) :
            return(tgt-vars(exp-var(c)))
         (c:KLSLet) :
            VAR-TABLE!()[n(c)] = compile-exp(value(c), make-vars)
            compile-comm(body(c))
         (c:KLSLetVar) :
            error("Untested.")
            VAR-TABLE!()[n(c)] = make-vars(expand(tt, type(c)))
            compile-comm(body(c))
         (c:KLSIf) :
            error("Untested.")
            val altL = fresh-id()
            val endL = fresh-id()
            val p = head(tgt-vars(pred-var(c)))
            jeq(altL, p, long(0))
            compile-comm(conseq(c))
            goto(endL)
            tlabel(altL)
            compile-comm(alt(c))
            tlabel(endL)
         (c:KLSSeq) :
            compile-comm(a(c))
            compile-comm(b(c))
         (c:KLSMatch) :
            error("Compile KLSMatch command.")
         (c:KLSSkip) :
            false

   defn compile-exp (e:KLSExp, rets:List<tgt-TgtType> -> List<tgt-Var>) -> List<tgt-Var> :
      defn ret (t:tgt-TgtType) :
         head(rets(list(t)))
         
      match(e) :
         (e:KLSVar) :
            val xs = rets(tgt-types(e))
            val ys = tgt-vars(e)
            do(set, xs, ys)
            xs
         (e:KLSRef) :
            error("Untested.")
            val st = KStructT(n(e))
            val sz = 8 + ceil8(size(tt, st))
            val x = ret(tgt-REF-TYPE)
            if sz > 0 :
               extend-heap(sz)
               add(x, heap-ptr(), long(REF-TAG))
               inc-heap-ptr(sz)
               store(x, long(tag(tt, n(e))), neg(REF-TAG))
               for (o in offsets(tt, st), v in tgt-vars(arg-vars(e))) do :
                  store(x, v, o + 8 - REF-TAG)
            else :
               shl(x, long(tag(tt, n(e))), long(3))
               add(x, x, long(MARKER-TAG))
            list(x)   
         (e:KLSRefArray) :
            error("Untested.")
            val st = KStructT(n(e))
            val rf = rfield(tt, n(e))
            val rft = field-type(tt, rf)
            val x = ret(tgt-REF-TYPE)
            ;Compute size and extend heap
            val sz = make-var(tgt-LONG-TYPE)
            val len = head(tgt-vars(length-var(e)))
            mul(sz, long(size(tt, rft)), len)
            add(sz, sz, long(size(tt, st) + 8 + 7))
            bit-and(sz, sz, long(-8))
            extend-heap(sz)
            ;Store into heap
            add(x, heap-ptr(), long(REF-TAG))
            inc-heap-ptr(sz)
            store(x, long(tag(tt, n(e))), neg(REF-TAG))
            for (o in offsets(tt, st), v in tgt-vars(arg-vars(e))) do :
               store(x, v, o + 8 - REF-TAG)
            list(x)   
         (e:KLSStruct) :
            error("Untested.")
            val st = KStructT(n(e))
            val xs = rets(expand(tt, st))
            val ys = tgt-vars(arg-vars(e))
            do(set, xs, ys)
            xs
         (e:KLSPtr) :
            error("Untested.")
            val x = ret(tgt-LONG-TYPE)
            defn* loop (l:KLoc, off:Int) :
               match(l) :
                  (l:Value) :
                     val p = head(tgt-vars(exp-var(l)))
                     addr(x, p, off)
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     add(x, p, long(off + 8 - REF-TAG))
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     if off > 0 : add(x, p, long(off))
                     else : set(x, p)
                  (l:Slot) :
                     val i = head(tgt-vars(index-var(l)))
                     val p = head(tgt-vars(exp-var(l)))
                     mul(x, long(size(tt, type(l))), i)
                     add(x, x, p)
                     add(x, x, long(off)) when off > 0
                  (l:Field) :
                     loop(base(l), off + field-offset(tt, n(l)))
            loop(exp(e), 0)
            list(x)
         (e:KLSRead) :
            error("Untested.")
            ;Offsets to load from
            defn value-offsets () :
               val t = match(exp(e)) :
                  (l:Deref|Deptr|Slot) : type(l)
                  (l:Field) : field-type(tt, n(l))
               offsets(tt, t)
               
            ;Target destinations   
            val xs = rets $ match(exp(e)) :
               (l:Value) : tgt-types(exp-var(l))
               (l:Deref|Deptr|Slot) : expand(tt, type(l))
               (l:Field) : expand(tt, field-type(tt, n(l)))
               
            defn* loop (l:KLoc, off:Int, base-n:Int) :
               match(l) :
                  (l:Value) :
                     val ys = tgt-vars(exp-var(l))
                     do(set, xs, tailn(ys, base-n))
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, off + o + 8 - REF-TAG)
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, off + o)
                  (l:Slot) :
                     val b = head(tgt-vars(exp-var(l)))
                     val i = head(tgt-vars(index-var(l)))
                     val p = make-var(tgt-LONG-TYPE)
                     mul(p, long(size(tt, type(l))), i)
                     add(p, p, b)
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, o + off)
                  (l:Field) :
                     loop(base(l),
                          off + field-offset(tt, n(l)),
                          base-n + field-index(tt, n(l)))
            loop(exp(e), 0, 0)
            xs
         (e:KLSWDo) :
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) :
                  match(function-lbl(gt, f)) :
                     (f:Int) : mem(f)
                     (f:Symbol) : xmem(f)
               (f:KLSVar) : head(tgt-vars(f))
            call(arity(e), xs, f, ys)
            xs
         (e:KLSWCallC) :
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) :
                  match(function-lbl(gt, f)) :
                     (f:Int) : mem(f)
                     (f:Symbol) : xmem(f)
               (f:KLSVar) : head(tgt-vars(f))
            ffi-call(xs, f, ys)
            xs
         (e:KLSWCallClosure) :
            error("Untested.")
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = head(tgt-vars(func-var(e)))
            val code = make-var(tgt-LONG-TYPE)
            load(code, f, 8 - REF-TAG)
            call(arity(e), xs, code, List(f, ys))
            xs
         (e:KLSSizeof) :
            error("Untested.")
            val x = ret(tgt-LONG-TYPE)
            set(x, long(size(tt, type(e))))
            list(x)
         (e:KLSTagof) :
            error("Untested.")
            val x = ret(tgt-LONG-TYPE)
            set(x, long(tag(tt, n(e))))
            list(x)
         (e:KLSConv) :
            error("Untested.")
            val x = ret(head(expand(tt, type(e))))
            val y = head(tgt-vars(exp-var(e)))
            conv(x, y, false)
            list(x)
         (e:KLSLitByte) :
            val x = ret(tgt-BYTE-TYPE)
            set(x, byte(value(e)))
            list(x)
         (e:KLSLitInt) :
            val x = ret(tgt-INT-TYPE)
            set(x, int(value(e)))
            list(x)
         (e:KLSLitLong) :
            val x = ret(tgt-LONG-TYPE)
            set(x, long(value(e)))
            list(x)
         (e:KLSLitFloat) :
            val x = ret(tgt-FLOAT-TYPE)
            load(x, mem(float-lbl(value(e))), 0)
            list(x)
         (e:KLSLitDouble) :
            val x = ret(tgt-DOUBLE-TYPE)
            load(x, mem(double-lbl(value(e))), 0)
            list(x)
         (e:KLSLitString) :
            val x = ret(tgt-LONG-TYPE)
            addr(x, mem(string-lbl(value(e))), 0)
            list(x)
         (e) :
            error("No branch defined for ~" % [e])

   defn compile-tail-exp (e:KLSExp) -> False :
      error("Untested.")
      match(e) :
         (e:KLSWDo) :
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) : mem(f)
               (f:KLSVar) : head(tgt-vars(f))
            tcall(arity(e), f, ys)
         (e:KLSWCallClosure) :
            val ys = tgt-vars(arg-vars(e))
            val f = head(tgt-vars(func-var(e)))
            val code = make-var(tgt-LONG-TYPE)
            load(code, f, 8 - REF-TAG)
            tcall(arity(e), code, List(f, ys))
      

   ;     Driver
   ;     ------
   val def-ins-vector = Vector<tgt-DefIns>()
   let-var DEF-INS-VECTOR = def-ins-vector :
      do(compile, comms(prog))
      compile-globals()
      val entry = compile-entry()
      tgt-Program(def-ins-vector, entry)


