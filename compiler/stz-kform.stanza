#include<"core/stringeater.stanza">
#include<"compiler/stz-algorithms.stanza">
#include<"compiler/stz-lexer.stanza">
#include<"compiler/stz-parser.stanza">
#include("compiler/stz-utils.stanza")
#include("compiler/stz-kl-ir.stanza")
#include("compiler/stz-padder.stanza")
#include("compiler/stz-tgt-ir.stanza")

defpackage stz/kform :
   import core
   import verse
   import stz/kl-ir
   import stz/utils
   import stz/lexer
   import stz/padder
   import stz/tgt-ir with: (prefix => tgt-)


;                          Parser
;                          ======

defsyntax kl-ir :
   val ut = unwrap-token

   defn seqs (xs:List<KExp>) :
      if empty?(xs) : error("Empty Begin Expression")
      else if empty?(tail(xs)) : head(xs)
      else : KSeq(head(xs), seqs(tail(xs)), false)
      
   defn seqs (xs:List<KLSComm>) :
      if empty?(xs) : error("Empty Begin Expression")
      else if empty?(tail(xs)) : head(xs)
      else : KLSSeq(head(xs), seqs(tail(xs)))

   defrule :
      sym = (?x) when ut(x) typeof Symbol : ut(x)
      int = (?x) when ut(x) typeof Int : ut(x)

      ;     KTypes
      ;     ------
      type = (($var ?n:#int)) : KTVar(n)
      type = (($and ?a:#type ?b:#type)) : KAnd(a, b)
      type = (($or ?a:#type ?b:#type)) : KAnd(a, b)
      type = (top) : KTop()
      type = (bot) : KBot()
      type = (($of ?n:#int)) : KOf(n)

      ;     KCommands
      ;     ---------
      comm = (($def ?x:#int ?v:#exp)) : KDef(x, v)
      comm = (($defvar ?x:#int)) : KDefVar(x, false)
      comm = (($defvar ?x:#int ?v:#exp)) : KDefVar(x, v)
      comm = (($defn ?x:#int (?targs:#int ...) (?a1:#type ...) (?args:#int ...) ?body:#exp)) :
         KDefn(x, KFn(false, targs, a1, args, body))
      comm = (($defn* ?x:#int (?targs:#int ...) (?a1:#type ...) (?args:#int ...) ?body:#exp)) :
         KDefn(x, KFn(true, targs, a1, args, body))
      comm = (($defmulti ?x:#int (?targs:#int ...) (?a1:#type ...))) :
         KDefmulti(x, targs, a1)         
      this = (($this ?x:#int)) : x
      this = () : false
      comm = (($defmethod ?m:#int ?t:#this (?targs:#int ...) (?a1:#type ...) (?args:#int ...) ?body:#exp)) :
         KDefmethod(fresh-id(), m, t, KFn(false, targs, a1, args, body))
      comm = (($defmethod* ?m:#int ?t:#this (?targs:#int ...) (?a1:#type ...) (?args:#int ...) ?body:#exp)) :
         KDefmethod(fresh-id(), m, t, KFn(true, targs, a1, args, body))
      comm = (($deftype ?x:#int)) : KDefType(x, false)   
      comm = (($deftype ?x:#int ?p:#type)) : KDefType(x, p)
      comm = (($lsdef ?x:#int ?t:#lstype ?v:#exp)) : KLSDef(x, t, v)
      comm = (($lsdef ?x:#int ?t:#lstype)) : KLSDef(x, t, false)
      comm = (($lsdefn ?x:#int (?targs:#int ...) (?a1:#lstype ...) ?a2:#lstype (?args:#int ...) ?body:#lscomm)) :
         KLSDefn(x, targs, a1, a2, args, body)
      lsparent = (($parent ?p:#lstype)) : p
      lsparent = () : false
      lsfield = ((?x:#int ?t:#lstype)) : StructField(x, t)
      lsfield? = (?f:#lsfield) : f
      lsfield? = () : false
      comm = (($lsdeftype ?x:#int ?p:#lsparent (?f:#lsfield ...) ?rf:#lsfield?)) :
         KLSDefType(x, p, f, rf)
      comm = (($extern ?x:#int ?t:#lstype ?n:#sym)) : KExtern(x, t, n)
      comm = (($externfn ?x:#int ?t:#lstype ?n:#sym)) : KExternFn(x, t, n)
      comm = (?e:#exp) : KExpComm(e)

      ;     KForm Expressions
      ;     -----------------
      ;exp = (($set ?x:#int ?v:#var)) : KSet(x, v, false)
      ;exp = (($cast ?x:#var ?t:#type)) : KCast(x, t, false)
      ;exp = (($checkvoid ?x:#var)) : KCheckVoid(x, false)
      ;exp = (($checklength ?x:#var ?length:#int)) : KCheckLength(x, length, false)
      ;exp = (($let ?def:#comm ?body:#exp)) : KLet(def, body, false)
      ;exp = (($letrec (?defs:#comm ...) ?body:#exp)) : KLetRec(defs, body, false)
      ;exp = (($begin ?es:#exp ...)) : seqs(es as List<KExp>)
      ;branch = (($branch (?xs:#int ...) (?ts:#type ...) ?body:#exp)) : KBranch(xs, ts, body)
      ;exp = (($dispatch (?xs:#var ...) ?bs:#branch ...)) : KDispatch(xs, bs, false)
      ;exp = (($match (?xs:#var ...) ?bs:#branch ...)) : KMatch(xs, bs, false)
      ;exp = (($new ?c:#type ?m:#comm ...)) : KNew(c, m, false)
      ;exp = (($do ?f:#var (?targs:#var ...) ?args:#var ...)) : KDo(f, targs, args, false)
      ;exp = (($call-ls ?f:#var (?targs:#var ...) ?args:#var ...)) : KCallLS(f, targs, args, false)
      ;exp = (($tuple ?es:#var ...)) : KTuple(es, false)
      ;exp = (($tuple-get ?v:#var ?i:#int)) : KTupleGet(v, i, false)
      ;exp = (($value ?v)) : KLiteral(ut(v), false)
      ;exp = (?v:#var) : v
      var = (($var ?n:#int)) : KVar(n, false)

      ;     KLSTypes
      ;     --------
      lstype = (byte) : KByteT()
      lstype = (int) : KIntT()
      lstype = (long) : KLongT()
      lstype = (float) : KFloatT()
      lstype = (double) : KDoubleT()
      lstype = (uk) : KUnknownT()
      lstype = (($ptr ?t:#lstype)) : KPtrT(t)
      lstype = (($ref ?t:#type)) : KRefT(t)
      lstype = (($fn (?a:#lstype ...) ?r:#lstype ?b:#lstype)) : KFnT(a, r, b)
      lstype = (($fn (?a:#lstype ...) ?b:#lstype)) : KFnT(a, false, b)
      lstype = (($struct ?x:#int)) : KStructT(x)
      
      ;     KLocs
      ;     -----
      loc = (($deref ?e:#lsexp ?t:#lstype)) : Deref(e, t) 
      loc = (($deptr ?e:#lsexp ?t:#lstype)) : Deptr(e, t)
      loc = (($slot ?e:#lsexp ?t:#lstype ?i:#lsexp)) : Slot(e, t, i)
      loc = (($field ?b:#loc ?x:#int)) : Field(b, x)
      loc = (?e:#lsexp) : Value(e)

      ;     LoStanza Commands
      ;     -----------------
      lscomm = (($jump ?e:#lsexp)) : KLSTCall(e)
      lscomm = (($set ?e:#loc ?v:#lsexp)) : KLSSet(e, v)
      lscomm = (($label ?x:#int)) : KLSLabel(x)
      lscomm = (($goto ?x:#int)) : KLSGoto(x)
      lscomm = (($return ?x:#lsexp)) : KLSReturn(x)
      lscomm = (($let ?n:#int ?v:#lsexp ?body:#lscomm)) : KLSLet(n, v, body)
      lscomm = (($letvar ?n:#int ?t:#lstype ?body:#lscomm)) : KLSLetVar(n, t, body)
      lscomm = (($begin ?cs:#lscomm ...)) : seqs(cs as List<KLSComm>)
      lscomm = (($if ?p:#lsexp ?c:#lscomm ?a:#lscomm)) : KLSIf(p, c, a)
      lscomm = (($if ?p:#lsexp ?c:#lscomm)) : KLSIf(p, c, KLSSkip())
      lsbranch = (($branch (?args:#int ...) (?ts:#type ...) ?body:#lscomm)) : KLSBranch(args, ts, body)
      lscomm = (($match (?args:#lsexp ...) ?b:#lsbranch ...)) : KLSMatch(args, b)
      lscomm = (?e:#lsexp) : KLSCall(e)

      ;     LoStanza Expressions
      ;     --------------------
      lsexp = (($ref ?x:#int ?args:#lsexp ...)) : KLSRef(x, args)
      lsexp = (($refarray ?x:#int ?len:#lsexp ?args:#lsexp ...)) : KLSRefArray(x, len, args)
      lsexp = (($struct ?x:#int ?args:#lsexp ...)) : KLSStruct(x, args)
      lsexp = (($ptr ?e:#loc)) : KLSPtr(e)
      lsexp = (($read ?e:#loc)) : KLSRead(e)
      lsexp = (($do ?t:#lstype ?f:#lsexp (?ts:#type ...) ?args:#lsexp ...)) : KLSDo(f, ts, args, t)
      lsexp = (($call-c ?t:#lstype ?f:#lsexp ?args:#lsexp ...)) : KLSCallC(f, args, t)
      lsexp = (($call-s ?t:#lstype ?f:#lsexp (?ts:#type ...) ?args:#lsexp ...)) : KLSCallS(f, ts, args, t)
      lsexp = (($sizeof ?t:#lstype)) : KLSSizeof(t)
      lsexp = (($tagof ?x:#int)) : KLSTagof(x)
      lsexp = (($conv ?x:#lsexp ?t:#lstype)) : KLSConv(x, t)
      lsexp = (($value ?v)) : KLSValue(ut(v))
      lsexp = (($var ?x:#int)) : KLSVar(x)

defn parse-kl-ir (file:String) -> KProg :
   val lexed = lex-file(file)
   with-syntax(kl-ir) :
      match-syntax(lexed) :
         (?cs:#comm ...) :
            KProg(cs)


;                       Stanza Machine State
;                       ====================

val HEAP-PTR-LBL = fresh-id()
val HEAP-LIM-LBL = fresh-id()
var EXTEND-HEAP-LBL : False|Int = false

val REF-TAG = 1
val MARKER-TAG = 2


;                     Type Analysis
;                     =============

;     Type Table Interface
;     --------------------
defclass TypeTable
defmulti expand (t:TypeTable, t:KLSType) -> List<tgt-TgtType> 
defmulti pad+expand (t:TypeTable, t:KLSType) -> List<tgt-TgtType> 
defmulti offsets (t:TypeTable, t:KLSType) -> List<Int>      
defmulti size (t:TypeTable, t:KLSType) -> Int               
defmulti field-offset (t:TypeTable, f:Int) -> Int           
defmulti field-index (t:TypeTable, f:Int) -> Int            
defmulti field-type (t:TypeTable, f:Int) -> KLSType         
defmulti rfield (t:TypeTable, n:Int) -> Int                 
defmulti tag (t:TypeTable, n:Int) -> Int                    

;     Analysis
;     --------
defn type-table (prog:KProg) :
   ;     Search through all types
   ;     ------------------------
   val lstypes = HashTable<Int, KLSDefType>({_})
   val types = HashTable<Int, KDefType>({_})
   for c in comms(prog) do :
      match(c) :
         (c:KDefType) : types[n(c)] = c
         (c:KLSDefType) : lstypes[n(c)] = c
         (c) : false

   ;     Compute Expansions
   ;     ------------------
   val struct-expansions = HashTable<Int, List<tgt-TgtType>>({_})
   defn expand (t:KLSType) :
      defn expand-struct (n:Int) :
         for f in fields(lstypes[n]) map-append :
            expand(type(f))
      match(t) :
         (t:KByteT) : list(tgt-BYTE-TYPE)
         (t:KIntT) : list(tgt-INT-TYPE)
         (t:KLongT) : list(tgt-LONG-TYPE)
         (t:KFloatT) : list(tgt-FLOAT-TYPE)
         (t:KDoubleT) : list(tgt-DOUBLE-TYPE)
         (t:KPtrT) : list(tgt-LONG-TYPE)
         (t:KRefT) : list(tgt-REF-TYPE)
         (t:KStructT) :
            if not key?(struct-expansions, n(t)) :
               struct-expansions[n(t)] = expand-struct(n(t))
            struct-expansions[n(t)]
         (t) : error("No expansion for ~." % [t])

   ;     Compute Field Types and Indices
   ;     -------------------------------
   val field-structs = HashTable<Int,Int>({_})
   val field-indices = HashTable<Int,Int>({_})
   val field-types = HashTable<Int,KLSType>({_})
   for def in values(lstypes) do :
      for (f in fields(def), i in 0 to false) do :
         field-structs[n(f)] = n(def)
         field-indices[n(f)] = i
         field-types[n(f)] = type(f)
      if rfield(def) != false :
         val f = rfield(def) as StructField
         field-structs[n(f)] = n(def)
         field-indices[n(f)] = length(fields(def))
         field-types[n(f)] = type(f)

   ;     Compute Alignments, Padding, and Size
   ;     -------------------------------------
   val pad+expansions = HashTable<Int, List<tgt-Type>>({_})
   val struct-alignments = HashTable<Int, Int>({_})
   val type-sizes = HashTable<Int,Int>({_})
   val field-offsets = HashTable<Int,Int>({_})

   defn type-size (t:KLSType) :
      match(t) :
         (t:KByteT) : 1
         (t:KIntT) : 4
         (t:KLongT) : 8
         (t:KFloatT) : 4
         (t:KDoubleT) : 8
         (t:KPtrT) : 8
         (t:KRefT) : 8
         (t:KUnknownT) : error("No size.")
         (t:KFnT) : error("No size.")
         (t:KStructT) :
            compute-struct(n(t)) when not key?(type-sizes, n(t))
            type-sizes[n(t)]

   defn type-alignment (t:KLSType) :
      match(t) :
         (t:KStructT) :
            compute-struct(n(t)) when not key?(struct-alignments, n(t))
            struct-alignments[n(t)]
         (t) : type-size(t)

   defn pad+expand (t:KLSType) :
      match(t) :
         (t:KStructT) :
            pad+expansions[n(t)] when not key?(pad+expansions, n(t))
            pad+expansions[n(t)]
         (t) : expand(t)

   defn compute-struct (n:Int) :
      ;Pad struct fields
      val def = lstypes[n]
      val items = for f in fields(def) stream :
         Item(type-alignment(type(f)), n(f))         
      val [items*, aln] = match(rfield(def)) :
         (rf:False) : pad(items, 1)
         (rf:StructField) : pad(items, type-alignment(type(rf)))

      ;Compute struct alignment, field offsets, type sizes, and padded expansions
      var accum = 0
      val expansion = Vector<tgt-Type>()
      for x in items* do :
         match(x) :
            (x:Item) :
               val f = data(x) as Int
               val ft = field-type(f)
               field-offsets[f] = accum
               do(add{expansion, _}, pad+expand(ft))
               accum = accum + type-size(ft)
            (x:Padding) :
               switch {size(x) == _} :
                  1 : add(expansion, tgt-BYTE-TYPE)
                  4 : add(expansion, tgt-INT-TYPE)
               accum = accum + size(x)
      type-sizes[n] = accum         
      pad+expansions[n] = to-list(expansion)
      struct-alignments[n] = aln
      
   do(compute-struct, keys(lstypes))         

   ;     Compute Struct Tags
   ;     -------------------
   val tag-table = HashTable<Int,Int>({_})
   val tag-types = Vector<Int>()
   for def in values(lstypes) do :
      tag-table[n(def)] = length(tag-types)
      add(tag-types, n(def))
   for def in values(types) do :
      tag-table[n(def)] = length(tag-types)
      add(tag-types, n(def))

   
   ;     Return Table
   ;     ------------
   new TypeTable :
      defmethod expand (this, t:KLSType) :
         expand(t)
      defmethod pad+expand (this, t:KLSType) :
         pad+expand(t)
      defmethod offsets (this, t:KLSType) :
         match(t) :
            (t:KStructT) :
               for f in fields(lstypes[n(t)]) map :
                  field-offset(this, n(f))
            (t) : list(0)
      defmethod size (this, t:KLSType) :
         type-size(t)
      defmethod field-offset (this, f:Int) :
         field-offsets[f]
      defmethod field-type (this, f:Int) :
         field-types[f]
      defmethod field-index (this, f:Int) :
         field-indices[f]
      defmethod rfield (this, s:Int) :
         n(rfield(lstypes[s]) as StructField)
      defmethod tag (this, n:Int) :
         tag-table[n]


;                   Command Buffer
;                   ==============

;     Emission of DefIns Structures
;     -----------------------------
var DEF-INS-VECTOR : False|Vector<tgt-DefIns> = false
defn emit (i:tgt-DefIns) : add(DEF-INS-VECTOR as Vector<tgt-DefIns>, i)


;     Emission of Ins Structures
;     --------------------------
var INSTRUCTION-VECTOR : False|Vector<tgt-Ins> = false
defn emit (i:tgt-Ins) : add(INSTRUCTION-VECTOR as Vector<tgt-Ins>, i)
defn args (xs:List<tgt-Var>) : emit(tgt-ArgsIns(xs))
defn set (x:tgt-Var, y:tgt-Imm) : emit(tgt-SetIns(x, y))
defn conv (x:tgt-Var, y:tgt-Imm, s?:True|False) : emit(tgt-ConvertIns(x, y, s?))
defn add (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-ADD-OP, y, z))
defn mul (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-MUL-OP, y, z))
defn shl (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-SHL-OP, y, z))
defn bit-and (x:tgt-Var, y:tgt-Imm, z:tgt-Imm) : emit(tgt-BinOp(x, tgt-AND-OP, y, z))
defn load (x:tgt-Var, y:tgt-Imm, o:Int) : emit(tgt-Load(x, y, o))
defn store (x:tgt-Imm, y:tgt-Imm, o:Int) : emit(tgt-Store(x, y, o))
defn addr (x:tgt-Var, y:tgt-Imm, o:Int) : emit(tgt-Addr(x, y, o))
defn call (a:Int, xs:List<tgt-Var>, f:tgt-Imm, ys:List<tgt-Imm>) : emit(tgt-Call(a, xs, f, ys))
defn ffi-call (xs:List<tgt-Var>, f:tgt-Imm, ys:List<tgt-Imm>) : error("Not currently supported.")
defn tcall (a:Int, f:tgt-Imm, ys:List<tgt-Imm>) : emit(tgt-TCall(a, f, ys))
defn return (xs:List<tgt-Imm>) : emit(tgt-Return(xs))
defn tlabel (n:Int) : emit(tgt-Label(n))
defn goto (n:Int) : emit(tgt-Goto(n))
defn jne (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-NE-OP, x, y))
defn jeq (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-EQ-OP, x, y))
defn jlt (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-LT-OP, x, y))
defn jle (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-LE-OP, x, y))
defn jult (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-ULT-OP, x, y))
defn jule (n:Int, x:tgt-Imm, y:tgt-Imm) : emit(tgt-Branch(n, tgt-ULE-OP, x, y))
defn byte (v:Int) : tgt-ByteImm(v)
defn int (v:Int) : tgt-IntImm(v)
defn long (v:Int) : tgt-LongImm(v)
defn mem (n:Int) : tgt-Mem(n)

;     Heap Instructions
;     -----------------
defn heap-ptr () :
   val p = make-var(tgt-LONG-TYPE)
   load(p, mem(HEAP-PTR-LBL), 0)
   p

defn heap-lim () :
   val p = make-var(tgt-LONG-TYPE)
   load(p, mem(HEAP-LIM-LBL), 0)
   p
   
defn extend-heap (sz:tgt-Imm) :
   val endL = fresh-id()
   val p = heap-ptr()
   add(p, p, sz)
   jule(endL, p, heap-lim())
   call(1, List(), mem(EXTEND-HEAP-LBL as Int), list(sz))
   tlabel(endL)

defn extend-heap (sz:Int) :
   extend-heap(long(sz))

defn inc-heap-ptr (sz:tgt-Imm) :
   val p = heap-ptr()
   add(p, p, sz)
   store(mem(HEAP-PTR-LBL), p, 0)

defn inc-heap-ptr (sz:Int) :
   inc-heap-ptr(long(sz))

;     Compound Value Instructions
;     ---------------------------
defn float-lbl (f:Float) :
   error("Create float label")

defn string-lbl (s:String) :
   error("Create string label")
   


;                  Variable Management
;                  ===================

;     Definition of VarTable
;     ----------------------
defclass VarTable <: Gettable<Int,List<tgt-Var>> &
                     Settable<Int,List<tgt-Var>>
defmulti make-var (v:VarTable, t:tgt-TgtType) -> tgt-Var
defmulti types (v:VarTable, n:Int) -> List<tgt-TgtType>
defmulti defs (v:VarTable) -> Vector<tgt-DefVar>

defn VarTable () :
   val defs = Vector<tgt-DefVar>()
   val table = HashTable<Int,List<tgt-Var>>({_})
   val type-table = HashTable<Int,tgt-TgtType>({_})
   
   new VarTable :
      defmethod make-var (this, t:tgt-TgtType) :
         val n = fresh-id()
         add(defs, tgt-DefVar(n, t))
         type-table[n] = t
         tgt-Var(n)
      defmethod set (this, n:Int, vs:List<tgt-Var>) :
         table[n] = vs
      defmethod get (this, n:Int) :
         table[n]
      defmethod types (this, x:Int) :
         for v in this[x] map :
            type-table[tgt-n(v)]
      defmethod defs (this) :
         defs

defn get (v:VarTable, ns:List<Int>) :
   map-append({v[_]}, ns)
defn get (t:VarTable, v:KLSVar) :
   t[n(v)]
defn get (t:VarTable, vs:List<KLSVar>) :
   t[map(n,vs)]

defn make-vars (v:VarTable, ts:List<tgt-TgtType>) :
   for t in ts map :
      make-var(v, t)

;     Implicit VarTable
;     -----------------
var VAR-TABLE : False|VarTable = false
defn VAR-TABLE! () : VAR-TABLE as VarTable
defn make-var (t:tgt-TgtType) : make-var(VAR-TABLE!(), t)
defn make-vars (ts:List<tgt-TgtType>) : map(make-var, ts)
defn tgt-vars (n:Int) : VAR-TABLE!()[n]
defn tgt-vars (ns:List<Int>) : VAR-TABLE!()[ns]
defn tgt-vars (v:KLSVar) : VAR-TABLE!()[v]
defn tgt-vars (vs:List<KLSVar>) : VAR-TABLE!()[vs]
defn tgt-types (v:KLSVar) : types(VAR-TABLE!(), v)


;                    Global Table
;                    ============

defclass GlobalTable
defmulti global? (t:GlobalTable, n:Int) -> True|False
defmulti function? (t:GlobalTable, n:Int) -> True|False

defn global-table (prog:KProg) :
   val globals = HashTable<Int,True>({_})
   val functions = HashTable<Int,True>({_})
   
   for c in comms(prog) do :
      defn mark-global (n:Int) :
         globals[n] = true
      defn mark-function (n:Int) :
         globals[n] = true
         functions[n] = true
      
      match(c) :
         (c:KDef|KDefVar) : mark-global(n(c))
         (c:KDefn) : mark-function(n(c))
         (c:KDefmulti) : mark-function(n(c))
         (c:KLSDef) : mark-global(n(c))
         (c:KLSDefn) : mark-function(n(c))
         (c:KExtern) : mark-global(n(c))
         (c:KExternFn) : mark-function(n(c))
         (c) : false

   new GlobalTable :
      defmethod global? (this, n:Int) : key?(globals, n)
      defmethod function? (this, n:Int) : key?(functions, n)



;               LoStanza Function Normalization
;               ===============================

defn in-reverse<?T> (xs:Vector<?T>) :
   for i in (length(xs) - 1) through 0 by -1 stream :
      xs[i]

defn normalize (def:KLSDefn) :
   defn norm-scope (c:KLSComm) :
      val def-vector = Vector<KeyValue<Int,KLSExp|KLSType>>()
      defn fresh-var (value:KLSExp) :
         val n = fresh-id()
         add(def-vector, n => value)
         KLSVar(n)

      defn norm-exp (e:KLSExp) :
         for se in e map :
            match(se) :
               (se:KLSVar) : se
               (se:KLSExp) : fresh-var(norm-exp(se))

      defn norm-comm (c:KLSComm) :
         match(c) :
            (c:KLSSet) :
               KLSSet(map(norm-exp, exp(c)),
                      norm-exp(value(c)))
            (c:KLSLet) :
               add(def-vector, n(c) => value(c))
               norm-comm(body(c))
            (c:KLSLetVar) :
               add(def-vector, n(c) => type(c))
               norm-comm(body(c))
            (c:KLSSeq) :
               KLSSeq(norm-comm(a(c))
                      norm-scope(b(c)))
            (c:KLSIf) :
               KLSIf(norm-exp(pred(c))
                     norm-scope(conseq(c))
                     norm-scope(alt(c)))
            (c:KLSMatch) :
               KLSMatch(map(norm-exp, args(c)),
                        map(map{norm-scope, _}, branches(c)))
            (c) :
               map(norm-exp, c)

      var ret = norm-comm(c)
      for entry in in-reverse(def-vector) do :
         ret = match(value(entry)) :
            (v:KLSExp) : KLSLet(key(entry), v, ret)
            (t:KLSType) : KLSLetVar(key(entry), t, ret)
      ret

   ;Return result
   KLSDefn(n(def),
           targs(def),
           a1(def),
           a2(def),
           args(def),
           norm-scope(body(def)))


;                   LoStanza Global Access
;                   ======================

defn make-global-readers (def:KLSDefn, gt:GlobalTable) :
   defn add-loc-readers (l:KLoc) -> KLoc :
      match(l) :
         (l:Value) :
            if exp(l) not-typeof KLSVar :
               error("Not in normalized form.")
            l
         (l:Field) : Field(add-loc-readers(base(l)), n(l))
         (l) : map(add-exp-readers, l)
   defn add-exp-readers (e:KLSExp) :
      match(e) :
         (e:KLSVar) :
            if global?(gt, n(e)) : KLSRead(Value(e))
            else : e
         (e:KLSPtr) : KLSPtr(add-loc-readers(exp(e)))
         (e:KLSRead) : KLSRead(add-loc-readers(exp(e)))
         (e) : map(add-exp-readers, e)
   defn add-comm-readers (c:KLSComm) :
      match(c) :
         (c:KLSSet) :
            KLSSet(add-loc-readers(exp(c))
                   add-exp-readers(value(c)))
         (c) :
            map(add-comm-readers, map(add-exp-readers, c))

   normalize $
   KLSDefn(n(def),
           targs(def),
           a1(def),
           a2(def),
           args(def),
           add-comm-readers(body(def)))


;                 LoStanza Call Conversion
;                 ========================

defn convert-calls (def:KLSDefn, gt:GlobalTable) :
   defn direct (f:KLSVar) :
      if function?(gt, n(f)) : n(f)
      else : f
   defn conv-comm (c:KLSComm) :
      map(conv-comm, map(conv-exp, c))
   defn conv-exp (e:KLSExp) :
      match(map(conv-exp, e)) :
         (e:KLSDo) :
            KLSWDo(length(args(e)), direct(func-var(e)), args(e), type(e))
         (e:KLSCallC) :
            KLSWCallC(direct(func-var(e)), args(e), type(e))
         (e:KLSCallS) :
            error("Convert Stanza call.")               
         (e) : e
   KLSDefn(n(def),
           targs(def),
           a1(def),
           a2(def),
           args(def),
           conv-comm(body(def)))

;                   Transformations
;                   ===============

defn transform (prog:KProg, gt:GlobalTable) :
   val cs = for c in comms(prog) map :
      match(c) :
         (c:KLSDefn) :
            val c2 = make-global-readers(c, gt)
            convert-calls(c2, gt)
         (c) : c
   KProg(cs)      
      

;                      Compilation
;                      ===========

defn compile (prog:KProg, tt:TypeTable) -> tgt-Program :
   ;     Overall Compiler
   ;     ----------------
   defn compile (c:KComm) :
      match(c) :
         (c:KDef) : error("Compile KDef command.")
         (c:KDefVar) : error("Compile KDefVar command.")
         (c:KDefn) : error("Compile KDefn command.")
         (c:KDefmulti) : error("Compile KDefmulti command.")
         (c:KDefmethod) : error("Compile KDefmethod command.")
         (c:KDefType) : error("Compile KDefType command.")
         (c:KExpComm) : error("Compile KExpComm command.")
         (c:KLSDef) :
            expand(tt, type(c))
         (c:KLSDefn) :
            compile-lsdefn(c)
         (c:KLSDefType) : error("Compile KLSDefType command.")
         (c:KExtern) : error("Compile KExtern command.")
         (c:KExternFn) : error("Compile KExternFn command.")

   ;     Entry Function Compiler
   ;     -----------------------
   defn compile-entry () :
      val var-table = VarTable()
      val ins-vector = Vector<tgt-Ins>()
      
      let-var INSTRUCTION-VECTOR = ins-vector :
         let-var VAR-TABLE = var-table :
            ;Compile initializing expressions
            for c in comms(prog) do :
               match(c) :
                  (c:KLSInit) : compile-comm(comm(c))
                  (c) : false

            ;Create entry function, and return label
            val n = fresh-id()
            emit(tgt-DefFn(n, defs(var-table), ins-vector))
            n

   ;     Compiling a LoStanza Function
   ;     -----------------------------
   defn compile-lsdefn (c:KLSDefn) :
      val var-table = VarTable()
      val ins-vector = Vector<tgt-Ins>()
;
;      ;Compile function prelude
      defn compile-prelude () :
         for (v in args(c), t in a1(c)) do :
            var-table[v] = make-vars(expand(tt, t))
         args(var-table[args(c)])

      ;Driver
      let-var INSTRUCTION-VECTOR = ins-vector :
         let-var VAR-TABLE = var-table :
            compile-prelude()
            compile-comm(body(c))
            
      emit(tgt-DefFn(n(c), defs(var-table), ins-vector))


   ;     Compiling LoStanza Constructs
   ;     -----------------------------
   ;Compile function body
   defn compile-comm (c:KLSComm) :
      match(c) :
         (c:KLSCall) :
            compile-exp(exp(c), List{})
         (c:KLSTCall) :
            compile-tail-exp(exp(c))
         (c:KLSSet) :
            defn value-offsets () :
               val t = match(exp(c)) :
                  (l:Deref|Deptr|Slot) : type(l)
                  (l:Field) : field-type(tt, n(l))
               offsets(tt, t)
                  
            defn* loop (l:KLoc, off:Int, base-n:Int) :
               match(l) :
                  (l:Value) :
                     val xs = tgt-vars(exp-var(l))
                     compile-exp(value(c), tailn{xs, base-n})
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o + 8 - REF-TAG)
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o)
                  (l:Slot) :
                     val i = head(tgt-vars(index-var(l)))
                     val b = head(tgt-vars(exp-var(l)))
                     val ys = compile-exp(value(c), make-vars)                        
                     val p = make-var(tgt-LONG-TYPE)
                     mul(p, long(size(tt, type(l))), i)
                     add(p, p, b)                        
                     for (o in value-offsets(), y in ys) do :
                        store(p, y, off + o)
                  (l:Field) :
                     loop(base(l),
                          off + field-offset(tt, n(l)),
                          base-n + field-index(tt, n(l)))
            loop(exp(c), 0, 0)
         (c:KLSLabel) :
            tlabel(n(c))
         (c:KLSGoto) :
            goto(n(c))
         (c:KLSReturn) :
            return(tgt-vars(exp-var(c)))
         (c:KLSLet) :            
            VAR-TABLE!()[n(c)] = compile-exp(value(c), make-vars)
            compile-comm(body(c))
         (c:KLSLetVar) :
            VAR-TABLE!()[n(c)] = make-vars(expand(tt, type(c)))
            compile-comm(body(c))
         (c:KLSIf) :
            val altL = fresh-id()
            val endL = fresh-id()
            val p = head(tgt-vars(pred-var(c)))
            jeq(altL, p, long(0))
            compile-comm(conseq(c))
            goto(endL)
            tlabel(altL)
            compile-comm(alt(c))
            tlabel(endL)
         (c:KLSSeq) :
            compile-comm(a(c))
            compile-comm(b(c))
         (c:KLSMatch) :
            error("Compile KLSMatch command.")
         (c:KLSSkip) :
            false

   defn compile-exp (e:KLSExp, rets:List<tgt-TgtType> -> List<tgt-Var>) -> List<tgt-Var> :
      defn ret (t:tgt-TgtType) :
         head(rets(list(t)))
         
      match(e) :
         (e:KLSVar) :
            val xs = rets(tgt-types(e))
            val ys = tgt-vars(e)
            do(set, xs, ys)
            xs
         (e:KLSRef) :
            val st = KStructT(n(e))
            val sz = 8 + ceil8(size(tt, st))
            val x = ret(tgt-REF-TYPE)
            if sz > 0 :
               extend-heap(sz)
               add(x, heap-ptr(), long(REF-TAG))
               inc-heap-ptr(sz)
               store(x, long(tag(tt, n(e))), neg(REF-TAG))
               for (o in offsets(tt, st), v in tgt-vars(arg-vars(e))) do :
                  store(x, v, o + 8 - REF-TAG)
            else :
               shl(x, long(tag(tt, n(e))), long(3))
               add(x, x, long(MARKER-TAG))
            list(x)   
         (e:KLSRefArray) :
            val st = KStructT(n(e))
            val rf = rfield(tt, n(e))
            val rft = field-type(tt, rf)
            val x = ret(tgt-REF-TYPE)
            ;Compute size and extend heap
            val sz = make-var(tgt-LONG-TYPE)
            val len = head(tgt-vars(length-var(e)))
            mul(sz, long(size(tt, rft)), len)
            add(sz, sz, long(size(tt, st) + 8 + 7))
            bit-and(sz, sz, long(-8))
            extend-heap(sz)
            ;Store into heap
            add(x, heap-ptr(), long(REF-TAG))
            inc-heap-ptr(sz)
            store(x, long(tag(tt, n(e))), neg(REF-TAG))
            for (o in offsets(tt, st), v in tgt-vars(arg-vars(e))) do :
               store(x, v, o + 8 - REF-TAG)
            list(x)   
         (e:KLSStruct) :
            val st = KStructT(n(e))
            val xs = rets(expand(tt, st))
            val ys = tgt-vars(arg-vars(e))
            do(set, xs, ys)
            xs
         (e:KLSPtr) :
            val x = ret(tgt-LONG-TYPE)
            defn* loop (l:KLoc, off:Int) :
               match(l) :
                  (l:Value) :
                     val p = head(tgt-vars(exp-var(l)))
                     addr(x, p, off)
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     add(x, p, long(off + 8 - REF-TAG))
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     if off > 0 : add(x, p, long(off))
                     else : set(x, p)
                  (l:Slot) :
                     val i = head(tgt-vars(index-var(l)))
                     val p = head(tgt-vars(exp-var(l)))
                     mul(x, long(size(tt, type(l))), i)
                     add(x, x, p)
                     add(x, x, long(off)) when off > 0
                  (l:Field) :
                     loop(base(l), off + field-offset(tt, n(l)))
            loop(exp(e), 0)
            list(x)
         (e:KLSRead) :
            ;Offsets to load from
            defn value-offsets () :
               val t = match(exp(e)) :
                  (l:Deref|Deptr|Slot) : type(l)
                  (l:Field) : field-type(tt, n(l))
               offsets(tt, t)
               
            ;Target destinations   
            val xs = rets $ match(exp(e)) :
               (l:Value) : tgt-types(exp-var(l))
               (l:Deref|Deptr|Slot) : expand(tt, type(l))
               (l:Field) : expand(tt, field-type(tt, n(l)))
               
            defn* loop (l:KLoc, off:Int, base-n:Int) :
               match(l) :
                  (l:Value) :
                     val ys = tgt-vars(exp-var(l))
                     do(set, xs, tailn(ys, base-n))
                  (l:Deref) :
                     val p = head(tgt-vars(exp-var(l)))
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, off + o + 8 - REF-TAG)
                  (l:Deptr) :
                     val p = head(tgt-vars(exp-var(l)))
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, off + o)
                  (l:Slot) :
                     val b = head(tgt-vars(exp-var(l)))
                     val i = head(tgt-vars(index-var(l)))
                     val p = make-var(tgt-LONG-TYPE)
                     mul(p, long(size(tt, type(l))), i)
                     add(p, p, b)
                     for (x in xs, o in value-offsets()) do :
                        load(x, p, o + off)
                  (l:Field) :
                     loop(base(l),
                          off + field-offset(tt, n(l)),
                          base-n + field-index(tt, n(l)))
            loop(exp(e), 0, 0)
            xs
         (e:KLSWDo) :
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) : mem(f)
               (f:KLSVar) : head(tgt-vars(f))
            call(arity(e), xs, f, ys)
            xs
         (e:KLSWCallC) :
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) : mem(f)
               (f:KLSVar) : head(tgt-vars(f))
            ffi-call(xs, f, ys)
            xs
         (e:KLSWCallClosure) :
            val xs = rets(expand(tt, type(e)))
            val ys = tgt-vars(arg-vars(e))
            val f = head(tgt-vars(func-var(e)))
            val code = make-var(tgt-LONG-TYPE)
            load(code, f, 8 - REF-TAG)
            call(arity(e), xs, code, List(f, ys))
            xs
         (e:KLSSizeof) :
            val x = ret(tgt-LONG-TYPE)
            set(x, long(size(tt, type(e))))
            list(x)
         (e:KLSTagof) :
            val x = ret(tgt-LONG-TYPE)
            set(x, long(tag(tt, n(e))))
            list(x)
         (e:KLSConv) :
            val x = ret(head(expand(tt, type(e))))
            val y = head(tgt-vars(exp-var(e)))
            conv(x, y, false)
            list(x)
         (e:KLSValue) :
            match(value(e)) :
               (v:Int) :
                  val x = ret(tgt-INT-TYPE)
                  set(x, int(v))
                  list(x)
               (v:Float) :
                  val x = ret(tgt-FLOAT-TYPE)
                  load(x, mem(float-lbl(v)), 0)
                  list(x)
               (v:String) :
                  val x = ret(tgt-LONG-TYPE)
                  addr(x, mem(string-lbl(v)), 0)
                  list(x)

   defn compile-tail-exp (e:KLSExp) -> False :
      match(e) :
         (e:KLSWDo) :
            val ys = tgt-vars(arg-vars(e))
            val f = match(func-var(e)) :
               (f:Int) : mem(f)
               (f:KLSVar) : head(tgt-vars(f))
            tcall(arity(e), f, ys)
         (e:KLSWCallClosure) :
            val ys = tgt-vars(arg-vars(e))
            val f = head(tgt-vars(func-var(e)))
            val code = make-var(tgt-LONG-TYPE)
            load(code, f, 8 - REF-TAG)
            tcall(arity(e), code, List(f, ys))
      

   ;     Driver
   ;     ------
   val def-ins-vector = Vector<tgt-DefIns>()
   let-var DEF-INS-VECTOR = def-ins-vector :
      do(compile, comms(prog))
      val entry = compile-entry()
      tgt-Program(def-ins-vector, entry)


;                      Testing
;                      =======

defn headline (str:String) :
   val n = length(str)
   val m = 30 - n / 2
   println()
   do(print{" "}, 0 to m)
   println(str)
   do(print{" "}, 0 to m)
   do(print{"="}, 0 to n)
   println()
      
defn main () :
   val filename = commandline-arguments()[1]
   val program = parse-kl-ir(filename)
   
   headline("KForm Program")
   println(program)

   headline("Compilation")
   val gt = global-table(program)
   val tt = type-table(program)
   val p2 = transform(program, gt)
   val p3 = compile(p2, tt)

   headline("Target")
   println(p3)

main()