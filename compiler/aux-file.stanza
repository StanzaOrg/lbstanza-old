defpackage stz/aux-file :
  import core
  import core/sha256
  import collections
  import stz/params
  import stz/proj-ir
  import stz/proj-utils
  import stz/package-stamps
  import stz/aux-file-errors
  import stz/aux-file-serializer
  import stz/file-stamps
  import stz/printing-utils

;============================================================
;==================== Aux File Definition ===================
;============================================================

;Represents a deserialized AuxFile.
;Also presents a mutable interface to add additional records to the
;auxfile.
public deftype AuxFile

;Check whether a given record exists in the AuxFile. Used to check
;whether a .pkg or external file is up-to-date.
public defmulti key? (f:AuxFile, r:PkgRecord|ExternalFileRecord) -> True|False

;Check whether a build target is up-to-date.
public defmulti target-up-to-date? (f:AuxFile, target:Symbol, settings:BuildRecordSettings, proj:ProjFile) -> True|False

;Add a new records to the AuxFile.
public defmulti add (f:AuxFile, r:AuxRecord) -> False

;Save the new records in the AuxFile to disk.
;At this moment, the new records "take effect" and becomes visible
;to the query operations.
public defmulti save (f:AuxFile) -> False

;Retrieve all the records in the auxfile.
public defmulti records (f:AuxFile) -> AuxRecords

;============================================================
;===================== Implementation =======================
;============================================================

public defn AuxFile (path:String) -> AuxFile :
  ;Track the records and its FileStamp to know whether it has changed.
  ;These are declared as variables because after the save operation,
  ;they will change.
  var records:AuxRecords
  var records-stamp:FileStamp|False

  ;Initialize records and records-stamp.
  defn read-records () :
    if file-exists?(path) :
      records = read-aux-records(path)
      records-stamp = filestamp(path)
    else :
      records = AuxRecords(STANZA-VERSION, [])
      records-stamp = false

  ;Call read-records() to re-read the auxfile if
  ;we detect that it has changed on-disk.
  defn read-records-if-changed () -> False :
    val changed? = 
      if file-exists?(path) :
        match(records-stamp:FileStamp) :
          not up-to-date?(records-stamp)
        else :
          true
    read-records() when changed?

  ;Accumulate newly added records here.
  val new-records = Vector<AuxRecord>()

  ;Initialize
  read-records()

  new AuxFile :
    defmethod records (this) :
      records
    defmethod key? (this, r:PkgRecord|ExternalFileRecord) :
      contains?(filter-by<PkgRecord|ExternalFileRecord>(/records(records)), r)
    defmethod target-up-to-date? (this, target:Symbol, settings:BuildRecordSettings, proj:ProjFile) :
      defn main () :
        val r = matching-record()
        match(r:BuildRecord) :
          matches-settings?(r) and
          record-up-to-date?(r) and
          matching-isolate?(r)
      defn matching-record () :
        for r in filter-by<BuildRecord>(/records(records)) find :
          /target(r) == target
      defn matches-settings? (r:BuildRecord) :
        /settings(r) == settings
      defn record-up-to-date? (r:BuildRecord) :
        all?(up-to-date?, packages(r)) and
        all?(up-to-date?, files(r))
      defn matching-isolate? (r:BuildRecord) :
        val isolate* = isolate-stmts(proj, packages(proj-isolate(r)))
        isomorphic?(proj-isolate(r), isolate*)
      main()
    defmethod add (this, r:AuxRecord) :
      add(new-records, r)

    ;Save to disk.
    ;- Detect new changes to the auxfile.
    ;- Saves the new records to disk.
    ;- Updates the records and its Filestamp.
    defmethod save (this) :
      read-records-if-changed()
      val records* = combine(records, new-records)
      write-aux-records(path, records*)
      records = records*
      records-stamp = filestamp(path)

;By default, the auxfile is read from the standard
;system location.
public defn AuxFile () :
  AuxFile(system-filepath(StanzaAuxFile))

;============================================================
;==================== Utilities =============================
;============================================================

public defn delete-aux-file () :
  val name = system-filepath(StanzaAuxFile)
  delete-file(name) when file-exists?(name)

;============================================================
;===================== Aux File Structure ===================
;============================================================

public defstruct AuxRecords :
  stanza-version:Tuple<Int>
  records: Tuple<AuxRecord>

public deftype AuxRecord

public defstruct PkgRecord <: AuxRecord & Hashable & Equalable :
  package: Symbol
  filestamp: FileStamp
  source-stamp: FileStamp
  flags: Tuple<Symbol>
  optimize?: True|False
with:
  constructor => #PkgRecord

public defstruct BuildRecord <: AuxRecord :
  target: Symbol
  packages: Tuple<PackageStamp>
  files: Tuple<FileStamp>
  settings: BuildRecordSettings
  proj-isolate: ProjIsolate

public defstruct ExternalFileRecord <: AuxRecord & Hashable & Equalable :
  filetype: ExtFileType
  dependencies: Tuple<FileStamp>
  commands: Tuple<String>

public deftype ExtFileType <: Equalable & Hashable
public defstruct ExternalFile <: ExtFileType: (filestamp:FileStamp)
with: (printer => true)
public defstruct ExternalFlag <: ExtFileType: (flag:String|Tuple<String>)
with: (printer => true)

public defstruct BuildRecordSettings <: Hashable & Equalable :
  inputs: Tuple<String|Symbol>
  vm-packages: Tuple<String|Symbol>
  platform: Symbol|False
  assembly: String|False
  output: String|False
  external-dependencies: String|False
  pkg-dir: String|False
  optimize?: True|False
  ccfiles: Tuple<String>
  ccflags: Tuple<String|Tuple<String>>
  flags: Tuple<Symbol>

;------------------------------------------------------------
;---------------- Canonical Constructor ---------------------
;------------------------------------------------------------

public defn PkgRecord (package:Symbol,
                       filestamp:FileStamp,
                       source-stamp:FileStamp,
                       flags:Tuple<Symbol>,
                       optimize?:True|False) :
  #PkgRecord(package, filestamp, source-stamp,
             qsort(flags), optimize?)

;------------------------------------------------------------
;------------------ Hashing and Equality --------------------
;------------------------------------------------------------

defn key (r:PkgRecord) :
  [package(r), filestamp(r), source-stamp(r), flags(r), optimize?(r)]
defmethod equal? (a:PkgRecord, b:PkgRecord) : key(a) == key(b)
defmethod hash (r:PkgRecord) : hash $ key(r)

defn key (r:ExternalFileRecord) : [filetype(r), dependencies(r), commands(r)]
defmethod equal? (a:ExternalFileRecord, b:ExternalFileRecord) : key(a) == key(b)
defmethod hash (r:ExternalFileRecord) : hash $ key(r)

defmethod equal? (a:ExternalFile, b:ExternalFile) : filestamp(a) == filestamp(b)
defmethod hash (f:ExternalFile) : hash(filestamp(f))
defmethod equal? (a:ExternalFlag, b:ExternalFlag) : flag(a) == flag(b)
defmethod hash (f:ExternalFlag) : hash(flag(f))

defn key (r:BuildRecordSettings) :
  [inputs(r), vm-packages(r), platform(r), assembly(r), output(r), external-dependencies(r),
   pkg-dir(r), optimize?(r), ccfiles(r), ccflags(r), flags(r)]
defmethod equal? (a:BuildRecordSettings, b:BuildRecordSettings) : key(a) == key(b)
defmethod hash (r:BuildRecordSettings) : hash $ key(r)

;============================================================
;======================== Printer ===========================
;============================================================

defmethod print (o:OutputStream, f:AuxFile) :
  print(o, records(f))

defmethod print (o:OutputStream, r:AuxRecords) :
  val version-str = string-join(stanza-version(r), ".")
  val items = [    
    simple-field("stanza-version", version-str)
    inline-fields(records(r))]
  print(o, "AuxRecords%_" % [colon-field-list(items)])

defmethod print (o:OutputStream, r:PkgRecord) :
  val items = [
    simple-field("filestamp", filestamp(r))
    simple-field("source-stamp", source-stamp(r))
    named-list-fields("flags", flags(r))
    bool-flag-field("optimize", optimize?(r))]
  print(o, "PkgRecord %~%_" % [package(r), colon-field-list(items)])  

defmethod print (o:OutputStream, r:ExternalFileRecord) :
  val file-str = match(filetype(r)) :
    (t:ExternalFile) : "file %~" % [filename(filestamp(t))]
    (t:ExternalFlag) : "flag %~" % [flag(t)]
  val file-stamp = attempt: filestamp(filetype(r) as? ExternalFile)
  val items = [
    falseable-field("filestamp", file-stamp)
    named-list-fields("dependencies", dependencies(r))
    named-list-fields("commands", seq(written,commands(r)))]
  print(o, "ExternalFieldRecord %_%_" % [file-str, colon-field-list(items)])

defmethod print (o:OutputStream, r:BuildRecord) :
  val items = [
    named-list-fields("packages", packages(r))
    named-list-fields("files", files(r))
    settings(r)
    proj-isolate(r)]
  print(o, "BuildRecord %~%_" % [target(r), colon-field-list(items)])

defmethod print (o:OutputStream, s:BuildRecordSettings) :
  val items = [
    named-list-fields("inputs", inputs(s))
    named-emptyable-list-fields("vm-packages", vm-packages(s))
    falseable-field("platform", platform(s))
    falseable-field("assembly", assembly(s))
    falseable-field("output", output(s))
    falseable-field("external-dependencies", external-dependencies(s))
    falseable-field("pkg-dir", pkg-dir(s))
    simple-field("optimize?", optimize?(s))
    named-emptyable-list-fields("ccfiles", ccfiles(s))
    named-emptyable-list-fields("ccflags", ccflags(s))
    named-emptyable-list-fields("flags", flags(s))]
  print(o, "BuildRecordSettings%_" % [colon-field-list(items)])

;============================================================
;================== Combining Records =======================
;============================================================

defn combine (file:AuxRecords, records:Seqable<AuxRecord>) :
  ;Strip category
  defn strip-key (r:AuxRecord) :
    match(r) :
      (r:PkgRecord) : [`pkg, filename(filestamp(r))]
      (r:BuildRecord) : [`build, target(r)]
      (r:ExternalFileRecord) :
        match(filetype(r)) :
          (f:ExternalFile) : [`external-file, filename(filestamp(f))]
          (f:ExternalFlag) : [`external-flag, flag(f)]

  ;Build table, and override earlier records with later records with same key
  val table = HashTable<?,AuxRecord>()
  for r in cat(/records(file), records) do :
    table[strip-key(r)] = r

  ;Build new AuxRecords
  AuxRecords(STANZA-VERSION, to-tuple(values(table)))
