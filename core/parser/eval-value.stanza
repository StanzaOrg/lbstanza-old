defpackage parser/eval-value :
  import core
  import collections
  import parser/context
  import parser/engine-value-stack
  import parser/errors
  import parser/ops

;============================================================
;=============== Evaluating Parsed Results ==================
;============================================================

;Given a result object, evaluate the appropriate callbacks to
;compute the final value.
;Throws an exception if the provided action throws an exception.
public defn evaluate-result (actions:Tuple<ActionCallback>,
                             overlay-actions:Tuple<(? -> ?)>,
                             overlay-params:Tuple<ActionParams>,
                             result) -> ? :
  match(result) :
    ;Create a context, and execute the appropriate action
    ;given the context.
    (result:ActionResult) :
      ;Retrieve the callback and whether it is lazy.
      val [callback, lazy?] =
        if overlay?(result) :
          val params = overlay-params[action-index(result)]
          [overlay-actions[action-index(result)], lazy?(params)]
        else :
          val action = actions[action-index(result)]
          [callback(action), lazy?(params(action))]
      ;Create the context for the action
      val ctxt =
        if lazy? : make-lazy-context(actions, overlay-actions, overlay-params, result)
        else : make-eager-context(actions, overlay-actions, overlay-params, result)
      ;Call the callback
      callback(ctxt)

    ;Evaluate each result, and return result as a list.
    (result:ListResult) :
      val accum = Vector<?>()
      val errors = Vector<Exception>()
      for v in results(result) do :
        try : add(accum, evaluate-result(actions, overlay-actions, overlay-params, v))
        catch (e:Exception&ErrorBundle) : add-all(errors, /errors(e))
        catch (e:Exception) : add(errors, e)
      if empty?(errors) : to-list(accum)
      else : throw(ParseErrors(to-tuple(errors)))

    ;If it is not one of the unevaluated results, then
    ;it is just the value as is.
    (result) :
      result

;============================================================
;================= Evaluating Guards ========================
;============================================================

;Evaluate the given guard action.
public defn evaluate-guard (action:? -> ?,
                            values:Array) -> True|False :
  action(make-noeval-context(values))

;============================================================
;============== Implementation of Contexts ==================
;============================================================

;Create an execution context for evaluating the given result.
;The context is lazy in the sense that the user controls when
;subvalues are evaluated. If they are never called, then they are never
;evaluated.
;
;Note that lazy contexts are used for evaluating single-binder
;actions as well. If there is only a single binder then the
;error-collecting behaviour of eager contexts are unnecessary.

defn make-lazy-context (actions:Tuple<ActionCallback>,
                        overlay-actions:Tuple<(? -> ?)>,
                        overlay-params:Tuple<ActionParams>,
                        value:ActionResult) -> Context :
  new Context :
    defmethod get (this, i:Int) :
      evaluate-result(actions, overlay-actions, overlay-params, args(value)[i])
    defmethod get-all (this) :
      fatal("Lazy contexts are designed to provide its bindings one at a time.")
    defmethod info (this) :
      info(value)

;Create an execution context for evaluating the given result.
;The context is eager in the sense that the system evaluates
;all the bindings before beginning execution of the action body.
;If any binding throws an exception, then the body is never executed.
defn make-eager-context (actions:Tuple<ActionCallback>,
                         overlay-actions:Tuple<(? -> ?)>,
                         overlay-params:Tuple<ActionParams>,
                         value:ActionResult) -> Context :
  new Context :
    defmethod get-all (this) :
      if any?({_ is UnevaluatedResult}, args(value)) :
        val accum = Vector<?>()
        val errors = Vector<Exception>()
        for arg in args(value) do :
          try : add(accum, evaluate-result(actions, overlay-actions, overlay-params, arg))
          catch (e:Exception&ErrorBundle) : add-all(errors, /errors(e))
          catch (e:Exception) : add(errors, e)
        if empty?(errors) : to-tuple(accum)
        else : throw(ParseErrors(to-tuple(errors)))          
      else :
        to-tuple(args(value))
    defmethod get (this, i:Int) :
      fatal("Eager contexts are designed to provide all its bindings at once.")
    defmethod info (this) :
      info(value)

;Assume that the ActionResult contains a single evaluated binder.
;Return the bound value.
defn extract-single-binder (value:ActionResult) -> ? :
  if length(args(value)) != 1 :
    fatal("Expected a single binder but received %_." % [length(args(value))])
  match(args(value)[0]) :
    (r:UnevaluatedResult) : fatal("Cannot extract unevaluated result.")
    (r) : r

;Create an execution result for evaluating guards.
;The context is a noeval context in the sense that retrievals of
;UnevaluatedResult is forbidden.
defn make-noeval-context (values:Array) -> Context :
  new Context :
    defmethod get (this, i:Int) :
      match(values[i]) :
        (r:UnevaluatedResult) : fatal("Cannot retrieve unevaluated result from noeval context.")
        (r) : r
    defmethod get-all (this) :
      fatal("Noeval contexts are designed to provide its bindings one at a time.")
    defmethod info (this) :
      fatal("Noeval contexts have no file information.")