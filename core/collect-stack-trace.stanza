defpackage core/collect-stack-trace :
  import core
  import collections
  import core/long-vector
  import core/stack-trace

;Collect a SingleStackTrace object.
public lostanza defn collect-stack-trace (vms:ptr<core/VMState>,
                                          frames:ptr<StackFrame>,
                                          last-frame:ptr<StackFrame>,
                                          pc:long) -> ptr<PackedStackTrace> :
  ;Collect the addresses of all the stack frames.
  val frame-addresses = stack-trace-frames(vms, frames, last-frame)
  val stack-trace-table = vms.stack-trace-table

  ;Pack them using the StackTraceBuilder.
  val builder = StackTraceBuilder()

  ;Add the records from the stack frames on the stack.
  ;Look up stack trace records using the saved RETURN address.
  var instruction-pointer:long = pc
  for (var i:long = frame-addresses.length - 1, i >= 0, i = i - 1) :
    val frame = frame-addresses.items[i] as ptr<StackFrame>
    val entry = stack-trace-entry(instruction-pointer, stack-trace-table)
    if entry != null :
      add-entry(builder, entry, frame)
    instruction-pointer = frame.return

  ;Pack stack trace into stable memory.
  val trace = pack(builder)

  ;Free the buffer holding the collected frames.
  free(frame-addresses)

  ;Return the packed trace.
  return trace

public lostanza defn collect-current-stack-trace (vms:ptr<core/VMState>,
                                                  last-frame:ptr<StackFrame>,
                                                  pc:long) -> ptr<PackedStackTrace> :
  return collect-stack-trace(vms, current-stack-frames(vms), last-frame, pc)

;Helper: Call stack-trace-frames by pulling out the first frame in the
;current stack.
lostanza defn current-stack-frames (vms:ptr<core/VMState>) -> ptr<StackFrame> :
  val stack = untag-stack(vms.heap.current-stack)
  return stack.frames

;Retrieve a pointer to the memory of a Stack.
;Temporarily casts x to a ref<Stack> (even though it is not in the
;same heap.)
lostanza defn untag-stack (x:long) -> ptr<Stack> :
  return addr!([x as ref<Stack>])

;Scan through the given frames from start to last, and
;accumulate the ptr<StackFrame> into a LSLongVector.
;The VMState is necessary for looking up the stackmap table.
lostanza defn stack-trace-frames (vms:ptr<core/VMState>,
                                  frames:ptr<StackFrame>,
                                  last-frame:ptr<StackFrame>) -> ptr<LSLongVector> :
  ;Discover return addresses
  val buffer = LSLongVector()

  labels :
    begin : goto loop(frames)
    loop (sp:ptr<StackFrame>) :
      ;Store in return buffer
      add(buffer, sp as long)

      ;Continue if we're not at the end of the stack
      if sp < last-frame :
        val map-index = sp.liveness-map
        val stackmap = vms.stackmap-table[map-index]
        goto loop(sp + stackmap.size)

  ;Return the buffer
  return buffer

;Given an instruction pointer return the StackTraceRecord
;associated with that frame.
;- trace-table: The table as defined in VMState.
lostanza defn stack-trace-entry (pc:long, trace-table:ptr<core/StackTraceTable>) -> ptr<core/StackTraceTableEntry> :
  for (var i:int = 0, i < trace-table.length, i = i + 1) :
    val entry = addr(trace-table.entries[i])
    if pc == (entry.lbl as long) :
      return entry
  return null
