;See License.txt for details about licensing.
defpackage reader :
   import core with :
      prefix(digit?, get-char, get-chars, peek?) => core-
   import collections
   import stz/algorithms

;============================================================
;================== Public Interface ========================
;============================================================

public defn read (s:StringInputStream) :
   val info = info(s)
   match(parse-1(Parser(tokenize(s, false)))) :
      (t:Token) : t
      (t:False) : throw(NoForm(info))

public defn read-all (s:StringInputStream) -> List<Token> :
   parse-list(Parser(tokenize(s, false)))

public defn read-all (text:String) -> List<Token> :
   read-all(StringInputStream(text))

public defn read-file (filename:String) -> List<Token> :
   read-all(StringInputStream(slurp(filename), filename))

public defn read-line (s:InputStream) -> List<Token> :
   parse-list(Parser(tokenize(LineInputStream(s), true)))

;============================================================
;================== Token Classes ===========================
;============================================================

defstruct Indentation :
   indent:Int
defmethod print (o:OutputStream, i:Indentation) :
   print-all(o, ["[Indentation " indent(i) "]"])

defstruct OpenToken :
   symbol:Symbol
defmethod print (o:OutputStream, t:OpenToken) :
   print-all(o, ["OPEN[" symbol(t) "]"])

defstruct CloseToken :
   symbol:Symbol
defmethod print (o:OutputStream, t:CloseToken) :
   print-all(o, ["CLOSE[" symbol(t) "]"])

defstruct QuoteToken
defmethod print (o:OutputStream, t:QuoteToken) :
   print(o, "QUOTE")

defstruct CaptureToken :
   symbol: Symbol
defmethod print (o:OutputStream, t:CaptureToken) :
   print-all(o, ["CAP[" symbol(t) "]"])

defstruct Identifier :
   symbol: Symbol
defmethod print (o:OutputStream, t:Identifier) :
   print-all(o, ["IDENT[" symbol(t) "]"])

defstruct Operator :
   symbol: Symbol
defmethod print (o:OutputStream, t:Operator) :
   print-all(o, ["OPERATOR[" symbol(t) "]"])

;============================================================
;==================== ParseStream ===========================
;============================================================

;Parsing abstraction used by tokenizer
deftype ParseStream :
   StringInputStream <: ParseStream
defmulti peek? (s:ParseStream, i:Int) -> Char|False
defmulti get-char (s:ParseStream) -> Char|False
defmulti get-chars (s:ParseStream, n:Int) -> String
defmulti line-info (s:ParseStream) -> FileInfo

defn peek? (s:ParseStream) : peek?(s, 0)
defn get-char! (s:ParseStream) : get-char(s) as Char

;Forward implementation for StringInputStream
defmethod peek? (s:StringInputStream, i:Int) : core-peek?(s, i)
defmethod get-char (s:StringInputStream) : core-get-char(s)
defmethod get-chars (s:StringInputStream, n:Int) : core-get-chars(s, n)
defmethod line-info (s:StringInputStream) : info(s)

;Implementation of LineInputStream
defn LineInputStream (s:InputStream) :
   val buffer = StringBuffer()
   var start:Int = 0

   ;Return true if we make progress
   defn read-line () -> True|False :
      defn* loop () :
         val c = core-get-char(s)
         match(c:Char) :
            add(buffer, c)
            loop() when c != '\n'
      val n1 = length(buffer)
      loop()
      length(buffer) > n1

   ;Fill buffer to satisfy at least n chars
   defn* fill-buffer (n:Int) -> True|False :
      if length(buffer) - start < n :
         fill-buffer(n) when read-line()
      else :
         true            
      
   new ParseStream :
      defmethod peek? (this, i:Int) :
         if fill-buffer(i + 1) :
            buffer[start + i]
      defmethod line-info (this) :
         FileInfo("NoFile", 0, 0)
      defmethod get-char (this) :
         val c = peek?(this, 0)
         if c is Char :
            start = start + 1
         c
      defmethod get-chars (this, n:Int) :
         if n > 0 :
            val c = peek?(this, n - 1)
            fatal("Less than %_ chars in buffer." % [n]) when c is-not Char
            val str = buffer[start to start + n]
            start = start + n
            str as String
         else :
            ""

;============================================================
;==================== Tokenizer =============================
;============================================================

;Create escape character table
val ESCAPE-TABLE = Array<Char|False>(256, false)
ESCAPE-TABLE[to-int('t')] = '\t'
ESCAPE-TABLE[to-int('b')] = '\b'
ESCAPE-TABLE[to-int('r')] = '\r'
ESCAPE-TABLE[to-int('n')] = '\n'
ESCAPE-TABLE[to-int('\\')] = '\\'
ESCAPE-TABLE[to-int('\"')] = '\"'
ESCAPE-TABLE[to-int('\'')] = '\''
ESCAPE-TABLE[to-int('|')] = '|'

defn tokenize (s:ParseStream, reluctant?:True|False) :
   ;Scope stack
   val scopes = Vector<Symbol>()

   ;Determine if character is operator character
   defn operator-char? (c) :
      if c == '>' :
         empty?(scopes) or (peek(scopes) != `>)
      else :
         OPERATOR-CHARS[c]

   ;Convert escape specifier to character
   defn escape-char (c:Char) -> Char :
      match(ESCAPE-TABLE[to-int(c)]) :
         (c2:Char) : c2
         (c2:False) : throw(InvalidEscapeChar(line-info(s), c))

   ;Eat paired characters
   val buf = StringBuffer()
   defn eat-escaped-chars () -> String|False :
      clear(buf)
      val end-char = get-char!(s)
      defn* loop () :
         match(get-char(s)) :
            (c1:Char) :
               if c1 == end-char :
                  to-string(buf)
               else if c1 == '\\' :
                  match(get-char(s)) :
                     (c2:Char) : add(buf, escape-char(c2))
                     (c2:False) : throw(NoEscapeSpecifier(line-info(s)))
                  loop()   
               else :
                  add(buf, c1)
                  loop()
            ;End of stream. Read failure.
            (c1:False) :
               false
      loop()         

   ;Eat until end of line
   defn eat-comment () -> True|False :
      if peek?(s) == ';' :
         ;Multiline comment
         if peek?(s,1) == '<' :
            get-char!(s) ;Eat semicolon
            val [tag-len, com-len] = tagged-block("multiline comment")
            get-chars(s, tag-len + com-len + tag-len)
            true
         ;Regular comment   
         else :
            defn eat? () :
               match(peek?(s)) :
                  (c:False) : false
                  (c:Char) : c != '\n'
            while eat?() :
               get-char!(s)
            true
         
   ;Eat whitespace
   defn eat-whitespace () :
      while whitespace?(peek?(s)) :
         get-char!(s)

   ;Compute the ending index of a symbol
   defn identifier-end (start:Int) -> False|Int :
      val i = find!({not id-char?(peek?(s,_))}, start to false)
      i when any?(necessary-id-char?{peek?(s,_)}, start to i)

   ;Compute the ending index of a number
   defn* number-end (start:Int) -> Int :
      find!({not number-char?(peek?(s,_))}, start to false)

   ;e.g. 'c'
   defn* eat-char () :
      val info = line-info(s)
      if peek?(s) == '\'' :
         match(eat-escaped-chars()) :
            (s:String) :
               if length(s) == 1 : Token(s[0], info)
               else : throw(InvalidCharString(info))
            (s:False) : throw(UnclosedCharString(info))

   ;e.g. "This is their\'s."
   defn* eat-string () :
      val info = line-info(s)
      if peek?(s) == '\"' :
         match(eat-escaped-chars()) :
            (s:String) : Token(s, info)
            (s:False) : throw(UnclosedString(info))

   ;e.g. \|My house|
   defn* eat-escaped-symbol () :
      val info = line-info(s)
      if peek?(s) == '\\' and peek?(s, 1) == '|' :
         get-char!(s)
         match(eat-escaped-chars()) :
            (s:String) : Token(to-symbol(s), info)
            (s:False) : throw(UnclosedSymbol(info))

   ;An identifier is a string of SYMBOL characters that contains at
   ;least one alpha character. Some special symbols represent values.
   ;e.g. my/identifier
   defn* eat-identifier () :
      match(identifier-end(0)) :
         (len:Int) :
            val info = line-info(s)
            val str = get-chars(s, len)
            switch {str == _} :
               "true" : Token(true, info)
               "false" : Token(false, info)
               else : Token(Identifier(to-symbol(str)), info)
         (len:False) :
            false

   ;An operator is a reluctant string of OPERATOR characters.
   ;e.g. <:
   defn eat-operator () :
      val len = look-forward(0) where :
         defn* look-forward (i:Int) :
            if operator-char?(peek?(s,i)) : look-forward(i + 1)
            else if necessary-id-char?(peek?(s,i)) : look-back(i - 1)
            else : i
         defn* look-back (i:Int) :
            if id-char?(peek?(s,i)) : look-back(i - 1)
            else : i + 1
      if len > 0 :
         val info = line-info(s)
         val sym = to-symbol(get-chars(s, len))
         Token(Operator(sym), info)

   ;Eat a starting indent (assumes beginning of line)
   defn* eat-indent () :
      val info = line-info(s)
      val len = find!({not whitespace?(peek?(s,_))}, 0 to false)
      get-chars(s, len)
      val indent = Token(Indentation(len), info)
      if eat-comment() :
         eat-indent()
      else if peek?(s) == '\n' :
         get-char!(s)
         eat-indent()
      else if peek?(s) is-not False :
         indent

   ;Eat a number
   ;e.g. 103L
   defn eat-number () :
      if digit?(peek?(s,0)) or
         (peek?(s) == '-' and digit?(peek?(s,1))) :
         val info = line-info(s)
         val str = get-chars(s, number-end(0))
         defn number? (x) :
            match(x) :
               (x:False) : throw(InvalidNumber(info))
               (x) : Token(x, info)
         if contains?(str, '.') :
            if suffix?(str, "f") or suffix?(str, "F") :
               number?(to-float(but-last(str)))
            else : number?(to-double(str))
         else :
            if suffix?(str, "y") or suffix?(str, "Y") :
               number?(to-byte(but-last(str)))
            else if suffix?(str, "l") or suffix?(str, "L") :
               number?(to-long(but-last(str)))
            else : number?(to-int(str))

   ;Tagged Properties
   ;Returns [tag-length, block-length]
   defn tagged-block (description:String) -> [Int Int]:
      ;Returns a sequence of remaining characters after and including start
      defn remaining-chars (start:Int) :
         val idx = to-seq(start to false)
         repeat-while $ fn () :
            match(peek?(s, next(idx))) :
               (c:Char) : One(c)
               (c:False) : None()
      ;Find the length of the tag
      val info = line-info(s)
      val tag-len =
         match(index-of(remaining-chars(0), '>')) :
            (i:Int) : i + 1
            (n:False) : throw(InvalidTag(info))
      ;Does position i contain the tag?
      defn tag? (i:Int) :
         for j in 0 to tag-len all? :
            peek?(s, i + j) == peek?(s, j)
      ;Search for the next occurrence of the tag
      defn* index-of-tag (start:Int) -> Int :
         match(index-of(remaining-chars(start), '<')) :
            (i:Int) : (start + i) when tag?(start + i) else index-of-tag(start + i + 1)
            (i:False) : throw(NoEndTagFound(info, description))
      ;Driver
      val end-tag-pos = index-of-tag(tag-len)
      [tag-len, end-tag-pos - tag-len]

   ;Eat a here string
   ;e.g. \<STR>This is my String<STR>
   defn eat-here-string () :
      if peek?(s) == '\\' and peek?(s,1) == '<' :
         val info = line-info(s)
         get-char!(s) ;Eat \
         val [tag-len, str-len] = tagged-block("here string")
         get-chars(s, tag-len)
         val str = get-chars(s, str-len)
         get-chars(s, tag-len)
         Token(str, info)

   ;e.g. [
   defn eat-structural-token () :
      val info = line-info(s)
      if open-brace?(peek?(s)) :
         Token(OpenToken(to-symbol(get-char!(s))), info)
      else if close-brace?(peek?(s)) :
         Token(CloseToken(to-symbol(get-char!(s))), info)
      else if peek?(s) == '`' :
         get-char!(s)
         Token(QuoteToken(), info)

   ;e.g. (
   defn eat-star-token () :
      val info = line-info(s)
      if open-brace?(peek?(s)) :
         Token(OpenToken(symbol-join(["*" get-char!(s)])), info)

   ;e.g. ?x
   defn eat-capture () :
      if (peek?(s) == '?') :
         match(identifier-end(1)) :
            (end:Int) :
               val info = line-info(s)
               get-char!(s)
               val sym = to-symbol(get-chars(s, end - 1))
               Token(CaptureToken(sym), info)
            (end:False) :
               false

   ;Update the scope stack
   defn update-stack (info:FileInfo, c:Symbol) :
      defn pop-stack () :
         if empty?(scopes) :
            throw(ExtraClosingToken(info, c))
         else if peek(scopes) != c :
            throw(WrongClosingToken(info, peek(scopes), c))
         else :
            pop(scopes)

      switch {c == _} :
         `\|<| : add(scopes, `\|>|)
         `\|[| : add(scopes, `\|]|)
         `\|{| : add(scopes, `\|}|)
         `\|(| : add(scopes, `\|)|)
         `\|*<| : add(scopes, `\|>|)
         `\|*[| : add(scopes, `\|]|)
         `\|*{| : add(scopes, `\|}|)
         `\|*(| : add(scopes, `\|)|)
         `\|>| : pop-stack()
         `\|]| : pop-stack()
         `\|}| : pop-stack()
         `\|)| : pop-stack()
         else : fatal("Invalid stack symbol: %~" % [c])

   generate<Token> :
      defn yield-token (t:Token) :
         ;Yield out the token
         yield(t)
         ;Update the stack
         match(item(t)) :
            (x:OpenToken|CloseToken) : update-stack(info(t), symbol(x))
            (x) : false
      defn yield-token (t:False) :
         false
         
      defn eat? (f: () -> False|Token) :
         match(f()) :
            (t:Token) :
               ;Yield out the token
               yield-token(t)
               ;Determine whether token can be followed by a star token
               val star? = item(t) is 
                  CloseToken|CaptureToken|Identifier|
                  Byte|Char|Int|Long|Float|Double|True|False|String
               ;Eat a star token if necessary
               yield-token(eat-star-token()) when star?
               ;Return true to indicate we successfully ate something
               true
            (t:False) :
               ;Nothing was eaten
               false

      defn eat-lexeme! () :
         val eaten =
            eat?(eat-capture) or
            eat?(eat-here-string) or
            eat?(eat-escaped-symbol) or
            eat?(eat-char) or
            eat?(eat-string) or
            eat?(eat-number) or
            eat?(eat-identifier) or
            eat?(eat-operator) or
            eat?(eat-structural-token)
         throw(InvalidToken(line-info(s), peek?(s))) when not eaten

      defn* eat-lexemes () :
         eat-whitespace()
         if peek?(s) is-not False :
            ;Case 1 of 3 : Rest of line is comments
            if eat-comment() :
               eat-lexemes()
            ;Case 2 of 3 : End of line   
            else if peek?(s) == '\n' :
               get-char!(s)
               if not (reluctant? and empty?(scopes)) :
                  eat?(eat-indent)
                  eat-lexemes()
            ;Case 3 of 3 : More lexemes   
            else :
               eat-lexeme!()
               eat-lexemes()

      eat-lexemes()

;============================================================
;================= Character Classes ========================
;============================================================

defn CharMap (s:String) : 
   val map = BitArray(256,false)
   for c in s do : map[to-int(c)] = true
   map
defn CharMap (s:Seqable<String>) :
   CharMap(append-all(s))
defn get (m:BitArray, c:Char|False) :
   match(c) :
      (c:Char) : m[to-int(c)]
      (c:False) : false

val digits = "0123456789"
val letters = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
val NECESSARY-ID-CHARS = CharMap([letters, "_?"])
val ID-CHARS = CharMap([letters "_?" digits "~!@#$%^*+-=/"])
val OPERATOR-CHARS = CharMap(["~!@#$%^*+-=/" ".:<&|"])
val OPEN-BRACE-CHARS = CharMap("([{<")
val CLOSE-BRACE-CHARS = CharMap(")]}>")
val WHITESPACE-CHARS = CharMap(" ,\r")
val NUMBER-CHARS = CharMap([letters "_?" digits "~!@#$%^*+-=/" "."])

defn whitespace? (c) : WHITESPACE-CHARS[c]
defn necessary-id-char? (c) : NECESSARY-ID-CHARS[c]
defn open-brace? (c) : OPEN-BRACE-CHARS[c]
defn close-brace? (c) : CLOSE-BRACE-CHARS[c]
defn id-char? (c) : ID-CHARS[c]
defn number-char? (c) : NUMBER-CHARS[c]
defn digit? (c) : 
   match(c) :
      (c:Char) : (c >= '0') and (c <= '9')
      (c) : false
      
;============================================================
;================== Utilities ===============================
;============================================================

defn but-last (str:String) :
   str[0 to (length(str) - 1)]

;============================================================
;===================== Parsing ==============================
;============================================================

deftype Parser
defmulti parse-list (p:Parser) -> List<Token>
defmulti parse-1 (p:Parser) -> False|Token

val OPEN-PAREN = `\|(|
val STAR-PAREN = `\|*(|
val CLOSE-PAREN = `\|)|
val OPEN-BRACKET = `\|{|
val STAR-BRACKET = `\|*{|
val CLOSE-BRACKET = `\|}|
val OPEN-BRACE = `\|[|
val STAR-BRACE = `\|*[|
val CLOSE-BRACE = `\|]|
val STAR-ANGLE = `\|*<|
val CLOSE-ANGLE = `\|>|

;Open shorthands
defn shorthand (t:OpenToken, xs:List) :
   switch {symbol(t) == _} :
      OPEN-PAREN : xs
      OPEN-BRACE : cons(`@tuple, xs)
      OPEN-BRACKET : cons(`@afn, xs)
      STAR-PAREN : cons(`@do, xs)
      STAR-BRACE : cons(`@get, xs)
      STAR-BRACKET : cons(`@do-afn, xs)
      STAR-ANGLE : cons(`@of, xs)
      else : fatal("Invalid token: %_" % [t])
;Capture shorthand
defn shorthand (t:CaptureToken) :
   List(`@cap, symbol(t))
;Quote shorthand
defn shorthand (t:QuoteToken, x) :
   List(`@quote, x)

;Matching end symbol given start symbol
defn matching-end (s:Symbol) :
   switch {s == _} :
      OPEN-PAREN : CLOSE-PAREN
      STAR-PAREN : CLOSE-PAREN
      OPEN-BRACKET : CLOSE-BRACKET
      STAR-BRACKET : CLOSE-BRACKET
      OPEN-BRACE : CLOSE-BRACE
      STAR-BRACE : CLOSE-BRACE
      STAR-ANGLE : CLOSE-ANGLE
      else : fatal("No matching end")

defn Parser (s:Seq<Token>) -> Parser :   
   ;Parse a sequence
   defn parse-seq (open, open-info:False|FileInfo) -> Seq<Token> :
      ;Preparse
      val xs = generate<Token> :
         defn* loop () :
            ;Yield and continue
            defn* continue (x:Token) : (yield(x), loop())
            defn* ignore () : (next(s), loop())
            ;No more tokens
            if empty?(s) :
               if open is OpenToken :
                  val end = matching-end(symbol(open as OpenToken))
                  throw(NoClosingToken(open-info as FileInfo, end))
            else :
               val x = peek(s)
               match(item(x), open) :
                  ; ... x y z )
                  (t:CloseToken, open:OpenToken) :
                     next(s)
                  ;(let :
                  ;    a b) c d
                  (t:CloseToken, open:Indentation) :
                     false
                  ;(x y z ...
                  (t:OpenToken, open) :
                     next(s)
                     val xs = parse-list(t, info(x))
                     continue(Token(shorthand(t,xs), info(x)))
                  ;?x y ...
                  (t:CaptureToken, open) :
                     next(s)
                     continue(Token(shorthand(t), info(x)))
                  ;` x y ...
                  (t:QuoteToken, open) :
                     continue(next(s))
                  ;x
                  (t:Identifier, open) :
                     next(s)
                     continue(Token(symbol(t), info(x)))
                  ;+
                  (t:Operator, open) :
                     ;Check if indent pending
                     defn indent-pending? () :
                        (not empty?(s)) and item(peek(s)) is Indentation
                     ;Grab operator
                     next(s)
                     val x = Token(symbol(t), info(x))
                     ;Line ending colon
                     if (symbol(t) == `:) and indent-pending?() :
                        yield(x) ;Yield colon
                        val i = item(next(s)) as Indentation ;Eat indentation
                        val xs = parse-list(i, open-info)
                        continue(Token(xs, info(x)))
                     ;Normal symbol   
                     else : continue(x)
                  ;automatic indentation
                  (t:Indentation, open:Indentation) :
                     ;ignore if not block ending indentation
                     ignore() when indent(t) >= indent(open)
                  (t:Indentation, open) : ignore()
                  ;other tokens
                  (t, open) : continue(next(s))
         loop()
         
      ;Parse
      generate<Token> :
         defn* loop () :
            defn* continue (x:Token) : (yield(x), loop())
            if not empty?(xs) :
               val x = next(xs)
               match(item(x)) :
                  (t:QuoteToken) :
                     throw(NoQuotedValue(info(x))) when empty?(xs)
                     continue(Token(shorthand(t, next(xs)), info(x)))
                  (t) : continue(x)
         loop()   

   ;Parse a list
   defn parse-list (open, open-info:False|FileInfo) :
      to-list(parse-seq(open, open-info))

   ;Return parser
   new Parser :
      defmethod parse-list (this) :
         parse-list(false, false)
      defmethod parse-1 (this) :
         val xs = parse-seq(false, false)
         try : next(xs) when not empty?(xs)            
         finally : free(xs)

;============================================================
;=================== Lexer Errors ===========================
;============================================================

public deftype LexerException <: Exception
defn LexerException (s:String) :
   new LexerException :
      defmethod print (o:OutputStream, this) :
         print(o, s)

defn LexerExceptions (xs:Seqable<LexerException>) :
   LexerException(string-join(xs, "\n"))

defn NoClosingToken (info:FileInfo, end:Symbol) :
   LexerException $ string-join $
   [info ": No closing token found. Expecting " end "."]

defn InvalidNumber (info:FileInfo) :
   LexerException $ string-join $
   [info ": Invalid number."]

defn InvalidToken (info:FileInfo, item) :
   LexerException $ to-string $
   "%_: Invalid token %~." % [info, item]

defn InvalidEscapeChar (info:FileInfo, c:Char) :
   LexerException $ string-join $
   [info ": Invalid escape character: " c "."]

defn NoEscapeSpecifier (info:FileInfo) :
   LexerException $ string-join $
   [info ": Expected an escape specifier after backslash."]

defn UnclosedString (info:FileInfo) :
   LexerException $ string-join $
   [info ": Unclosed string. "]

defn UnclosedCharString (info:FileInfo) :
   LexerException $ string-join $
   [info ": Unclosed character. "]

defn UnclosedSymbol (info:FileInfo) :
   LexerException $ string-join $
   [info ": Unclosed symbol. "]

defn InvalidCharString (info:FileInfo) :
   LexerException $ string-join $
   [info ": Invalid character string. Must have length 1."]

defn WrongClosingToken (info:FileInfo, expected:Symbol, actual:Symbol) :
   LexerException $ string-join $
   [info ": Wrong closing parenthesis. Expecting " expected " but got " actual "."]

defn ExtraClosingToken (info:FileInfo, c:Symbol) :
   LexerException $ string-join $
   [info ": Extra closing token found: " c "."]

defn InvalidTag (info:FileInfo) :
   LexerException $ string-join $
   [info ": Invalid tag for here string."]

defn NoEndTagFound (info:FileInfo, description:String) :
   LexerException $ string-join $
   [info ": No ending tag found for " description "."]

defn NoForm (info:FileInfo) :
   LexerException $ string-join $
   [info ": No forms left in stream."]

defn NoQuotedValue (info:FileInfo) :
   LexerException $ string-join $
   [info ": No s-expression following quote operator."]