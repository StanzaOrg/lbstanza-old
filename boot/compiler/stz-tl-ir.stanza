defpackage stz/tl-ir :
   import core
   import collections
   import stz/primitives
   import stz/utils

#use-overlay(stz/ast-lang, stz/printer-lang)

;              Type Hierarchy Definition
;              =========================

public deftype HEntry
public defmulti n (h:HEntry) -> Int
public defmulti args (h:HEntry) -> List<Int>
public defmulti parent (h:HEntry) -> False|Type

public defstruct HSHEntry <: HEntry :
   n: Int with: (as-method => true)
   args: List<Int> with: (as-method => true)
   parent: False|Type with: (as-method => true)

public defstruct LSHEntry <: HEntry :
   n: Int with: (as-method => true)
   args: List<Int> with: (as-method => true)
   parent: False|Type with: (as-method => true)
   fields: List<Field>
   rfield: Field|False

public defstruct Field :
   n: Int
   name: Symbol
   type: LSType

public deftype THierarchy <: Many<HEntry>
public defmulti get (h:THierarchy, n:Int) -> HEntry
public defmulti add (h:THierarchy, e:HEntry) -> False

public defn THierarchy () :
   val entries = HashTable<Int,HEntry>()
   new THierarchy :
      defmethod add (this, e:HEntry) :
         entries[n(e)] = e
      defmethod get (this, n:Int) :
         entries[n]
      defmethod to-seq (this) :
         to-seq(values(entries))

;     Comparison Values
;     -----------------
public deftype Unknown <: Equalable
public val unknown:Unknown = new Unknown
defmethod print (o:OutputStream, u:Unknown) :
   print(o, "unknown")

public deftype Possibly <: Equalable
public val possibly:Possibly = new Possibly
defmethod print (o:OutputStream, m:Possibly) :
   print(o, "possibly")

;     Common Functions
;     ----------------
public defmulti info (t:TComm) -> False|FileInfo

public defmulti type (t:TExp) -> False|Type
public defmulti exp-type (t:TExp) -> False|Type
public defmulti ctxt (t:TExp) -> Context|False
public defmulti info (t:TExp) -> False|FileInfo

public defmulti info (c:LSComm) -> False|FileInfo

public defmulti type (t:LSExp) -> False|LSType
public defmulti exp-type (t:LSExp) -> False|LSType
public defmulti ctxt (t:LSExp) -> Context|False
public defmulti info (t:LSExp) -> False|FileInfo

public defast :
   ;     Contexts
   ;     --------
   deftype Context
   defnodes Context :
      CDo: (args:List<False|Type>, ret:False|Type, all-args:List<List<Type>>)
      CExp: (type:False|Type)
      CMulti: (type:False|TArrow)
      CLSDo: (args:List<False|LSType>, ret:False|LSType, all-args:List<List<LSType>>)
      CLSExp: (type:False|LSType)

   ;     Selections
   ;     ----------
   deftype Selection
   defnodes Selection :
      SVar: (n:Int)
      SSel: (sels: List<True|False|Possibly|Unknown>)

   ;     Top Level Commands
   ;     ------------------
   deftype TComm :
      info: False|FileInfo with: (as-method => true)
   defnodes TComm :
      TDef :
         n: Int
         type: False|Type
         value: TExp
      TDefTuple :
         ns: List<Int>
         types: List<False|Type>
         value: TExp
      TDefVar :
         n: Int
         type: False|Type
         value: False|TExp
      TDefn :
         tail?: True|False
         n: Int
         targs: List<Int>
         cargs: List<Int>
         a1: List<False|Type>
         a2: False|Type
         args: List<Int>
         body: TExp
      TDefmulti :
         n: Int
         targs: List<Int>
         cargs: List<Int>
         a1: List<False|Type>
         a2: False|Type
      TDefmethod :
         tail?: True|False
         n: Int
         multi: TExp
         thisn: False|Int
         targs: List<Int>
         cargs: List<Int>
         a1: List<False|Type>
         a2: False|Type
         args: List<Int>
         body: TExp
      TInit :
         exp: TExp

      TLDef :
         n: Int
         type: LSType
         value: LSExp
      TLDefVar :
         n: Int
         type: LSType
         value: False|LSExp
      TLDefn :
         tail?: True|False
         n: Int
         targs: List<Int>
         cargs: List<Int>
         a1: List<LSType>
         a2: LSType
         args: List<Int>
         body: LSComm
      TLDefmethod :
         tail?: True|False
         n: Int
         multi: TExp
         targs: List<Int>
         cargs: List<Int>
         a1: List<LSType>
         a2: LSType
         args: List<Int>
         body: LSComm         
      TExtern :
         n: Int
         type: LSType
      TLInit :
         comm: LSComm


   ;     HiStanza Expressions
   ;     --------------------
   deftype TExp :
      type:False|Type with: (as-method => true)
      exp-type:False|Type with: (as-method => true)
      ctxt:False|Context with: (as-method => true)
      info:False|FileInfo with: (as-method => true)

   defnodes TExp :
      ;=== Stanza Forms ===
      TLet : (n:Int, ntype:False|Type, value:TExp, body:TExp)
      TLetVar : (n:Int, ntype:False|Type, value:False|TExp, body:TExp)
      TLetTuple : (ns:List<Int>, ntypes:List<False|Type>, value:TExp, body:TExp)
      TLetRec : (defns:List<TDefn>, body:TExp)
      TFn : (tail?:True|False, a1:List<False|Type>, a2:False|Type, args:List<Int>, body:TExp)
      TMulti : (funcs:List<TFn>)
      TSeq : (a:TExp, b: TExp)
      TMatch : (args:List<TExp>, branches:List<TBranch>)
      TBranch : (args:List<Int>, atypes:List<False|Type>, body:TExp)
      TNew : (class:Type, methods:List<TDefmethod>)
      TRef : (n:Int)
      TCast : (exp:TExp, targ:Type)
      TUpCast : (exp:TExp, targ:Type)
      TSet : (ref:TRef, value:TExp)
      TDo : (func:TExp, args:List<TExp>)
      TPrim : (op:Primitive, args:List<TExp>)
      TLiteral : (value:?)
      TTupleExp : (exps:List<TExp>)
      TMix : (sel:False|Selection, exps:List<TExp>)
      TCurry : (ref:TRef, targs:List<Type>, cargs:List<False|Type>)

   ;     Type Expressions
   ;     ----------------
   deftype Type
   defnodes Type :
      TMixed: (types:List<TArrow>)
      TPoly : (targs:List<Int>, cargs:List<Int>, func:TArrow)
      TCap : (n:Int)
      TOf : (n:Int, type:Type)
      TTuple : (types:List<Type>)
      TOr : (a:Type, b:Type)
      TAnd : (a:Type, b:Type)
      TVar : (n:Int)
      TArrow : (a:Type, b:Type)
      TGradual : ()
      TBot : ()
      TUVar : (n:Int)

   ;     LoStanza Types
   ;     --------------
   deftype LSType
   defnodes LSType :
      ByteT
      IntT
      LongT
      FloatT
      DoubleT
      UnknownT
      PolyT : (targs:List<Int>, cargs:List<Int>, func:FnT)
      PtrT: (type:LSType)
      RefT: (type:Type)
      FnT: (a:List<LSType>, r:False|LSType, b:LSType)
      StructT: (n:Int, type:Type)
      UVarT: (n:Int)

   ;     LoStanza Commands
   ;     -----------------
   deftype LSComm :
      info: False|FileInfo with: (as-method => true)
   defnodes LSComm :
      LSCall: (exp:LSExp)
      LSSet: (exp:LSExp, value:LSExp)
      LSLabels: (ns:List<Int>, body:LSComm)
      LSLabel: (n:Int)
      LSGoto: (n:Int)
      LSReturn: (exp:LSExp)
      LSDef: (n:Int, type:False|LSType, value:LSExp)
      LSDefVar: (n:Int, type:LSType, value:False|LSExp)      
      LSSeq: (a:LSComm, b:LSComm)
      LSIf: (pred:LSExp, conseq:LSComm, alt:LSComm)
      LSMatch: (args:List<LSExp>, branches:List<LSBranch>)
      LSBranch: (args:List<Int>, atypes:List<False|LSType>, body:LSComm)
      LSSkip

   ;     LoStanza Expressions
   ;     --------------------
   deftype LSExp :
      type: False|LSType with: (as-method => true)
      exp-type: False|LSType with: (as-method => true)
      ctxt: False|Context with: (as-method => true)
      info: False|FileInfo with: (as-method => true)

   defnodes LSExp :
      LSVar: (n:Int)
      LSNew: (n:Int, targ:Type, args:List<LSExp>)
      LSArray: (n:Int, targ:Type, args:List<LSExp>, length:LSExp)
      LSStruct: (n:Int, targ:Type, args:List<LSExp>)
      LSAddr: (exp:LSExp)
      LSDeref: (exp:LSExp)
      LSSlot: (exp:LSExp, index:LSExp)
      LSField: (exp:LSExp, name:Symbol)
      LSDo: (func:LSExp, args:List<LSExp>)
      LSDoInStack: (func:LSExp, args:List<LSExp>, ret:LSType)
      LSDoStack: (func:LSExp, arg:LSExp, ret:LSType)
      LSCallC: (func:LSExp, args:List<LSExp>)
      LSPrim: (op:LSPrimitive, args:List<LSExp>)
      LSSizeof: (targ:LSType)
      LSTagof: (n:Int)
      LSCast: (exp:LSExp, targ:LSType)
      LSLiteral: (value:?)
      LSAnd: (a:LSExp, b:LSExp)
      LSOr: (a:LSExp, b:LSExp)
      LSLetExp: (comm:LSComm, exp:LSExp)
      LSMix: (sel:False|Selection, exps:List<LSExp>)
      LSCurry: (ref:LSVar, targs:List<Type>, cargs:List<False|Type>)
      LSFn: (ref:TExp)

   ;     Mappers and Doers
   ;     -----------------
   make-mapper(f: TExp -> TExp, c:TComm)
   make-mapper(f: LSComm -> LSComm, c:TComm)
   make-mapper(f: LSExp -> LSExp, c:TComm)
   make-mapper(f: Type -> Type, c:TComm)
   make-doer(f: TExp -> ?, c:TComm)
   make-doer(f: LSComm -> ?, c:TComm)
   make-doer(f: LSExp -> ?, c:TComm)

   make-mapper(f: TExp -> TExp, e:TExp)
   make-mapper(f: TComm -> TComm, e:TExp)
   make-mapper(f: Context -> Context, e:TExp)
   make-mapper(f: Type -> Type, e:TExp)
   make-mapper(f: Selection -> Selection, e:TExp)
   make-doer(f: TComm -> ?, e:TExp)
   make-doer(f: TExp -> ?, e:TExp)
   
   make-mapper(f: LSExp -> LSExp, c:LSComm)
   make-mapper(f: LSComm -> LSComm, c:LSComm)
   make-mapper(f: LSType -> LSType, c:LSComm)
   make-doer(f: LSExp -> ?, c:LSComm)
   make-doer(f: LSComm -> ?, c:LSComm)
   
   make-mapper(f: LSExp -> LSExp, e:LSExp)
   make-mapper(f: LSComm -> LSComm, e:LSExp)
   make-mapper(f: LSType -> LSType, e:LSExp)
   make-mapper(f: Type -> Type, e:LSExp)
   make-mapper(f: TExp -> TExp, e:LSExp)
   make-mapper(f: Context -> Context, e:LSExp)
   make-mapper(f: Selection -> Selection, e:LSExp)

   make-doer(f: LSExp -> ?, e:LSExp)
   make-doer(f: LSComm -> ?, e:LSExp)
   make-doer(f: TExp -> ?, e:LSExp)

   make-mapper(f: Type -> Type, t:Type)
   make-doer(f: Type -> ?, t:Type)

   make-mapper(f: LSType -> LSType, t:LSType)
   make-mapper(f: Type -> Type, t:LSType)
   make-doer(f: LSType -> ?, t:LSType)
   make-doer(f: Type -> ?, t:LSType)

   make-mapper(f: Type -> Type, c:Context)
   make-mapper(f: LSType -> LSType, c:Context)
   
   defmapper (f:TExp -> TExp) :
      (x:TExp) : f(x)
      (x:TRef) : f(x) as TRef
      (x:False|TExp) : f(x as TExp) when x not-typeof False
      (x:List<TExp>) : map(f, x)
      (x:List<TFn>) : map({f(_) as TFn}, x)
      (x:List<TBranch>) : map({f(_) as TBranch}, x)
   defdoer (f:TExp -> ?) :
      (x:TExp) : f(x)
      (x:TRef) : f(x)
      (x:False|TExp) : f(x as TExp) when x not-typeof False
      (x:List<TExp>) : do(f, x)
      (x:List<TFn>) : do(f, x)
      (x:List<TBranch>) : do(f, x)

   defmapper (f:LSComm -> LSComm) :
      (x:LSComm) : f(x)
      (x:List<LSBranch>) : map({f(_) as LSBranch}, x)
   defdoer (f:LSComm -> ?) :
      (x:LSComm) : f(x)
      (x:List<LSBranch>) : do(f, x)

   defmapper (f:LSExp -> LSExp) :
      (x:LSExp) : f(x)
      (x:False|LSExp) : f(x as LSExp) when x not-typeof False
      (x:List<LSExp>) : map(f, x)
      (x:LSVar) : f(x) as LSVar
   defdoer (f:LSExp -> ?) :
      (x:LSExp) : f(x)
      (x:False|LSExp) : f(x as LSExp) when x not-typeof False
      (x:List<LSExp>) : do(f, x)
      (x:LSVar) : f(x)

   defmapper (f:Type -> Type) : 
      (x:Type) : f(x)
      (x:False|Type) : f(x as Type) when x not-typeof False
      (x:TArrow) : f(x) as TArrow
      (x:False|TArrow) : f(x as Type) as False|TArrow when x not-typeof False
      (x:List<Type>) : map(f, x)
      (x:List<False|Type>) :
         for xi in x map :
            f(xi as Type) when xi not-typeof False
      (x:List<TArrow>) :
         map({f(_) as TArrow}, x)
      (xss:List<List<Type>>) :
         for xs in xss map :
            map(f, xs)
   defdoer (f:Type -> ?) : 
      (x:Type) : f(x)
      (x:TArrow) : f(x)
      (x:List<Type>) : do(f, x)
      (x:List<TArrow>) : do(f, x)
      (x:False|Type) : f(x as Type) when x not-typeof False
      (x:List<False|Type>) :
         for xi in x do :
            f(xi as Type) when xi not-typeof False
      (xss:List<List<Type>>) :
         for xs in xss do :
            do(f, xs)

   defmapper (f:LSType -> LSType) :
      (x:FnT) : f(x) as FnT
      (x:LSType) : f(x)
      (x:False|LSType) : f(x as LSType) when x not-typeof False
      (x:List<LSType>) : map(f, x)
      (xs:List<False|LSType>) :
         for x in xs map :
            f(x as LSType) when x not-typeof False
      (xss:List<List<LSType>>) :
         for xs in xss map :
            map(f, xs)
   defdoer (f:LSType -> ?) :
      (x:FnT) : f(x)
      (x:LSType) : f(x)
      (x:False|LSType) : f(x as LSType) when x not-typeof False
      (x:List<LSType>) : do(f, x)
      (xs:List<False|LSType>) :
         for x in xs do :
            f(x as LSType) when x not-typeof False
      (xss:List<List<LSType>>) :
         for xs in xss do :
            do(f, xs)
               
   defmapper (f:TComm -> TComm) :
      (x:List<TDefn>) : map({f(_) as TDefn}, x)
      (x:List<TDefmethod>) : map({f(_) as TDefmethod}, x)
   defdoer (f:TComm -> ?) :
      (x:List<TDefn>) : do(f, x)
      (x:List<TDefmethod>) : do(f, x)

   defmapper (f:Selection -> False|Selection) :
      (x:False|Selection) : f(x as Selection) when x not-typeof False
   defdoer (f:Selection -> ?) :
      (x:False|Selection) : f(x as Selection) when x not-typeof False

   defmapper (f:Context -> Context) :
      (x:Context) : f(x)
      (x:False|Context) : f(x as Context) when x not-typeof False
   defdoer (f:Context -> ?) :
      (x:Context) : f(x)
      (x:False|Context) : f(x as Context) when x not-typeof False

   ignore :
      Int
      List<Int>
      True|False
      False|Int
      Symbol
      False|FileInfo
      ?
      LSPrimitive
      Primitive

public defstruct TProg :
   hier: THierarchy
   comms: List<TComm>

public defn type (c:CDo) -> False|TArrow :
   defn #V (t:False|Type) :
      fail() when t typeof False
      t as Type
   attempt: TArrow(TTuple(map(#V,args(c))), #V(ret(c)))
   
public defn type (c:CLSDo) -> False|FnT :
   defn #V (t:False|LSType) :
      fail() when t typeof False
      t as LSType
   attempt: FnT(map(#V,args(c)), false, #V(ret(c)))

defprinter (p:TProg) :
   TProg :
      ($prog comms ...)

defprinter (e:TComm) :
   TDef :
      ($def n type value)
   TDefTuple :
      ($deftuple (ns ...) (types ...) value)
   TDefVar :
      ($defvar n type value)
   TDefn :
      if tail?(e) : ($defn* n (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
      else : ($defn n (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
   TDefmulti :
      ($defmulti n (targs ...) (cargs ...) (a1 ...) a2)
   TDefmethod :
      if tail?(e) : ($defmethod* n multi thisn (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
      else : ($defmethod n multi thisn (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
   TInit :
      custom{exp(e)}

   TLDef :
      ($ls-def n type value)
   TLDefVar :
      ($ls-defvar n type value)
   TLDefn :
      if tail?(e) : ($ls-defn* n (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
      else : ($ls-defn n (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
   TLDefmethod :
      if tail?(e) : ($ls-defmethod* n multi (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
      else : ($ls-defmethod n multi (targs ...) (cargs ...) (a1 ...) a2 (args ...) body)
   TExtern :
      ($ls-extern n type)
   TLInit :
      custom{comm(e)}
      

defprinter (e:TExp) :
   TLet :
      ($let n ntype value body)
   TLetVar :
      ($let-var n ntype value body)
   TLetTuple :
      ($let-tuple (ns ...) (ntypes ...) value body)
   TLetRec :
      ($let-rec (defns ...) body)
   TFn :
      if tail?(e) : ($fn* (a1 ...) a2 (args ...) body)
      else : ($fn (a1 ...) a2 (args ...) body)
   TMulti :
      ($multi funcs ...)
   TSeq :
      ($seq a b)
   TMatch :
      ($match (args ...) branches ...)
   TBranch :
      ($branch (args ...) (atypes ...) body)
   TNew :
      ($new class methods ...)
   TRef :
      custom{to-symbol("$v%_:%~" % [n(e), type(e)])}
   TCast :
      ($cast exp targ)
   TUpCast :
      ($upcast exp targ)
   TSet :
      ($set ref value)
   TDo :
      ($do func args ...) 
   TPrim :
      ($prim op args ...)
   TLiteral :
      custom{value(e)}
   TTupleExp :
      ($tuple exps ...)
   TMix :
      ($mix sel (exps ...) type ctxt)
   TCurry :
      ($tcurry ref (targs ...) (cargs ...) type)
      

defprinter (t:Type) :
   TCap :
      ($cap n)
   TOf :
      ($of n type)
   TTuple :
      ($tuple types ...)
   TOr :
      ($or a b)
   TAnd :
      ($and a b)
   TVar :
      custom{to-symbol("$t%_" % [n(t)])}
   TPoly :
      ($poly targs cargs func)
   TMixed :
      ($mixed types ...)
   TArrow :
      ($-> a b)
   TGradual :
      custom{`?}
   TBot :
      custom{`void}
   TUVar :
      custom{to-symbol("$ut%_" % [n(t)])}
      

defprinter (t:LSType) :
   ByteT :
      ($byte)
   IntT :
      ($int)
   LongT :
      ($long)
   FloatT :
      ($float)
   DoubleT :
      ($double)
   UnknownT :
      ($?)
   PolyT :
      ($poly (targs ...) (cargs ...) func)
   PtrT :
      ($ptr type)
   RefT :
      ($ref type)
   FnT :
      ($-> (a ...) r b)
   StructT :
      ($struct n type)
   UVarT:
      custom{to-symbol("$ut%_" % [n(t)])}
      
defprinter (c:LSComm) :
   LSCall :
      custom{exp(c)}
   LSSet :
      ($ls-set exp value)
   LSLabels :
      ($ls-labels (ns ...) body)
   LSLabel :
      ($ls-label n)
   LSGoto :
      ($ls-goto n)
   LSReturn :
      ($ls-return exp)
   LSDef :
      ($ls-def n type value)
   LSDefVar :
      ($ls-defvar n type value)
   LSSeq :
      ($ls-seq a b)
   LSIf :
      ($ls-if pred conseq alt)
   LSMatch :
      ($ls-match (args ...) branches ...)
   LSBranch :
      ($ls-branch (args ...) (atypes ...) body)
   LSSkip :
      ($ls-skip)


defprinter (e:LSExp) :
   LSVar :
      custom{"$v%_" % [n(e)]}
   LSNew :
      ($ls-new n targ args ...)
   LSArray :
      ($ls-array n targ length args ...)
   LSStruct :
      ($ls-struct n targ args ...)
   LSAddr :
      ($ls-addr exp)
   LSDeref :
      ($ls-deref exp)
   LSSlot :
      ($ls-slot exp index)
   LSField :
      ($ls-field exp name)
   LSDo :
      ($ls-do func args ...)
   LSDoInStack :
      ($ls-do-in-stack ret func args ...)
   LSDoStack :
      ($ls-do-stack ret func arg)
   LSPrim :
      ($ls-prim op args ...)
   LSCallC :
      ($ls-callc func args ...)
   LSSizeof :
      ($ls-sizeof targ)
   LSTagof :
      ($ls-tagof n)
   LSCast :
      ($ls-cast exp type)
   LSLiteral :
      ($ls-value value)
   LSAnd :
      ($ls-and a b)
   LSOr :
      ($ls-or a b)
   LSLetExp :
      ($ls-let-exp comm exp)
   LSMix :
      ($ls-mix sel exps ...)
   LSCurry :
      ($ls-curry ref (targs ...) (cargs ...) type)
   LSFn:
      ($ls-fn ref)

defprinter (c:Context) :
   CExp :
      ($exp type)
   CMulti :
      ($multi type)
   CDo :
      ($do (args ...) ret)
   CLSDo :
      ($ls-do (args ...) ret)
   CLSExp :
      ($ls-exp type)


defprinter (s:Selection) :
   SVar :
      custom{"$s%_" % [n(s)]}
   SSel :
      ($sel sels ...)
      
public defn children (t:Type) -> Many<Type> :
   val ts = Vector<Type>(4)
   do(add{ts, _}, t)
   ts

public defn children (t:LSType) -> Many<LSType> :
   val ts = Vector<LSType>(4)
   do(add{ts, _:LSType}, t)
   ts

;                        Mappers and Doers
;                        =================

public defn dor (fe: LSExp -> ?, e: LSExp) :
   defn fc (c:LSComm) : dor(fe, c)
   do(fe, e)
   do(fc, e)
   
public defn dor (fe: LSExp -> ?, c: LSComm) :
   defn fc (e:LSComm) : dor(fe, e)
   do(fe, c)
   do(fc, c)

public defn mapr (f: Type -> Type, t:LSType) :
   defn ft (t:LSType) : mapr(f, t)
   map(ft, map(f, t))