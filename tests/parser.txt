defpackage mypackage :
   import core
   import reader

;============================================================
;===================== Syntax Tree ==========================
;============================================================

deftype Exp
defstruct AddOp <: Exp : (x:Exp, y:Exp)
defstruct SubOp <: Exp : (x:Exp, y:Exp)
defstruct MulOp <: Exp : (x:Exp, y:Exp)
defstruct DivOp <: Exp : (x:Exp, y:Exp)
defstruct CallOp <: Exp : (f:Exp, args:List<Exp>)
defstruct Identifier <: Exp : (name:Symbol)
defstruct Number <: Exp : (value:Double)

defmethod print (o:OutputStream, e:Exp) :
   print{o, _} $ match(e) :
      (e:AddOp) : "(%_ + %_)" % [x(e), y(e)]
      (e:SubOp) : "(%_ - %_)" % [x(e), y(e)]
      (e:MulOp) : "(%_ * %_)" % [x(e), y(e)]
      (e:DivOp) : "(%_ / %_)" % [x(e), y(e)]
      (e:CallOp) : "%_(%,)" % [f(e), args(e)]
      (e:Identifier) : name(e)
      (e:Number) : value(e)

;============================================================
;================== Parser Definition =======================
;============================================================

defsyntax mylang :
   public defproduction exp : Exp
   public defproduction exp0 : Exp
   public defproduction exp1 : Exp
   public defproduction term : Exp
   public defproduction id : Identifier
   public defproduction num : Number

   defrule id = (?x) when unwrap-token(x) is Symbol :
      Identifier(unwrap-token(x))
   defrule num = (?x) when unwrap-token(x) is Double :
      Number(unwrap-token(x))

   defrule exp = (?x:#exp0) : x
   defrule exp = (?x:#exp + ?y:#exp0) : AddOp(x, y)
   defrule exp = (?x:#exp - ?y:#exp0) : SubOp(x, y)
   
   defrule exp0 = (?x:#exp1) : x
   defrule exp0 = (?x:#exp0 * ?y:#exp1) : MulOp(x, y)
   defrule exp0 = (?x:#exp0 / ?y:#exp1) : DivOp(x, y)

   defrule exp1 = (?x:#term) : x

   defrule term = ((?e:#exp)) : e
   defrule term = (?x:#id) : x
   defrule term = (?x:#num) : x

defsyntax call-op :
   import (exp1, exp) from mylang
   defrule exp1 = (?x:#exp1(?args:#exp ... ?rest ...)) :
      if not empty?(rest) :
         fatal("%_: Expected expression here." % [closest-info(rest)])
      CallOp(x, args)

defn parse-exps (form) -> List<Exp> :
   with-syntax(mylang + call-op) :
      match-syntax(form) :
         (?es:#exp ...) :
            es          

;============================================================
;===================== Driver ===============================
;============================================================

defn main () :
   ;Ensure filename given
   if length(command-line-arguments()) < 2 :
      println("Please provide a filename.")
      exit(-1)

   ;Get filename
   val filename = command-line-arguments()[1]

   ;Read
   val read = read-file(filename)

   ;Parse
   val exps = parse-exps(read)

   ;Print
   do(println, exps)

main()