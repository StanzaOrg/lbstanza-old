deftype MyStruct
defstruct MyA <: MyStruct :
   a: Int
   b: Int
defstruct MyB <: MyStruct :
   a: Int
   b: Int
   c: List<Int>
defstruct MyC <: MyStruct :
   a: MyStruct
   b: MyStruct

defserializer (out:FileOutputStream, in:FileInputStream) :
   defunion mystruct (MyStruct) :
      MyA : (a:int, b:int)
      MyB : (a:int, b:int, c:ints)
      MyC : (a:mystruct, b:mystruct)
   with :
      append-args => (false)

   reader defn read-list<?T> (f: () -> ?T) :
      val n = read-int()
      to-list(repeatedly(f, n))
      
   writer defn write-list<?T> (f: T -> False, xs:List<?T>) :
      put(out, length(xs))
      do(f, xs)

   defatom int (x:Int) :
      writer :
         put(out, x)
      reader :
         match(get-int(in)) :
            (i:Int) : i
            (i:False) : fatal("Unexpected end of file.")

   defatom ints (xs:List<Int>) :
      writer : write-list(write-int, xs)
      reader : read-list(read-int)

defn serialize (out:FileOutputStream, x:MyStruct) :
   defn write-int (x:Int) :
      put(out, x)
   defn write-ints (xs:List<Int>) :
      put(out, length(xs))
      do(write-int, xs)
   defn write-mystruct (x:MyStruct) :
      match(x) :
         (x:MyA) :
            core/put(out, 0Y)
            write-int(a(x))
            write-int(b(x))
         (x:MyB) :
            core/put(out, 1Y)
            write-int(a(x))
            write-int(b(x))
            write-ints(c(x))
         (x:MyC) :
            core/put(out, 2Y)
            write-mystruct(a(x))
            write-mystruct(b(x))
   write-mystruct(x)

defn deserialize (in:FileInputStream) -> MyStruct :
   defn read-int () -> Int :
      match(get-int(in)) :
         (i:Int) : i
         (i:False) : fatal("Unexpected end of file.")
   defn read-ints () -> List<Int> :
      val n = read-int()
      map(read-int{}, 0 to n)
   defn read-mystruct () -> MyStruct :
      val tag = core/get-byte(in) as Byte
      switch {tag == _} :
         0Y :
            MyA(read-int(), read-int())
         1Y :
            MyB(read-int(), read-int(), read-ints())
         2Y :
            MyC(read-mystruct(), read-mystruct())
   read-mystruct()
      

