#use-added-syntax(tests)
defpackage stz/test-packed-class-table :
  import core
  import collections
  import stz/packed-class-table

defn records () -> Tuple<ClassTableRecord> :
  val indices = to-seq(0 to false)
  [LeafClassRecord(next(indices), "False", 0, [])
   LeafClassRecord(next(indices), "True", 0, [])
   LeafClassRecord(next(indices), "Byte", 1, [])
   LeafClassRecord(next(indices), "Char", 1, [])
   LeafClassRecord(next(indices), "Int", 4, [])
   LeafClassRecord(next(indices), "Float", 4, [])
   LeafClassRecord(next(indices), "Stack", 40, [])
   ArrayClassRecord(next(indices), "Fn", 16, 8, [], [0])
   ArrayClassRecord(next(indices), "Type", 16, 8, [], [0])
   LeafClassRecord(next(indices), "LivenessTracker", 16, [])
   LeafClassRecord(next(indices), "Box", 8, [0])
   ArrayClassRecord(next(indices), "StackFrame", 16, 8, [], [])
   ArrayClassRecord(next(indices), "GlobalRoots", 4, 4, [], [])
   ArrayClassRecord(next(indices), "StackMap", 8, 4, [], [])
   ArrayClassRecord(next(indices), "StackTraceTable", 8, 40, [], [])
   LeafClassRecord(next(indices), "StackTraceTableEntry", 40, [])
   ArrayClassRecord(next(indices), "ClassRecord", 20, 4, [], [])
   ArrayClassRecord(next(indices), "ArrayRecord", 24, 4, [], [])
   LeafClassRecord(next(indices), "VMState", 296, [])
   ArrayClassRecord(next(indices), "ExternTable", 8, 16, [], [])
   LeafClassRecord(next(indices), "ExternEntry", 16, [])
   ArrayClassRecord(next(indices), "ExternDefnTable", 8, 16, [], [])
   LeafClassRecord(next(indices), "ExternDefnEntry", 16, [])
   LeafClassRecord(next(indices), "LSLongVector", 16, [])
   LeafClassRecord(next(indices), "Long", 8, [])
   LeafClassRecord(next(indices), "Double", 8, [])
   ArrayClassRecord(next(indices), "String", 12, 1, [], [])
   LeafClassRecord(next(indices), "StringSymbol", 8, [0])
   ArrayClassRecord(next(indices), "Tuple", 8, 8, [], [0])]

defn test-packer () :
  val label-counter = to-seq(1000 to false)
  new StaticPacker :
    defmethod make-label (this) :
      next(label-counter)
    defmethod emit-comment (this, str:String) :
      println("//%_" % [str])
    defmethod emit-label (this, id:Int) :
      println(".label L%_" % [id])
    defmethod emit-pointer-to-label (this, id:Int) :
      println(".pointer-to-label L%_" % [id])
    defmethod emit (this, x:Int) :
      println(".int %_" % [x])
    defmethod emit (this, x:Long) :
      println(".long %_" % [x])
    defmethod emit (this, s:String) :
      println(".string %~" % [s])

deftest static-pack-class-table :
  pack(records(), test-packer())
