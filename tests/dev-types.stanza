defpackage stz-test-suite/dev-types :
  import core
  import collections
  import stz/type-solver
  import stz/type-hierarchy
  import stz/types
  import stz/type-locs
  import stz/type-formatter
  import stz/type-equation-solver
  import stz-test-suite/test-tools
  import stz-test-suite/type-reader
  import stz-test-suite/type-test-ir

;============================================================
;=================== Execution ==============================
;============================================================

public defn execute (prog:TestTypeProgram) :
  defn fmt (x:TypeStmt) : format(type-formatter(prog),x)
  defn fmt (n:Int) : format(type-formatter(prog),TVar(n,false))
  defn fmt (s:SearchResult) : format(type-formatter(prog),s)
  defn fmt (r:SimplifyResult) : format(type-formatter(prog),r)
  defn fmt (t:Type) : format(type-formatter(prog),t)
  defn fmt (f:False) : false
  
  val hier = make-hierarchy(prog)
  for stmt in stmts(prog) do :  
    match(stmt) :
      (stmt:Capture) :
        println("Capture Test: %_" % [fmt(stmt)])
        val captures = capture-variance(args(stmt) as Tuple<Int>, b(stmt))
        for capture in captures do :
          val arg = key(capture)
          val variance = value(capture)
          val sresult = capture-search(arg, CapRight, a(stmt), b(stmt), hier)
          val simp = simplify(sresult, true)
          val type? = match(simp:Solved) : evaluate-constraint(constraint(simp), variance)
          println("Capturing %_" % [fmt(arg)])
          println(Indented(fmt(sresult)))
          println("Simplifies to:")
          println(Indented(fmt(simp)))
          println("Evaluates to:")
          println(Indented(fmt(type?)))
          println("")
      (stmt:Subtype) :
        val sresult = subtype-search(a(stmt), b(stmt), hier)
        val simp = simplify(sresult, false)
        println("Subtype Test: %_" % [fmt(stmt)])
        println(Indented(fmt(sresult)))
        println("Simplifies to:")
        println(Indented(fmt(simp)))
        println("")
      (stmt:Infer) :
        val sresult = capture-search(a(stmt), b(stmt), hier)
        val simp = simplify(sresult, true)
        val type? = match(simp:Solved) :
          val variance = capture-variance(a(stmt), b(stmt))
          evaluate-constraint-conservative(constraint(simp), variance)
        println("Inference Test: %_" % [fmt(stmt)])
        println(Indented(fmt(sresult)))
        println("Simplifies to:")
        println(Indented(fmt(simp)))
        println("Evaluates to:")
        println(Indented(fmt(type?)))
      (stmt:Solve) :
        println("Solve:")
        do(println{Indented(_)}, eqns(stmt))
        val state = SolverState(eqns(stmt), hier)
        solve(state)
        println("Solutions:")
        println(Indented("%n" % [solutions(state)]))
    println("\n")

;Create the hierarchy of the program.
defn make-hierarchy (prog:TestTypeProgram) -> TypeHierarchy :
  ;Populate special table.
  val special-table = IntTable<SpecialType>()
  for r in hierarchy(prog) do :
    val name = table(names(prog))[n(r)]
    switch(name) :
      `Tuple : special-table[n(r)] = CoreTuple
      else : false

  ;Create the type relations.
  val relations = for r in hierarchy(prog) map :
    val child = child(r) as TOf
    val child-args = map(n, args(child) as Tuple<TVar>)
    TypeRelation(n(child), get?(special-table,n(child)), child-args, parent(r))

  ;Return the hierarchy.
  TypeHierarchy(relations)

;============================================================
;==================== Main Launcher =========================
;============================================================

public defn main () :
  val file = data-file("type1.txt")
  val prog = read-test-type-program(file)
  println(format(prog))
  println("\n")
  execute(prog)

within (package,sig) = set-stack-trace-filter() :
  package != `core
main()