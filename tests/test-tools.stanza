defpackage stz-test-suite/test-tools :
  import core
  import collections

;Helper: Retrieve the stanza executable.
public defn stanza-executable () -> String :
  match(get-env("STANZA")) :
    (file:String) : file
    (f:False) : "stanza"

;Call Stanza, and throw an exception on failure.
public defn call-stanza (args:Tuple<String>) -> False :
  val new-args = to-tuple $ cat-all $ [[stanza-executable()], args]
  val ret = call-system(stanza-executable(), new-args)
  throw(Exception("Call to Stanza failed.")) when ret != 0

;Ensure that a directory exists, and create one if it doesn't.
public defn ensure-directory (name:String) -> False :
  if file-exists?(name) :
    if file-type(name) is-not DirectoryType :
      throw(Exception("File %~ exists and is not a directory." % [name]))
  else :
    create-dir(name)

;Return a file in the build directory.
public defn build-file (name:String) -> String :
  ensure-directory("build")
  to-string("build/%_" % [name])

;Return a file in the data directory.
public defn data-file (name:String) -> String :
  ensure-directory("data")
  to-string("data/%_" % [name])

;Return a file in the output directory.
public defn output-file (name:String) -> String :
  ensure-directory("output")
  to-string("output/%_" % [name])