defpackage stz/main :
   import core
   import reader
   import stz/compiler
   import stz/arg-parser
   import stz/params

;============================================================
;================== Config File Parsing =====================
;============================================================

defsyntax stanza-config :
   defn ut (x) :
      unwrap-token(x)

   defn first-info (form) -> Maybe<FileInfo> :
      match(form) :
         (form:Token) : One(info(form))
         (form:List) : first(first-info, form)
         (form) : None()

   defn PE (form, msg) :
      throw $ match(value?(first-info(form))) :
         (info:FileInfo) : Exception("%_: %_" % [info, msg])
         (info:False) : Exception(msg)

   defrule :
      sym = (?x) when ut(x) typeof Symbol : ut(x)
      string = (?x) when ut(x) typeof String : ut(x)
      string! = (?s:#string) : s
      string! != () : PE(form, "Expected a string here.")

      platform! = (os-x) : `os-x
      platform! = (linux) : `linux
      platform! != (?x:#sym) : PE(form, "%_ is not a supported platform." % [x])
      platform! != () : PE(form, "Expected a platform here.")

      entry = (install-dir = ?path:#string!) :
         STANZA-INSTALL-DIR = path
      entry = (platform = ?p:#platform!) :
         OUTPUT-PLATFORM = p
      entry != () :
         PE(form, "Invalid configuration rule.")

defn read-config-file () :
   ;Extract parameters from config file
   label break :
      defn extract-from-file (filename:String) :
         if file-exists?(filename) :
            val forms = read-file(filename)
            with-syntax(stanza-config) :
               match-syntax(forms) :
                  (?e:#entry ...) : false
            break(false)

      defn extract-from-env (name:String) :
         match(get-env(name)) :
            (path:String) : extract-from-file(to-string("%_/.stanza" % [path]))
            (path:False) : false

      extract-from-file(".stanza")
      extract-from-env("STANZA_CONFIG")
      extract-from-env("HOME")
      throw(Exception("Could not locate .stanza configuration file."))

   ;Verify parameters
   val stanza-exe = to-string("%_/stanza" % [STANZA-INSTALL-DIR])
   if not file-exists?(stanza-exe) :
      throw $ Exception(STANZA-EXE-MSG % [STANZA-INSTALL-DIR, stanza-exe])

val STANZA-EXE-MSG = "Stanza install directory is set to %_, but could not locate stanza compiler at %_."   

;============================================================
;================== Compilation =============================
;============================================================

defn call-cc (asmfile:String, outfile:String) :   
   val cmd = to-string $
      "cc -std=c99 %_ %_/runtime/driver.c -o %_ -lm" % [
         asmfile, STANZA-INSTALL-DIR, outfile]
   println(cmd)
   call-system(cmd)

defn compile (parsed:ParseResult) :
   ;Set up configuration
   read-config-file()
   if (not has-flag?(parsed, "s")) and (not has-flag?(parsed, "o")) :
      throw $ Exception("Command compile requires at least one of the -s flag or the -o flag to be given.")
   add-flag(`OPTIMIZE) when has-flag?(parsed, "optimize")

   ;Call CC
   defn call-cc? (asmfile:String) :
      match(flag?(parsed, "o")) :
         (outfile:String) : call-cc(asmfile, outfile)
         (outfile:False) : false         

   ;Compile to assembly
   val files = args(parsed)
   match(flag?(parsed, "s")) :
      (asmfile:String) :
         compile(files, asmfile)
         call-cc?(asmfile)
      (asmfile:False) :
         val asmfile* = to-string("%_.s" % [gensym("tmp")])
         compile(files, asmfile*)
         call-cc?(asmfile*)
         delete-file(asmfile*)

;============================================================
;================== Main Interface ==========================
;============================================================

defn main () :
   val compile-comm = Command("compile", [
                         SingleFlag("s", true),
                         SingleFlag("o", true),
                         SingleFlag("platform", true)
                         SingleFlag("ccflags", true)
                         MarkerFlag("optimize")
                         MarkerFlag("no-implicits")])
   val comms = [compile-comm]
   try :      
      val parsed = parse-args(comms, compile-comm, command-line-arguments()[1 to false])
      switch {command(parsed) == _} :
         "compile" : compile(parsed)
   catch (e:Exception) :
      println(e)
      false

main()